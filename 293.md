Sneaky Burlap Pigeon

Medium

# Overflow Issue in Newton's Convergence Method


### Summary
When adding a specified amount of tokens, the `targetValue` is calculated using Newton's method. 
However, this method does not impose a restriction on the maximum value.
As a result, a very large `targetValue` may be attempted or even cause an overflow.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L198
The issue arises from the implementation found at `Value.sol#L198`. The `nextTX128` variable can exceed the maximum limit, or overflow could occure.

```solidity
    function stepT(
        uint256 tX128,
        uint256[] memory esX128,
        uint256[] memory xs,
        int256 ftX128
    ) internal pure returns (uint256 nextTX128) {
        int256 dftX128 = dfdt(tX128, esX128, xs);
        bool posNum = ftX128 > 0;
        bool posDenom = dftX128 > 0;
        if (posNum && posDenom) {
            nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(dftX128));
        } else if (posNum && !posDenom) {
198:        nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(-dftX128));
        } else if (!posNum && posDenom) {
            nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(dftX128));
        } else {
            nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(-dftX128));
        }
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
The PoC below demonstrates that the user cannot deposit 6.14e20 of token1 using the `addSingleForValue` function, even though they can successfully deposit 12e20 of token1 using the `addValueSingle` function.
In the PoC overflow occured. (https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/FullMath.sol#L46)

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_convergence1() public {
        uint256[4] memory initBalances = getBalances(alice);
        vm.prank(alice);
        valueFacet.addValueSingle(alice, 0x3, 11e20, 0, tokens[1], 0);
        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("diffs[1]  : %e", diffs[1]);
    }
    function test_convergence2() public {
        uint256[4] memory initBalances = getBalances(alice);
        vm.prank(alice);
        valueFacet.addSingleForValue(alice, 0x3, tokens[1], 6.140833264451424007e20, 0, 0);
        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("diffs[1]  : %e", diffs[1]);
    }
```
forge test --match-test "test_convergence" -vv
Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_convergence1() (gas: 553072)
Logs:
  diffs[1]  : -1.2e21

[FAIL: EvmError: Revert] test_convergence2() (gas: 282964)
```
After mitigation, Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_convergence1() (gas: 553072)
Logs:
  diffs[1]  : -1.2e21

[PASS] test_convergence2() (gas: 645530)
Logs:
  diffs[1]  : -6.140833264451424007e20
```

### Impact
The time-sensitive function is DoSed.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L198
```diff
    function stepT(
        uint256 tX128,
        uint256[] memory esX128,
        uint256[] memory xs,
        int256 ftX128
    ) internal pure returns (uint256 nextTX128) {
        int256 dftX128 = dfdt(tX128, esX128, xs);
        bool posNum = ftX128 > 0;
        bool posDenom = dftX128 > 0;
        if (posNum && posDenom) {
            nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(dftX128));
        } else if (posNum && !posDenom) {
-198:        nextTX128 =
+             nextTX128 = ((uint256(-dftX128) <= (uint256(ftX128) >> 128)) ||
+                (FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(-dftX128)) >= type(uint256).max - tX128)) ? type(uint256).max : 
                tX128 +
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(-dftX128));
+            for (uint256 i = 0; i < xs.length; i++) {
+                if (xs[i] > 0 && nextTX128 >= xs[i] * (esX128[i] + TWOX128))
+                    nextTX128 = xs[i] * (esX128[i] + TWOX128);
+            }
        } else if (!posNum && posDenom) {
            nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(dftX128));
        } else {
            nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(-dftX128));
        }
    }
```