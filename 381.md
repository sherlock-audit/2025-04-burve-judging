Abundant Boysenberry Cod

Medium

# Loss of fees due to prematurily increasing staked values

### Summary

When adding value through `ValueFacet::addValueSingle`, the `addEarnings` function is called after updating `valueStaked` and `bgtValueStaked`, which inadvertently dilutes the share of fees distributed. This results in reduced rewards for existing liquidity providers.

```solidity
function addEarnings(Closure storage self, VertexId vid, uint256 earnings) internal {
    uint8 idx = vid.idx();
    // Round protocol take down.
    uint256 protocolAmount = FullMath.mulX128(earnings, self.protocolTakeX128, false);
    SimplexLib.protocolTake(idx, protocolAmount);
    uint256 userAmount = earnings - protocolAmount;
    uint256 unspent;

    if (self.bgtValueStaked > 0) {
        // Round BGT take down.
        uint256 bgtExAmount = (userAmount * self.bgtValueStaked) / self.valueStaked;
        uint256 bgtEarned;

        (bgtEarned, unspent) = SimplexLib.bgtExchange(idx, bgtExAmount);

        self.bgtPerBgtValueX128 += (bgtEarned << 128) / self.bgtValueStaked;
        userAmount -= bgtExAmount;
    }

    uint256 reserveShares = ReserveLib.deposit(vid, unspent + userAmount);

    if (unspent > 0) {
        // rare case
        uint256 unspentShares = (reserveShares * unspent) / (userAmount + unspent);

        self.unexchangedPerBgtValueX128[idx] += (unspentShares << 128) / self.bgtValueStaked;
        reserveShares -= unspentShares;
    }

    // Allocate to non-BGT value stakers
    self.earningsPerValueX128[idx] += (reserveShares << 128) / (self.valueStaked - self.bgtValueStaked);
    // Denominator is guaranteed non-zero due to initial pool conditions.
}
```

The function above distributes earnings proportionally to stakers in a rewards-per-share fashion. However, because `addValueSingle` [updates](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L208-L209) `valueStaked` and `bgtValueStaked` *before* calling `addEarnings`, the new liquidity unfairly reduces the earnings share for previous participants.

### Root Cause

In both `ValueFacet::addValueSingle` and `ValueFacet::addSingleForValue`, `valueStaked` and `bgtValueStaked` are prematurely incremented before invoking `addEarnings`, causing the fee distribution to be diluted.

### Internal Pre-conditions

1. None

### External Pre-conditions

1. None

### Attack Path

None

### Impact

Existing liquidity providers receive fewer rewards due to dilution caused by prematurely updated staked values.

### PoC

*No proof-of-concept provided.*

### Mitigation

Introduce a dedicated method in the `Closure` library to update `valueStaked` and `bgtValueStaked` **after** the `addEarnings` logic has executed. Modify `ValueFacet::addValueSingle` and `ValueFacet::addSingleForValue` to use this updated pattern, ensuring rewards are calculated based on the correct prior state.
