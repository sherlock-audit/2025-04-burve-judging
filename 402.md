Cool Canvas Butterfly

Medium

# Kodiak island deposits may be reverted when small amounts of liquidity are added

### Summary

`island.getMintAmounts` may be reverted or `island.mint` may be reverted

### Root Cause


During minting, a portion is deposited to Kodiak island based on weight. While deposits aren't made when `liqInRange` is 0, small amounts greater than 0 will call `mintIsland`.

The `mintIsland` function calls the `island.getMintAmounts` function to calculate the value of the mintable Kodiak island share and passes it to `mintShares`. However, even if `liqInRange` is greater than 0, `mintShares` can be 0 or `island.getMintAmounts` may revert. If `mintShares` is 0, `island.mint` will be reverted. [single/Burve.sol#L310-L333](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/single/Burve.sol#L310-L333)

```solidity
function mint(
  address recipient,
  uint128 mintNominalLiq,
  uint160 lowerSqrtPriceLimitX96,
  uint160 upperSqrtPriceLimitX96
)
  public
  withinSqrtPX96Limits(lowerSqrtPriceLimitX96, upperSqrtPriceLimitX96)
  returns (uint256 shares)
{
  // compound v3 ranges
  compoundV3Ranges();

  uint256 islandShares = 0;

  // mint liquidity for each range
  for (uint256 i = 0; i < distX96.length; ++i) {
      uint128 liqInRange = uint128(
          shift96(uint256(mintNominalLiq) * distX96[i], true)
      );

@>    if (liqInRange == 0) {
          continue;
      }

      TickRange memory range = ranges[i];
@>    if (range.isIsland()) {
@>        islandShares = mintIsland(recipient, liqInRange);
      } else {
          // mint the V3 ranges
          pool.mint(
              address(this),
              range.lower,
              range.upper,
              liqInRange,
              abi.encode(msg.sender)
          );
      }
  }

  ...
}

function mintIsland(
    address recipient,
    uint128 liq
) internal returns (uint256 mintIslandShares) {
    (uint160 sqrtRatioX96, , , , , , ) = pool.slot0();

    (uint256 amount0, uint256 amount1) = getAmountsForLiquidity(
        sqrtRatioX96,
        liq,
        island.lowerTick(),
        island.upperTick(),
        true
    );
    (uint256 mint0, uint256 mint1, uint256 mintShares) = island
@>      .getMintAmounts(amount0, amount1);

    ...
@>  island.mint(mintShares, address(this));

    ...
}
```

Here is the [Kodiak island code](https://berascan.com/address/0x4a254b11810b8ebb63c5468e438fc561cb1bb1da#code). You can see that `island.mint` is reverted when mintAmount is zero. In `island.getMintAmounts`, the amount of tokens deposited on Kodiak islands affects the result of the calculation. If one of the tokens has a balance of zero and the other is non-zero, `mintAmount = FullMath.mulDiv(amount1Max, totalSupply, amount1Current)` will be rounded down, so it can be zero. If both tokens are deposited, the transaction can be canceled by `require(amount0Mint > 0 && amount1Mint > 0, "mint 0")`;.

```solidity
function mint(uint256 mintAmount, address receiver) external whenNotPaused nonReentrant returns (uint256 amount0, uint256 amount1, uint128 liquidityMinted) {
    require(!restrictedMint || msg.sender == _manager || !isManaged(), "restricted");

    uint256 totalSupply = totalSupply();

    (uint160 sqrtRatioX96,,,,,,) = pool.slot0();

    if (totalSupply > 0) {
@>      require(mintAmount > 0, "mint 0");
        (uint256 amount0Current, uint256 amount1Current) = getUnderlyingBalances();

        amount0 = FullMath.mulDivRoundingUp(amount0Current, mintAmount, totalSupply);
        amount1 = FullMath.mulDivRoundingUp(amount1Current, mintAmount, totalSupply);
    } 
    ...
}

function getMintAmounts(uint256 amount0Max, uint256 amount1Max) external view returns (uint256 amount0, uint256 amount1, uint256 mintAmount) {
    uint256 totalSupply = totalSupply();
    if (totalSupply > 0) {
@>      (amount0, amount1, mintAmount) = _computeMintAmounts(totalSupply, amount0Max, amount1Max);
    }
    ...
}

function _computeMintAmounts(uint256 totalSupply, uint256 amount0Max, uint256 amount1Max) private view returns (uint256 amount0, uint256 amount1, uint256 mintAmount) {
@>  (uint256 amount0Current, uint256 amount1Current) = getUnderlyingBalances();

    // compute proportional amount of tokens to mint
    if (amount0Current == 0 && amount1Current > 0) {
@>      mintAmount = FullMath.mulDiv(amount1Max, totalSupply, amount1Current); // can be zero
    } else if (amount1Current == 0 && amount0Current > 0) {
@>      mintAmount = FullMath.mulDiv(amount0Max, totalSupply, amount0Current); // can be zero
    } else if (amount0Current == 0 && amount1Current == 0) {
        revert("");
    } else {
        // only if both are non-zero
        uint256 amount0Mint = FullMath.mulDiv(amount0Max, totalSupply, amount0Current);
        uint256 amount1Mint = FullMath.mulDiv(amount1Max, totalSupply, amount1Current);
@>      require(amount0Mint > 0 && amount1Mint > 0, "mint 0"); // revert

        mintAmount = amount0Mint < amount1Mint ? amount0Mint : amount1Mint;
    }

    // compute amounts owed to contract
    amount0 = FullMath.mulDivRoundingUp(mintAmount, amount0Current, totalSupply);
    amount1 = FullMath.mulDivRoundingUp(mintAmount, amount1Current, totalSupply);
}

function getUnderlyingBalances() public view returns (uint256 amount0Current, uint256 amount1Current) {
    (uint160 sqrtRatioX96, int24 tick,,,,,) = pool.slot0();
@>  return _getUnderlyingBalances(sqrtRatioX96, tick);
}

function _getUnderlyingBalances(uint160 sqrtRatioX96, int24 tick) internal view returns (uint256 amount0Current, uint256 amount1Current) {
    ...

    // add any leftover in contract to current holdings
@>  amount0Current += fee0 + token0.balanceOf(address(this)) - managerBalance0;
@>  amount1Current += fee1 + token1.balanceOf(address(this)) - managerBalance1;
}
```


### Internal Pre-conditions

1. Kodiak islands are enabled
2. If Kodiak islands is set to a low weight, it is more likely to trigger an issue as it is more likely to deposit a small amount of liquidity on Kodiak islands.


### External Pre-conditions

1. The `getUnderlyingBalances` results may increase due to accumulated interest or direct token transfers to Kodiak islands.


### Attack Path

A user deposits a small amount of liquidity.


### Impact

`mint` may fail.


### PoC

_No response_

### Mitigation

Use try-catch for `island.getMintAmounts` calls. If it reverts or returns 0, skip providing liquidity to Kodiak islands.