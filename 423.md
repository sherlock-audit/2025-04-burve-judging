Magnificent Hemp Parakeet

High

# Incorrect earnings calculation in `removeValue()` function causes partial user losses

### Summary

Asset removal before invoking `trimBalance()` in `removeValue()` function causes partial loss of user earnings, as the asset's `remove()` function uses outdated earnings per value variable to calculate user collected earnings.

### Root Cause

In [`ValueFacet.sol:190`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L190) the asset's `remove()` function is invoked prior to the closure's `removeValue()`. (resulting in outdated earnings per value calculations)
In [`Asset.sol:114`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/Asset.sol#L114) the asset's `remove()` function invoke `collect` function.
In [`Asset.sol:143`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/Asset.sol#L143) the asset's `collect()` function gets outdated earnings per value from the closure.


### Internal Pre-conditions

1. Closures deposit their fee earnings into an active vault.

### External Pre-conditions

1. Vault distributes reward to closures.

### Attack Path

1. User call `removeValue()` function.

### Impact

Users may experience a loss exceeding 1% of their earnings if the pool is not sufficiently active, causing the `earnings per value` to remain outdated for an extended period.


### PoC

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {SimplexDiamond as BurveDiamond} from "../../src/multi/Diamond.sol";
import {SimplexFacet} from "../../src/multi/facets/SimplexFacet.sol";
import {SwapFacet} from "../../src/multi/facets/SwapFacet.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {VaultFacet} from "../../src/multi/facets/VaultFacet.sol";
import {MockERC20} from "../mocks/MockERC20.sol";

contract PoC1 is Test {
    address payable diamond;
    BurveDiamond public burveDiamond;
    SimplexFacet public simplexFacet;
    SwapFacet public swapFacet;
    ValueFacet public valueFacet;
    VaultFacet public vaultFacet;
    uint16 cid = 3;

    address user = address(1);
    address randomUser = address(2);
    address fakeRewardDistributor = address(3);
    address[] tokens;
    address[] vaults;

    uint128 addValue = 10_000;
    uint128 bgtValue = 1_000;
    uint256 tokenAmount = 1000 * 1e18;

    function setUp() public {
        // address received after run script/Deploy.s.sol
        diamond = payable(0xa513E6E4b8f2a923D98304ec87F64353C4D5C853);
        burveDiamond = BurveDiamond(diamond);
        simplexFacet = SimplexFacet(diamond);
        swapFacet = SwapFacet(diamond);
        valueFacet = ValueFacet(diamond);
        vaultFacet = VaultFacet(diamond);

        // get tokens list
        tokens = simplexFacet.getTokens();

        // get vaults list
        vaults = new address[](tokens.length);
        for (uint256 i = 0; i < tokens.length; i++) {
            (vaults[i], ) = vaultFacet.viewVaults(tokens[i]);
        }

        vm.deal(user, 1000 * 1e18);
        vm.deal(randomUser, 1000 * 1e18);
        for (uint256 i = 0; i < tokens.length; i++) {
            deal(tokens[i], user, type(uint256).max);
            deal(tokens[i], randomUser, type(uint256).max);
            deal(tokens[i], fakeRewardDistributor, type(uint256).max);
            vm.startPrank(user);
            MockERC20(tokens[i]).approve(diamond, type(uint256).max);
            vm.stopPrank();
            vm.startPrank(randomUser);
            MockERC20(tokens[i]).approve(diamond, type(uint256).max);
            vm.stopPrank();
        }

        // User adds liquidity
        vm.startPrank(user);
        valueFacet.addValue(user, cid, addValue, bgtValue);
        vm.stopPrank();

        // fake vault reward distribution
        vm.startPrank(fakeRewardDistributor);
        for (uint256 i = 0; i < tokens.length; i++) {
            MockERC20(tokens[i]).transfer(vaults[i], tokenAmount);
        }
        vm.stopPrank();
    }

    function testNormalRemoveValue() public {
        // User removes value
        vm.startPrank(user);
        valueFacet.removeValue(user, cid, addValue, bgtValue);

        // User collects earnings
        (uint256[16] memory collectedBalance, uint256 collectedBgt) = valueFacet
            .collectEarnings(user, cid);
        vm.stopPrank();

        for (uint256 i = 0; i < tokens.length; i++) {
            console2.log(
                "Collected balance of token %s: %s",
                tokens[i],
                collectedBalance[i]
            );
        }
    }

    function testRemoveValueAfterCallTrimBalances() public {
        // Random user adds value => triggers trimBalances()
        vm.startPrank(randomUser);
        valueFacet.addValue(randomUser, cid, addValue, bgtValue);
        vm.stopPrank();

        // User removes value
        vm.startPrank(user);
        valueFacet.removeValue(user, cid, addValue, bgtValue);

        // User collects earnings
        (uint256[16] memory collectedBalance, uint256 collectedBgt) = valueFacet
            .collectEarnings(user, cid);
        vm.stopPrank();

        for (uint256 i = 0; i < tokens.length; i++) {
            console2.log(
                "Collected balance of token %s: %s",
                tokens[i],
                collectedBalance[i]
            );
        }
    }
}

```

### Mitigation

Move the asset's `remove()` function below the closure's `removeValue()`.