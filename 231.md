Damaged Admiral Grasshopper

High

# Vault earnings are wrongly accounted leading to user collect less earnings than expected

### Summary

When calculates closure balance, the pass earnings deposited into reserve are not accounted, leading to incorrect earnings amount and user receives less tokens when they collect earnings.

### Root Cause

When trim balance, the vault earnings owned to the closures are withdrawn and deposited into the reserve.

[VertexImpl::trimBalance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Vertex.sol#L78-L82):
```solidity
        reserveSharesEarned = ReserveLib.deposit(
            vProxy,
            self.vid,
            residualReal - bgtResidual
        );
```

[ReserveLib::deposit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Reserve.sol#L37-L40):
```solidity
        Reserve storage reserve = Store.reserve();
        uint8 idx = vid.idx();
        uint128 balance = vProxy.balance(RESERVEID, true);
@>      vProxy.deposit(RESERVEID, amount);
```

[VaultE4626Impl::deposit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L125-L126):
```solidity
        self.shares[cid] += newShares;
        self.totalShares += newShares;
```
The next time when trims balance and calculates the earnings, protocol retrieves real balance of the current closure but not accounts in the past earnings owned to the closure.

[VertexImpl::trimeBalance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Vertex.sol#L67-L75):
```solidity
        VaultProxy memory vProxy = VaultLib.getProxy(self.vid);
@>      uint256 realBalance = vProxy.balance(cid, false);
        // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
        // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
            return (0, 0);
        }
        uint256 residualReal = realBalance - targetReal;
```

Because the past earnings owned to the closure were not reflected in the closure's vertex, the real balance can be less than expected, leading to the earnings are computed incorrectly, results in user collects less rewards.

```solidity
    function balance(
        VaultE4626 storage self,
        VaultTemp memory temp,
        ClosureId cid,
        bool roundUp
    ) internal view returns (uint128 amount) {
        if (self.totalShares == 0) return 0;
        uint256 newlyAdding = FullMath.mulX128(
            temp.vars[1],
            temp.vars[3],
            roundUp
        );
        uint256 totalAssets = temp.vars[0] + newlyAdding - temp.vars[2];

        uint256 fullAmount = roundUp
            ? FullMath.mulDivRoundingUp(
@>              self.shares[cid],
                totalAssets,
                self.totalShares
            )
            : FullMath.mulDiv(self.shares[cid], totalAssets, self.totalShares);

        // For the pegged assets we're interested in,
        // it would be insane to have more than 2^128 of any token so this is unlikely.
        // And if it is hit, users will withdraw until it goes below because their LP is forcibly trading
        // below NAV.
@>      amount = min128(fullAmount);
    }
```

Assuming there is a closure contains 2 tokens (2 vertexes):
1. Alice adds 20 value to the closure, **closure share** is `10`, vault proxy **total share** is `10`, **total asset** is `10` and **target balance** is `10` (for simplicity, we limit the discussion to a single vertex);
2. The underlying vault accrues 5 fees, so **total asset** becomes `15`;
3. Bob adds 20 value to the closure, trimming balance is triggered, the closure balance is calculated to `15` (closure share * total assets / total share), residual balance is `5` (closure balance - target balance);
4. The earnings are withdrawn from the closure, **closure share** becomes `6.67` (closure share - residual balance * total share / total asset), **total share** becomes `6.67`;
5.  Earnings are then deposited into the reserve, **reserve share** becomes `3.33` (residual balance * total share / total asset), **total share** returns back to `10`;
6. By now balance is trimmed, then Bob's tokens are deposited, the newly minted closure share is `6.67` (deposit amount * total share / asset amount), therefore **closure share** becomes `13.34`, and **total share** is updated to `16.67`, **total asset** is `25` and **target balance** is `20`;
7. The underlying vault accrues 5 fees again, **total asset** becomes `30`;
8. Bob calls to collect the earnings, he is expected to receive half of the fees (i.e. `2.5`), however, when trimming balance is triggered, the closure balance is calculated to `24` (closure share * total assets / total share), so residual balance is only `4` (closure balance - target balance);
9. As a result, Bob would only claim `2` earnings instead of `2.5`.

As can be seen, the root cause of this issue is that when calculate closure balance, it does not count in the past earnings that were owned to the closure, leading to closure balance becomes less, hence the earnings to the users.

### Internal Pre-conditions

Earnings are accounted more than once.

### External Pre-conditions

None

### Attack Path

None

### Impact

User receives less earnings than expected.

### PoC

Please run `forge test --mt testAudit_EarningsWronglyAccounted`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "./mocks/MockERC20.sol";
import "openzeppelin-contracts/mocks/docs/ERC4626Fees.sol";
import {VaultType} from "../src/multi/vertex/VaultPointer.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";
import "../src/integrations/adjustor/DecimalAdjustor.sol";

contract AuditTest is Test {
    uint8 constant MAX_TOKENS = 16;

    address owner = makeAddr("OWNER");
    address diamond;

    MockERC20 tokenA = new MockERC20("Token A", "A", 18);
    MockERC20 tokenB = new MockERC20("Token B", "B", 6);

    MockERC4626WithFees vaultA = new MockERC4626WithFees(tokenA, "Vault A", "VA");
    MockERC4626WithFees vaultB = new MockERC4626WithFees(tokenB, "Vault B", "VB");

    function setUp() public {
        vm.startPrank(owner);

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            // Configure adjustor
            {
                DecimalAdjustor decimalAdjustor = new DecimalAdjustor();
                mixedAdjustor.setAdjustor(address(tokenB), address(decimalAdjustor));
            }
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        diamond = address(new SimplexDiamond(facets, "ValueToken", "BVT"));

        vm.label(diamond, "Diamond");
        vm.label(address(tokenA), "Token A");
        vm.label(address(tokenB), "Token B");
        vm.label(address(vaultA), "Vault A");
        vm.label(address(vaultB), "Vault B");

        // Add Vertex
        {
            // TokenA
            SimplexFacet(diamond).addVertex(address(tokenA), address(vaultA), VaultType.E4626);

            // TokenB
            SimplexFacet(diamond).addVertex(address(tokenB), address(vaultB), VaultType.E4626);
        }

        // Add Closure
        {
            bool success;

            uint16 cid = 3; // 0b11
            uint128 startingTarget = 1e12;
            uint128 baseFeeX128 = 0;
            uint128 protocolTakeX128 = 0;
            // uint128 baseFeeX128 = 34028236692093846346337460743176821145; // 10%
            // uint128 protocolTakeX128 = 34028236692093846346337460743176821145;  //10%

            uint256 amountA = startingTarget / 10 ** (18 - tokenA.decimals());
            uint256 amountB = startingTarget / 10 ** (18 - tokenB.decimals());

            tokenA.mint(owner, amountA);
            tokenA.approve(diamond, amountA);
            tokenB.mint(owner, amountB);
            tokenB.approve(diamond, amountB);
            SimplexFacet(diamond).addClosure(cid, startingTarget, baseFeeX128, protocolTakeX128);
        }

        vm.stopPrank();
    }

    function testAudit_EarningsWronglyAccounted() public {
        uint16 cid = 3;

        // Alice adds value
        address alice = makeAddr("Alice");
        tokenA.mint(alice, 10e18 + 1);
        tokenB.mint(alice, 10e6 + 1);

        vm.startPrank(alice);
        tokenA.approve(diamond, 10e18 + 1);
        tokenB.approve(diamond, 10e6 + 1);
        ValueFacet(diamond).addValue(alice, cid, 20e18, 0);
        vm.stopPrank();

        // Vault accrues fees
        {
            tokenA.mint(address(vaultA), 5e18);
        }

        // Bob adds value
        address bob = makeAddr("Bob");
        tokenA.mint(bob, 10e18 + 1);
        tokenB.mint(bob, 10e6 + 1);

        vm.startPrank(bob);
        tokenA.approve(diamond, 10e18 + 1);
        tokenB.approve(diamond, 10e6 + 1);
        ValueFacet(diamond).addValue(bob, cid, 20e18, 0);
        vm.stopPrank();

        // Vault accrues fees again, since both Alice and Bob have the same values, they are expect to split the earnings, i.e. 2.5 for each
        {
            tokenA.mint(address(vaultA), 5e18);
        }

        // Bob collects earnings, he only receives 2 instead of 2.5
        vm.prank(bob);
        (uint256[MAX_TOKENS] memory collectedBalances, ) = ValueFacet(diamond).collectEarnings(bob, cid);
        assertApproxEqAbs(collectedBalances[0], 2e18, 1e11);
    }
}

contract MockERC4626WithFees is ERC4626Fees {

    uint256 entryFeeBasisPoints;
    uint256 exitFeeBasisPoints;
    address feeRecipient;

    constructor(
        IERC20 asset_, 
        string memory name_, 
        string memory symbol_
    ) ERC4626(asset_) ERC20(name_, symbol_) {
        feeRecipient = msg.sender;
    }

    function setEntryFee(uint256 entryFeeBasisPoints_) public {
        entryFeeBasisPoints = entryFeeBasisPoints_;
    }

    function setExitFee(uint256 exitFeeBasisPoints_) public {
        exitFeeBasisPoints = exitFeeBasisPoints_;
    }

    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }

    function _exitFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }
}
```

### Mitigation

When trim balance and calculate the closure balance, the past earnings owned to the closure should be accounted.