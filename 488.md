Cool Canvas Butterfly

Medium

# Token withdrawal silently completes even if tokens are insufficient when backup vault is missing

### Summary

When calling `VaultProxy.withdraw`, if there is no backup vault and the withdrawable amount is insufficient, it withdraws smaller amount and completed without reverting.


### Root Cause


In `VaultProxy.withdraw` function, when the withdrawable amount of the active vault is less than the balance, the `available` amount is adjusted to the withdrawable amount. This could happen due to fees or lockup mechanisms. The deficit is supposed to be withdrawn from the backup vault.

[multi/vertex/VaultProxy.sol#L194-L199](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/vertex/VaultProxy.sol#L194-L199) 

```solidity
function withdraw(
    VaultProxy memory self,
    ClosureId cid,
    uint256 amount
) internal {
    // We effectively don't allow withdraws beyond uint128 due to the capping in balance.
    uint128 available = self.active.balance(cid, false);
@>  uint256 maxWithdrawable = self.active.withdrawable();
@>  if (maxWithdrawable < available) available = uint128(maxWithdrawable);

    if (amount > available) {
        self.active.withdraw(cid, available);
@>      self.backup.withdraw(cid, amount - available);
    } else {
        self.active.withdraw(cid, amount);
    }
}
```

However, looking at `VaultPointer.withdraw`, the transaction doesn't revert even if this Vertex has no backup vault. 

[multi/vertex/VaultPointer.sol#L54](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/vertex/VaultPointer.sol#L54)

```solidity
function withdraw(
    VaultPointer memory self,
    ClosureId cid,
    uint256 amount
) internal {
@>  if (isNull(self) || amount == 0) return;

    if (self.vType == VaultType.E4626) {
        getE4626(self).withdraw(self.temp, cid, amount);
    } else {
        revert VaultTypeUnrecognized(self.vType);
    }
}
```

Therefore, when calling `VaultProxy.withdraw`, if there is no backup vault and the withdrawable amount is insufficient, it withdraws smaller amount and completed without reverting.

### Internal Pre-conditions

1. No backup vault is set for the Vertex


### External Pre-conditions

1. The withdrawable amount from the integrated third-party money market is less than the deposited token amount due to fees or lockup (`maxWithdrawable < available`)


### Attack Path

1. Issues can occur in situations that trigger `Vertex.withdraw` or `VaultProxy.withdraw`. For example, when calling functions like `removeValue`


### Impact

User receives less than the requested withdrawal amount.


### PoC

_No response_

### Mitigation

```diff
function withdraw(
    VaultProxy memory self,
    ClosureId cid,
    uint256 amount
) internal {
    // We effectively don't allow withdraws beyond uint128 due to the capping in balance.
    uint128 available = self.active.balance(cid, false);
    uint256 maxWithdrawable = self.active.withdrawable();
    if (maxWithdrawable < available) available = uint128(maxWithdrawable);

    if (amount > available) {
+       require(self.backup.slotAddress != bytes32(0));
        self.active.withdraw(cid, available);
        self.backup.withdraw(cid, amount - available);
    } else {
        self.active.withdraw(cid, amount);
    }
}
```