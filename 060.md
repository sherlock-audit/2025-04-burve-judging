Urban Latte Platypus

Medium

# Owner is able to take exchanged BGT from the exchange

### Summary

[sendBalance](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L110) function allows owner to transfer any tokens of `BGTExchanger` contract to anywhere else. It even allows BGT token as well.

To get BGT tokens from `BGTExchanger` contract, whitelisted `exchangers` need to call `exchange` function to increase `owed` balance, and call `withdraw` function to withdraw BGT token from the contract.

If owner transfers BGT tokens of the contract by using `sendBalance` function after whitelisted exchangers exchanged tokens for BGT, those exchangers could not withdraw BGT token due to lack of BGT balance.

### Root Cause

[sendBalance](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L110) function of `BGTExchanger` contract does not check if the token is BGT token or not.

If the token is the BGT token, there should be additional logic to prevent the above issue.

### Internal Pre-conditions

1. Fund BGT token by using `fund` function
2. Owner whitelists exchanger.
3. Whitelisted exchanger exchange BGT token.

### External Pre-conditions

- Nothing

### Attack Path

1. Owner calls `sendBalance` function with the following arguments.
- token: BGT token
- to: any address
- amount: The amount bigger than `BGT token balance of the contract - currently withdrawal BGT amount by exchangers`

### Impact

Some exchangers could not withdraw BGT token due to lack of token balance of BGTExchanger contract.

### PoC

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {Test} from "forge-std/Test.sol";
import {BGTExchanger} from "../../src/integrations/BGTExchange/BGTExchanger.sol";
import {MockERC20} from "../mocks/MockERC20.sol";

contract BGTExchangerTest is Test {
    BGTExchanger bgtExchanger;
    address owner;
    address alice;
    MockERC20 ibgt;
    MockERC20 usdc;

    function setUp() public {
        owner = makeAddr("owner");
        alice = makeAddr("alice");

        ibgt = new MockERC20("ibgt", "ibgt", 18);
        usdc = new MockERC20("usdc", "usdc", 6);

        vm.startPrank(owner);
        bgtExchanger = new BGTExchanger(address(ibgt));
        vm.stopPrank();
    }

    function testOwnerIsAbleToTakeExchangedBGT() public {
        // add alice as exchanger
        vm.startPrank(owner);
        bgtExchanger.addExchanger(alice);

        // set USDC exchange rate
        bgtExchanger.setRate(address(usdc), (2 << 128)); // 1:2
        vm.stopPrank();

        // fund 100e18 iBGT
        deal(address(ibgt), address(this), 100e18);
        ibgt.approve(address(bgtExchanger), 100e18);
        bgtExchanger.fund(100e18);

        uint256 bgtBalance = bgtExchanger.bgtBalance();

        // exchange USDC for iBGT
        vm.startPrank(alice);

        uint128 amountIn = 20e18;
        deal(address(usdc), alice, amountIn);
        usdc.approve(address(bgtExchanger), amountIn);

        (uint256 exchangedBgtAmount, ) = bgtExchanger.exchange(
            address(usdc),
            amountIn
        );

        vm.stopPrank();

        // check returned amounts
        assertEq(exchangedBgtAmount, amountIn * 2, "bgtAmount");

        // Owner is able to withdraw whole BGT balance inside the contract including the exchanged assets.
        vm.startPrank(owner);
        bgtExchanger.sendBalance(address(ibgt), owner, bgtBalance);
        vm.stopPrank();

        // The alice could not withdraw exchanged BGT
        vm.startPrank(alice);
        bgtExchanger.withdraw(alice, exchangedBgtAmount);
        vm.stopPrank();
    }
}
```

### Mitigation

Update `bgtBalance` value inside `sendBalance` function if the token is the BGT token.