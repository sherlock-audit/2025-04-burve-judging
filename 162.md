Interesting Taupe Ostrich

Medium

# Eth sent to protocol not withdrawable like erc20s

### Summary

Protocol utilizes has a `withdraw()` function that allows the contract owner to sweep a given token from the protocol. This happens if these tokesn are donated or accidentally sent to the contract. However, there is no such functionality for a native token like `ETH`.


### Root Cause

In `SimplexFacet`, the [`withdraw()`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/SimplexFacet.sol#L241-L262) function is defined as follows:
```solidity
    /// @notice Withdraws the given token from the protocol.
    // Normally tokens supporting the AMM ALWAYS resides in the vaults.
    // The only exception is
    // 1. When fees are earned by the protocol.
    // 2. When someone accidentally sends tokens to this address.
    // 3. When someone donates.
    /// @dev Only callable by the contract owner.
    function withdraw(address token) external {
        AdminLib.validateOwner();

        uint256 balance = IERC20(token).balanceOf(address(this));

        if (TokenRegLib.isRegistered(token)) {
            uint8 idx = TokenRegLib.getIdx(token);
            uint256 earned = SimplexLib.protocolGive(idx);
            emit FeesWithdrawn(token, balance, earned);
        }

        if (balance > 0) {
            TransferHelper.safeTransfer(token, msg.sender, balance);
        }
    }
```
As seen, the `withdraw()` function is defined for ERC20 tokens only. 

Now, the Diamond contract defines a [`receive()`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/Diamond.sol#L238) function that is called when native tokens are sent to the contract. However, it does not have any logic to handle the received tokens. This means that if native tokens are accidentally sent to the contract, they will be stuck in the contract.
```solidity
    receive() external payable {}
```

### Internal Pre-conditions

1. The contract accepts native tokens sent to it.


### External Pre-conditions

1. Native tokens are sent to the contract.


### Attack Path

1. Native tokens are sent to the contract.
2. The contract does not have any logic to withdraw the received tokens.


### Impact

Native tokens remain stuck in the contract.

### PoC

// (1) Create a new test file: `test/facets/Diamond.t.sol`
// (2) Paste the following in it:
```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";

contract DiamondTest is MultiSetupTest {
    uint256 public constant FUNDING_ETH = 1 ether;

    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(3);
        _initializeClosure(0x7);
        vm.stopPrank();
    }

    function testStuckEth() public {
        vm.deal(alice, FUNDING_ETH);
        assertEq(alice.balance, FUNDING_ETH);

        vm.startPrank(alice);
        // alice donates eth to diamond
        (bool success, ) = diamond.call{value: FUNDING_ETH}(new bytes(0));
        assertEq(success, true);
        vm.stopPrank();

        // alice has no eth now
        assertEq(alice.balance, 0);

        // Here, funds have been sent to Diamond contract. However, they can never be retrieved
        assertEq(diamond.balance, FUNDING_ETH);
    }
}
```
// (3) Run: `forge test --mt testStuckEth -vvvv`

### Mitigation

Define a `withdrawEth()` function in `Diamond` that allows the contract owner to withdraw native tokens from the contract.
```solidity
    import {TransferHelper} from "../TransferHelper.sol";

    function withdrawEth() external {
        AdminLib.validateOwner();
        uint256 balance = address(this).balance;
        require(balance > 0, "No ETH to withdraw");
        TransferHelper.safeTransferETH(msg.sender, balance);
    }
```