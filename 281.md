High Hemp Mockingbird

High

# Inconsistency of the received amount due to tax calculations in removeValueSingle and removeSingleForValue

### Summary

Resulting token amount when removing value using the same conditions in the [removeValueSingle()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L214) and [removeSingleForValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L248) functions is different due to a mismatch in the tax calculations.

### Root Cause

[Docs](https://docs.burve.fi/technicals/multi-pool-details/value-accounting#accounting:~:text=Any%20implicit%20swap%20from%20adding/removing%20a%20single%20token%20from%20the%20pool%20needs%20to%20be%20taxed) clearly states: _"Any **implicit** swap from adding/removing a single token from the pool needs to be taxed"_. So when you add/remove liquidity with only one token, you upset the current proportion in the pool. The system must internally “rebalance” this added/removed value among all the assets in the pool according to their target shares (which is implicit swap). And it is the "rebalanced" amount that should be taxed.
But there is a mistake in the contract where in `removeTokenForValue()` function full amount of user's tokens are taxed (instead of just implicit swap):
```solidity
function removeTokenForValue(
    Closure storage self,
    VertexId vid,
    uint256 amount,
    uint256 bgtPercentX256,
    SearchParams memory searchParams
) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
    ...
    uint256 taxedRemove = UnsafeMath.divRoundingUp(
        amount << 128, //@audit @note full amount
        ONEX128 - self.baseFeeX128
    );
    tax = taxedRemove - amount;
    ...
}
```
While in the `removeValueSingle()` tax is applied on implicit swap only:
```solidity
function removeValueSingle(
    ...
) internal returns (uint256 removedAmount, uint256 tax) {
    ...
    // We first calculate what value is effectively "added" by not removing the tokens.
    // And then we make sure to remove that amount of value with the out token.
    uint256 fairVBalance = iterSingleValueDiff(self, valIter, false);
    removedAmount = self.balances[valIter.vIdx] - fairVBalance;
    // Now we have the addedValue which we can remove, and the fair balance for our vertex.
    uint256 veX128 = SimplexLib.getEX128(valIter.vIdx);
    uint256 currentValueX128 = ValueLib.v(
        self.targetX128,
        veX128,
        fairVBalance,
        false
    );
    uint256 finalAmount = ValueLib.x(
        self.targetX128,
        veX128,
        currentValueX128 - valIter.valueSumX128,
        true
    );
    //@audit @note this is amount of implicit swap
    uint256 untaxedRemove = fairVBalance - finalAmount;
    self.setBalance(valIter.vIdx, finalAmount);
    tax = FullMath.mulX128(untaxedRemove, self.baseFeeX128, true);
    removedAmount += untaxedRemove - tax;
    ...
}
```

### Internal Pre-conditions

1. `baseFeeX128` should not be zero

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Users receive different amounts of tokens when using `removeValueSingle()` and `removeTokenForValue()` with the same conditions because the tax application does not match the documentation.

### PoC

To run PoC, the first thing to do is to fix the bug in [removeValueSingle():236](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L236) where unitilized variable are used for tax calculations:
```diff
uint256 realTax = FullMath.mulDiv(
-   removedBalance,
+   realRemoved, 
    nominalTax,
    removedNominal
);
```
Then add this tests to `Burve/test/facets/ValueFacet.t.sol`:
<details>
<summary>PoC</summary>

```solidity
//exact value that received in testAddRemoveSingleForValue
uint256 valueGivenFromFirstRemove = uint256(100881230975204614);

function testAddRemoveSingleForValue() public {
    uint256 oneX128 = 1 << 128;
    vm.prank(owner);
    simplexFacet.setClosureFees(0xF, uint128(oneX128 / 100), 0); // 1%
    vm.stopPrank();
    vm.startPrank(alice);
    // Simply add and remove.
    valueFacet.addSingleForValue(alice, 0xF, tokens[0], 1e18, 0, 0);
    console.log("w/ fees A.balance before (removeSingleForValue):", ERC20(tokens[0]).balanceOf(alice));
    //removed value = 100881230975204614
    uint256 valueGiven = valueFacet.removeSingleForValue(alice, 0xF, tokens[0], 1e17, 0, 0);
    assert(valueGivenFromFirstRemove == valueGiven);  
    (,,uint256[MAX_TOKENS] memory _balances,,) = simplexFacet.getClosureValue(0xF);
    console.log("w/ fees vault balance after (removeSingleForValue):", _balances[0]); //100898899899899899899
    console.log("w/ fees A.balance after (removeSingleForValue):", ERC20(tokens[0]).balanceOf(alice));//999999999999100000000000000000
    vm.stopPrank();
}

function testAddRemoveSingleForValue2() public {
    uint256 oneX128 = 1 << 128;
    vm.prank(owner);
    simplexFacet.setClosureFees(0xF, uint128(oneX128 / 100), 0); // 1%
    vm.stopPrank();
    vm.startPrank(alice);
    // Simply add and remove.
    valueFacet.addSingleForValue(alice, 0xF, tokens[0], 1e18, 0, 0);
    console.log("w/ fees A.balance before (removeValueSingle):", ERC20(tokens[0]).balanceOf(alice));
    //removed value = 100881230975204614
    //@audit @note value used same that received in testAddRemoveSingleForValue
    valueFacet.removeValueSingle(alice, 0xF, uint128(valueGivenFromFirstRemove), 0, tokens[0], 0);  
    (,,uint256[MAX_TOKENS] memory _balances,,) = simplexFacet.getClosureValue(0xF);
    console.log("w/ fees vault balance after (removeValueSingle):", _balances[0]);//100888989898989898993
    //@audit @note if we will use realRemoved instead of removedBalance (because it's uninialized which is wrong)
    console.log("w/ fees A.balance after (removeValueSingle):", ERC20(tokens[0]).balanceOf(alice));//999999999999099498041958303267 less on 0.5%
    vm.stopPrank();
}

//exact value that received in testAddRemoveWithoutFeesSingleForValue
uint256 valueGivenFromFirstRemoveWithoutFees = uint256(99870998065686192);

function testAddRemoveWithoutFeesSingleForValue() public {
    uint256 oneX128 = 1 << 128;
    vm.prank(owner);
    // simplexFacet.setClosureFees(0xF, uint128(oneX128 / 100), 0); // 1%
    vm.stopPrank();
    vm.startPrank(alice);
    // Simply add and remove.
    valueFacet.addSingleForValue(alice, 0xF, tokens[0], 1e18, 0, 0);
    console.log("w/o fees A.balance before (removeSingleForValue):", ERC20(tokens[0]).balanceOf(alice));
    //removed value = 99870998065686192
    uint256 valueGiven = valueFacet.removeSingleForValue(alice, 0xF, tokens[0], 1e17, 0, 0);
    assert(valueGivenFromFirstRemoveWithoutFees == valueGiven);  
    (,,uint256[MAX_TOKENS] memory _balances,,) = simplexFacet.getClosureValue(0xF);
    console.log("w/o fees vault balance after (removeSingleForValue):", _balances[0]); //100900000000000000000
    console.log("w/o fees A.balance after (removeSingleForValue):", ERC20(tokens[0]).balanceOf(alice));//999999999999100000000000000000
    vm.stopPrank();
}

function testAddRemoveWithoutFeesSingleForValue2() public {
    uint256 oneX128 = 1 << 128;
    vm.prank(owner);
    // simplexFacet.setClosureFees(0xF, uint128(oneX128 / 100), 0); // 1%
    vm.stopPrank();
    vm.startPrank(alice);
    // Simply add and remove.
    valueFacet.addSingleForValue(alice, 0xF, tokens[0], 1e18, 0, 0);
    console.log("w/o fees A.balance after (removeValueSingle):", ERC20(tokens[0]).balanceOf(alice));
    //removed value = 99870998065686192
    valueFacet.removeValueSingle(alice, 0xF, uint128(valueGivenFromFirstRemoveWithoutFees), 0, tokens[0], 0);  
    (,,uint256[MAX_TOKENS] memory _balances,,) = simplexFacet.getClosureValue(0xF);
    console.log("w/o fees vault balance after (removeValueSingle):", _balances[0]);//100900000000000000001
    console.log("w/o fees A.balance after (removeValueSingle):", ERC20(tokens[0]).balanceOf(alice));//999999999999099999999999999999
    vm.stopPrank();
}
```

</details>

Run tests with command:
```shell
forge test -vv --mt testAddRemoveSingleForValue & forge test -vv --mt testAddRemoveWithoutFeesSingleForValue
```

Output:
```shell
[PASS] testAddRemoveSingleForValue() (gas: 1236168)
Logs:
  w/ fees A.balance before (removeSingleForValue): 999999999999000000000000000000
  w/ fees vault balance after (removeSingleForValue): 100888989898989898989
  w/ fees A.balance after (removeSingleForValue): 999999999999100000000000000000

[PASS] testAddRemoveSingleForValue2() (gas: 1194606)
Logs:
  w/ fees A.balance before (removeValueSingle): 999999999999000000000000000000
  w/ fees vault balance after (removeValueSingle): 100888989898989898993
  w/ fees A.balance after (removeValueSingle): 999999999999099498041958303267

[PASS] testAddRemoveWithoutFeesSingleForValue() (gas: 1089394)
Logs:
  w/o fees A.balance before (removeSingleForValue): 999999999999000000000000000000
  w/o fees vault balance after (removeSingleForValue): 100900000000000000000
  w/o fees A.balance after (removeSingleForValue): 999999999999100000000000000000

[PASS] testAddRemoveWithoutFeesSingleForValue2() (gas: 1047713)
Logs:
  w/o fees A.balance after (removeValueSingle): 999999999999000000000000000000
  w/o fees vault balance after (removeValueSingle): 100900000000000000001
  w/o fees A.balance after (removeValueSingle): 999999999999099999999999999999
```

As you can clearly see by the output _w/ fees A.balance after (removeValueSingle)_ is less than _w/ fees A.balance after (removeSingleForValue)_ while conditions are the same. (Note that in `removeValueSingle()` amount is smaller because of the double tax deduction bug in `ValueFacet` and `Closure`. Otherwise it would be bigger.)

### Mitigation

Two ways to fix this issue:
1. Hard way: apply tax only on implicit swap in `removeSingleForValue()`
2. Easy way: apply tax on whole amount in `removeValueSingle()` too:
```solidity
//Closure.sol
function removeValueSingle(...) internal returns (uint256 removedAmount, uint256 tax) {
    ...
    /*@audit @note 
    do this before balance change
    amount will be same like in removeSingleForValue()
    */
    removedAmount = self.balances[valIter.vIdx] - finalAmount;
    tax = FullMath.mulX128(removedAmount, self.baseFeeX128, true);
    ...
}
```
This will fix the inconsistency, you can even check it again with tests.