Interesting Taupe Ostrich

High

# Incorrect netting logic in `commit()` results in incorrect withdrawals

### Summary

The `VaultE4626.commit()` function attempts to optimize vault interactions by netting `deposits` and `withdrawals` before executing them. However, in the branch where `assetsToWithdraw > assetsToDeposit`, the subtraction logic is incorrectly ordered. This results in the full withdrawal amount being preserved rather than netted, leading to incorrect `totalVaultShares` updates and potential vault  imbalances.


### Root Cause

In [`commit()`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L71-L74) function, the line `assetsToWithdraw -= assetsToDeposit` is placed after `assetsToDeposit` is set to `0`, causing the subtraction to always subtract zero. 
```solidity
            else if (assetsToWithdraw > assetsToDeposit) {
>>              assetsToDeposit = 0;
                // @audit-issue This subtracts 0 every time
>>              assetsToWithdraw -= assetsToDeposit;
            }
            ---snip---
            else if (assetsToWithdraw > 0) {
            // @audit-issue incorrect share update
>>          self.totalVaultShares -= self.vault.withdraw(
                assetsToWithdraw,
                address(this),
                address(this)
            );
            }
```
This bypasses the netting logic, allowing full withdrawals to proceed even when they should have been reduced by the pending deposits.


### Internal Pre-conditions

1. `assetsToWithdraw > assetsToDeposit`
2. `assetsToDeposit > 0`
3. Vault contains sufficient assets for `withdraw()`


### External Pre-conditions

1. User attempts to deposit or withdraw that triggers `commit()` call.


### Attack Path

1. Set `assetsToWithdraw = 15e9`, `assetsToDeposit = 1e9`
2. Call `commit()`
3. Code enters the `else if (assetsToWithdraw > assetsToDeposit)` branch
4.` assetsToDeposit = 0` is executed
5. `assetsToWithdraw -= assetsToDeposit` becomes `assetsToWithdraw -= 0`
6. Full `15e9` is withdrawn instead of the net `14e9`


### Impact

This behavior allows full withdrawals to proceed even when there is an offsetting deposit in the same cycle.


### Poc
// (1) Add the following test in `test/multi/E4626.t.sol`:
```solidity
    function testIncorrectNettingInCommit() public {
        ClosureId cid = ClosureId.wrap(1);

        // First deposit
        {
            VaultTemp memory temp;
            vault.fetch(temp);
            vault.deposit(temp, cid, 2e10);
            vault.commit(temp);
            assertEq(vault.balance(temp, cid, false), 2e10);
        }
        assertEq(token.balanceOf(address(this)), (1 << 128) - 2e10);

        // Small deposit + slightly larger withdraw (but not more than 2e10 total)
        {
            VaultTemp memory temp;
            vault.fetch(temp);
            vault.deposit(temp, cid, 1e9); // deposit 1e9
            vault.withdraw(temp, cid, 15e9); // withdraw 15e9 (assetsToWithdraw > assetsToDeposit)
            vault.commit(temp);
        }

        {
            VaultTemp memory temp;
            vault.fetch(temp);

            uint256 finalBalance = vault.balance(temp, cid, false);

            // Expected balance should be 2e10 - (15e9 - 1e9) = 6e9
            // As the netting is wrong, this number does not match

            // i.e The netting does this: (assetsToWithdraw -= assetsToDeposit) = (assetsToWithdraw -= 0)
            // so it withdraws the whole 15e9 instead of 14e9 => This leaves 5e9 instead of 6e9
            assertEq(finalBalance, 5e9);

            // Therefore, we expect contract balance to increase by 15e9
            assertEq(token.balanceOf(address(this)), (1 << 128) - 5e9);
        }
    }
```
// (2) Run: `forge test --mt testIncorrectNettingInCommit -vvvv`


### Mitigation

Modify as follows:
```diff
            else if (assetsToWithdraw > assetsToDeposit) {
-               assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit;
+               assetsToDeposit = 0;
            }
```