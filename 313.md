Orbiting Zinc Parrot

High

# Incorrect calculation of Island shares to burn or transfer during the burn/transfer calls causes the losses to the user

### Summary

In the `Burve::burnIsland()` function, we are calculating the island shares to burn like this:

```solidity
    function burnIsland(
        uint256 shares
    ) internal returns (uint256 islandBurnShares) {
        // calculate island shares to burn
@>        islandBurnShares = FullMath.mulDiv(
            islandSharesPerOwner[msg.sender],
            shares,
            balanceOf(msg.sender)
        );

        ...

    }
```

Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L416)

Basically, we are burning the island shares proportionally to the LP tokens burnt with respect to the current balance of the user. This is correct as long as no fee is accrued in island or in the Uniswap ranges. But as the fee starts to accrue, things will change. The LP share will become more valuable as well as the island shares. For example, In the beginning we were getting LP tokens in 1:1 ratio as the liquidity deposited:

```solidity
function mint(
        address recipient,
        uint128 mintNominalLiq,
        uint160 lowerSqrtPriceLimitX96,
        uint160 upperSqrtPriceLimitX96
    )
        public
        withinSqrtPX96Limits(lowerSqrtPriceLimitX96, upperSqrtPriceLimitX96)
        returns (uint256 shares)
    {

        ...

        if (totalShares == 0) {
            // If this is the first mint, it has to be dead shares, burned by giving it to this contract.
@>            shares = mintNominalLiq;
            if (shares < MIN_DEAD_SHARES)
                revert InsecureFirstMintAmount(shares);
            if (recipient != address(this))
                revert InsecureFirstMintRecipient(recipient);
        } else {
@>            shares = FullMath.mulDiv(
                mintNominalLiq,
                totalShares,
                totalNominalLiq
            );
        }

    }
```

Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L267C8-L280C10)

But as the fees is accrued, the `totalNomialLiq` will increase through compounding and this fee will be deposited back into the uniswap pool again:

```solidity
    function compoundV3Ranges() internal {
        // collect fees
        collectV3Fees();

        (uint160 sqrtRatioX96, , , , , , ) = pool.slot0();

@>        uint128 compoundedNominalLiq = collectAndCalcCompound();
        if (compoundedNominalLiq == 0) {
            return;
        }

@>        totalNominalLiq += compoundedNominalLiq;
```

Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L745C1-L750C49)

Because of this compounding, the new shares issued will be less than the liquidity provided. We can see this through an easy example:

1. Total Shares before is equal to `100` and so does the liqudity.
2. Alice provide 100 liquidity, get's : `100 * 100 / 100 = 100 shares`
3. 10 unit fees accrued in the pool, total liquidity increased by 10. 
4. Alice again provides 100 liquidity, get's: `100 * 100  / 110 = ~90.9 shares`

This is how the shares mechanism works which will give the correct amount of rewards to the LP token holders. But this is only true for the Uniswap ranges. In kodiak island ranges, the fee accrued will not be claimed by the `Burve` contract itself and the total liquidity will not increase in the contract as well. But for these ranges, we will get island shares from the kodiak island which will represent our liquidity. They also shares the same mechanism as we have shared above. As the fee is accrued in these ranges, it will be represented in the balances of the island or we can redeposit the collected fees by calling [`KodiakIsland::rebalance(...)`](https://berascan.com/address/0x4a254B11810B8EBb63C5468E438FC561Cb1bB1da#code#F3#L176). Both gives the correct balances.

But in `Burve::burnIsland(...)` function, we are calculating the amount of shares to burn based on the LP shares we are burning proportional to the LP shares we hold. We are not taking into account these  accrued fees or conversion ratio. So if we take our above given example and let's consider we minted the island shares in the previous step, if the island is giving shares in the 1:1 ratio, then for minting `90` LP tokens, we are getting `100` island shares. So now if we try to burn `100` LP shares, the amount of island shares that will be burnt will be equal to `200 * 100 / 190 = 105.2 island shares`  which is clearly incorrect. We are not taking into account any conversion factor or anything but just burning out the proportional amount. 
The same thing will also happen when the island's have some accrued fees. So now it will also not mint in 1:1 ratio. We only took the example of LP shares for the sake of simplicity. But remember that the same thing will also happen for the island's shares as well. 

Now the above mentioned thing is not a big issue when we are burning the island shares. Because at the end of the day, the removed tokens will goes back to the user only. Only thing they are loosing is some yield that they could have earned for those removed extra tokens. And they have to go though the hassle of depositing them again.  But the real issue lies when a user tries to transfer his LP shares to someone else. Because we are also calculating the amount of island shares to transfer in the same way:

```solidity
 function _update(
        address from,
        address to,
        uint256 value
    ) internal virtual override {
        // We handle mints and burns in their respective calls.
        // We just want to handle transfers between two valid addresses.
        if (
            from != address(0) &&
            to != address(0) &&
            address(island) != address(0)
        ) {
            // Move the island shares that correspond to the LP tokens being moved.
@>            uint256 islandTransfer = FullMath.mulDiv(
                islandSharesPerOwner[from],
                value,
                balanceOf(from)
            );
```

Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L713)

Now this is a very big issue. Because now if a user tries to transfer his tokens to someone else (maybe because they have sold them or in any other case), the amount of island shares that will go with it will be more than what they received for the actual deposits. And the receiver is now getting more liquidity and in return more tokens which causes the sender losses. 






### Root Cause

The calculation of island shares to burn or transfer is incorrect. 

### Internal Pre-conditions

N/A

### External Pre-conditions

The fee must have accrued in the pool or in the kodiak islands.

### Attack Path

Given above

### Impact

The user will lose some percentage of their deposited tokens as well as some amount of fee yields.

### PoC

Add the below given test in `Burve.t.sol` file:

```solidity
    function test_islandSharesToTransferOrBurnIsIncorrect() public forkOnly {
        uint128 mintLiq = 100 ether;

        // Mint
        deal(address(token0), address(sender), type(uint256).max);
        deal(address(token1), address(sender), type(uint256).max);
        vm.startPrank(sender);
        token0.approve(address(burve), type(uint256).max);
        token1.approve(address(burve), type(uint256).max);
        burve.testMint(address(alice), mintLiq, 0, type(uint128).max);
        vm.stopPrank();

        // get the kodiak island
        IKodiakIsland island = burve.island();

        // calc island burn
        uint256 islandShares = burve.islandSharesPerOwner(alice);

        // rebalance the kodiak island first
        island.rebalance();


        console.log("---- Mint and transfer With fee ---");
        // store the current state
        uint snapshot = vm.snapshotState();

        // mint some tokens to burve to represent fee
        deal(address(token0), address(burve), 1000 ether);
        deal(address(token1), address(burve), 1000 ether);

        // get the burve token balance before for alice
        uint aliceBurveBalanceBefore = burve.balanceOf(alice);

        // get the island shares of alice before the new mint as well
        uint aliceIslandSharesBeforeMint = burve.islandSharesPerOwner(alice);

        // now alice mints again
        vm.startPrank(sender);
        token0.approve(address(burve), type(uint256).max);
        token1.approve(address(burve), type(uint256).max);
        burve.testMint(address(alice), mintLiq, 0, type(uint128).max);
        vm.stopPrank();

        // get the burve balance after
        uint aliceBurveBalanceAfter = burve.balanceOf(alice);

        // get the island shares of alice after
        uint aliceIslandSharesAfterMint = burve.islandSharesPerOwner(alice);

        // new tokens minted
        console.log("token minted to alice before: ", aliceBurveBalanceBefore);
        console.log("new tokens minted to alice for same amount of liqiudity: ", aliceBurveBalanceAfter - aliceBurveBalanceBefore);
        console.log("New island shares minted: ", aliceIslandSharesAfterMint - aliceIslandSharesBeforeMint);

        // get the island share balance of alice before

        uint256 islandSharesBefore = burve.islandSharesPerOwner(alice);

        // alice sells 100 LP shares
        vm.prank(alice);
        burve.transfer(address(this), 100 ether);

        // get the island share balance of alice after
        uint256 islandSharesAfter = burve.islandSharesPerOwner(alice);

        // island shares transferred to the receiver
        console.log("islands shares transferred: ", islandSharesBefore - islandSharesAfter);

        // revert to the old state
        vm.revertToState(snapshot);

        console.log("\n");
        console.log("---- Mint and transfer Without fee ---");

        // alice mints again for the same amount of liquidity but with no fee this time
        // get the burve token balance before for alice
        aliceBurveBalanceBefore = burve.balanceOf(alice);

        // get the island shares of alice before the new mint as well
        aliceIslandSharesBeforeMint = burve.islandSharesPerOwner(alice);

        // now alice mints again
        vm.startPrank(sender);
        token0.approve(address(burve), type(uint256).max);
        token1.approve(address(burve), type(uint256).max);
        burve.testMint(address(alice), mintLiq, 0, type(uint128).max);
        vm.stopPrank();

        // get the burve balance after
        aliceBurveBalanceAfter = burve.balanceOf(alice);
                
        // get the island shares of alice after
        aliceIslandSharesAfterMint = burve.islandSharesPerOwner(alice);
        
        // new tokens minted
        console.log("token minted to alice before: ", aliceBurveBalanceBefore);
        console.log("new tokens minted to alice for same amount of liqiudity: ", aliceBurveBalanceAfter - aliceBurveBalanceBefore);
        console.log("New island shares minted: ", aliceIslandSharesAfterMint - aliceIslandSharesBeforeMint);


         // get the island share balance of alice before
        islandSharesBefore = burve.islandSharesPerOwner(alice);

        // alice sells 100 shares
        vm.prank(alice);
        burve.transfer(address(this), 100 ether);

        // get the island share balance of alice after
        islandSharesAfter = burve.islandSharesPerOwner(alice);

        // island shares transferred to the receiver
        console.log("islands shares transferred: ", islandSharesBefore - islandSharesAfter);

    }
```

_Output:_

```bash
[PASS] test_islandSharesToTransferOrBurnIsIncorrect() (gas: 3515290)
Logs:
  ---- Mint and transfer With fee ---
  token minted to alice before:  100000000000000000000
  new tokens minted to alice for same amount of liqiudity:  1086246271969810608
  New island shares minted:  37280827296979871206
  islands shares transferred:  73760434622682128353


  ---- Mint and transfer Without fee ---
  token minted to alice before:  100000000000000000000
  new tokens minted to alice for same amount of liqiudity:  100000000000000000000
  New island shares minted:  37280827296979871206
  islands shares transferred:  37280827296979871206

```

We can see almost double amount of island shares are transffered for the same amount of LP shares. 

### Mitigation

Maybe try to include the Kodiak island conversion factor for calculating the amount and pool conversion factor