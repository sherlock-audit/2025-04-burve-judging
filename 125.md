Tart Ultraviolet Sawfish

High

# Erroneous `assetsToDeposit = 0` reset leads to over-withdrawing funds during `E4626.commit`

### Summary

There's a critical problem in how `E4626.commit` handles the "neutralizing weights" of the deposits and withdrawals when the `assetsToWithdraw > assetsToDeposit`:
```solidity

    // Actually make our deposit/withdrawal
    function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        uint256 assetsToDeposit = temp.vars[1];
        uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
                assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit; // !@audit: it errounesously evaluates to 0 each time this if-else case match is met
            } else {
                // Perfect net!
                return;
          
```

### Root Cause

This optimization mechanism has a significant flaw:

Since `assetsToDeposit = 0` is nullified before it is used to determine how much should be decremented from the `assetsToWithdraw`, `assetsToWithdraw -= assetsToDeposit` will always evaluate to `assetsToWithdraw -= 0`.

https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/vertex/E4626.sol#L71-L73

### Internal Pre-conditions

None.

### External Pre-conditions

A user simply performing a sequence of actions, such that the `amountToWithdraw > 0`, `amountToDeposit > 0`, and `amountToWithdraw > amountToDeposit`.

### Attack Path

Whenever in the aforementioned conditions (see them above) `E4626.commit` is called, the contract underestimates the deposit amount and overestimates the withdrawal amount.

This will lead to withdrawing too much tokens, an unnecessary surplus of `(+assetsToDeposit)` will be withdrawn mistakengly, plus the original `assetsToWithdraw` amount:
```solidity
        } else if (assetsToWithdraw > 0) {
            // We don't need to hyper-optimize the receiver.
            self.totalVaultShares -= self.vault.withdraw( // @audit: overwithdraws
                assetsToWithdraw,
                address(this),
                address(this)
            );
        }
    }
```

### Impact

Withdrawing too much tokens, the surplus can be almost `2x` times more than intended, e.g. when `amountToDeposit = N`, `amountToWithdraw = N + 1`, the real withdrawn amount will be `amountToWithdraw = N + 1`, while the `amountToDeposit` will be zeroed-out: `amountToDeposit = 0`.

This means that no tokens will be deposited, and too much will be withdrawn.

Normally in such a case `(N + 1) - N` == `1` would be withdrawn, since `N + 1 > N`.


Due to this bug, the vault behaves completely incorrectly in some cases (which are not rare to occur at all).

This affects the whole vault, since the shares owned by the protocol in the underlying vault are greatly diminished:
```solidity
    // The first function called on vaultProxy creation to prep ourselves for other operations.
    function fetch(
        VaultE4626 storage self,
        VaultTemp memory temp
    ) internal view {
        temp.vars[0] = self.vault.previewRedeem(self.totalVaultShares); // Total assets
        temp.vars[3] = self.vault.previewRedeem(
            self.vault.previewDeposit(1 << 128)
        ); // X128 fee discount factor.
    }

```


In some cases there could be a DoS due to attempting to withdraw too much tokens.

### PoC
For instance, the abovementioned conditions will be met in:
```solidity
exchanged.
    function trimBalance(
        Vertex storage self,
        ClosureId cid,
        uint256 targetReal,
        uint256 value,
        uint256 bgtValue
    ) internal returns (uint256 reserveSharesEarned, uint256 bgtResidual) {
        VaultProxy memory vProxy = VaultLib.getProxy(self.vid);
        uint256 realBalance = vProxy.balance(cid, false);

        // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
        // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
            return (0, 0);
        }
        
        uint256 residualReal = realBalance - targetReal;
        vProxy.withdraw(cid, residualReal);
        bgtResidual = FullMath.mulDiv(residualReal, bgtValue, value);
        reserveSharesEarned = ReserveLib.deposit(
            vProxy,
            self.vid,
            residualReal - bgtResidual
        );
        vProxy.commit();
    }
```

### Mitigation

```diff
    // Actually make our deposit/withdrawal
    function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        uint256 assetsToDeposit = temp.vars[1];
        uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
-               assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit; // !@audit: it errounesously evaluates to 0 each time this if-else case match is met
+               assetsToDeposit = 0;
            } else {
                // Perfect net!
                return;
            }
        }
```