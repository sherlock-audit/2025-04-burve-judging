Sneaky Burlap Pigeon

Medium

# Invariant Breaking2

### Summary
In the current implementation, the initial `deMinimusX128` is set to 100. This value is less than 1 wei (1 << 128). During a user swap, due to rounding, the `deMinimus` limitation is broken.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/SimplexFacet.t.sol#L608-L617
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L24
```solidity
    function init(SearchParams storage self) internal {
        self.maxIter = 5;
        self.deMinimusX128 = 100;
        self.targetSlippageX128 = 1e12;
    }
```
As can be seen, the `deMinimusX128` is set to 100 (which is less than 1e6), instead of setting `deMinimus` to 100.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L433
```solidity
    function swapInExact(
        Closure storage self,
        VertexId inVid,
        VertexId outVid,
        uint256 inAmount
    )
        internal
        returns (uint256 outAmount, uint256 tax, uint256 valueExchangedX128)
    {
        ...
        uint256 newOutBalance = ValueLib.x(
            self.targetX128,
            esX128[outIdx],
            newOutValueX128,
            true
        );
        outAmount = self.balances[outIdx] - newOutBalance;
        self.setBalance(outIdx, newOutBalance);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
1. Add this function:
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L893
```solidity
    function realValue(Closure storage self) internal returns (uint256 _realValueX128) {
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!self.cid.contains(i)) continue;
            _realValueX128 += ValueLib.v(self.targetX128, esX128[i], self.balances[i], false);
        }
    }
```
2. Change this function:
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L280
```solidity
    function queryValue(address owner, uint16 closureId) external 
        returns (
            uint256 valueX128,              
            uint256 targetX128,           
            uint256[MAX_TOKENS] memory,
            uint256 deMinimusX128         
        ) {
        ClosureId cid = ClosureId.wrap(closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        valueX128 = c.realValue();
        targetX128 = c.targetX128;
        SearchParams memory search = Store.simplex().searchParams;
        deMinimusX128 = uint256(search.deMinimusX128);
    }
```
3. Add this testing function:
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_invariant2() public {
        //SearchParams memory sp = SearchParams(10, 500, 1e18);
        //vm.prank(owner);
        //simplexFacet.setSearchParams(sp);

        uint256 valueX128;
        uint256 targetX128;
        uint256 deMinimusX128;
        
        console.log("Step1:");
        (valueX128, targetX128, , deMinimusX128) = valueFacet.queryValue(address(0), 0x7);
        console.log("valueX128     :", valueX128);
        console.log("targetX128    :", targetX128);
        console.log("deMinimusX128 :", deMinimusX128);
        console.log("valueX128 - targetX128 * 3                   : ", (int256)(valueX128) - (int256)(targetX128) * 3);
        console.log("");

        vm.prank(alice);
        swapFacet.swap(alice, tokens[0], tokens[1], 1e18, 0, 0x7);

        console.log("Step2:");
        (valueX128, targetX128, , deMinimusX128) = valueFacet.queryValue(address(0), 0x7);
        console.log("valueX128     :", valueX128);
        console.log("targetX128    :", targetX128);
        console.log("deMinimusX128 :", deMinimusX128);
        console.log("1<<128        : %e", uint256(1<<128));
        console.log("valueX128 - targetX128 * 3                   : %e", (int256)(valueX128) - (int256)(targetX128) * 3);
        console.log("valueX128 - (targetX128 + deMinimusX128) * 3 : %e", (int256)(valueX128) - (int256)(targetX128 + deMinimusX128) * 3);
    }
```
forge test --match-test "test_invariant2" -vv
Result:
```bash
[PASS] test_invariant2() (gas: 390878)
Logs:
  Step1:
  valueX128     : 102084710076281539039012382229530463436800000000000000000000
  targetX128    : 34028236692093846346337460743176821145600000000000000000000
  deMinimusX128 : 100
  valueX128 - targetX128 * 3                   :  0

  Step2:
  valueX128     : 102084710076281539039152825926928403944008268433775760147502
  targetX128    : 34028236692093846346337460743176821145600000000000000000000
  deMinimusX128 : 100
  1<<128        : 3.40282366920938463463374607431768211456e38
  valueX128 - targetX128 * 3                   : 1.40443697397940507208268433775760147502e38
  valueX128 - (targetX128 + deMinimusX128) * 3 : 1.40443697397940507208268433775760147202e38
```

### Impact
In the ReadMe:
> The "value" of the closure (according to the formulas) can never be more than deMinimus * (number of tokens in the closure) from the target value of the pool times number of tokens in the closure.

1. Invariant Breaking.


### Mitigation
Consider increasing `deMinimusX128` greater than (1 << 128).
