Sneaky Burlap Pigeon

High

# The `addSingleForValue` Function Receives More Fees than the `addValueSingle` Function


### Summary
The `addValueSingle` function operates similarly to `addValue + swap`, and the `addValue` function does not accumulate fees. Consequently, while some fees are deducted in the `addValueSingle` function, the `addSingleForValue` function receives fees amounting to `depositAmount * feeRate`.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L307
```solidity
    function addTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // For simplicity, we tax the entire amount in first. This overcharges slightly but an exact solution
        // would overcomplicate the contract and any approximation is game-able.
307:    tax = FullMath.mulX128(amount, self.baseFeeX128, true);
        amount -= tax;
        // Use the ValueLib's newton's method to solve for the value added and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky. We up the balance first for the ValueLib call, then set to do the checks.
        // We need to set the new target before we can setBalance, but we need up to balance to calc new target.
        self.balances[idx] += amount;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
                self.targetX128
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        value = ((newTargetX128 - self.targetX128) * self.n) >> 128; // Round down received value balance.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down.
        self.targetX128 = newTargetX128;
        // Now that we set the new target we can set balance to check validity.
        self.setBalance(idx, self.balances[idx]);
        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_addValueSingle_addSingleForValue1() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 100), 0);
        
        valueFacet.addValue(address(this), 0x7, 3 * 99e20, 0);

        uint256[4] memory initBalances = getBalances(alice);
        vm.prank(alice);
        valueFacet.addValueSingle(alice, 0x7,  9.8940812967163518726e19, 0, tokens[0], 0);

        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("addValueSingle : diffs0 : %e", diffs[0]);

        (uint256 value, , , ) = valueFacet.queryValue(alice, 0x7);
        console.log("addValueSingle : value  : %e", value);
    }
    
    function test_addValueSingle_addSingleForValue2() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 100), 0);
        
        valueFacet.addValue(address(this), 0x7, 3 * 99e20, 0);

        uint256[4] memory initBalances = getBalances(alice);
        vm.prank(alice);
        valueFacet.addSingleForValue(alice, 0x7, tokens[0], 1e20, 0, 0);

        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("addSingleForValue : diffs0 : %e", diffs[0]);

        (uint256 value, , , ) = valueFacet.queryValue(alice, 0x7);
        console.log("addSingleForValue : value  : %e", value);
    }
```
forge test --match-test "test_addValueSingle_addSingleForValue" -vv
Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_addValueSingle_addSingleForValue1() (gas: 1351083)
Logs:
  addValueSingle : diffs0 : -9.9666865949605510041e19
  addValueSingle : value  : 9.8940812967163518726e19

[PASS] test_addValueSingle_addSingleForValue2() (gas: 1438373)
Logs:
  addSingleForValue : diffs0 : -1e20
  addSingleForValue : value  : 9.8940812967163518726e19
```

As can be seen, for a value of `9.8940812967163518726e19`, using the `addSingleForValue` function consumes `1e20 token0`, while the `addValueSingle` function uses `9.9666865949605510041e19 token0`. This results in an excess usage of `0.0333e19 token0`. This delta is calculated as `depositAmount * feeRate / n`.

### Impact
The `addSingleForValue` function accumulates more fees than the `addValueSingle` function.
This leads to a loss for the users. This percent is at leat 6.25%(1/16).

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L307
```solidity
    function addTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // For simplicity, we tax the entire amount in first. This overcharges slightly but an exact solution
        // would overcomplicate the contract and any approximation is game-able.
-307:    tax = FullMath.mulX128(amount, self.baseFeeX128, true);
+        tax = FullMath.mulX128(amount, self.baseFeeX128, true) * (self.n - 1) / self.n;
        amount -= tax;
        // Use the ValueLib's newton's method to solve for the value added and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky. We up the balance first for the ValueLib call, then set to do the checks.
        // We need to set the new target before we can setBalance, but we need up to balance to calc new target.
        self.balances[idx] += amount;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
                self.targetX128
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        value = ((newTargetX128 - self.targetX128) * self.n) >> 128; // Round down received value balance.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down.
        self.targetX128 = newTargetX128;
        // Now that we set the new target we can set balance to check validity.
        self.setBalance(idx, self.balances[idx]);
        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
    }
```