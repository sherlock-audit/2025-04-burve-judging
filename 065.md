Cheesy Garnet Otter

High

# BGT Token Balance Tracking Error Leads to Withdrawal Failures

### Summary

The absence of `bgtBalance` updates in the `sendBalance` function causes an inconsistency between the tracked and actual BGT balances. This allows the contract owner to withdraw BGT tokens without adjusting the tracked balance, potentially leading to withdrawal failures for exchangers.

### Root Cause

In `https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L110-L113` lines 110-113, the `sendBalance` function allows the owner to transfer any token (including BGT) without updating the `bgtBalance` state variable, which is used to track the available BGT for exchanges:

```solidity
function sendBalance(address token, address to, uint256 amount) external {
    AdminLib.validateOwner();
    TransferHelper.safeTransfer(token, to, amount);
}
```

### Internal Pre-conditions

- The contract must be funded with BGT tokens using the `fund` function, which sets `bgtBalance` to a positive value.
- The owner calls `sendBalance` with the BGT token address to transfer BGT tokens out of the contract without updating `bgtBalance`.
- At least one address must be registered as an exchanger by calling `addExchanger`.
- Exchange rates for at least one token must be set by calling `setRate`.


### External Pre-conditions

None

### Attack Path

1. Owner funds the contract with BGT tokens using the `fund` function, increasing `bgtBalance`.
2. Owner calls `sendBalance` with the BGT token address to withdraw some BGT from the contract without updating `bgtBalance`.
3. An exchanger calls `exchange` to swap tokens for BGT, relying on the incorrect `bgtBalance`.
4. The exchanger attempts to `withdraw` their BGT.
5. The withdrawal transaction reverts because the actual BGT balance is insufficient, despite the contract indicating otherwise.

### Impact

 - **Denial of Service:** Exchangers are unable to withdraw BGT tokens they are rightfully owed.
- **Locked Funds:** Exchange tokens may be permanently locked if `bgtBalance` is consistently higher than the actual token balance.

### PoC

add to `BGTExchanger.t.sol` 
```solidity
function testWithdrawFailsDueToBalanceMismatch() public {
    // Fund mainEx with 10e18 BGT
    deal(address(ibgt), address(this), 10e18);
    ibgt.approve(address(bgtExchanger), 10e18);
    bgtExchanger.fund(10e18);

    // Owner sends 5e18 BGT to alice using sendBalance
    vm.prank(owner);
    bgtExchanger.sendBalance(address(ibgt), alice, 5e18);

    // Now, actual BGT balance in bgtExchanger is 5e18, but bgtBalance is still 10e18
    assertEq(
        ibgt.balanceOf(address(bgtExchanger)),
        5e18,
        "actual BGT balance"
    );
    assertEq(bgtExchanger.bgtBalance(), 10e18, "tracked bgtBalance");

    // Add alice as exchanger
    vm.prank(owner);
    bgtExchanger.addExchanger(alice);

    // Set rate for usdc, so that for 10e6 usdc, bgtAmount = 10e18 ibgt
    uint256 rate = (1e12 << 128); // 1e6 usdc => 1e18 ibgt
    vm.prank(owner);
    bgtExchanger.setRate(address(usdc), rate);

    // Have alice exchange 10e6 usdc
    vm.startPrank(alice);
    deal(address(usdc), alice, 10e6);
    usdc.approve(address(bgtExchanger), 10e6);
    (uint256 bgtAmount, uint256 spendAmount) = bgtExchanger.exchange(
        address(usdc),
        10e6
    );
    assertEq(bgtAmount, 10e18, "bgtAmount");
    assertEq(spendAmount, 10e6, "spendAmount");

    // Try to withdraw 10e18, should fail due to insufficient actual balance
    vm.expectRevert(bytes("ST")); // Expect the SafeTransfer error
    bgtExchanger.withdraw(alice, 10e18);
    vm.stopPrank();
}
```
### Mitigation

Update the `sendBalance` function to check if the token being sent is the BGT token, and if so, decrement `bgtBalance` accordingly.