Great Iris Zebra

High

# Users Retain All Swap Fees in The `removeValueSingle` Function

### Summary
In `Closure::removeValueSingle`, the `removedNominal` represents the value after deducting the fee. However, in `ValueFacet::removeValueSingle`, `removedNominal` is incorrectly used as if it includes the fee. Consequently, the fees are not withdrawn from the vault and are instead treated as yield from the vault. Additionally, there is no protocol fee associated with the yield from the vault.

### Root Cause
The root cause lies in line 234, which calls the `withdraw` function.
At this point, the withdrawal amount does not include the fees.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L236
```solidity
    function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
233:    uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
234:    Store.vertex(vid).withdraw(cid, realRemoved, false);
        uint256 realTax = FullMath.mulDiv(
236:        removedBalance,
            nominalTax,
            removedNominal
        );
240:     c.addEarnings(vid, realTax);
        removedBalance = realRemoved - realTax; // How much the user actually gets.
        require(removedBalance >= minReceive, PastSlippageBounds());
        TransferHelper.safeTransfer(token, recipient, removedBalance);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function testRemoveValueSingle() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x3, uint128(oneX128 / 1000), uint128(oneX128 / 10));
        valueFacet.addValue(alice, 0x3, 1e21, 0);

        vm.startPrank(alice);
        valueFacet.removeValueSingle(alice, 0x3, 1e20, 0, tokens[0], 0);

        (, , uint256[MAX_TOKENS] memory earnings, ) = valueFacet.queryValue(alice, 0x3);
        console.log("user's earnings[0] : %e", earnings[0]);

        uint256[4] memory initBalances = getBalances(owner);
        vm.startPrank(owner);
        simplexFacet.withdraw(tokens[0]);
        uint256[4] memory currentBalances = getBalances(owner);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("protocol fees      : %e\n", diffs[0]);
    }
```
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] testRemoveValueSingle() (gas: 947668)
Logs:
  user's earnings[0] : 4.024390243902439e16
  protocol fees      : 0e0
```
After mitigation:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] testRemoveValueSingle() (gas: 1045604)
Logs:
  user's earnings[0] : 3.6219512195121952e16
  protocol fees      : 4.918699186991869e15
```

### Impact
Users receive all swap fees, which means the owner cannot collect the protocol fees.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L233-L240
```diff
    function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
-233    uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
+233    uint256 realRemoved = AdjustorLib.toReal(token, removedNominal + nominalTax, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);
        uint256 realTax = FullMath.mulDiv(
-236        removedBalance,
+236        realRemoved,
            nominalTax,
-238        removedNominal
+238        removedNominal + nominalTax
        );
240     c.addEarnings(vid, realTax);
        removedBalance = realRemoved - realTax; // How much the user actually gets.
        require(removedBalance >= minReceive, PastSlippageBounds());
        TransferHelper.safeTransfer(token, recipient, removedBalance);
    }
```

