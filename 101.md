Flaky Ultraviolet Parrot

High

# DoS on withdrawals due to an infinite recursion

### Summary

The `getOwed()` function will be entered over and over between bgtExchangers causing an infinite recursion (under a condition that we will explain later), which will DoS withdrawals, locking user funds.

### Root Cause

- It is stated in [the docs](https://docs.burve.fi/technicals/risk-assessment#safety-assumptions) that the previous bgtExchanger will be used as backup upon migration.
- That is stated in the [comments also](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/SimplexFacet.sol#L318) in  `setBGTExchanger()`.
- So, if we migrate to a bgtExchanger that was used in the past, the [following code](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L75) will cause an infinite recursion:
```solidity
    function getOwed(address caller) public view returns (uint256 _owed) {
        _owed = owed[caller];
        if (address(backupEx) != address(0)) {
@>         _owed += backupEx.getOwed(caller);
        }
        _owed -= withdrawn[caller];
    }
``` 
- `getOwed()` is used in the [withdraw()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L82) function, so the `withdraw()` function will be DoS'ed.

#### Note
Please note that that this is not an **attack** by a trusted user, it simply a scenario that may happen unknowingly by an admin, as it is stated in [Sherlock docs](https://github.com/sherlock-protocol/sherlock-v2-docs/blob/2668590b5fd3c5aae6b3120a0e1e7b79524bd673/audits/judging/guidelines/README.md) **" if the (external) admin will unknowingly cause issues, it can be considered a valid issue."**

### Attack Path
- We are currently in the bgtExchanger named B, which has A as a backup bgtExchanger.
- The admin migrates back to the A bgtExchanger, and sets the backup in it to B.
- So now we have A points to B, and B points to A, so `backupEx.getOwed(caller)` will keep going back and forth in `getOwed()`.
- And since `getOwed()` is used in `withdraw()` , this will cause a DoS on it and locks user funds indefinitely.

### Internal Pre-conditions

The admin needs to migrate to a bgtExchanger which was already used in the past. It doesn't need to be just two exchangers, but if D points to C, C points to B, B points to A, and A points to D this issue will still happen.

### External Pre-conditions

N/A

### Impact

- DoS on a critical functionality, which is withdrawals.
- Freezing of user funds, since they can't withdraw so their funds will be locked in the contract. 

### PoC
Put the following `PoC.t.sol` under `./Burve/test/integrations/BGTExchange/`
```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {Test} from "forge-std/Test.sol";

import {IBGTExchanger} from "../../../src/integrations/BGTExchange/IBGTExchanger.sol";
import {BGTExchanger} from "../../../src/integrations/BGTExchange/BGTExchanger.sol";
import {MockERC20} from "../../mocks/MockERC20.sol";

/// Exposes methods to manipulate internal state of the BGT Exchanger for testing purposes.
contract ExposedBGTExchanger is BGTExchanger {
    constructor(address _bgtToken) BGTExchanger(_bgtToken) {}

    function setOwed(address caller, uint256 amount) external {
        owed[caller] = amount;
    }

    function setWithdrawn(address caller, uint256 amount) external {
        withdrawn[caller] = amount;
    }

    function setBackupEx(address _backupEx) external {
        backupEx = IBGTExchanger(_backupEx);
    }
}

contract PoCTest is Test {
    ExposedBGTExchanger exchangerA;
    ExposedBGTExchanger exchangerB;
    address owner;
    address alice;
    MockERC20 ibgt;

    function setUp() public {
        owner = makeAddr("owner");
        alice = makeAddr("alice");

        ibgt = new MockERC20("ibgt", "ibgt", 18);

        vm.startPrank(owner);
        exchangerA = new ExposedBGTExchanger(address(ibgt));
        exchangerB = new ExposedBGTExchanger(address(ibgt));
        vm.stopPrank();

        // Give some tokens to exchangerA
        deal(address(ibgt), address(exchangerA), 20e18);

        // Setup owed balances
        vm.startPrank(owner);
        exchangerA.setOwed(alice, 5e18);
        exchangerB.setOwed(alice, 5e18);

        // Setup backups (A points to B, B points to A)
        exchangerA.setBackupEx(address(exchangerB));
        exchangerB.setBackupEx(address(exchangerA));
        vm.stopPrank();
    }

    function testWithdraw_DoS_InfiniteRecursion() public {
        vm.startPrank(alice);

        // it will revert with OutOfGas
        vm.expectRevert();
        exchangerA.withdraw(owner, 5e18);

        vm.stopPrank();
    }
}
```

#### Expected output with the `-vvvv` tag (proving the infinite recursion) :
```bash
[⠊] Compiling...
[⠰] Compiling 1 files with Solc 0.8.28
[⠒] Solc 0.8.28 finished in 2.30s
Compiler run successful!

Ran 1 test for test/integrations/BGTExchange/PoC.t.sol:BGTExchangerTest
[PASS] testWithdraw_DoS_InfiniteRecursion() (gas: 1589708)
Traces:
  [1589708] BGTExchangerTest::testWithdraw_DoS_InfiniteRecursion()
    ├─ [0] VM::startPrank(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6])
    │   └─ ← [Return] 
    ├─ [0] VM::expectRevert(custom error 0xf4844814)
    │   └─ ← [Return] 
    ├─ [1571415] ExposedBGTExchanger::withdraw(owner: [0x7c8999dC9a822c1f0Df42023113EDB4FDd543266], 5000000000000000000 [5e18])
    │   ├─ [1561756] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   ├─ [1554938] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   ├─ [1552120] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   ├─ [1549302] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   │   ├─ [1546484] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   │   │   ├─ [1543666] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   │   │   │   ├─ [1540848] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   │   │   │   │   ├─ [1538030] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   │   │   │   │   │   ├─ [1535212] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
    │   │   │   │   │   │   │   │   │   │   ├─ [1532394] ExposedBGTExchanger::getOwed(alice: [0x328809Bc894f92807417D2dAD6b7C998c1aFdac6]) [staticcall]
```
