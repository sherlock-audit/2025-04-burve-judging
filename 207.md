Damaged Admiral Grasshopper

High

# Underlying vault earnings are wrongly computed when trim balances

### Summary

When calculates underlying vault earnings when trim balances, the underlying vault fee is wrongly applied to the total assets, leading to the earning are wrongly computed and user receives much less earnings than expected or not earnings at all.

### Root Cause

When trim balances, `VaultE4626` is fetched and the underlying vault fee is applied to `temp.vars[0]`(total assets) .

[VaultE4626Impl::fetch()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L50-L59):
```solidity
    // The first function called on vaultProxy creation to prep ourselves for other operations.
    function fetch(
        VaultE4626 storage self,
        VaultTemp memory temp
    ) internal view {
@>      temp.vars[0] = self.vault.previewRedeem(self.totalVaultShares); // Total assets
        temp.vars[3] = self.vault.previewRedeem(
            self.vault.previewDeposit(1 << 128)
        ); // X128 fee discount factor.
    }
```

This decreases vault proxy balance (`realBalance`), which would in turn decrease the earnings (`residualReal`). 
[VaultE4626Impl::balance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L176-L184):
```solidity
@>      uint256 totalAssets = temp.vars[0] + newlyAdding - temp.vars[2];

        uint256 fullAmount = roundUp
            ? FullMath.mulDivRoundingUp(
                self.shares[cid],
                totalAssets,
                self.totalShares
            )
@>          : FullMath.mulDiv(self.shares[cid], totalAssets, self.totalShares);
```

[VertexImpl::trimBalance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Vertex.sol#L68-L75):
```solidity
@>      uint256 realBalance = vProxy.balance(cid, false);
        // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
        // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
@>          return (0, 0);
        }
@>      uint256 residualReal = realBalance - targetReal;
```

Because earnings are reduced, user will receive less profit when they collect earnings, it's also possible that `realBalance` is computed as less than `targetReal`, hence no earnings at all.

In fact, when [trimBalance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Vertex.sol#L60-L66) is called, the earnings (residualReal) are first withdrawn from the vault and then deposited back again.

[VertexImpl::trimBalance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Vertex.sol#L68-L83):
```solidity
        uint256 realBalance = vProxy.balance(cid, false);
        // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
        // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
            return (0, 0);
        }
        uint256 residualReal = realBalance - targetReal;
@>      vProxy.withdraw(cid, residualReal);
        bgtResidual = FullMath.mulDiv(residualReal, bgtValue, value);
@>      reserveSharesEarned = ReserveLib.deposit(
            vProxy,
            self.vid,
            residualReal - bgtResidual
        );
        vProxy.commit();
```

When withdraws, `temp.vars[2]` (asset to withdraw) is increased.

[VaultE4626Impl::withdraw()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L153):
```solidity
        temp.vars[2] += amount;
```

When deposits, `temp.vars[1]` (asset to deposit) is increased by the same amount (assuming `bgtResidual` is 0).

[VaultE4626Impl::deposit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L127):
```solidity
        temp.vars[1] += amount;
```

Then in [commit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L62), `assetsToDeposit` and `assetsToWithdraw` can be perfect net, meaning the assets won't be actually deposited to or withdrawn from the underlying vault.

[VaultE4626Impl::commit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L63-L78):
```solidity
@>      uint256 assetsToDeposit = temp.vars[1];
@>      uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
                assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit;
            } else {
                // Perfect net!
@>              return;
            }
        }
```

Therefore, the underlying vault fee should not be applied when calculating the earnings.

### Internal Pre-conditions

Internal calls trigger trimming balances.

### External Pre-conditions

The underlying vault charges a fee.

### Attack Path

None

### Impact

User collect much less earnings than expected or no earnings at all.

### PoC

The POC show the scenario when:
1. The underlying vault exit fee is $10%$;
2. $1000$ asset is deposited in the underlying vault;
3. The underlying vault accrues $10$ earnings;
4. Alice calls to collect the earnings, the earnings are to be withdrawn from the underlying vault, so she expects to receive $9$ considering the exit fee, but in fact she receives nothing at all.

Please run `forge test --mt testAudit_EarningsAreWronglyComputed`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "./mocks/MockERC20.sol";
import "openzeppelin-contracts/mocks/docs/ERC4626Fees.sol";
import {VaultType} from "../src/multi/vertex/VaultPointer.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";
import "../src/integrations/adjustor/DecimalAdjustor.sol";

contract AuditTest is Test {
    uint8 constant MAX_TOKENS = 16;

    address owner = makeAddr("OWNER");
    address diamond;

    MockERC20 tokenA = new MockERC20("Token A", "A", 18);
    MockERC20 tokenB = new MockERC20("Token B", "B", 6);

    MockERC4626WithFees vaultA = new MockERC4626WithFees(tokenA, "Vault A", "VA");
    MockERC4626WithFees vaultB = new MockERC4626WithFees(tokenB, "Vault B", "VB");

    function setUp() public {
        vm.startPrank(owner);

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            // Configure adjustor
            {
                DecimalAdjustor decimalAdjustor = new DecimalAdjustor();
                mixedAdjustor.setAdjustor(address(tokenB), address(decimalAdjustor));
            }
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        diamond = address(new SimplexDiamond(facets, "ValueToken", "BVT"));

        vm.label(diamond, "Diamond");
        vm.label(address(tokenA), "Token A");
        vm.label(address(tokenB), "Token B");
        vm.label(address(vaultA), "Vault A");
        vm.label(address(vaultB), "Vault B");

        // Add Vertex
        {
            // TokenA
            SimplexFacet(diamond).addVertex(address(tokenA), address(vaultA), VaultType.E4626);

            // TokenB
            SimplexFacet(diamond).addVertex(address(tokenB), address(vaultB), VaultType.E4626);
        }

        // Add Closure
        {
            bool success;

            uint16 cid = 3; // 0b11
            uint128 startingTarget = 1e12;
            uint128 baseFeeX128 = 0;
            uint128 protocolTakeX128 = 0;
            // uint128 baseFeeX128 = 34028236692093846346337460743176821145; // 10%
            // uint128 protocolTakeX128 = 34028236692093846346337460743176821145;  //10%

            uint256 amountA = startingTarget / 10 ** (18 - tokenA.decimals());
            uint256 amountB = startingTarget / 10 ** (18 - tokenB.decimals());

            tokenA.mint(owner, amountA);
            tokenA.approve(diamond, amountA);
            tokenB.mint(owner, amountB);
            tokenB.approve(diamond, amountB);
            SimplexFacet(diamond).addClosure(cid, startingTarget, baseFeeX128, protocolTakeX128);
        }

        vm.stopPrank();
    }

    function testAudit_EarningsAreWronglyComputed() public {
        uint16 cid = 3;

        vaultA.setExitFee(1000);

        // Alice adds value
        address alice = makeAddr("Alice");
        tokenA.mint(alice, 1000e18 + 1);
        tokenB.mint(alice, 1000e6 + 1);

        vm.startPrank(alice);
        tokenA.approve(diamond, 1000e18 + 1);
        tokenB.approve(diamond, 1000e6 + 1);
        ValueFacet(diamond).addValue(alice, cid, 2000e18, 0);
        vm.stopPrank();

        // Mock underlying vault earns fees
        {
            tokenA.mint(address(vaultA), 10e18);
        }

        // Alice collects earnings
        vm.prank(alice);
        (uint256[MAX_TOKENS] memory collectedBalances, ) = ValueFacet(diamond).collectEarnings(alice, cid);

        // No earnings are collected
        assertEq(collectedBalances[0], 0);
    }
}

contract MockERC4626WithFees is ERC4626Fees {

    uint256 entryFeeBasisPoints;
    uint256 exitFeeBasisPoints;
    address feeRecipient;

    constructor(
        IERC20 asset_, 
        string memory name_, 
        string memory symbol_
    ) ERC4626(asset_) ERC20(name_, symbol_) {
        feeRecipient = msg.sender;
    }

    function setEntryFee(uint256 entryFeeBasisPoints_) public {
        entryFeeBasisPoints = entryFeeBasisPoints_;
    }

    function setExitFee(uint256 exitFeeBasisPoints_) public {
        exitFeeBasisPoints = exitFeeBasisPoints_;
    }

    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }

    function _exitFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }
}
```

### Mitigation

_No response_