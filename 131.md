Skinny Mocha Boa

High

# Profits from trimming can be stolen by minting value tokens

### Summary

In multi-token closures, tokens can include LSTs where the value of tokens increases over time.
To stablize values of tokens, it cuts profits generated by LSTs in `trimBalance` function that distributes the profits to LP.

On the other hand, LP providers can mint value tokens from their liquidity which can be used outside the protocol.
When value tokens are minted, the staked value is decreased.

However, before minting or burning value tokens, it does not trim the profits of LSTs, so users can steal the profits by burning value tokens.

### Root Cause

The root cause of the issue is in `mint` and `burn` functions of [`ValueTokenFacet`](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/facets/ValueTokenFacet.sol#L16-L32) contract, where it does not call `trimAllBalances` function before modifying the staked value.

```solidity
    function mint(uint256 value, uint256 bgtValue, uint16 _cid) external {
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_cid);
        Closure storage c = Store.closure(cid); // Validates cid.
        c.unstakeValue(value, bgtValue);
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        _mint(msg.sender, value);
    }

    function burn(uint256 value, uint256 bgtValue, uint16 _cid) external {
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        _burn(msg.sender, value);
        ClosureId cid = ClosureId.wrap(_cid);
        Closure storage c = Store.closure(cid); // Validates cid.
        c.stakeValue(value, bgtValue);
        Store.assets().add(msg.sender, cid, value, bgtValue);
    }
```

### Internal Pre-conditions

One of vertices in a closure is an LST, e.g. `stETH`.

### External Pre-conditions

The LST's price went up that generates profits from trimming.

### Attack Path

1. A closure has total of 10,000 value that includes 1,000 `stETH` and other ETH related tokens.
2. Alice who had deposited to the closure had minted 5,000 value tokens, which makes 5,000 staked value remaining.
3. After a while, the rate of `stETH` increased, so the profits for trimming are generated, which we say 10 ETH worth of profits.
4. As Alice has no staked value, she shouldn't receive any profits from trimming.
5. However, Alice burns 5,000 value tokens, call `collectEarnings` function of `ValueFacet` contract, and receives 5 ETH, and then mints the value tokens again.
6. As a result, the profits for staked users are stolen and less profits are returned to the staked LP providers.

### Impact

Stealing of profits from staked users.

### PoC

None

### Mitigation

In `mint` and `burn` functions, it should call `trimAllBalances` function before modifying the staked value.

```diff
    function mint(uint256 value, uint256 bgtValue, uint16 _cid) external {
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_cid);
        Closure storage c = Store.closure(cid); // Validates cid.
+       c.trimAllBalances(); 
        c.unstakeValue(value, bgtValue);
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        _mint(msg.sender, value);
    }

    function burn(uint256 value, uint256 bgtValue, uint16 _cid) external {
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        _burn(msg.sender, value);
        ClosureId cid = ClosureId.wrap(_cid);
        Closure storage c = Store.closure(cid); // Validates cid.
+       c.trimAllBalances();
        c.stakeValue(value, bgtValue);
        Store.assets().add(msg.sender, cid, value, bgtValue);
    }
```
