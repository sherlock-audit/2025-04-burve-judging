Damaged Admiral Grasshopper

High

# User loses earnings from underlying vaults when they remove values

### Summary

When user removes values from a closure, their value is updated before trim balances, this leads to loss of earnings from underlying vault.

### Root Cause

[_trimBalance()]() is used for collecting earnings from the underlying vault of a specific vertex, and earning shares are updated accordingly.

[ClosureImpl::_trimBalance()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L742-L756):
```solidity
@>      self.earningsPerValueX128[idx] += (earnings << 128) / nonBgtValueStaked;
        if (self.bgtValueStaked > 0) {
            (uint256 bgtEarned, uint256 unspent) = SimplexLib.bgtExchange(
                idx,
                bgtReal
            );
@>          self.bgtPerBgtValueX128 += (bgtEarned << 128) / self.bgtValueStaked;
            // rare
            if (unspent > 0) {
                uint256 unspentShares = ReserveLib.deposit(vid, unspent);
@>              self.unexchangedPerBgtValueX128[idx] +=
                    (unspentShares << 128) /
                    self.bgtValueStaked;
            }
        }
```

When user removes values from a closure, `trimAllBalances()` will be triggered to collect earnings from all the underlying vaults of the vertexes in the closure.

[ClosureImpl::removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L221):
```solidity
        trimAllBalances(self);
```

The problem is user value is updated (decreased) before trimming balances, this can lead to the user collect less earnings or no earnings at all.

[ValueFacet::removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L189-L194):
```solidity
        Closure storage c = Store.closure(cid);
@>      Store.assets().remove(msg.sender, cid, value, bgtValue);
@>      uint256[MAX_TOKENS] memory nominalReceives = c.removeValue(
            value,
            bgtValue
        );
```

### Internal Pre-conditions

User calls to remove values from a closure.

### External Pre-conditions

There are uncollected earnings in the vaults before user removes values. The likelihood is high especially when the closure contains many vertexes.

### Attack Path

None.

### Impact

User loses the earnings from the underlying vaults.

### PoC

Please run `forge test --mt testAudit_EarningLossWhenRemoveValue`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "./mocks/MockERC20.sol";
import "openzeppelin-contracts/mocks/docs/ERC4626Fees.sol";
import {VaultType} from "../src/multi/vertex/VaultPointer.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";
import "../src/integrations/adjustor/DecimalAdjustor.sol";

contract AuditTest is Test {
    uint8 constant MAX_TOKENS = 16;

    address owner = makeAddr("OWNER");
    address diamond;

    MockERC20 tokenA = new MockERC20("Token A", "A", 18);
    MockERC20 tokenB = new MockERC20("Token B", "B", 6);

    MockERC4626WithFees vaultA = new MockERC4626WithFees(tokenA, "Vault A", "VA");
    MockERC4626WithFees vaultB = new MockERC4626WithFees(tokenB, "Vault B", "VB");

    function setUp() public {
        vm.startPrank(owner);

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            // Configure adjustor
            {
                DecimalAdjustor decimalAdjustor = new DecimalAdjustor();
                mixedAdjustor.setAdjustor(address(tokenB), address(decimalAdjustor));
            }
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        diamond = address(new SimplexDiamond(facets, "ValueToken", "BVT"));

        vm.label(diamond, "Diamond");
        vm.label(address(tokenA), "Token A");
        vm.label(address(tokenB), "Token B");
        vm.label(address(vaultA), "Vault A");
        vm.label(address(vaultB), "Vault B");

        // Add Vertex
        {
            // TokenA
            SimplexFacet(diamond).addVertex(address(tokenA), address(vaultA), VaultType.E4626);

            // TokenB
            SimplexFacet(diamond).addVertex(address(tokenB), address(vaultB), VaultType.E4626);
        }

        // Add Closure
        {
            bool success;

            uint16 cid = 3; // 0b11
            uint128 startingTarget = 1e12;
            uint128 baseFeeX128 = 0;
            uint128 protocolTakeX128 = 0;
            // uint128 baseFeeX128 = 34028236692093846346337460743176821145; // 10%
            // uint128 protocolTakeX128 = 34028236692093846346337460743176821145;  //10%

            uint256 amountA = startingTarget / 10 ** (18 - tokenA.decimals());
            uint256 amountB = startingTarget / 10 ** (18 - tokenB.decimals());

            tokenA.mint(owner, amountA);
            tokenA.approve(diamond, amountA);
            tokenB.mint(owner, amountB);
            tokenB.approve(diamond, amountB);
            SimplexFacet(diamond).addClosure(cid, startingTarget, baseFeeX128, protocolTakeX128);
        }

        vm.stopPrank();
    }

    function testAudit_EarningLossWhenRemoveValue() public {
        // Alice adds value
        address alice = makeAddr("Alice");
        tokenA.mint(alice, 100e18 + 1);
        tokenB.mint(alice, 100e6 + 1);

        vm.startPrank(alice);
        tokenA.approve(diamond, 100e18 + 1);
        tokenB.approve(diamond, 100e6 + 1);
        ValueFacet(diamond).addValue(alice, 3, 200e18, 0);
        vm.stopPrank();

        // Mock underlying vault accures fees
        {
            tokenA.mint(address(vaultA), 5e18);
        }

        // Alice removes
        vm.prank(alice);
        uint256[16] memory receivedBalances = ValueFacet(diamond).removeValue(alice, 3, 200e18, 0);

        uint256 tokenABalanceBeforeCollectEarnings = tokenA.balanceOf(alice);

        // Alice collect earnings
        vm.prank(alice);
        ValueFacet(diamond).collectEarnings(alice, 3);      

        uint256 tokenABalanceAfterCollectEarnings = tokenA.balanceOf(alice); 

        // No earnings collected
        assertEq(tokenABalanceAfterCollectEarnings - tokenABalanceBeforeCollectEarnings, 0);
    }
}

contract MockERC4626WithFees is ERC4626Fees {
    uint256 entryFeeBasisPoints;
    uint256 exitFeeBasisPoints;
    address feeRecipient;

    constructor(
        IERC20 asset_, 
        string memory name_, 
        string memory symbol_
    ) ERC4626(asset_) ERC20(name_, symbol_) {
        feeRecipient = msg.sender;
    }

    function setEntryFee(uint256 entryFeeBasisPoints_) public {
        entryFeeBasisPoints = entryFeeBasisPoints_;
    }

    function setExitFee(uint256 exitFeeBasisPoints_) public {
        exitFeeBasisPoints = exitFeeBasisPoints_;
    }

    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }

    function _exitFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }
}
```

### Mitigation

Update user value after removing values from a closure.

```diff
        Closure storage c = Store.closure(cid);
-       Store.assets().remove(msg.sender, cid, value, bgtValue);
        uint256[MAX_TOKENS] memory nominalReceives = c.removeValue(
            value,
            bgtValue
        );
+       Store.assets().remove(msg.sender, cid, value, bgtValue);
```