Sunny Cloth Cod

High

# Phantom Shares Vulnerability in Fee-Charging Vaults Will Block All Swaps Once a Tipping Point is Reached

### Summary

The incorrect share calculation for deposits into `ReserveLib.sol` for fee-charging vaults will cause denial of service for all users as accumulated fees will create a growing discrepancy between tracked balances and actual balances, eventually preventing swaps.

### Root Cause

Based on the sponsor's comments, ERC4626 vaults that charge a fee on deposit/withdrawal are to be examined. [[1]](https://discord.com/channels/812037309376495636/1363140869502402623/1366828235471454280) [[2]](https://discord.com/channels/812037309376495636/1363140869502402623/1367860945350561832)

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/f1708a10-791e-4414-841b-3ce5e0a5d891)

The root cause is how the `shares` mapping tracks accounting in [ReserveLib.sol::deposit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Reserve.sol#L32); in the case of a fee charging vault, the amount actually deposited into the vault will be less than the original amount, but the system increments the `shares` based on the raw amount. 
```solidity
    function deposit(
        VaultProxy memory vProxy,
        VertexId vid,
        uint256 amount
    ) internal returns (uint256 shares) {
        Reserve storage reserve = Store.reserve();
        uint8 idx = vid.idx();
        uint128 balance = vProxy.balance(RESERVEID, true);
        vProxy.deposit(RESERVEID, amount);
        shares = (balance == 0)
            ? amount * SHARE_RESOLUTION

        // Calculates shares based on system shares mapping and balance which do not increment by actual amount deposited
-->         : (amount * reserve.shares[idx]) / balance; // No need for mulDiv.

        // Increment by incorrect shares amount
-->     reserve.shares[idx] += shares;
    }

```

This creates a phantom shares situation where the system tracks more shares than what actually exists in the vault. The vulnerability manifests through a chain of events:

1. Each time fees are earned and deposited into a fee-charging vault, the system records more shares than it should
2. These phantom shares accumulate over time with each operation
3. When the system attempts to withdraw tokens during a swap operation, it calculates withdrawal amounts based on these inflated share counts
4. Once the discrepancy reaches a critical threshold, the `Vertex.trimBalance` function detects that `targetReal > realBalance` and emits an InsufficientBalance event
5. The transaction continues but later fails when attempting to withdraw more tokens than actually exist in the vault

The tipping point occurs when the accumulated phantom shares cause the calculated withdrawal amount to exceed the actual available balance. This is influenced by:

1. The fee percentage charged by the vault
2. The number of operations performed
3. The initial liquidity in the vault as this liquidity provided by the protocol is never recorded for in the internal accounting; this is correct for the record but just highlighting that the number of operations to reach the tipping point is contingent on this initial liquidity provided aswell

In the test case, the system broke after 876 swaps when it believed it had approximately 8x more tokens (7.972e20) than it actually did (9.752e19).

### Internal Pre-conditions

1. The protocol needs to be using at least one fee-charging vault.
2. Users need to perform swaps that generate fees for that vertex.
3. Accumulated shares need to reach the point where tracked balances exceed actual balances, causing checks in `trimBalance` to revert the swap.

### External Pre-conditions

NA

### Attack Path

1. A fee-charging vault is used for a vertex
2. Users perform normal operations (adding liquidity, swapping)
3. Each swap generates fees that are processed through `ReserveLib.deposit`
4. For each fee deposit, ReserveLib calculates shares based on the pre-fee amount but the vault only deposits the post-fee amount
5. This discrepancy accumulates over multiple operations
6. After enough operations (876 swaps in the test case), the system believes it has ~8x more tokens than it actually does
7. When attempting to perform operations that require withdrawing these tokens, the transaction reverts with `InsufficientBalance`
8. The vertex becomes unusable as all subsequent swaps involving the token will fail indefinetely.

### Impact

The vertex eventually becomes inaccessible as key functions stop working. Users are unable to swap tokens with this vertex as the counterparty due to the system trying to withdraw more tokens than actually exist in the vaults. This affects all users of the protocol simultaneously and the issue requires a complex migration process to fix once it manifests.

### PoC

- The test inherits the `MultiSetUp` base contract from the protocol; copy the following contract into the projects test suite
- A simple 2% fee charging vault is used
- The test is in a failed state, change `i < 876` to show system working fine before tipping point
- These logs can be added to `ReserveLib.sol::deposit()` to see the deposits incrementing incorrectly

```solidity
    function deposit(
        VaultProxy memory vProxy,
        VertexId vid,
        uint256 amount
    ) internal returns (uint256 shares) {
        Reserve storage reserve = Store.reserve();
        uint8 idx = vid.idx();
        uint128 balance = vProxy.balance(RESERVEID, true);
        vProxy.deposit(RESERVEID, amount);
        // If someone tries to share inflate attack this, they'd have to donate to the underlying vault,
        // which then splits the donation across existing deposits from other people using the vault,
        // including the other closures. So there's no way to inflate shares here.
        shares = (balance == 0)
            ? amount * SHARE_RESOLUTION
            : (amount * reserve.shares[idx]) / balance; // No need for mulDiv.
        reserve.shares[idx] += shares;
        console.log("\n ReserveLib::deposit");
        console.log("amount: %s", amount);
        console.log("Shares Increment amount: %s", shares);
        console.log("total shares of token:", reserve.shares[idx]);
    }
```
Proof of Concept:

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.17;

import {Test, console} from "forge-std/Test.sol";

import {IERC4626} from "openzeppelin-contracts/interfaces/IERC4626.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import {MockERC4626} from "../mocks/MockERC4626.sol";
import "openzeppelin-contracts/token/ERC20/extensions/ERC4626.sol";

import {SimplexDiamond} from "../../src/multi/Diamond.sol";
import {IDiamond} from "Commons/Diamond/interfaces/IDiamond.sol";
import {DiamondCutFacet} from "Commons/Diamond/facets/DiamondCutFacet.sol";
import {LibDiamond} from "Commons/Diamond/libraries/LibDiamond.sol";

import {MultiSetupTest} from "../facets/MultiSetup.u.sol";

import {VaultFacet} from "../../src/multi/facets/VaultFacet.sol";
import {SimplexFacet} from "../../src/multi/facets/SimplexFacet.sol";
import {VaultType} from "../../src/multi/vertex/VaultPointer.sol";
import {ClosureId} from "../../src/multi/closure/Id.sol";
import {TokenRegistry} from "../../src/multi/Token.sol";
import {Store} from "../../src/multi/Store.sol";

contract Finding4PhantomShares is MultiSetupTest {
    TokenandVaultDeployer tokenandVaultDeployer;
    CustomFacet public customFacet;

    uint8 constant MAX_TOKENS = 16;

    uint256 public INIT_VALUE = 500e18;
    uint256 public MINT_AMOUNT = 1000000e18;
    uint256 public LP_AMOUNT = INIT_VALUE;
    int256 public SWAP_AMOUNT_POSITIVE = 100e18;

    address[MAX_TOKENS] tokensAddresses;
    TokenandVaultDeployer.TokenVaults[MAX_TOKENS] vaultAddresses;

    address charlie = makeAddr("charlie");
    address dave = makeAddr("dave");
    address eve = makeAddr("eve");

    address[] public actors = [owner, alice, bob, charlie, dave, eve];

    function setUp() public virtual{
        vm.startPrank(owner);
        _newDiamond();

        tokenandVaultDeployer = new TokenandVaultDeployer();
        // deploy token 0 - regular token
        tokensAddresses[0] = tokenandVaultDeployer.createToken("TOKEN0", "TKN0", 18);
        // deploy fee charging vault for token 0 :: 2% fee
        vaultAddresses[0] = tokenandVaultDeployer.createActiveandBackupFeeChargingVaultPairOneToken(tokensAddresses[0], 200);

        // deploy token 1 - regular token
        tokensAddresses[1] = tokenandVaultDeployer.createToken("TOKEN1", "TKN1", 18);
        // deploy regular vault 
        vaultAddresses[1] = tokenandVaultDeployer.createActiveandBackupVaultPairOneToken(tokensAddresses[1]);

        // Mint users tokens
        mintTokensMultipleUsers(actors, tokensAddresses[0], MINT_AMOUNT);
        mintTokensMultipleUsers(actors, tokensAddresses[1], MINT_AMOUNT);
        require(tokensAddresses.length == MAX_TOKENS);
        require(vaultAddresses.length == MAX_TOKENS);

        // Deploy custom facet
        _customFacet();
        customFacet = CustomFacet(diamond);

        vm.stopPrank();
    }

    /*//////////////////////////////////////////////////////////////
                            PROOF OF CONCEPT
    //////////////////////////////////////////////////////////////*/
    function test_Finding4_PhantomShares() public {
        // Add vertices
        addVertexWithBothVaults(0);
        addVertexWithBothVaults(1);

        // Create closure with base fee of 1% and protocol takes 10%
        address[] memory vertices = new address[](2);
        vertices[0] = tokensAddresses[0];
        vertices[1] = tokensAddresses[1];
        createClosure(vertices, uint128(LP_AMOUNT), 3402823669209384634633746074317682115, 34028236692093846346337460743176821146);
        printDivider("Closure Created");

        // Alice adds 500 value to each token, therefore pool is 1000 tokens for each token
        userAddsValueAndPrintEarnings(alice, "alice", tokensAddresses[0], 3);
        userAddsValueAndPrintEarnings(alice, "alice", tokensAddresses[1], 3);

        // Dave swaps from token 0 to token 1 and then back i times
        uint256 i= 0;
        /**
         * @dev tipping point is 876
         * 875 = PASS : 876 = FAIL
         */
        for(i = 0; i < 876; i++) {
            if (i% 2 == 0){
                userSwaps(dave, tokensAddresses[0], tokensAddresses[1], SWAP_AMOUNT_POSITIVE, 0, 3);
            } else {
                userSwaps(dave, tokensAddresses[1], tokensAddresses[0], SWAP_AMOUNT_POSITIVE, 0, 3);
            }
        }
        console.log("\n ----- Dave swaps %s times -----\n", i + 1);

        collectEarningsAndPrint(alice, "alice", 3);
    }

    /*//////////////////////////////////////////////////////////////
                                HELPERS
    //////////////////////////////////////////////////////////////*/
    /**
     *            ----- ADD VALUE & PRINT EARNINGS -----
     */
    function userAddsValueAndPrintEarnings(address user, string memory userString, address token, uint16 cId) public {
        console.log("\n----- %s adds liquidity-----", userString);
        userAddSingleForValue(user, cId, token, LP_AMOUNT / 2, 0, 0);
        printEarnings(user, userString, cId, tokensAddresses);
    }
    /**
     *                ----- COLLECT EARNINGS -----
     */
    function collectEarningsAndPrint(address user, string memory userString, uint16 cId) public {
        console.log("----- %s collects earnings -----", userString);
        printEarnings(user, userString, cId, tokensAddresses);
        vm.startPrank(user);
        valueFacet.collectEarnings(user, cId);
        vm.stopPrank();
    }
    /*//////////////////////////////////////////////////////////////
                      PROTOCOL BASIC ENTRY POINTS
    //////////////////////////////////////////////////////////////*/

    /**
     *                  ----- ADD VERTICES -----
     */

    function addVertexWithBothVaults(uint256 tokenId) public{
        _addVertexThroughSimplexFacet(tokensAddresses[tokenId], vaultAddresses[tokenId].active, VaultType.E4626);
        _addVertexThroughVaultFacet(tokensAddresses[tokenId], vaultAddresses[tokenId].backup, VaultType.E4626);
    }

    function addMaxVertices() public {
        for(uint256 i = 0; i < 16; i++){
            _addVertexThroughSimplexFacet(tokensAddresses[i], vaultAddresses[i].active, VaultType.E4626);
            _addVertexThroughVaultFacet(tokensAddresses[i], vaultAddresses[i].backup, VaultType.E4626);
        }
    }

    /**
     *                ----- ADD CLOSURE -----
     */

    function createClosure(address[] memory tokenAddrs, uint128 startingTarget, uint128 baseFeeX128, uint128 protocolTakeX128) public {
        if(tokenAddrs.length == 0) revert("No tokens provided");

        ClosureId cid = customFacet.getClosureId(tokenAddrs);

        _initialiseClosure( cid,  startingTarget, baseFeeX128, protocolTakeX128);
    }

    /**
     *               ----- USER ADDS VALUE -----
     */
    function userAddSingleForValue(address user, uint16 cId, address token, uint256 amount, uint256 bgtPercentX256, uint128 minValue) public returns (uint256 requiredBalance){
        _approveToken(user, diamond, token);
        vm.startPrank(user);
        (requiredBalance) = valueFacet.addSingleForValue(user, cId, token, uint128(amount), bgtPercentX256, minValue);
        vm.stopPrank();
    }
    /**
     *                ----- USER SWAPS -----
     */

    function userSwaps(address user, address tokenIn, address tokenOut, int256 amount, uint256 amountLimit, uint16 cId) public returns (uint256 inAmount, uint256 outAmount){
        _approveToken(user, diamond, tokenIn);
        vm.startPrank(user);
        (inAmount, outAmount) = swapFacet.swap(user, tokenIn, tokenOut, amount, amountLimit, cId);
        vm.stopPrank();
    }

    /*//////////////////////////////////////////////////////////////
                                  VIEW
    //////////////////////////////////////////////////////////////*/
    function viewEarnings(address user, uint16 cId) public view returns(uint256[MAX_TOKENS] memory earnings, uint256 bgtEarnings){
        (,,earnings, bgtEarnings) = _queryLPStandings(user, cId);
    }

    function printEarnings(address user, string memory userString, uint16 cId, address[16] memory tokenEarnings) public view {
        (uint256[MAX_TOKENS] memory earnings, uint256 bgtEarnings) = viewEarnings(user, cId);

        console.log("\nEarnings for user: ", userString);
        uint256 count = 0;
        for(uint256 i = 0; i < tokenEarnings.length; i++) {
            for(uint256 j = 0; j < MAX_TOKENS; j++) {
                if(tokenEarnings[i] == tokensAddresses[j]) {
                    if(earnings[j] > 0) {
                        console.log("Earnings for Token", j, ": ", earnings[j]);
                        count++;
                    }
                }
            }
        }
        if(count == 0) {
            console.log("No Earnings");
        }

        if(bgtEarnings > 0) {
            console.log("BGT Earnings: ", bgtEarnings);
        }
    }
    /*//////////////////////////////////////////////////////////////
                                  MISC
    //////////////////////////////////////////////////////////////*/
    function mintTokens(address recipient, address token, uint256 amount) public {
        _mintTokens(recipient, token, amount);
    }

    function mintTokensMultipleUsers(address[] memory recipients, address token, uint256 amount) public {
        for(uint256 i = 0; i < recipients.length; i++){
            _mintTokens(recipients[i], token, amount);
        }
    }

    function approveTokens(address approver, address approved, address token) public {
        _approveToken(approver, approved, token);
    }

    function printBalance(address user, string memory userString, address token) public view {
        for(uint256 i = 0; i < tokensAddresses.length; i++) {
            if(token == tokensAddresses[i]) {
                console.log("%s token %s balance: ", userString, i, MockERC20(token).balanceOf(user));
            }
        }
    }

    function printDivider(string memory dividerTitle) public pure{
        console.log("\n ----- %s -----", dividerTitle);
    }

   /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////
                                                                                                                                                
    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/

    /*//////////////////////////////////////////////////////////////
                            HELPERS-INTERNAL
    //////////////////////////////////////////////////////////////*/
    /**
     *               ----- CREATE CUSTOM FACET -----
     */
    
    function _customFacet() internal {
        IDiamond.FacetCut[] memory cuts = new IDiamond.FacetCut[](1);

        bytes4[] memory selectors = new bytes4[](1);
        selectors[0] = CustomFacet.getClosureId.selector;

        cuts[0] = (
            IDiamond.FacetCut({
                facetAddress: address(new CustomFacet()),
                action: IDiamond.FacetCutAction.Add,
                functionSelectors: selectors
            })
        );

        vm.startPrank(owner);
        DiamondCutFacet cutFacet = DiamondCutFacet(diamond);
        cutFacet.diamondCut(cuts, address(0), "");
        vm.stopPrank();
    }

    /**
     *                  ----- ADD VAULTS -----
     */

     // Used For first vault of a token
    function _addVertexThroughSimplexFacet(address _token, address _vaultAddress, VaultType _vaultType) internal {
        vm.startPrank(owner);
        simplexFacet.addVertex( _token, _vaultAddress, _vaultType);

        // mint admin tokens
        _mintTokens(owner, _token, INIT_VALUE);

        // Approve diamond to spend tokens
        _approveToken(owner, diamond, _token);

        vm.stopPrank();
    }

    // Used For second vault of a token
    function _addVertexThroughVaultFacet(address _token, address _vaultAddress, VaultType _vaultType) internal {
        vm.startPrank(owner);
        vaultFacet.addVault( _token, _vaultAddress, _vaultType);

        // Warp to when vault can be accepted
        vm.warp(block.timestamp + 5 days);

        vaultFacet.acceptVault(_token);

        // mint admin tokens
        _mintTokens(owner, _token, INIT_VALUE);

        // Approve diamond to spend tokens
        _approveToken(owner, diamond, _token);

        vm.stopPrank();
    }

    /**
     *                  ----- ADD CLOSURES -----
     */

    function _initialiseClosure(ClosureId cid, uint128 startingTarget, uint128 baseFeeX128, uint128 protocolTakeX128) internal {
        vm.startPrank(owner);
        simplexFacet.addClosure(ClosureId.unwrap(cid), startingTarget, baseFeeX128, protocolTakeX128);
        vm.stopPrank();
    }
    /*//////////////////////////////////////////////////////////////
                             VIEW INTERNAL
    //////////////////////////////////////////////////////////////*/
    function _queryLPStandings(address user, uint16 cId) public view returns (
            uint256 value,
            uint256 bgtValue,
            uint256[MAX_TOKENS] memory earnings,
            uint256 bgtEarnings
        ){
        (value, bgtValue, earnings, bgtEarnings) = valueFacet.queryValue(user, cId);
    }
    
    /*//////////////////////////////////////////////////////////////
                             MISC-INTERNAL
    //////////////////////////////////////////////////////////////*/

    function _mintTokens(address recipient, address token, uint256 amount) internal { 
        vm.startPrank(owner);
        MockERC20(token).mint(recipient, amount);
        vm.stopPrank();
    }

    function _approveToken(address approver, address approved, address token) internal {
        vm.startPrank(approver);
        MockERC20(token).approve(
            address(approved),
            type(uint256).max
        );
        vm.stopPrank();
    }
}

    /*//////////////////////////////////////////////////////////////
                            OTHER CONTRACTS
    //////////////////////////////////////////////////////////////*/
contract TokenandVaultDeployer is Test {

    struct TokenVaults {
        address active;
        address backup;
    }

    uint256 constant TOKENS = 16;
    // Helper function to create a single token with specified decimals
    function createToken(string memory name, string memory symbol, uint8 decimals) public returns (address token) {
        token = address(new MockERC20(name, symbol, decimals));
    }

    // Helper function to create a vault for a given token
    function createVault(address token, string memory name, string memory symbol) public returns (address vault) {
        vault = address(new MockERC4626(MockERC20(token), name, symbol));
    }
    function createFeeChargingVault(address token, string memory name, string memory symbol, uint256 feeRate) public returns (address vault) {
        vault = address(new FeeChargingERC4626(MockERC20(token), name, symbol, feeRate));
    }

    // Helper function to create active and backup vault for a given token
    function createActiveandBackupVaultPairOneToken(address token) public returns(TokenVaults memory vaults){
        vaults.active = createVault(token, "Active Vault", "AV");
        vaults.backup = createVault(token, "Backup Vault", "BV");
    }

    function createActiveandBackupFeeChargingVaultPairOneToken(address token, uint256 feeRate) public returns(TokenVaults memory vaults){
        vaults.active = createFeeChargingVault(token, "Active Vault", "AV", feeRate);
        vaults.backup = createFeeChargingVault(token, "Backup Vault", "BV", feeRate);
    }
}

contract CustomFacet {

    function getClosureId(address[] memory tokens) external view returns (ClosureId) {
    uint16 cid = 0;
    TokenRegistry storage tokenReg = Store.tokenRegistry();
    for (uint256 i = 0; i < tokens.length; ++i) {
        uint16 idx = uint16(1 << tokenReg.tokenIdx[tokens[i]]);
        cid |= idx;
    }
    return ClosureId.wrap(cid);
    }   
}

contract FeeChargingERC4626 is ERC4626 {
    using Math for uint256;
    
    // Fee represented as basis points (1/10000)
    // 200 = 2% fee
    uint256 private immutable _feeRate;
    
    constructor(
        ERC20 asset,
        string memory name,
        string memory symbol,
        uint256 feeRate // 200 = 2%
    ) ERC20(name, symbol) ERC4626(asset) {
        _feeRate = feeRate;
    }
    
    // Override to charge fee on deposit
    function _convertToShares(uint256 assets, Math.Rounding rounding) internal view override returns (uint256) {
        // Calculate fee amount
        uint256 feeAmount = (assets * _feeRate) / 10000;
        // Assets after fee
        uint256 assetsAfterFee = assets - feeAmount;
        
        // Use standard conversion with fee-adjusted assets
        return super._convertToShares(assetsAfterFee, rounding);
    }
    
    // Override to charge fee on withdrawal
    function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view override returns (uint256) {
        // First get raw assets
        uint256 assets = super._convertToAssets(shares, rounding);
        
        // Calculate fee amount
        uint256 feeAmount = (assets * _feeRate) / 10000;
        
        // Return assets after fee
        return assets - feeAmount;
    }
}
``` 

Failure trace:
```markdown
Logs:
 ReserveLib::deposit
  amount: 900000000000000001
  Shares Increment amount: 16815655212426292550
  total shares of token: 19916367968459224404463

 ReserveLib::deposit
  amount: 900000000000000001
  Shares Increment amount: 90000000000000000100
  total shares of token: 39555000000000000043800

 ReserveLib::deposit
  amount: 900000000000000001
  Shares Increment amount: 16757019498630589146
  total shares of token: 19933124987957854993609

 ReserveLib::deposit
  amount: 900000000000000001
  Shares Increment amount: 90000000000000000100
  total shares of token: 39645000000000000043900

emit InsufficientBalance(vid: 256, cid: 3, targetReal: 797297982547613806428 [7.972e20], realBalance: 97527497092793018884 [9.752e19])
...

    │   │   └─ ← [Revert] EvmError: Revert
    │   └─ ← [Revert] EvmError: Revert
    └─ ← [Revert] EvmError: Revert
```
The logs are just for the final 2 swaps before the revert, this shows:
1. The deposits are incrementing correctly by the same amount when dave is depositing token 1 into the regular vault.
2. Working incorrectly for token 0 into the fee charging vault and it getting cumulatively worse.

From the event emission, we see the system believes it has 7.972e20 tokens when it actually only has 9.752e19.


### Mitigation

Modify the ReserveLib.deposit function to calculate shares based on the actual amount deposited after fees, rather than the pre-fee amount