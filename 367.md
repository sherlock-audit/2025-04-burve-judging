Kind Midnight Troll

Medium

# DoS in `removeSingleForValue` when users want to remove `bgtValue` which is 100% of `value`

### Summary

The `Burve` protocol allows users to add `value` to `closures`, where they can specify how much of that `value` should be staked to `BGT`. Later, users can opt out to remove that value, either by extracting `value` directly from all tokens in the closure, or by specifying concrete token `values` or amounts. The protocol enforces strict rules that the `BGT` value should be `<=` to the `value`. However, there seems to be an issue when a user adds value and sets the `bgtValue` to be `==` to the added value. If he/she later tries to use `removeSingleForValue` and remove 100% `bgtValue`, the function will revert, as it will try to remove more `value` compared to the `bgtValue`.

### Root Cause

In `Closure::removeTokenForValue(...)`, after calculating what the `value` that needs to be removed is, the protocol does a [check](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/closure/Closure.sol#L371) to see if it is `0`, and rounds up if it is not. Afterwards, the `bgtValue` to be removed is calculated based on this `value`; however, it is rounded [down](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/closure/Closure.sol#L372). Because the protocol adds `1` to the `value`, it effectively artificially increases the amount being removed, meaning that the flow will revert later on in the `Assets::remove()` function if the user wants to remove 100% `bgtValue`. The `remove` function will deduct the said `value` and `bgtValue`, and will [compare](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/Asset.sol#L126-L129) them to ensure that `value >= bgtValue`. However, as mentioned, `value` will be increased by `1`, meaning that more `value` will be removed compared to `bgtValue`, leading to a revert.

### Internal Pre-conditions

1. Users calls `addValue` with `value = 100e18` and `bgtValue = 100e18`.
2. User calls `removeSingleForValue` with `bgtPercentX256 = type(uint256).max`, requiring the `bgValue` to be 100% of the removed `value`,

### External Pre-conditions

N/A

### Attack Path

N/A

### Impact

The user will be unable to remove 100% of `bgtValue` when removing single token amounts for value.

### PoC

I have created my own separate test suit:

<details>

<summary>Set-Up</summary>

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {IDiamond} from "Commons/Diamond/interfaces/IDiamond.sol";
import {DiamondCutFacet} from "Commons/Diamond/facets/DiamondCutFacet.sol";
import {InitLib, BurveFacets} from "../src/multi/InitLib.sol";
import {SimplexDiamond as BurveDiamond} from "../src/multi/Diamond.sol";
import {SimplexFacet} from "../src/multi/facets/SimplexFacet.sol";
import {LockFacet} from "../src/multi/facets/LockFacet.sol";
import {MockERC20} from "../test/mocks/MockERC20.sol";
import {MockERC4626} from "../test/mocks/MockERC4626.sol";
import {SwapFacet} from "../src/multi/facets/SwapFacet.sol";
import {ValueFacet} from "../src/multi/facets/ValueFacet.sol";
import {ValueTokenFacet} from "../src/multi/facets/ValueTokenFacet.sol";
import {VaultType} from "../src/multi/vertex/VaultProxy.sol";
import {IAdjustor} from "../src/integrations/adjustor/IAdjustor.sol";
import {NullAdjustor} from "../src/integrations/adjustor/NullAdjustor.sol";
import {Strings} from "openzeppelin-contracts/utils/Strings.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {VaultFacet} from "../src/multi/facets/VaultFacet.sol";
import {BGTExchanger} from "../src/integrations/BGTExchange/BGTExchanger.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "openzeppelin-contracts/token/ERC20/utils/SafeERC20.sol";

contract AuditTest is Test {
    using SafeERC20 for IERC20;
    /* Deployer */

    address deployerAddr = makeAddr("Deployer");

    uint256 constant INITIAL_MINT_AMOUNT = 1e30;
    uint128 constant INITIAL_VALUE = 1_000_000e18;

    /* Diamond */
    address public diamond;
    ValueFacet public valueFacet;
    ValueTokenFacet public valueTokenFacet;
    SimplexFacet public simplexFacet;
    SwapFacet public swapFacet;
    LockFacet public lockFacet;
    VaultFacet public vaultFacet;
    MockERC20 public USDC;
    MockERC20 public WETH;
    MockERC20 public WBTC;
    MockERC20 public tokenWithMoreDecimals;
    MockERC20 public bgtToken;

    MockERC4626 public USDC_Vault;
    MockERC4626 public WETH_Vault;
    MockERC4626 public WBTC_Vault;
    MockERC4626 public tokenWithMoreDecimals_Vault;

    /* Test Tokens */
    address[] public tokens;
    MockERC4626[] public vaults;

    BGTExchanger public bgtExchanger;

    function setUp() public {
        vm.startPrank(deployerAddr);
        BurveFacets memory facets = InitLib.deployFacets();
        diamond = address(new BurveDiamond(facets, "ValueToken", "BVT"));
        console2.log("Burve deployed at:", diamond);

        valueFacet = ValueFacet(diamond);
        valueTokenFacet = ValueTokenFacet(diamond);
        simplexFacet = SimplexFacet(diamond);
        swapFacet = SwapFacet(diamond);
        lockFacet = LockFacet(diamond);
        vaultFacet = VaultFacet(diamond);

        USDC = new MockERC20("USDC", "USDC", 6);
        WETH = new MockERC20("WETH", "WETH", 18);
        WBTC = new MockERC20("WBTC", "WBTC", 8);
        tokenWithMoreDecimals = new MockERC20("Token With More Decimals", "TWD", 20);
        bgtToken = new MockERC20("bgtToken", "bgtToken", 18);

        bgtExchanger = new BGTExchanger(address(bgtToken));
        simplexFacet.setBGTExchanger(address(bgtExchanger));
        bgtExchanger.addExchanger(address(diamond));
        bgtExchanger.setRate(address(USDC), 113427455640312814857969558651062452224);

        deal(address(bgtToken), address(deployerAddr), 600);

        bgtToken.approve(address(bgtExchanger), type(uint256).max);
        bgtExchanger.fund(200);

        deal(address(USDC), address(deployerAddr), 10000000e6);
        deal(address(WETH), address(deployerAddr), 10000000e18);
        deal(address(WBTC), address(deployerAddr), 10000000e8);
        deal(address(tokenWithMoreDecimals), address(deployerAddr), 10000000e20);

        //vm.label(address(USDC), "USDC");
        vm.label(address(WETH), "WETH");
        vm.label(address(WBTC), "WBTC");
        vm.label(address(tokenWithMoreDecimals), "Token With More Decimals");

        USDC_Vault = new MockERC4626(USDC, "USDC Vault", "USDC_Vault");
        WETH_Vault = new MockERC4626(WETH, "WETH Vault", "WETH_Vault");
        WBTC_Vault = new MockERC4626(WBTC, "WBTC Vault", "WBTC_Vault");
        tokenWithMoreDecimals_Vault =
            new MockERC4626(tokenWithMoreDecimals, "Token With More Decimals Vault", "TWD_Vault");

        vm.label(address(USDC_Vault), "USDC_Vault");
        vm.label(address(WETH_Vault), "WETH_Vault");
        vm.label(address(WBTC_Vault), "WBTC_Vault");
        vm.label(address(tokenWithMoreDecimals_Vault), "Token With More Decimals Vault");

        simplexFacet.addVertex(address(USDC), address(USDC_Vault), VaultType.E4626);
        simplexFacet.addVertex(address(WETH), address(WETH_Vault), VaultType.E4626);
        simplexFacet.addVertex(address(WBTC), address(WBTC_Vault), VaultType.E4626);
        simplexFacet.addVertex(address(tokenWithMoreDecimals), address(tokenWithMoreDecimals_Vault), VaultType.E4626);

        SafeERC20.forceApprove(USDC, address(diamond), 10000000000e6);
        SafeERC20.forceApprove(WETH, address(diamond), type(uint256).max);
        SafeERC20.forceApprove(WBTC, address(diamond), type(uint256).max);
        SafeERC20.safeIncreaseAllowance(tokenWithMoreDecimals, address(diamond), type(uint256).max);

        uint256 oneX128 = 1 << 128;
        uint128 baseFeeX128 = uint128(oneX128 / 1000);
        simplexFacet.addClosure(1, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(2, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(3, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(4, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(5, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(6, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(7, INITIAL_VALUE, baseFeeX128, baseFeeX128);
        simplexFacet.addClosure(15, INITIAL_VALUE, baseFeeX128, baseFeeX128);

        vm.stopPrank();
    }
}
```

</details>

The following test can be run with `forge test --mt testDosWithBgtValue`

```solidity
    function testDosWithBgtValue() public {
        deal(address(USDC), address(this), 10000e6);
        deal(address(WETH), address(this), 10000e18);
        deal(address(WBTC), address(this), 10000e8);
        deal(address(tokenWithMoreDecimals), address(this), 10000e20);

        USDC.approve(address(diamond), type(uint256).max);
        WETH.approve(address(diamond), type(uint256).max);
        WBTC.approve(address(diamond), type(uint256).max);
        tokenWithMoreDecimals.approve(address(diamond), type(uint256).max);

        valueFacet.addValue(address(this), 15, 100e18, 100e18);

        vm.expectRevert();
        // @audit - issue is coming from the fact that value is increased by 1, and bgtValue is rounded down
        valueFacet.removeSingleForValue(address(this), 15, address(USDC), 25e6, type(uint256).max, 0);
    }
```

```bash
    │   │   │   └─ ← [Stop] 
    │   │   └─ ← [Revert] InsufficientNonBgtValue(74999957387114203829 [7.499e19], 74999957387114203830 [7.499e19])
    │   └─ ← [Revert] InsufficientNonBgtValue(74999957387114203829 [7.499e19], 74999957387114203830 [7.499e19])
    └─ ← [Return] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 16.25ms (3.75ms CPU time)
```

### Mitigation

The fix is not straightforward, as the protocol does multiple rounding ups and downs. IMO, a solution would be to think of a better way to round up the `value` in `removeTokenForValue`, because currently it will always round up, as `(value << 128)` will only be `==` to `0`, if `value` is `0`.