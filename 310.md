Tart Tin Cod

Medium

# Bypassing the `realTax` when we remove value only for one token

** SUMMARY **

Users can bypass the taxes when they remove for one token value from the closure. This is possible when malicious user deposits value for a certain token calling `addValueSingle` and then add very small amounts for every token in the closure via `addValue`. After that simply call `removeValue` and remove the desirable value from every token but only has a significant amount in one token. 

** ROOT CAUSE **

LINKS : 
https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L58-L96

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L100-L135

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L176-L210

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L214-L244

** IMPACT **

Attacking scenario:

First, the malicious user deposits certain amount of certain token via `addValueSingle`. 

```Solidity

  function addValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 maxRequired
    ) external nonReentrant returns (uint256 requiredBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        (uint256 nominalRequired, uint256 nominalTax) = c.addValueSingle(
            value,
            bgtValue,
            vid
        );
        requiredBalance = AdjustorLib.toReal(token, nominalRequired, true);
        uint256 realTax = FullMath.mulDiv( // The user will pay taxes here.
            requiredBalance,
            nominalTax,
            nominalRequired
        );
        if (maxRequired > 0)
            require(requiredBalance <= maxRequired, PastSlippageBounds());
        TransferHelper.safeTransferFrom(
            token,
            msg.sender,
            address(this),
            requiredBalance
        );
        c.addEarnings(vid, realTax);
        Store.vertex(vid).deposit(cid, requiredBalance - realTax);
        Store.assets().add(recipient, cid, value, bgtValue);
    }

```

Then the user add value to every token in the closure via `addValue` . The value could be small and unsignificant, because of that there is no required minimum for deposit. 

```Solidity

 function addValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory requiredBalances)
    {
        if (value == 0) revert DeMinimisDeposit(); // Only checking for 0 value
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
        uint256[MAX_TOKENS] memory requiredNominal = c.addValue(
            value,
            bgtValue
        );
        // Fetch balances
        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue; // Irrelevant token.
            address token = tokenReg.tokens[i];
            uint256 realNeeded = AdjustorLib.toReal(
                token,
                requiredNominal[i],
                true
            );
            requiredBalances[i] = realNeeded; // adding the value 
            TransferHelper.safeTransferFrom(
                token,
                msg.sender,
                address(this),
                realNeeded
            );
            Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded);
        }
        Store.assets().add(recipient, cid, value, bgtValue);
    }
``` 
Then, the user calls `removeValue` removing value for every token, but in particular, the user has a significant amount only in one token. 

```Solidity

  function removeValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory receivedBalances)
    {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
        Store.assets().remove(msg.sender, cid, value, bgtValue); // All the tokens are stored in one variable
        uint256[MAX_TOKENS] memory nominalReceives = c.removeValue(
            value,
            bgtValue
        );
        // Send balances
        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue;
            address token = tokenReg.tokens[i];
            uint256 realSend = AdjustorLib.toReal(
                token,
                nominalReceives[i],
                false
            );
            receivedBalances[i] = realSend; // Send the balance of a token.
            // Users can remove value even if the token is locked. It actually helps derisk us.
            Store.vertex(VertexLib.newId(i)).withdraw(cid, realSend, false);
            TransferHelper.safeTransfer(token, recipient, realSend);
        }
    }

``` 
This way users can bypass the taxes implemented for removing value for one token.

** MITIGATION **

Adding minimal deposit value preventing from deposit unsignificant value  or implement taxes in `removeValue`.
