Sneaky Burlap Pigeon

High

# Almost Profit From `VaultE4626` Are Lost

# Almost Profit From `VaultE4626` Are Lost

### Summary
In the `E4626::commit` function, the deposit is ignored due to an incorrect offset between the deposit and withdrawal amounts.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/vertex/E4626.sol#L72-L73
```solidity
    function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        ...
        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            ...else if (assetsToWithdraw > assetsToDeposit) {
72:             assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit;
            } ...
        }
        ...
    }
```
As can be seen, `assetsToDeposit` is set to zero first, which is then deducted from `assetsToWithdraw`.
### Internal pre-conditions
bgtValueStaked > 0

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/vertex/Vertex.sol#L60
```solidity
60: function trimBalance(
        Vertex storage self,
        ClosureId cid,
        uint256 targetReal,
        uint256 value,
        uint256 bgtValue
    ) internal returns (uint256 reserveSharesEarned, uint256 bgtResidual) {
        VaultProxy memory vProxy = VaultLib.getProxy(self.vid);
        uint256 realBalance = vProxy.balance(cid, false);
        // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
        // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
            return (0, 0);
        }
        uint256 residualReal = realBalance - targetReal;
        vProxy.withdraw(cid, residualReal);
        bgtResidual = FullMath.mulDiv(residualReal, bgtValue, value);
        reserveSharesEarned = ReserveLib.deposit(
            vProxy,
            self.vid,
            residualReal - bgtResidual
        );
        vProxy.commit();
    }
```
In the `Vertex::trimBalance()` function, the yield from `VaultE4626` is withdrawn, deducted by `bgtResidual`, and then redeposited. This is recorded in `temp.vars`, and the action is performed in `vProxy.commit()`. However, while the withdrawal occurs, the deposit does not. Consequently, `VaultE4626.totalVaultShares` is less than it should be. These lost funds remain in this `diamond` contract as donation funds, and when the owner withdraws protocol fees these are withdrawn resulting the losses are distributed among users.

### Impact
Almost the yield from `VaultE4626` are lost. 
Users receive yield shares without underlying assets.
Due to the deflation of shares(totalshares not the totalVaultShares), the assets owned by the `closure` may be less than they should.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/vertex/E4626.sol#L72-L73
```diff
    function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        uint256 assetsToDeposit = temp.vars[1];
        uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
-72:            assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit;
+               assetsToDeposit = 0;
            } ...
        }
        ...
```

### Coded Poc
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_commit() public {
        uint256[4] memory alice_initBalances = getBalances(alice);
        uint256[4] memory bob_initBalances = getBalances(bob);
        vm.prank(alice);
        valueFacet.addValue(alice, 0x7, 3e23, 3e20);

        MockERC20(tokens[1]).mint(address(vaults[1]), 3e18);
        console.log("token1 diamond valance: %e\n", MockERC20(tokens[1]).balanceOf(diamond));

        vm.prank(alice);
        valueFacet.collectEarnings(alice, 0x7);
        vm.prank(alice);
        valueFacet.removeValue(alice, 0x7, 3e23, 3e20);
        
        uint256[4] memory alice_currentBalances = getBalances(alice);
        int256[4] memory alice_diffs = diffBalances(alice_currentBalances, alice_initBalances);
        console.log("alice_diffs[1]        : %e", alice_diffs[1]);
        console.log("token1 diamond valance: %e\n", MockERC20(tokens[1]).balanceOf(diamond));

        vm.prank(bob);
        valueFacet.addValue(bob, 0x7, 3e23, 3e20);

        MockERC20(tokens[1]).mint(address(vaults[1]), 2.9e18);

        vm.prank(bob);
        valueFacet.collectEarnings(bob, 0x7);
        vm.prank(bob);
        valueFacet.removeValue(bob, 0x7, 3e23, 3e20);

        uint256[4] memory bob_currentBalances = getBalances(bob);
        int256[4] memory bob_diffs = diffBalances(bob_currentBalances, bob_initBalances);
        console.log("  bob_diffs[1]        : %e", bob_diffs[1]);
        console.log("token1 diamond valance: %e", MockERC20(tokens[1]).balanceOf(diamond));
    }
```
forge test --match-test "test_commit" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_commit() (gas: 2159811)
Logs:
  token1 diamond valance: 0e0

  alice_diffs[1]        : 2.984986054692935324e18
  token1 diamond valance: 2.985074626776564936e18

    bob_diffs[1]        : -1e0
  token1 diamond valance: 2.985074626776564936e18
```
As can be seen, when the yield of `VaultE4626` is 3e18, Alice receives 2.984e18, while the owner takes 2.985e18 as protocol fees. After this, even if there is a subsequent yield of 2.9e18 from VaultE4626, Bob is unable to take the profit.

After mitigation, Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_commit() (gas: 2321754)
Logs:
  token1 diamond valance: 0e0

  alice_diffs[1]        : 2.985074626776564931e18
  token1 diamond valance: 0e0

    bob_diffs[1]        : 2.885571710677696914e18
  token1 diamond valance: 0e0
```