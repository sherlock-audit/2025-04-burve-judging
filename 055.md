Skinny Mocha Boa

Medium

# Unfair tax charged in `addSingleForValue` and `removeSingleForValue`

### Summary

The multi-token pool users are usually supposed to add liquidity by depositing a portion of all pool tokens.
To improve UX and simplify the deposit, users can deposit only one token to receive the value in return.

How this works under the hood is that the portions of the deposited token are swapped into the other tokens in the pool and assumed all tokens are deposited.
There are two functions to support this, which are `addSingleForValue` and `addValueSingle`.

As the one token deposit process requires swapping, the swap fee is charged to the deposited amount.
Assuming there are N tokens in the pool, it requires swapping the deposited token into N-1 other tokens, except the deposited token itself.

The issue here is that the tax calculation is correct in `addValueSingle` but incorrect in `addSingleForValue`, as implemented [`here`](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/closure/Closure.sol#L307-L308).

```solidity
    function addTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // For simplicity, we tax the entire amount in first. This overcharges slightly but an exact solution
        // would overcomplicate the contract and any approximation is game-able.
        tax = FullMath.mulX128(amount, self.baseFeeX128, true);
        amount -= tax;
        // Use the ValueLib's newton's method to solve for the value added and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky. We up the balance first for the ValueLib call, then set to do the checks.
        // We need to set the new target before we can setBalance, but we need up to balance to calc new target.
        self.balances[idx] += amount;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
                self.targetX128
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        value = ((newTargetX128 - self.targetX128) * self.n) >> 128; // Round down received value balance.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down.
        self.targetX128 = newTargetX128;
        // Now that we set the new target we can set balance to check validity.
        self.setBalance(idx, self.balances[idx]);
        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
    }
```

It first subtracts the tax from the amount before calculating the new `t` value, so this means is that the swap fee is charged to the deposited token itself.

This logic works same for `removeSingleForValue` as well.

### Root Cause

The root cause of the issue is that the implementation charges swap fees to the deposited/removed token itself.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

- Alice consumes X amount of a token to receive 100 value through `addValueSingle`.
- Bob also consumes X amount of the same token through `addSingleForValue` and receives less than 100 value.

### Impact

The tax is unfairly charged, which means the value received is less than the expected amount.

### PoC

Put this `Audit.t.sol` in `/test/facets`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ValueLib} from "../../src/multi/Value.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";

contract MultiTokenAuditTest is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);

        _newDiamond();
        _newTokens(3);
        _fundAccount(alice);
        _fundAccount(bob);

        vm.startPrank(owner);
        _initializeClosure(0x3, 100e18); // 1, 2
        _initializeClosure(0x5, 100e18); // 1, 3
        simplexFacet.setClosureFees(0x3, 1 << 125, 0); // 1% fee
        simplexFacet.setClosureFees(0x5, 1 << 125, 0); // 1% fee

        vm.stopPrank();
    }

    function testAuditTaxDiff() public {
        vm.prank(alice);
        uint256 requiredBalance = valueFacet.addValueSingle(alice, 0x3, 10e18, 0, tokens[0], 0);

        vm.prank(bob);
        uint256 receivedValue = valueFacet.addSingleForValue(bob, 0x5, tokens[0], uint128(requiredBalance), 0, 0);

        console.log("Received value", receivedValue);

        assertApproxEqAbs(receivedValue, 10e18, 1e16);
    }
}
```

When you run the test, you can see that Bob receives `93` value while Alice receives `100` value.

### Mitigation

The correct implementation would require some complex math to calculate the correct amount for the deposited token itself so that it can be excluded from the tax calculation.

As a simple solution(although it might not be perfect), the eligible amount for tax can be `amount * (N - 1) / N`.
