Urban Mango Walrus

High

# Missing Protection Against Deflation Attack

### Summary

Each `E4626` of `Vertex` is paired with an external ERC4626, and for every `E4626`, it maintains its own `totalShares` and `shares[cid]` for each `Closure`.

In the `deposit` or `withdraw` functions, the system first calculates the number of shares to mint or burn based on the specified asset `amount`, and then calls `ERC4626.deposit` or `ERC4626.withdraw`.

However, due to rounding errors, the actual amount deposited or withdrawn through `ERC4626.deposit` or `ERC4626.withdraw` may differ from the expected `amount`. This discrepancy can deflate the share value in `E4626`.

This can lead to incorrect distribution of funds among `Closures`, potentially causing other `Closures` to lose funds and emit an `InsufficientBalance` event, or attackers to gain unauthorized funds.

And also the dust assets locked in E4626 would remain locked forever, leading to potential loss of value and inefficiencies in the system.



### Root Cause

[E4626.sol#L62-L191](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L62-L191)

When users add liquidity or swap tokens, each `Closure` interacts with `Vertex`, `VaultProxy`, `VaultPointer`, and ultimately with `E4626`.

The `deposit` or `withdraw` function in `E4626` is called first, which calculates the number of shares to mint or burn based on the asset `amount` and the current market price.

```solidity
function deposit(
    VaultE4626 storage self,
    VaultTemp memory temp,
    ClosureId cid,
    uint256 amount
) internal {
    uint256 newlyAdding = FullMath.mulX128(
        temp.vars[1],
        temp.vars[3],
        true // Round up to round shares down.
    );
    uint256 totalAssets = temp.vars[0] + newlyAdding - temp.vars[2];

    uint256 discountedAmount = FullMath.mulX128(
        amount,
        temp.vars[3],
        false // Round down to round shares down.
    );
    uint256 newShares = totalAssets == 0
        ? discountedAmount
        : FullMath.mulDiv(self.totalShares, discountedAmount, totalAssets);
    self.shares[cid] += newShares;
    self.totalShares += newShares;
    temp.vars[1] += amount;
}
```

```solidity
function withdraw(
    VaultE4626 storage self,
    VaultTemp memory temp,
    ClosureId cid,
    uint256 amount
) internal {
    uint256 newlyAdding = FullMath.mulX128(
        temp.vars[1],
        temp.vars[3],
        false // Round down to round shares removed up.
    );
    uint256 totalAssets = temp.vars[0] + newlyAdding - temp.vars[2];
    uint256 sharesToRemove = FullMath.mulDiv(
        self.totalShares,
        amount,
        totalAssets
    );
    self.shares[cid] -= sharesToRemove;
    self.totalShares -= sharesToRemove;
    temp.vars[2] += amount;
}
```

After calculating the shares, the system calls the `commit` function to interact with the external `E4626`.

```solidity
function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
    uint256 assetsToDeposit = temp.vars[1];
    uint256 assetsToWithdraw = temp.vars[2];

    if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
        if (assetsToDeposit > assetsToWithdraw) {
            assetsToDeposit -= assetsToWithdraw;
            assetsToWithdraw = 0;
        } else if (assetsToWithdraw > assetsToDeposit) {
            assetsToDeposit = 0;
            assetsToWithdraw -= assetsToDeposit;
        } else {
            return;
        }
    }

    if (assetsToDeposit > 0) {
        SafeERC20.forceApprove(
            self.token,
            address(self.vault),
            assetsToDeposit
        );
        self.totalVaultShares += self.vault.deposit(
            assetsToDeposit,
            address(this)
        );
        SafeERC20.forceApprove(self.token, address(self.vault), 0);
    } else if (assetsToWithdraw > 0) {
        self.totalVaultShares -= self.vault.withdraw(
            assetsToWithdraw,
            address(this),
            address(this)
        );
    }
}
```

The shares minted are calculated using `FullMath.mulDiv(self.totalShares, discountedAmount, totalAssets)`, where the `totalAssets` is based on the redeemable value of all market shares held by `E4626`.

For example, the first depositor deposits `1` wei, which is deposited into a market, and mints `1` share. However, due to rounding, the market may not return any share to `E4626`. Repeating this process would mint `1` share without increasing `totalAssets`.

Similarly, if `totalAssets` on deposit increases to `1` but later becomes `0` due to market price changes, the same issue persists.

Suppose `10 wei` are deposited into `E4626`, which gives `10` shares to that `Closure` in `E4626`. Assume the share price in the underlying `ERC4626` is `2.1` assets per share. Now, if the `Closure` withdraws `9` assets, `E4626` would burn `9` shares and call `ERC4626.withdraw(9)`. However, since the share price is `2.1` assets per share, the actual withdrawal would be `10` assets, and `9` shares would be burned. This leads to a deflation of the share value in `E4626`.

The extra `1` asset would be locked in `E4626` but not counted in `totalAssets`.


### Internal Pre-conditions

1. A `Closure` is created, deposits `10` assets into `E4626`, and receives `10` shares.


### External Pre-conditions

1. In the underlying `ERC4626`, the share value is `2.1` assets per share.


### Attack Path

1. The attacker withdraws `9` assets, burns `9` shares, but in `ERC4626`, they actually burn `5` market shares and receive `10` assets.
2. The `totalAssets` in `E4626` is reduced by `10`, deflating the share value.
3. The extra `1` asset is locked in `E4626` but not counted in `totalAssets`.


### Impact

This vulnerability can be exploited to cause incorrect distribution of funds among `Closures`. This may result in other `Closures` losing funds and emitting an `InsufficientBalance` event, which could lead users to withdraw all assets from `Burve`. Additionally, liquidators in the affected `Closure` could gain unauthorized funds.

Moreover, the dust assets locked in `E4626` would remain locked forever, leading to potential loss of value and inefficiencies in the system.


### PoC

_No response_

### Mitigation

_No response_