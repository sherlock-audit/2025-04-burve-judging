Shiny Macaroon Raccoon

Medium

# Incorrect Order of valueStaked and bgtValueStaked Updates in addTokenForValue

# Incorrect Order of valueStaked and bgtValueStaked Updates in addTokenForValue

### Summary

In the `Closure::addTokenForValue` function, the contract updates `self.valueStaked` and `self.bgtValueStaked` **before** distributing the fee earnings, even though the added token amount includes the portion that will be taken as protocol and LP fees. This leads to the newly added user improperly participating in the fee distribution meant exclusively for existing liquidity providers, ultimately harming normal LP users’ interests.


### Root Cause

The issue is in `Closure.sol`’s `addTokenForValue`:

```solidity
function addTokenForValue(
    Closure storage self,
    VertexId vid,
    uint256 amount,
    uint256 bgtPercentX256,
    SearchParams memory searchParams
) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
...
    self.setBalance(idx, self.balances[idx]);
    self.valueStaked += value;
    self.bgtValueStaked += bgtValue;
}
```

The increases to `valueStaked` and `bgtValueStaked` must occur **after** the fee earnings are distributed.
When a user calls `ValueFacet::addSingleForValue`, the protocol and LPs take a fee, meaning part of the token amount provided by the user never enters the pool.
However, in `ValueFacet.sol`’s `addSingleForValue` function, it first calls `Closure::addTokenForValue`, which already updates `self.valueStaked` and `self.bgtValueStaked` **before** the fee is distributed.

```solidity
function addSingleForValue(
    address recipient,
    uint16 _closureId,
    address token,
    uint128 amount,
    uint256 bgtPercentX256,
    uint128 minValue
) external nonReentrant returns (uint256 valueReceived) {
    ClosureId cid = ClosureId.wrap(_closureId);
    Closure storage c = Store.closure(cid); // Validates cid.
    VertexId vid = VertexLib.newId(token); // Validates token.
    TransferHelper.safeTransferFrom(
        token,
        msg.sender,
        address(this),
        amount
    );
    SearchParams memory search = Store.simplex().searchParams;
    uint256 bgtValue;
    uint256 nominalTax;
    uint256 nominalIn = AdjustorLib.toNominal(token, amount, false); // Round down value deposited.
@>  (valueReceived, bgtValue, nominalTax) = c.addTokenForValue(   // @audit update self.valueStaked and self.bgtValueStaked
        vid,
        nominalIn,
        bgtPercentX256,
        search
    );
    require(valueReceived > 0, DeMinimisDeposit());
    require(valueReceived >= minValue, PastSlippageBounds());
    uint256 realTax = FullMath.mulDiv(amount, nominalTax, nominalIn);

@>  c.addEarnings(vid, realTax);  // @audit distribute the fee earnings
    Store.vertex(vid).deposit(cid, amount - realTax);
    Store.assets().add(recipient, cid, valueReceived, bgtValue);
}
```

The actual fee distribution happens **later**:

```solidity
c.addEarnings(vid, realTax);
Store.vertex(vid).deposit(cid, amount - realTax);
Store.assets().add(recipient, cid, valueReceived, bgtValue);
```

This means that when `addEarnings(vid, realTax)` runs, it uses the **updated** `self.valueStaked` and `self.bgtValueStaked`.
Thus, the user who just added tokens unfairly participates in the fee distribution, which is incorrect because the fee is intended to reward existing LPs.
The new user should **not** reclaim part of the extra fee they just paid.


### Attack Path

By updating the staked values before fee distribution, the system allows the new depositor to improperly dilute the share of the fees meant for long-term liquidity providers.
As a result, part of the user’s own fee contribution gets immediately cycled back to them, undermining the fairness of the reward system.


### Affected Code

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L160-L169

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L331-L332


### Impact

This misordering damages the interests of normal LP users by allowing new depositors to illegitimately share in the fees meant to reward existing liquidity. Over time, this unfair redistribution erodes LP rewards, distorts protocol incentives, and enables potential add-remove attackers to systematically exploit the reward mechanism.


### Mitigation

Reorder the logic so that `self.valueStaked` and `self.bgtValueStaked` are only updated **after** the call to `addEarnings`. This ensures that the new depositor’s stake does not improperly dilute the fee pool and that only existing LPs benefit from the collected protocol fees.