Sneaky Burlap Pigeon

Medium

# Users Lose Fees in the `addValueSingle` Function


### Summary
In the current implementation, `totalStaked` is managed within the `Closure`, while individual users' `stakedValue` is recorded in the `AssetBook`. In the `addValueSingle` function, the `addEarnings` function is executed after `Clouser.addValueSingle` and before `Store.assets().add`. As a result, the fees for the new adder's share are accounted for but remain inaccessible.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L132
```solidity
    function addValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 maxRequired
    ) external nonReentrant returns (uint256 requiredBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
113:    (uint256 nominalRequired, uint256 nominalTax) = c.addValueSingle(
            value,
            bgtValue,
            vid
        );
        requiredBalance = AdjustorLib.toReal(token, nominalRequired, true);
        uint256 realTax = FullMath.mulDiv(
            requiredBalance,
            nominalTax,
            nominalRequired
        );
        if (maxRequired > 0)
            require(requiredBalance <= maxRequired, PastSlippageBounds());
        TransferHelper.safeTransferFrom(
            token,
            msg.sender,
            address(this),
            requiredBalance
        );
132:    c.addEarnings(vid, realTax);
        Store.vertex(vid).deposit(cid, requiredBalance - realTax);
134:    Store.assets().add(recipient, cid, value, bgtValue);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_addValueSingle() public {
        
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 1000), 0);

        valueFacet.addValue(alice, 0x7, 3e21, 0);

        vm.prank(bob);
        valueFacet.addValueSingle(bob, 0x7, 2e21, 0, tokens[0], 0);

        uint256[4] memory alice_initBalances = getBalances(alice);
        uint256[4] memory bob_initBalances = getBalances(bob);
        
        vm.startPrank(alice);
        valueFacet.collectEarnings(alice, 0x7);

        vm.startPrank(bob);
        valueFacet.collectEarnings(bob, 0x7);
        
        uint256[4] memory alice_currentBalances = getBalances(alice);
        int256[4] memory alice_diffs = diffBalances(alice_currentBalances, alice_initBalances);
        console.log("alice_diffs0 : %e", alice_diffs[0]);

        uint256[4] memory bob_currentBalances = getBalances(bob);
        int256[4] memory bob_diffs = diffBalances(bob_currentBalances, bob_initBalances);
        console.log("  bob_diffs0 : %e", bob_diffs[0]);
    }
```
forge test --match-test "test_addValueSingle" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_addValueSingle() (gas: 1574631)
Logs:
  alice_diffs0 : 8.42142333538127037e17
    bob_diffs0 : 0e0
```
After mitigation, Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_addValueSingle() (gas: 1611125)
Logs:
  alice_diffs0 : 8.42142333538127037e17
    bob_diffs0 : 5.61428222358751359e17
```
After the mitigation, Bob(the adder) received the fee without impacting Alice(the previous user). 
This fee should be allocated to either the previous users or the new `adder`.
Since the `addValueSingle` function is equivalent to `addValue + swap`, the `adders` are entitled to the fees they pay.

### Impact
1. Some fees are locked in the vault.
2. Users lose their fees.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L132
```diff
    function addValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 maxRequired
    ) external nonReentrant returns (uint256 requiredBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        (uint256 nominalRequired, uint256 nominalTax) = c.addValueSingle(
            value,
            bgtValue,
            vid
        );
        requiredBalance = AdjustorLib.toReal(token, nominalRequired, true);
        uint256 realTax = FullMath.mulDiv(
            requiredBalance,
            nominalTax,
            nominalRequired
        );
        if (maxRequired > 0)
            require(requiredBalance <= maxRequired, PastSlippageBounds());
        TransferHelper.safeTransferFrom(
            token,
            msg.sender,
            address(this),
            requiredBalance
        );
-132:    c.addEarnings(vid, realTax);
        Store.vertex(vid).deposit(cid, requiredBalance - realTax);
        Store.assets().add(recipient, cid, value, bgtValue);
+        c.addEarnings(vid, realTax);
    }
```