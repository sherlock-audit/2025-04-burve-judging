Soft Basil Squid

High

# `SendBalance` of `BGTExchanger` allows `bgtToken` transfer and even it doesn't deduct `bgtBalance`

### Summary

The `SendBalance` function in the `BGTExchanger` contract allows the transfer of `bgtToken` without deducting the corresponding `bgtBalance`.

So it allows following exchanges when the actual `bgtBalance` is smaller than required bgtAmount.
And users can't withdraw their bgt balance expected.

### Root Cause

In `SendBalance` function of `BGTExchanger.sol`, it doesn't check if token is `bgtToken`.

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L110-L113


### Internal Pre-conditions

Admin call the `SendBalance` function with `bgtToken` as a transfer token

### External Pre-conditions

Users try to exchange `bgtToken` with the amount more than actual bgt token balance and withdraw the exchanged `bgtToken`

### Attack Path

User calls exchange function of `BGTExchanger` when it's `bgtBalance` is bigger than `actual bgt token balance` and try to withdraw it.
The amount of `bgtToken` that user exchange is bigger than actual bgt token balance of exchanger and is equal or smaller than `bgtBalance` of exchanger.

### Impact

Users are still allowed to exchange bgtToken when the actual balance of BGTExchanger is smaller than required amount and they can't withdraw their `bgtToken` exchanged.

### PoC

Add below code to `BGTExchanger.t.sol`

```solidity
import {IERC20Errors} from "openzeppelin-contracts/interfaces/draft-IERC6093.sol";

function testExchangeFailAfterSendBalanceBGT() public {
        // add alice as exchanger
        vm.startPrank(owner);
        bgtExchanger.addExchanger(alice);
        vm.stopPrank();

        // set USDC exchange rate
        vm.startPrank(owner);
        bgtExchanger.setRate(address(usdc), (2 << 128)); // 1:2
        vm.stopPrank();

        // fund exchanger
        deal(address(ibgt), address(this), 2e18);
        ibgt.approve(address(bgtExchanger), 2e18);
        bgtExchanger.fund(2e18);

        vm.startPrank(owner);

        // verfiy iBGT balance
        assertEq(bgtExchanger.bgtBalance(), 2e18);

        //// can send iBGT to alice
        uint256 recipientBalance = ibgt.balanceOf(alice);
        bgtExchanger.sendBalance(address(ibgt), alice, 2e18);
        assertEq(ibgt.balanceOf(alice), recipientBalance + 2e18);

        //// iBGT balance is unchanged even though actual balance is 0
        //// it allows following exchange, also user can't withdraw their balance
        assertEq(bgtExchanger.bgtBalance(), 2e18);
        assertEq(ibgt.balanceOf(address(bgtExchanger)), 0);

        // exchange USDC for iBGT
        vm.startPrank(alice);

        deal(address(usdc), alice, 1100);
        usdc.approve(address(bgtExchanger), 1000);

        (uint256 bgtAmount, uint256 spendAmount) = bgtExchanger.exchange(
            address(usdc),
            1000
        );

        vm.stopPrank();

        // check state
        assertEq(bgtExchanger.bgtBalance(), 2e18 - 2000, "bgtBalance");
        assertEq(bgtExchanger.owed(alice), 2000, "owed");

        // check balances
        assertEq(usdc.balanceOf(alice), 100, "alice USDC");
        assertEq(usdc.balanceOf(address(bgtExchanger)), 1000, "exchanger USDC");

        vm.startPrank(alice);

        vm.expectRevert();
        //// User can't withdraw their balance
        bgtExchanger.withdraw(alice, 2000);
        
        vm.stopPrank();
    }
```

### Mitigation

If protocol want to make the feature to transfer `bgtToken` via `SendBalance` function, it should deduct `bgtBalance` value when the `token` is `bgtToken`.
If not, it should prevent it.