Abundant Boysenberry Cod

Medium

# Inflation of nominal liquidity due to depositing less in the island (due to idle balances)

### Summary

When users mint into the `Burve` pool, they specify `mintNominalLiq`, a nominal liquidity amount that is distributed into the island according to [`[distX96](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L253)`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L253).

However, during the island minting process (`Burve::mintIsland`), liquidity is first estimated via `Burve::getAmountsForLiquidity`, and then actual token amounts are minted into the island:

```solidity
function mintIsland(address recipient, uint128 liq) internal returns (uint256 mintIslandShares) {
    (uint160 sqrtRatioX96,,,,,,) = pool.slot0();

    (uint256 amount0, uint256 amount1) =
        getAmountsForLiquidity(sqrtRatioX96, liq, island.lowerTick(), island.upperTick(), true);

    (uint256 mint0, uint256 mint1, uint256 mintShares) = island.getMintAmounts(amount0, amount1);

    islandSharesPerOwner[recipient] += mintShares;
    totalIslandShares += mintShares;
}
```

The issue arises because `KodiakIsland` internally accounts for accumulated fees and idle token balances when computing minted shares:

```solidity
function _computeMintAmounts(uint256 totalSupply, uint256 amount0Max, uint256 amount1Max) private view returns (uint256 amount0, uint256 amount1, uint256 mintAmount) {
    (uint256 amount0Current, uint256 amount1Current) = getUnderlyingBalances();

    if (amount0Current == 0 && amount1Current > 0) {
        mintAmount = FullMath.mulDiv(amount1Max, totalSupply, amount1Current);
    } else if (amount1Current == 0 && amount0Current > 0) {
        mintAmount = FullMath.mulDiv(amount0Max, totalSupply, amount0Current);
    } else if (amount0Current == 0 && amount1Current == 0) {
        revert("");
    } else {
        uint256 amount0Mint = FullMath.mulDiv(amount0Max, totalSupply, amount0Current);
        uint256 amount1Mint = FullMath.mulDiv(amount1Max, totalSupply, amount1Current);
        require(amount0Mint > 0 && amount1Mint > 0, "mint 0");

        mintAmount = amount0Mint < amount1Mint ? amount0Mint : amount1Mint;
    }

    amount0 = FullMath.mulDivRoundingUp(mintAmount, amount0Current, totalSupply);
    amount1 = FullMath.mulDivRoundingUp(mintAmount, amount1Current, totalSupply);
}
```

Since the actual token amounts (`mint0`, `mint1`) are less than originally estimated (`amount0`, `amount1`), the user deposits less than expected but still gets credited for the original higher liquidity amount. This inflates the user's nominal liquidity share relative to other users.

### Root Cause

- `mint0` and `mint1` are reduced by internal island logic after accumulated fees and idle balances are considered.
- The original `liq` amount is not recalculated based on the final deposited amounts.

Thus, `mintNominalLiquidity` is overstated, causing liquidity share inflation.

### Internal Pre-conditions

- Island weight is non-zero.

### External Pre-conditions

- Island holds accumulated fees or leftover idle token balances.

### Attack Path

1. The island has idle balances or fees.
2. The user initiates a mint.
3. The user deposits lower actual token amounts while retaining the original high nominal liquidity.
4. The user obtains an inflated share of the pool at the expense of others.

### Impact

- **Liquidity inflation:** Users gain a disproportionate share.
- **Dilution of honest users.**
- **Long-term economic imbalance** in the pool.

### PoC
Run this PoC in a Berachain forked enviroment:
```solidity
    function testLiquidityProviding() public {
        IUniswapV3Pool _pool = IUniswapV3Pool(0xDDD01143BF7639b40C7063a9376052fB9a7DD640);
        IKodiakIsland _island = IKodiakIsland(0xF06EA29FCF4765200742d29E685973a1870EaC98);

        (uint160 sqrtRatioX96,,,,,,) = _pool.slot0();

        uint160 sqrtRatioAX96 = TickMath.getSqrtRatioAtTick(_island.lowerTick());
        uint160 sqrtRatioBX96 = TickMath.getSqrtRatioAtTick(_island.upperTick());

        (uint256 amount0Liq, uint256 amount1Liq) =
            LiquidityAmounts.getAmountsForLiquidity(sqrtRatioX96, sqrtRatioAX96, sqrtRatioBX96, 1e18, true);

        (uint256 mint0, uint256 mint1, uint256 mintShares) = _island.getMintAmounts(amount0Liq, amount1Liq);

        console2.log("liq0 ", amount0Liq);
        console2.log("mint0", mint0);

        console2.log("liq1 ", amount1Liq);
        console2.log("mint1", mint1);

        
         uint128 liq =LiquidityAmounts.getLiquidityForAmounts(sqrtRatioX96, sqrtRatioAX96, sqrtRatioBX96, mint0, mint1);

          assert(liq < 1e18);
    }
}
```

LOGS:

```bash
  liq0  23380500898
  mint0 23380500898
  liq1  9690693377824765039255185
  mint1 9690083747021064755040931
```

This confirms that after `island.getMintAmounts`, the effective liquidity is **lower** but still credited at a higher initial amount.

### Mitigation

- After obtaining `mint0` and `mint1`, **recalculate** the effective liquidity.
- Adjust the minted `mintNominalLiquidity` accordingly.

This guarantees that users must deposit fair token amounts for their claimed liquidity shares, preserving fairness and pool integrity.