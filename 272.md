Sneaky Burlap Pigeon

Medium

# Underflow Issue in Newton's Convergence Method


### Summary
When adding a specified amount of tokens, the `targetValue` is calculated using Newton's method. However, Newton's method is only applicable in monotonic sections, and `(e + 1) * t - (e + 1) * (e + 1) * t * t / (x + e * t)` does not guarantee monotonicity. 
Consequently, `ft/dfdt`  can point in the opposite direction, and `dfdt` can even be zero.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L194
The issue arises from the implementation found at `Value.sol#L194`. In the calculation of `nextTX128`, an underflow could occur.

```solidity
    function stepT(
        uint256 tX128,
        uint256[] memory esX128,
        uint256[] memory xs,
        int256 ftX128
    ) internal pure returns (uint256 nextTX128) {
        int256 dftX128 = dfdt(tX128, esX128, xs);
        bool posNum = ftX128 > 0;
        bool posDenom = dftX128 > 0;
        if (posNum && posDenom) {
194:        nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(dftX128));
        } else if (posNum && !posDenom) {
            nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(-dftX128));
        } else if (!posNum && posDenom) {
            nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(dftX128));
        } else {
            nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(-dftX128));
        }
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
The PoC below demonstrates that the user cannot deposit 9e20 of token1 using the `addSingleForValue` function, even though they can successfully deposit 1.2e21 of token1 using the `addValueSingle` function.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_convergence1() public {
        uint256[4] memory initBalances = getBalances(alice);
        vm.prank(alice);
        valueFacet.addValueSingle(alice, 0x3, 11e20, 0, tokens[1], 0);
        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("diffs[1]  : %e", diffs[1]);
    }
    function test_convergence2() public {
        uint256[4] memory initBalances = getBalances(alice);
        vm.prank(alice);
        valueFacet.addSingleForValue(alice, 0x3, tokens[1], 9e20, 0, 0);
        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("diffs[1]  : %e", diffs[1]);
    }
```
forge test --match-test "test_convergence" -vv
Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_convergence1() (gas: 553006)
Logs:
  diffs[1]  : -1.2e21

[FAIL: panic: arithmetic underflow or overflow (0x11)] test_convergence2() (gas: 283211)
```
After mitigation ,Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_convergence1() (gas: 553006)
Logs:
  diffs[1]  : -1.2e21

[PASS] test_convergence2() (gas: 637032)
Logs:
  diffs[1]  : -9e20
```

### Impact
Time sensitiviy function is DoSed

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L194
```diff
    function stepT(
        uint256 tX128,
        uint256[] memory esX128,
        uint256[] memory xs,
        int256 ftX128
    ) internal pure returns (uint256 nextTX128) {
        int256 dftX128 = dfdt(tX128, esX128, xs);
        bool posNum = ftX128 > 0;
        bool posDenom = dftX128 > 0;
        if (posNum && posDenom) {
+            if (dftX128 != 0 && tX128 >= FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(dftX128))) {
194:        nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(dftX128));
+            } else {
+                nextTX128 = type(uint256).max;
+                for (uint256 i = 0; i < xs.length; i++) {
+                    if (xs[i] > 0 && nextTX128 >= xs[i] * (esX128[i] + TWOX128))
+                        nextTX128 = xs[i] * (esX128[i] + TWOX128);
+                }}
        } else if (posNum && !posDenom) {
            nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(ftX128), 1 << 128, uint256(-dftX128));
        } else if (!posNum && posDenom) {
            nextTX128 =
                tX128 +
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(dftX128));
        } else {
            nextTX128 =
                tX128 -
                FullMath.mulDiv(uint256(-ftX128), 1 << 128, uint256(-dftX128));
        }
    }
```
This mitigation is not a complete solution, but it mitigates the scenarios in this report.