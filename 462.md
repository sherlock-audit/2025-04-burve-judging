Cool Canvas Butterfly

High

# Deposit less amount of liquidity when redepositing interest and incorrectly update totalNominalLiq

### Summary


When reinvesting interest, the amount to be deposited in each range is calculated incorrectly. Interest for Kodiak islands weight remains in the contract without being reinvested. However, `totalNominalLiq` is recorded as if this amount of liquidity was added. When attempting reinvestment again, `totalNominalLiq` increases more than actual due to the remaining tokens.

### Root Cause


Interest earned from pool is collected and reinvested back into the pool. Only UniV3 pools are eligible for reinvestment, while Kodiak islands are not. The distribution is based on specified weights for each price range.

To apply weights, `weight * 2^96/weight_sum` is calculated and stored at `distX96` . Kodiak islands also has a weight and is stored in the distX96 array. For example, with Kodiak, range1, and range2 having weights of 30:35:35, `distX96` stores `[30 * 2^96 / 100, 35 * 2^96 / 100, 35 * 2^96 / 100]`.

In `compoundV3Ranges`, deposits are made proportionally to each range's weight. `collectAndCalcCompound` calculates the amount of liquidity that could be minted when depositing tokens(collected interest and existing tokens) in the contract and adds this to `totalNominalLiq`.

`compoundLiq` calculates and stores the amount of liquidity to mint for each range. In the above example (30:35:35), the results would be:

- compoundLiq[0]: 0 (Kodiak island)
- compoundLiq[1]: `(compoundedNominalLiq * (35 * 2^96 / 100)) * 2^96`
- compoundLiq[2]: `(compoundedNominalLiq * (35 * 2^96 / 100)) * 2^96`

Only 70% of `compoundedNominalLiq` is actually deposited in the pool. However, when updating `totalNominalLiq`, it records that `compoundedNominalLiq` amount of liquidity was added. The remaining 30% stays in the Burve contract and is used in the next `compoundV3Ranges` call, causing `totalNominalLiq` to accumulate duplicates. [single/Burve.sol#L739](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/single/Burve.sol#L739)

```solidity
  function compoundV3Ranges() internal {
      // collect fees
      collectV3Fees();

      (uint160 sqrtRatioX96, , , , , , ) = pool.slot0();

      uint128 compoundedNominalLiq = collectAndCalcCompound();
      if (compoundedNominalLiq == 0) {
          return;
      }

@>    totalNominalLiq += compoundedNominalLiq;

      // calculate liq and mint amounts
      uint256 totalMint0 = 0;
      uint256 totalMint1 = 0;

      TickRange[] memory memRanges = ranges;
      uint128[] memory compoundLiqs = new uint128[](distX96.length);

      for (uint256 i = 0; i < distX96.length; ++i) {
          TickRange memory range = memRanges[i];

@>        if (range.isIsland()) {
              continue;
          }

          uint128 compoundLiq = uint128(
@>            shift96(uint256(compoundedNominalLiq) * distX96[i], true)
          );
          compoundLiqs[i] = compoundLiq;

          if (compoundLiq == 0) {
              continue;
          }

          (uint256 mint0, uint256 mint1) = getAmountsForLiquidity(
              sqrtRatioX96,
              compoundLiq,
              range.lower,
              range.upper,
              true
          );
          totalMint0 += mint0;
          totalMint1 += mint1;
      }

      // approve mints
      SafeERC20.forceApprove(token0, address(this), totalMint0);
      SafeERC20.forceApprove(token1, address(this), totalMint1);

      // mint to each range
      for (uint256 i = 0; i < distX96.length; ++i) {
          TickRange memory range = memRanges[i];

          if (range.isIsland()) {
              continue;
          }

          uint128 compoundLiq = compoundLiqs[i];
          if (compoundLiq == 0) {
              continue;
          }

          pool.mint(
              address(this),
              range.lower,
              range.upper,
@>            compoundLiq,
              abi.encode(address(this))
          );
      }

      // reset approvals
      SafeERC20.forceApprove(token0, address(this), 0);
      SafeERC20.forceApprove(token1, address(this), 0);
}
```


### Internal Pre-conditions

1. Kodiak islands and UniV3 ranges are set


### External Pre-conditions

None

### Attack Path

1. Trigger `compoundV3Ranges` by calling mint or burn. The problem is that compoundV3Ranges is behaving incorrectly. 


### Impact

While `totalNinalLiq` records that `compoundedNinalLiq` has been added, the actual liquidity added is 100-n% of `compoundedNinalLiq`.  (where n is Kodiak's weight ratio). The remaining n% that isn't deposited is used in the next interest reinvestment, causing duplicate updates to `totalNominalLiq`.

When `totalNominalLiq` becomes larger than actual, `mint` issues fewer Burve shares than should be issued, preventing the protocol from functioning properly.


### PoC

_No response_

### Mitigation

Distribute the interest using `dist_univ3_X96`, which is calculated using only the weights between the UniV3 ranges, excluding Kodiak island.