Orbiting Zinc Parrot

Medium

# Incorrect amount approval can cause DoS in `Burve::mintIlsland(...)`

### Summary

Burve gives approval to island for the amounts provided by `Burve::getAmountsForLiquidity(...)` but the amounts used by Kodiak island can be very different from that.

### Root Cause

In kodiak island, only the initial liquidity is provided using the `LiquidityAmounts.getAmountsForLiquidity(...)` function. After that the tokens amount are calculated on the basis of the proportion of the assets we currently hold which also includes any fee or any amount directly transferred to the kodiak island:

```solidity
    function mint(uint256 mintAmount, address receiver) external whenNotPaused nonReentrant returns (uint256 amount0, uint256 amount1, uint128 liquidityMinted) {
        require(!restrictedMint || msg.sender == _manager || !isManaged(), "restricted");

        uint256 totalSupply = totalSupply();

        (uint160 sqrtRatioX96,,,,,,) = pool.slot0();

        if (totalSupply > 0) {
            require(mintAmount > 0, "mint 0");
            (uint256 amount0Current, uint256 amount1Current) = getUnderlyingBalances();

@>            amount0 = FullMath.mulDivRoundingUp(amount0Current, mintAmount, totalSupply);
@>            amount1 = FullMath.mulDivRoundingUp(amount1Current, mintAmount, totalSupply);
        } else {
            // if supply is 0 mintAmount == liquidity to deposit
@>            (amount0, amount1) = LiquidityAmounts.getAmountsForLiquidity(sqrtRatioX96, lowerTick.getSqrtRatioAtTick(), upperTick.getSqrtRatioAtTick(), SafeCast.toUint128(mintAmount));

            _mint(address(0), INITIAL_MINT); // Solution to this issue: https://github.com/transmissions11/solmate/issues/178
            mintAmount = mintAmount - INITIAL_MINT;
        }
```

But in the `Burve::mintIlsland(...)` function, this amount is calculated using `Burve::getAmountsForLiquidity(...)` function which internally uses the same `LiquidityAmounts.getAmountsForLiquidity(...)`. Then we are approving the island for the amounts returned by this function for minting the island shares:

```solidity
function mintIsland(
        address recipient,
        uint128 liq
    ) internal returns (uint256 mintIslandShares) {
        (uint160 sqrtRatioX96, , , , , , ) = pool.slot0();

@>        (uint256 amount0, uint256 amount1) = getAmountsForLiquidity(
            sqrtRatioX96,
            liq,
            island.lowerTick(),
            island.upperTick(),
            true
        );
        (uint256 mint0, uint256 mint1, uint256 mintShares) = island
            .getMintAmounts(amount0, amount1);

        islandSharesPerOwner[recipient] += mintShares;
        totalIslandShares += mintShares;

        // transfer required tokens to this contract
        TransferHelper.safeTransferFrom(
            address(token0),
            msg.sender,
            address(this),
            mint0
        );
        TransferHelper.safeTransferFrom(
            address(token1),
            msg.sender,
            address(this),
            mint1
        );

        // approve transfer to the island
@>        SafeERC20.forceApprove(token0, address(island), amount0);
@>        SafeERC20.forceApprove(token1, address(island), amount1);

        island.mint(mintShares, address(this));

        SafeERC20.forceApprove(token0, address(island), 0);
        SafeERC20.forceApprove(token1, address(island), 0);

        // deposit minted shares to the station proxy
        SafeERC20.forceApprove(island, address(stationProxy), mintShares);
        stationProxy.depositLP(address(island), mintShares, recipient);
        SafeERC20.forceApprove(island, address(stationProxy), 0);

        return mintShares;
    }
```

Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L328C1-L331C66)

Now this amounts ratio returned by kodiak island can be very different from these calculated amount and one of them could be more than the amount returned by the `LiquidityAmounts` library and can cause revert due to insufficient approval. 

Although the amounts transferred from the user will be correct as we are collecting the amounts returned by `island::getMintAmounts(...)` function but the approval amount is wrong. 

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

N/a

### Impact

Can cause DoS if the island token ratio changes due to fees or any other case.

### PoC

Kind of submitting this at last minute. Will provide later if necessary.

### Mitigation

Use the amounts returned by `island::getMintAmounts(...)` for approvals as well.