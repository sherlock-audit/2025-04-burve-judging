Damaged Admiral Grasshopper

High

# Simplex ownership cannot be transferred

### Summary

`acceptOwnership()` is missed when adding facets to SimplexDiamond, leading to the ownership cannot be transferred.

### Root Cause

When creates `SimplexDiamond`, BaseAdminFacet is added to the diamond.

[SimplexDiamond::constructor()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/Diamond.sol#L67-L77):
```solidity
        {
            bytes4[] memory adminSelectors = new bytes4[](3);
@>          adminSelectors[0] = BaseAdminFacet.transferOwnership.selector;
@>          adminSelectors[1] = BaseAdminFacet.owner.selector;
@>          adminSelectors[2] = BaseAdminFacet.adminRights.selector;
            cuts[2] = FacetCut({
                facetAddress: address(new BaseAdminFacet()),
                action: FacetCutAction.Add,
                functionSelectors: adminSelectors
            });
        }
``` 

`transferOwnership()` in `BaseAdminFacet` does not transfer the ownership directly to the new owner, it requires the new pending owner to accept the ownership.

[BaseAdminFacet::transferOwnership()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/lib/Commons/src/Util/Admin.sol#L154-L156):
```solidity
    function transferOwnership(address _newOwner) external override {
        AdminLib.reassignOwner(_newOwner);
    }
```

[BaseAdminFacet::acceptOwnership()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/lib/Commons/src/Util/Admin.sol#L162-L166):
```solidity
    /// The pending owner can accept their ownership rights.
    function acceptOwnership() external {
        emit IERC173.OwnershipTransferred(AdminLib.getOwner(), msg.sender);
        AdminLib.acceptOwnership();
    }
```

The problem is that `acceptOwnership()` is not added to the cut, as a result, the ownership won't be accepted.

### Internal Pre-conditions

Simplex owner transfers the ownership.

### External Pre-conditions

None

### Attack Path

None

### Impact

The new pending owner won't be able to accept the ownership.

### PoC

Please run `forge test --mt testAudit_SimplexOwnershipCannotBeTransferred`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";

contract AuditTest is Test {

    address owner = makeAddr("OWNER");

    SimplexDiamond simplex;

    function setUp() public {

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        vm.prank(owner);
        simplex = new SimplexDiamond(facets, "ValueToken", "BVT");
    }

    function testAudit_SimplexOwnershipCannotBeTransferred() public {
        address newOwner = makeAddr("NEW OWNER");

        bool success;
        bytes memory returnData;

        vm.prank(owner);
        (success, ) = address(simplex).call(
            abi.encodeWithSelector(BaseAdminFacet.transferOwnership.selector, newOwner)
        );
        require(success, "Transfer Ownership failed");

        vm.prank(newOwner);
        (success, returnData) = address(simplex).call(
            abi.encodeWithSelector(BaseAdminFacet.acceptOwnership.selector)
        );
        // Ownership cannot be accepted
        assertFalse(success);

        (success, returnData) = address(simplex).call(
            abi.encodeWithSelector(BaseAdminFacet.owner.selector)
        );
        require(success, "Get owner failed");

        // The owner does not change
        address currentOwner = abi.decode(returnData, (address));
        assertEq(currentOwner, owner);
    }
}
```

### Mitigation

Add `acceptOwnership()` to admin cut.

```diff
        {
-           bytes4[] memory adminSelectors = new bytes4[](3);
+           bytes4[] memory adminSelectors = new bytes4[](4);
            adminSelectors[0] = BaseAdminFacet.transferOwnership.selector;
            adminSelectors[1] = BaseAdminFacet.owner.selector;
            adminSelectors[2] = BaseAdminFacet.adminRights.selector;
+           adminSelectors[3] = BaseAdminFacet.acceptOwnership.selector;
            cuts[2] = FacetCut({
                facetAddress: address(new BaseAdminFacet()),
                action: FacetCutAction.Add,
                functionSelectors: adminSelectors
            });
        }
```