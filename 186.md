Interesting Taupe Ostrich

Medium

# Missing `acceptOwnership` selector in Diamond-cuts prevents ownership transfer

### Summary

The `acceptOwnership` selecter from the `BaseAdminFacet` is missing from the diamond cuts. Since this is not included, it cannot be called through the diamond contract resulting in the inability to accept ownership upon transfer.


### Root Cause

Issue arises in the `SimplexDiamond` [constructor](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/Diamond.sol#L68-L76) where the `BaseAdminFacet` selectors are defined.
```solidity
        {
            bytes4[] memory adminSelectors = new bytes4[](3);
            adminSelectors[0] = BaseAdminFacet.transferOwnership.selector;
            adminSelectors[1] = BaseAdminFacet.owner.selector;
            adminSelectors[2] = BaseAdminFacet.adminRights.selector;
            // @audit-issue one function ommited (acceptOwnership())
            cuts[2] = FacetCut({
                facetAddress: address(new BaseAdminFacet()),
                action: FacetCutAction.Add,
                functionSelectors: adminSelectors
            });
        }
```
While `transferOwnership`, `owner`, and `adminRights` selectors are included, the `acceptOwnership` selector was omitted from the array of admin selectors.


### Internal Pre-conditions

1. The diamond contract must be deployed
2. The `BaseAdminFacet` must be installed on the diamond
3. The current owner must initiate a transfer of ownership

### External Pre-conditions

1. The intended new owner must attempt to accept ownership


### Attack Path

1. Current owner calls `transferOwnership(newOwner)` on the diamond contract
2. The `BaseAdminFacet` processes the transfer request and updates the pending owner
3. The new owner attempts to call `acceptOwnership()` to complete the transfer
4. The diamond contract's fallback function tries to find the `acceptOwnership` selector
5. Since the selector is missing from the diamond cuts, the fallback function reverts with `FunctionNotFound`


### Impact

This vulnerability prevents the proper transfer of ownership of the diamond contract. While ownership can be initiated, it cannot be completed, leaving the contract in an inconsistent state where:
1. The new owner cannot accept ownership
2. The ownership transfer process is permanently stuck

### PoC

// (1) In `MultiSetup.u.sol`,
```diff
    // Add this New import
+   import {BaseAdminFacet} from "Commons/Util/Admin.sol";

    // make the following modifications
    contract MultiSetupTest is Test {
         /* Diamond */
        ---snip---
        // Add this
+       BaseAdminFacet public baseAdminFacet;

        ---snip---

        function _newDiamond() internal {
            ---snip---
            // Add this
+           baseAdminFacet = BaseAdminFacet(diamond);
            ---snip---
        }
    }
```
// (2) Create a new test file => `test/facets/BaseAdminFacet.t.sol` and paste the following:
```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";

contract BaseAdminFacetTest is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(3);
        _initializeClosure(0x7);
        vm.stopPrank();
    }

    function testAcceptOwnershipRevertsWithFunctionNotFound() public {
        vm.startPrank(owner);
        // owner initiates transfer to Alice
        baseAdminFacet.transferOwnership(alice);
        vm.stopPrank();

        // alice attempts to accept ownership
        vm.startPrank(alice);
        // This reverts with FunctionNotFound(0x79ba5097)
        vm.expectRevert();
        baseAdminFacet.acceptOwnership();
        vm.stopPrank();
    }
}
```
// (3) Run: `forge test --mt testAcceptOwnershipRevertsWithFunctionNotFound -vvvv`

### Mitigation

Include this selector in the diamond cuts:
```diff
        {
-           bytes4[] memory adminSelectors = new bytes4[](3);
+           bytes4[] memory adminSelectors = new bytes4[](4);
            adminSelectors[0] = BaseAdminFacet.transferOwnership.selector;
            adminSelectors[1] = BaseAdminFacet.owner.selector;
            adminSelectors[2] = BaseAdminFacet.adminRights.selector;
+           adminSelectors[3] = BaseAdminFacet.acceptOwnership.selector;
            cuts[2] = FacetCut({
                facetAddress: address(new BaseAdminFacet()),
                action: FacetCutAction.Add,
                functionSelectors: adminSelectors
            });
        }
```