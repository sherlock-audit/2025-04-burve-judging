Swift Opal Squid

High

# `E4626::commit` will drain the underlying  vault by always withdrawing more assets than required

### Summary

The net assetsToWithdraw in [`E4626::commits`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L72) was miscalculated due to a wrong order of operation always resulting the more assets being withdrawn from the vault every time `assetsToWithdraw` is greater than `assetsToDeposit`

### Root Cause

When performing multiple operations on a vault (wrapped by E4626), the pending deposits is balanced against the pending withdrawals and only the net balance is either withdrawn or deposited. For instance, if we had a pending deposit of 500 and a pending withdrawal of 1000, the net effect would be no deposit and a withdrawal of only 500 (1000 - 500).

The issue here arises when we try to balance the amounts in `E4626::commit`, here is the code,

```solidity
    function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        uint256 assetsToDeposit = temp.vars[1];
        uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
@>                assetsToDeposit = 0;
@>                assetsToWithdraw -= assetsToDeposit;
            } else {
                // Perfect net!
                return;
            }
        }

        ...
```

`assetsToDeposit` is set to 0 before being subracted from `assetsToWithdraw`.

Following the example above with
```solidity
    assetsToDeposit = 500
    assetsToWithdraw = 1000

    // since assetsToWithdraw > assetsToDeposit, we take second branch
    assetsToDeposit = 0
    assetsToWithdraw -= assetsToDeposit // 1000 - 0 = 1000
```

### Internal Pre-conditions

1. `vaultProxy::deposit` and `vaultProxy::withdraw` is called in any order
2. `assetsToWithdraw` > assetsToWithdraw` in `E4626::commit`
3. `vaultProxy::commit` is called

### External Pre-conditions

-

### Attack Path

-

### Impact

`E46262` will withdraw more assets than necessary from the underlying vault whenever `assetsToWithdraw` is greater than `assetsToDeposit` and the net balance would be treated as protocol Fees and sent to the admin whenever `SimplexFacet::withdraw` is called.

```solidity
    function withdraw(address token) external {
        AdminLib.validateOwner();

@>        uint256 balance = IERC20(token).balanceOf(address(this));

        if (TokenRegLib.isRegistered(token)) {
            uint8 idx = TokenRegLib.getIdx(token);
            uint256 earned = SimplexLib.protocolGive(idx);
            emit FeesWithdrawn(token, balance, earned);
        }

        if (balance > 0) {
@>            TransferHelper.safeTransfer(token, msg.sender, balance);
        }
    }
```

This scenario would be met almost in every transaction when we call `trimBalance` which will naturally drive the protocol to insolvency.

```solidity
    function trimBalance(
        Vertex storage self,
        ClosureId cid,
        uint256 targetReal,
        uint256 value,
        uint256 bgtValue
    ) internal returns (uint256 reserveSharesEarned, uint256 bgtResidual) {
        VaultProxy memory vProxy = VaultLib.getProxy(self.vid);

        uint256 realBalance = vProxy.balance(cid, false);
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
            return (0, 0);
        }
        uint256 residualReal = realBalance - targetReal;
@>        vProxy.withdraw(cid, residualReal);
        bgtResidual = FullMath.mulDiv(residualReal, bgtValue, value);
@>        reserveSharesEarned = ReserveLib.deposit(
            vProxy,
            self.vid,
            residualReal - bgtResidual
        );
        vProxy.commit();
    }
```

### PoC

_No response_

### Mitigation

Change the order of operation
```diff
-     assetsToDeposit = 0;
    assetsToWithdraw -= assetsToDeposit;
+ assetsToDeposit = 0
```