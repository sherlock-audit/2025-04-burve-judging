Sweet Slate Dove

Medium

# In `commit` function withdrawals are higher than intended and thus will incur higher fees from underlying vault

### Summary

Whenever `addValue`, `depositValue` is called, `trimBalance` gets called internally in `Vertex`:

```solidity
    function trimBalance(
        Vertex storage self,
        ClosureId cid,
        uint256 targetReal,
        uint256 value,
        uint256 bgtValue
    ) internal returns (uint256 reserveSharesEarned, uint256 bgtResidual) {
        VaultProxy memory vProxy = VaultLib.getProxy(self.vid);
        uint256 realBalance = vProxy.balance(cid, false);
        // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
        // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
        if (targetReal > realBalance) {
            emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
            return (0, 0);
        }
        uint256 residualReal = realBalance - targetReal;
        vProxy.withdraw(cid, residualReal);
        bgtResidual = FullMath.mulDiv(residualReal, bgtValue, value);
        reserveSharesEarned = ReserveLib.deposit(
            vProxy,
            self.vid,
            residualReal - bgtResidual
        );
        vProxy.commit();
    }
```

If the balance of the vertex's vaults is more than the closure balance, the difference/residual (`residualReal`) is withdrawn, and the non-bgt fraction of this residual is deposited into the reserve. What is to be noted is that in `trimBalance`,  the call `vProxy.withdraw` updates the `temp.vars[2]` memory variable, and `ReserveLib.deposit` updates the `temp.vars[1]` memory variable. Then, finally, at the end of the `trimBalance` function, `vProxy.commit();` is called.  This is where the problem arises:

```solidity
    function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        uint256 assetsToDeposit = temp.vars[1];
        uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
                assetsToDeposit = 0; //@audit - should be problematic
                assetsToWithdraw -= assetsToDeposit;
            } else {
                // Perfect net!
                return;
            }
        }
```

If `assetsToWithdraw > assetsToDeposit` then the order of operations should be: 

```solidity
                assetsToWithdraw -= assetsToDeposit;
                assetsToDeposit = 0;
```

But in the code, it is the opposite. This leads to an incorrect amount of tokens being withdrawn from the underlying vault and higher fees being applied on withdrawals.

### Root Cause

Incorrect order of operation in [`commit`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L66-L78) function leads to the vulnerability:

```solidity
else if (assetsToWithdraw > assetsToDeposit) {
                assetsToDeposit = 0; //@audit - should be problematic
                assetsToWithdraw -= assetsToDeposit;
}
```

It first assigns 0 to `assetsToDeposit` and then tries to subtract from `assetsToWithdraw`, but no value is subtract because of the zero-assignment. Then it tries to [`withdraw`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L92-L99) from the underlying vault:

```solidity
else if (assetsToWithdraw > 0) {
            // We don't need to hyper-optimize the receiver.
            self.totalVaultShares -= self.vault.withdraw(
                assetsToWithdraw,
                address(this),
                address(this)
            );
        }
```

Since the `assetsToDeposit` is never subtracted, we end up withdrawing more from the assets than intended.

### Internal Pre-conditions

In the `commit` function, `assetsToWithdraw` must be greater than `assetsToDeposit`.

### External Pre-conditions

None

### Attack Path

This is already explained in the summary, as well as the root cause.

### Impact

We end up withdrawing a larger number of tokens from the underlying vault than intended. Because of this, the protocol misses out on the yield from the underlying vault. Also, the following comment is written in the `commit` function:

```solidity
        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
```

This does not happen. Fees won't be saved on withdrawal. As the assets to be deposited are not being subtracted from the assets to be withdrawn, we end up removing more assets, and hence a higher fee is charged on the withdrawals, which is a loss to the protocol.

### PoC

_No response_

### Mitigation

If `assetsToWithdraw > assetsToDeposit`, then the order of operations in the `commit` function should be: 

```solidity
                assetsToWithdraw -= assetsToDeposit;
                assetsToDeposit = 0;
```