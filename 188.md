Winning Butter Monkey

Medium

# Incorrect swap size check in `SwapFacet.sol::swap`

### Summary

When users try to swap tokens in the swap facet the amount to be swapped is supposed to be validated against the minimum swap size constant due to this reason in the code:
```solidity
    /// We restrict swaps to be larger than this size as to avoid
    /// people gaming the deMinimus. Although even then, that's not too big of an issue.
    /// This is a nominal value.
    uint128 public constant MIN_SWAP_SIZE = 16e8;
```

But the issue here is that, during the swap, the MIN_SWAP_SIZE validated against the `inAmount` which is the `swapAmount + fees` so in cases where there are 0 fees the real swap amount is validated but in cases when there is fees the real swap Amount is not validated making the user bypass the check to deposit lesser.

### Root Cause

 In [`swap`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/SwapFacet.sol#L52-L146) we can find the check

```solidity
function swap(
        address recipient, address inToken, address outToken, int256 amountSpecified, uint256 amountLimit, uint16 _cid ) external nonReentrant returns (uint256 inAmount, uint256 outAmount) {
...
        ClosureId cid = ClosureId.wrap(_cid);
        Closure storage c = Store.closure(cid);
        uint256 valueExchangedX128;
        uint256 realTax;
        if (amountSpecified > 0) {
            inAmount = uint256(amountSpecified);
            uint256 nominalIn = AdjustorLib.toNominal(
                inVid.idx(),
                inAmount,
                false
            );
            require(
@1->                nominalIn >= MIN_SWAP_SIZE,
                BelowMinSwap(nominalIn, MIN_SWAP_SIZE)
            );

            uint256 nominalOut;
            uint256 nominalTax;
@2->            (nominalOut, nominalTax, valueExchangedX128) = c.swapInExact(
                inVid,
                outVid,
                nominalIn
            );
            outAmount = AdjustorLib.toReal(outVid.idx(), nominalOut, false);

...

}
```

from @2 we can see the function makes a call to closure.sol::swapInExact to swap tokens, however from `swapInExact` we can see that the tax is being deducted before he actual swap goes on

```solidity
        // First tax the in token.
        uint8 inIdx = inVid.idx();
        tax = FullMath.mulX128(inAmount, self.baseFeeX128, true);
        inAmount -= tax;
        // Calculate the value added by the in token.
```

This means that the swapAmount = `inAmount - fees` and @1 is not validating against the real swap Amount

this also applies to swap exactOut swaps too

### Internal Pre-conditions

closure fees must be set

### External Pre-conditions

no pre-con

### Attack Path

* Call swap with an amount which when fees is subtracted it will be less than MIN_SWAP_SIZE i.e amount should be such that `amount - fees < MIN_SWAP_SIZE` for exact in swaps

* Call swap exact out with any amount that is less than min swap size but the inAmount(swapAmount + fees) will be greater than min swap size

check poc for more insight

### Impact

This will make users be able to swap less than the minimum swap size which breaks the invariant of the system.

### PoC

Add `console.log("swapAmount", inAmount);` to the last line of `closure.sol::swapInExact` to see the actual swap amount

add ` console.log("swapAmount", untaxedInAmount);` after the last line of setBalance in `closure.sol::swapOutExact` 

add this test to `test/facets/SwapFacet.t.sol`

```solidity
function testExactInputSwap22() public {
        uint256 amount = 16e8;
        uint256 beforeBalance0 = token0.balanceOf(alice);
        uint256 beforeBalance1 = token1.balanceOf(alice);

        vm.prank(owner);
        simplexFacet.setClosureFees(0x3, 1 << 126, 1 << 127); 

        vm.startPrank(alice);
        (uint256 inAmount, uint256 outAmount) = swapFacet.swap(
            alice, // recipient
            tokens[0], // tokenIn
            tokens[1], // tokenOut
            int256(amount), // positive for exact input
            0, // no price limit{}
            0x3
        );
        vm.stopPrank();

        assertEq(inAmount, amount);

        console.log("inAmount: ", inAmount);
        console.log("outAmount: ", outAmount);
    }
```

add this for exact out swaps

```solidity
function testExactOutputSwap2() public {
        uint256 amount = 1299999999; 
        uint256 beforeBalance0 = token0.balanceOf(alice);
        uint256 beforeBalance1 = token1.balanceOf(alice);
        
        vm.prank(owner);
        simplexFacet.setClosureFees(0x3, 1 << 126, 1 << 127); 

        vm.startPrank(alice);
        (uint256 inAmount, uint256 outAmount) = swapFacet.swap(
            alice, // recipient
            tokens[0], // tokenIn
            tokens[1], // tokenOut
            -int256(amount), // negative for exact out
            type(uint256).max, // no price limit
            0x3
        );
        vm.stopPrank();

        assertEq(outAmount, amount);

        console.log("inAmount: ", inAmount);
        console.log("outAmount: ", outAmount);
    }
```

After running these test observe that the swap amount logged is way less than the inAmount that is also logged

### Mitigation

min swap size validation check should take fees into consideration when checking against `inAmount`.