Great Iris Zebra

High

# The `addSingleForValue` Function Collects More Fees than It Should

### Summary
The `addValueSingle` function integrates `addValue` and `swap`, collecting fees exclusively for the amount utilized in the `swap`. In contrast, the `addSingleForValue` function calculates fees based on `depositAmount * feeRate`, while the `addValueSingle` function computes fees as `depositAmount * feeRate * (n-1) / n`.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L307
```solidity
    function addTokenForValue(
        ...
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // For simplicity, we tax the entire amount in first. This overcharges slightly but an exact solution
        // would overcomplicate the contract and any approximation is game-able.
307:    tax = FullMath.mulX128(amount, self.baseFeeX128, true);
        amount -= tax;
        // Use the ValueLib's newton's method to solve for the value added and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky. We up the balance first for the ValueLib call, then set to do the checks.
        // We need to set the new target before we can setBalance, but we need up to balance to calc new target.
        self.balances[idx] += amount;
        ...
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L198-L205
```solidity
    function addValueSingle(
        Closure storage self,
        uint256 value,
        uint256 bgtValue,
        VertexId vid
    ) internal returns (uint256 requiredAmount, uint256 tax) {
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        // We still need to trim all balances here because value is changing.
        trimAllBalances(self);
        // Compute scale before modifying our target.
        uint256 scaleX128 = FullMath.mulDivX256(
            value,
            self.n * self.targetX128,
            true
        ) + ONEX128;
        {
            uint256 valueX128 = value << 128;
            self.targetX128 +=
                valueX128 /
                self.n +
                ((valueX128 % self.n) > 0 ? 1 : 0);
        }
        SingleValueIter memory valIter = SingleValueIter({
            scaleX128: scaleX128,
            vIdx: vid.idx(),
            valueSumX128: 0
        });

        // We first calculate what value is effectively "lost" by not adding the tokens.
        // And then we make sure to add that amount of value to the deposit token.
        uint256 fairVBalance = iterSingleValueDiff(self, valIter, true);
        requiredAmount = fairVBalance - self.balances[valIter.vIdx];
        // Now we have the missing value and the currently fair balance for our vertex.
        uint256 finalAmount;
        {
            uint256 veX128 = SimplexLib.getEX128(valIter.vIdx);
            uint256 currentValueX128 = ValueLib.v(
                self.targetX128,
                veX128,
                fairVBalance,
                false
            );
            // To get the required amount.
            finalAmount = ValueLib.x(
                self.targetX128,
                veX128,
                currentValueX128 + valIter.valueSumX128,
                true
            );
        }
        {
198:        uint256 untaxedRequired = finalAmount - fairVBalance;
            self.setBalance(valIter.vIdx, finalAmount);
            uint256 taxedRequired = UnsafeMath.divRoundingUp(
                untaxedRequired << 128,
                ONEX128 - self.baseFeeX128
            );
            tax = taxedRequired - untaxedRequired;
205:        requiredAmount += taxedRequired;
        }
        // This needs to happen after any fee earnings.
        self.valueStaked += value;
        self.bgtValueStaked += bgtValue;
    }
```

### Impact
The `addSingleForValue` function collects more fees than the `addValueSingle` function, resulting in a loss for users.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L307
```solidity
    function addTokenForValue(
        ...
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // For simplicity, we tax the entire amount in first. This overcharges slightly but an exact solution
        // would overcomplicate the contract and any approximation is game-able.
-307:   tax = FullMath.mulX128(amount, self.baseFeeX128, true);
+       tax = FullMath.mulX128(amount, self.baseFeeX128, true) * (self.n - 1) / self.n;
        amount -= tax;
        // Use the ValueLib's newton's method to solve for the value added and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky. We up the balance first for the ValueLib call, then set to do the checks.
        // We need to set the new target before we can setBalance, but we need up to balance to calc new target.
        self.balances[idx] += amount;
        ...
    }
```