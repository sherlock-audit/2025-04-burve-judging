Sneaky Burlap Pigeon

Medium

# The Withdrawn User Could Not Receive the Last Pending Yield


### Summary
In the `ValueFacet::removeValue` function, assets are removed from the `assetbook` first, followed by another removal from the closure. However, in the `closure.removeValue` function, the pending yield is accounted for. As a result, users may not receive their last pending yield.
Additionally, due to a mistake in the `queryValue` function, when there are no accumulated rewards for the user, the pending reward cannot be confirmed.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L190
```solidity
    function removeValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory receivedBalances)
    {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
190:    Store.assets().remove(msg.sender, cid, value, bgtValue);
        uint256[MAX_TOKENS] memory nominalReceives = c.removeValue(
            value,
            bgtValue
        );
        ...
    }
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L305-L314
```solidity
    function queryValue(
        address owner,
        uint16 closureId
    )
        external
        view
        returns (
            uint256 value,
            uint256 bgtValue,
            uint256[MAX_TOKENS] memory earnings,
            uint256 bgtEarnings
        )
    {
        ClosureId cid = ClosureId.wrap(closureId);
        (
            uint256[MAX_TOKENS] memory realEPVX128,
            uint256 bpvX128,
            uint256[MAX_TOKENS] memory upvX128
        ) = Store.closure(cid).viewTrimAll();
        (value, bgtValue, earnings, bgtEarnings) = Store.assets().query(
            owner,
            cid
        );
        uint256 nonValue = value - bgtValue;
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
305:        if (earnings[i] > 0) {
                VertexId vid = VertexLib.newId(i);
                earnings[i] = ReserveLib.query(vid, earnings[i]);
                earnings[i] += FullMath.mulX128(
                    realEPVX128[i],
                    nonValue,
                    false
                );
                earnings[i] += FullMath.mulX128(upvX128[i], bgtValue, false);
314:        }
        }
        bgtEarnings += FullMath.mulX128(bpvX128, bgtValue, false);
    }
```
In lines 305 to 314, if there are no accumulated earnings (earnings[i] = 0), the pending yield (realEPVX128[i] * nonValue) is skipped.

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_removeValue() public {
        vm.startPrank(alice);
        uint256[4] memory initBalances = getBalances(alice);

        valueFacet.addValue(alice, 0x7, 3e20, 0);
        MockERC20(tokens[1]).mint(address(vaults[1]), 3e20);
        
        (, , uint256[MAX_TOKENS] memory earnings, ) = valueFacet.queryValue(alice, 0x7);
        console.log("alice_earnings[1] : %e", earnings[1]);
        
        valueFacet.removeValue(alice, 0x7, 3e20, 0);
        //valueFacet.collectEarnings(alice, 0x7);
        
        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("alice_diffs[1]    : %e", diffs[1]);
        vm.stopPrank();
    } 
```
forge test --match-test "test_removeValue" -vv
Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_removeValue() (gas: 1231335)
Logs:
  alice_earnings[1] : 0e0
  alice_diffs[1]    : 0e0
```
As can be seen, Alice was unable to confirm the yield and, could not receive it.

After mitigation, Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_removeValue() (gas: 1309570)
Logs:
  alice_earnings[1] : 4.9999999750000001249e19
  alice_diffs[1]    : 4.9999999750000001248e19
```

### Impact
The withdrawn user could not receive the last pending yield

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L190
```diff
    function removeValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory receivedBalances)
    {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
-190:   Store.assets().remove(msg.sender, cid, value, bgtValue);
        uint256[MAX_TOKENS] memory nominalReceives = c.removeValue(
            value,
            bgtValue
        );
+       Store.assets().remove(msg.sender, cid, value, bgtValue);
        ...
    }
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L305-L314
```solidity
    function queryValue(
        ...
    {
        ...
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
305:        if (earnings[i] > 0) {
                VertexId vid = VertexLib.newId(i);
                earnings[i] = ReserveLib.query(vid, earnings[i]);
+           }
                earnings[i] += FullMath.mulX128(
                    realEPVX128[i],
                    nonValue,
                    false
                );
                earnings[i] += FullMath.mulX128(upvX128[i], bgtValue, false);
-314:       }
        }
        bgtEarnings += FullMath.mulX128(bpvX128, bgtValue, false);
    }
```
