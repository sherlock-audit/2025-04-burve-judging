Damaged Admiral Grasshopper

High

# User cannot remove value if the underlying value charges a fee

### Summary

When user removes values, the funds are withdrawn from the underlying vault and transferred to the recipient. Because the recipient's token amount does not including withdrawal fee, the tokens can be insufficient and the transaction will revert.

### Root Cause

User calls [removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L176-L181) to receive tokens back. The received token amount is calculated in [ClosureImpl::removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L216-L220).

[ValueFacet::removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L191-L194):
```solidity
        uint256[MAX_TOKENS] memory nominalReceives = c.removeValue(
            value,
            bgtValue
        );
```

Then the tokens are withdrawn from the underlying vault.

[ValueFacet::removeValue()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L197-L208):
```solidity
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue;
            address token = tokenReg.tokens[i];
            uint256 realSend = AdjustorLib.toReal(
                token,
                nominalReceives[i],
                false
            );
            receivedBalances[i] = realSend;
            // Users can remove value even if the token is locked. It actually helps derisk us.
@>          Store.vertex(VertexLib.newId(i)).withdraw(cid, realSend, false);
            TransferHelper.safeTransfer(token, recipient, realSend);
```

[VaultE4626Impl::commit()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L92-L99):
```solidity
        } else if (assetsToWithdraw > 0) {
            // We don't need to hyper-optimize the receiver.
            self.totalVaultShares -= self.vault.withdraw(
                assetsToWithdraw,
                address(this),
                address(this)
            );
        }
```

The problem is that if the underlying vault charges a fee, then the actual withdrawn token amount would be less than the amount transferred to the recipient, as a result, the transaction will revert due to insufficient ERC20 balance error.

### Internal Pre-conditions

User removes value.

### External Pre-conditions

The underlying value charges a fee.

### Attack Path

None

### Impact

User cannot remove value.

### PoC

Please run `forge test --mt testAudit_CannotWithdrawal`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "./mocks/MockERC20.sol";
import "./mocks/MockERC4626.sol";
import {VaultType} from "../src/multi/vertex/VaultPointer.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";
import "../src/integrations/adjustor/DecimalAdjustor.sol";

contract AuditTest is Test {
    uint8 constant MAX_TOKENS = 16;

    address owner = makeAddr("OWNER");
    address diamond;

    MockERC20 tokenA = new MockERC20("Token A", "A", 18);
    MockERC20 tokenB = new MockERC20("Token B", "B", 6);

    MockERC4626WithFees vaultA = new MockERC4626WithFees(tokenA, "Vault A", "VA");
    MockERC4626WithFees vaultB = new MockERC4626WithFees(tokenB, "Vault B", "VB");

    function setUp() public {
        vm.startPrank(owner);

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            // Configure adjustor
            {
                DecimalAdjustor decimalAdjustor = new DecimalAdjustor();
                mixedAdjustor.setAdjustor(address(tokenB), address(decimalAdjustor));
            }
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        diamond = address(new SimplexDiamond(facets, "ValueToken", "BVT"));

        vm.label(diamond, "Diamond");
        vm.label(address(tokenA), "Token A");
        vm.label(address(tokenB), "Token B");
        vm.label(address(vaultA), "Vault A");
        vm.label(address(vaultB), "Vault B");

        // Add Vertex
        {
            // TokenA
            SimplexFacet(diamond).addVertex(address(tokenA), address(vaultA), VaultType.E4626);

            // TokenB
            SimplexFacet(diamond).addVertex(address(tokenB), address(vaultB), VaultType.E4626);
        }

        // Add Closure
        {
            bool success;

            uint16 cid = 3; // 0b11
            uint128 startingTarget = 1e12;
            // uint128 baseFeeX128 = 34028236692093846346337460743176821145; // 10%
            // uint128 protocolTakeX128 = 34028236692093846346337460743176821145;  //10%
            uint128 baseFeeX128 = 0;
            uint128 protocolTakeX128 = 0;

            uint256 amountA = startingTarget / 10 ** (18 - tokenA.decimals());
            uint256 amountB = startingTarget / 10 ** (18 - tokenB.decimals());

            tokenA.mint(owner, amountA);
            tokenA.approve(diamond, amountA);
            tokenB.mint(owner, amountB);
            tokenB.approve(diamond, amountB);
            SimplexFacet(diamond).addClosure(cid, startingTarget, baseFeeX128, protocolTakeX128);
        }

        vm.stopPrank();
    }

    function testAudit_CannotWithdrawal() public {
        // Set withdrawal fee
        vaultA.setWithdrawalFee(1000);

        // Alice adds value
        address alice = makeAddr("Alice");
        tokenA.mint(alice, 0.5e18);
        tokenB.mint(alice, 0.5e6);

        vm.startPrank(alice);
        tokenA.approve(diamond, 0.5e18);
        tokenB.approve(diamond, 0.5e6);
        ValueFacet(diamond).addValue(alice, 3, 1e18, 0);
        vm.stopPrank();

         // Alice removes value and transaction will revert
        vm.prank(alice);
        vm.expectRevert(bytes("ST"));
        ValueFacet(diamond).removeValue(alice, 3, 1e18, 0);
    }
}

contract MockERC4626WithFees is ERC4626Fees {

    uint256 entryFeeBasisPoints;
    uint256 exitFeeBasisPoints;
    address feeRecipient;

    constructor(
        IERC20 asset_, 
        string memory name_, 
        string memory symbol_
    ) ERC4626(asset_) ERC20(name_, symbol_) {
        feeRecipient = msg.sender;
    }

    function setEntryFee(uint256 entryFeeBasisPoints_) public {
        entryFeeBasisPoints = entryFeeBasisPoints_;
    }

    function setExitFee(uint256 exitFeeBasisPoints_) public {
        exitFeeBasisPoints = exitFeeBasisPoints_;
    }

    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }

    function _exitFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }
}
```

### Mitigation

It is recommended to apply underlying vault fee to the toke amount sent to the recipient.