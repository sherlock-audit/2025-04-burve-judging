Sneaky Burlap Pigeon

High

# Protocol Lacks Swap Fees in the `removeValueSingle` Function


### Summary
In `ValueFacet.sol#L236`, when calculating the `realTax`, the function mistakenly uses `removedBalance`, which is still zero, instead of `realRemoved`. As a result, the protocol does not collect the fees.

### Root Cause
The root cause is found in line 240, where the `addEarnings` function is called. At this point, `realTax` is zero.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L236
```solidity
    function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);
        uint256 realTax = FullMath.mulDiv(
236:        removedBalance,
            nominalTax,
            removedNominal
        );
240:    c.addEarnings(vid, realTax);
        removedBalance = realRemoved - realTax; // How much the user actually gets.
        require(removedBalance >= minReceive, PastSlippageBounds());
        TransferHelper.safeTransfer(token, recipient, removedBalance);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_removeValueSingle() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x7, uint128(oneX128 / 1000), uint128(oneX128 / 10));

        valueFacet.addValue(alice, 0x7, 3e20, 0);

        vm.startPrank(alice);
        valueFacet.removeValueSingle(alice, 0x7, 1e20, 0, tokens[1], 0);

        uint256[4] memory initBalances = getBalances(owner);
        vm.startPrank(owner);
        simplexFacet.withdraw(tokens[1]);
        uint256[4] memory currentBalances = getBalances(owner);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("earned protocol fees: %e\n", diffs[1]);
    }
```
forge test --match-test "test_removeValueSingle" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_removeValueSingle() (gas: 1014911)
Logs:
  earned protocol fees: 0e0
```
After mitigation, Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_removeValueSingle() (gas: 1100786)
Logs:
  earned protocol fees: 6.32183908045977e15
```

### Impact
The owner can not collect the protocol fees.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L236
```diff
    function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
233:    uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
234:    Store.vertex(vid).withdraw(cid, realRemoved, false);
        uint256 realTax = FullMath.mulDiv(
-236:        removedBalance,
+           realRemoved
            nominalTax,
            removedNominal
        );
+       Store.vertex(vid).withdraw(cid, realTax, false);
240:     c.addEarnings(vid, realTax);
-        removedBalance = realRemoved - realTax; // How much the user actually gets.
+        removedBalance = realRemoved;
        require(removedBalance >= minReceive, PastSlippageBounds());
        TransferHelper.safeTransfer(token, recipient, removedBalance);
    }
```

