Elegant Punch Dog

High

# [H-1] `realTax` collected in `ValueFacet::removeValueSingle` will always be zero incurring losses to the protocol

### Summary

`removeValueSingle` is a function in the `ValueFacet` where a user can "Remove exactly this much value to the given closure by receiving a single token" but the `realTax` collected in this function always zero incurring losses to the protocol

### Root Cause

  Reference : [Root Cause](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L235-L239)
 Consider the Snippet from the `ValueFacet::removeValueSingle` :

```solidity
 
    function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
@>    ) external nonReentrant returns (uint256 removedBalance) { // Declared here
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); 
        VertexId vid = VertexLib.newId(token); 
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);
@>        uint256 realTax = FullMath.mulDiv(
@>          removedBalance, // unassigned Value i.e defaults to 0
            nominalTax,
            removedNominal
        );

          c.addEarnings(vid, realTax);
@>        removedBalance = realRemoved - realTax; // assigned here 
        require(removedBalance >= minReceive, PastSlippageBounds());
        TransferHelper.safeTransfer(token, recipient, removedBalance);
    }

```

In the above function when `realTax` is calculated the `removedBalance` is used in multiplication but `removedBalance` gets a value assigned to it much later , meaning it is always zero during the calculation and `realTax` will also be zero resulting in no fee being collected

### Internal Pre-conditions

NONE

### External Pre-conditions

 User should call `ValueFacet::removeValueSingle`

### Attack Path

If a user calls `ValueFacet::removeValueSingle` to withdraw their funds the issue will be triggered 

### Impact

1.The Fee from `ValueFacet::removeValueSingle` will always be zero which results in losses to the protocol 
2. Users who deposit a single token in the protocol can exploit this by easily withdrawing without worrying about the fee which will disrupt the diversification of tokens in the protocol
3. Protocol encourages users to diversify the tokens deposited by having no fee on `ValueFacet::addValue` and `ValueFacet::removeValue` functions , so it would be unfair for all these users if the fee collected from `ValueFacet::removeValueSingle` is zero

### PoC


1. import `console` in `ValueFacet.sol` and `ValueFacet.t.sol`
   
     ```solidity
   import {console} from "forge-std/console.sol";
   ```
    Import `SimplexFacet` in  `ValueFacet.t.sol`

```solidity
  import {SimplexFacet} from "../../src/multi/facets/SimplexFacet.sol";
```

2. In `ValueFacet::removeValueSingle` make the following changes 

 ```diff
       function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance) {
        .....
        ....
        ..
        .  
        Store.assets().remove(msg.sender, cid, value, bgtValue);

        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(value, bgtValue, vid);

        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);

+        console.log("removedBalance", removedBalance);

        uint256 realTax = FullMath.mulDiv(removedBalance, nominalTax, removedNominal);


+        console.log("realTax", realTax);
         c.addEarnings(vid, realTax);
        removedBalance = realRemoved - realTax;
        ...
        ..
        .
        }
 ```



PoC: Run the test below by typing the following command in the terminal `forge test --mt test_NoFeeInRemoveValueSingle -vv`

<details>
<summary>test_NoFeeInRemoveValueSingle</summary>

```solidity

function test_NoFeeInRemoveValueSingle() public {
        //Set some Fee
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0xD, uint128(oneX128 / 10000), 0); // One basis point. Realistic.

        // Simply add and remove.
        address _token = address(tokens[2]);
  
        uint256 valueReceived = valueFacet.addSingleForValue(address(this), 0xD, tokens[2], 1e18, 0, 0);
       

        uint256 tokensReceived =
            valueFacet.removeValueSingle(address(this), 0xD, uint128(valueReceived), 0, tokens[2], 0);

    
    }
```

Logs :

```solidity
removedBalance 0
realTax 0
```
</details>

### Mitigation

Use `realRemoved` instead of `removedBalance` when calculating `realTax` to migtigate the issue


Make the following changes in `ValueFacet::removeValueSingle`


```diff
    
     function removeValueSingle(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance) {
        .....
        ....
        ..
        .  
        Store.assets().remove(msg.sender, cid, value, bgtValue);

        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(value, bgtValue, vid);

        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);

-        uint256 realTax = FullMath.mulDiv(removedBalance, nominalTax, removedNominal);
+        uint256 realTax = FullMath.mulDiv(realRemoved, nominalTax, removedNominal);
         c.addEarnings(vid, realTax);
        removedBalance = realRemoved - realTax;
        ...
        ..
        .

```