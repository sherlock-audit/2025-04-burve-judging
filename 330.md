Huge Laurel Halibut

Medium

# Inconsistency between viewTrimAll and _trimBalance in Burve Protocol

## Description
https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L727-L809
An inconsistency has been identified between the `viewTrimAll` and `_trimBalance` functions in the Burve protocol's `Burve/src/multi/closure/Closure.sol` file. The `viewTrimAll` function is designed to simulate the behavior of `_trimBalance` without modifying state, however it handles the unspent variable differently than `_trimBalance` does.

In the `_trimBalance` function, unused funds (unspent) are deposited into the reserve through `ReserveLib.deposit(vid, unspent)`, which returns `unspentShares`. These shares are then distributed among BGT value holders. However, in the viewTrimAll function, this step is missing - unspent is directly distributed without being converted to unspentShares.

This inconsistency causes the `viewTrimAll` function to return inaccurate data about earnings distribution, which may mislead users and lead to incorrect decision-making
## Proof of Concept
Code from _trimBalance function:
```solidity
function _trimBalance(
    Closure storage self,
    VertexId vid,
    uint256 nonBgtValueStaked
) private {
    uint8 idx = vid.idx();
    // Roundup the balance we need.
    uint256 realBalance = AdjustorLib.toReal(idx, self.balances[idx], true);
    (uint256 earnings, uint256 bgtReal) = Store.vertex(vid).trimBalance(
        self.cid,
        realBalance,
        self.valueStaked,
        self.bgtValueStaked
    );
    self.earningsPerValueX128[idx] += (earnings << 128) / nonBgtValueStaked;
    if (self.bgtValueStaked > 0) {
        (uint256 bgtEarned, uint256 unspent) = SimplexLib.bgtExchange(
            idx,
            bgtReal
        );
        self.bgtPerBgtValueX128 += (bgtEarned << 128) / self.bgtValueStaked;
        // rare
        if (unspent > 0) {
            uint256 unspentShares = ReserveLib.deposit(vid, unspent);
            self.unexchangedPerBgtValueX128[idx] +=
                (unspentShares << 128) /
                self.bgtValueStaked;
        }
    }
}
```
Code from viewTrimAll function (problematic part):
```solidity
function viewTrimAll(
    Closure storage self
) internal view returns (
    uint256[MAX_TOKENS] memory earningsPerValueX128,
    uint256 bgtPerBgtValueX128,
    uint256[MAX_TOKENS] memory unexchangedPerBgtValueX128
) {
    // ... (other code)
    
    if (self.bgtValueStaked > 0) {
        (uint256 bgtEarned, uint256 unspent) = SimplexLib.viewBgtExchange(
            idx,
            bgtReal
        );
        bgtPerBgtValueX128 += (bgtEarned << 128) / self.bgtValueStaked;
        // rare
        if (unspent > 0) {
            // Missing call to ReserveLib.deposit(vid, unspent)
            unexchangedPerBgtValueX128[idx] +=
                (unspent << 128) /
                self.bgtValueStaked;
        }
    }
    
    // ... (other code)
}
```

## Implications:
The `viewTrimAll` function distributes unspent directly, while `_trimBalance` distributes unspentShares obtained through `ReserveLib.deposit(vid, unspent)`.
This leads to a discrepancy between what users see (via `viewTrimAll`) and what they actually receive (via `_trimBalance)`.
Users relying on data from viewTrimAll (e.g., through queryValue) may see inaccurate values of their earnings.
When users actually collect their earnings through `collectEarnings`, the amount may differ from what was shown in `queryValue`.

## Recommendation
To address this inconsistency, it is recommended to modify the viewTrimAll function to accurately simulate the behavior of `_trimBalance`

```solidity
function viewTrimAll(
    Closure storage self
) internal view returns (
    uint256[MAX_TOKENS] memory earningsPerValueX128,
    uint256 bgtPerBgtValueX128,
    uint256[MAX_TOKENS] memory unexchangedPerBgtValueX128
) {
    // ... (other code)
    
    if (self.bgtValueStaked > 0) {
        (uint256 bgtEarned, uint256 unspent) = SimplexLib.viewBgtExchange(
            idx,
            bgtReal
        );
        bgtPerBgtValueX128 += (bgtEarned << 128) / self.bgtValueStaked;
        // rare
        if (unspent > 0) {
            // Add simulation of ReserveLib.deposit
            uint256 unspentShares = ReserveLib.viewDeposit(vid, unspent);
            unexchangedPerBgtValueX128[idx] +=
                (unspentShares << 128) /
                self.bgtValueStaked;
        }
    }
    
    // ... (other code)
}
```
This would require adding a viewDeposit function to ReserveLib that simulates the behavior of deposit without changing state:
```solidity
function viewDeposit(VertexId vid, uint256 amount) internal view returns (uint256 shares) {
    // Simulate deposit logic without changing state
    // ...
}

```
These changes will ensure consistency between viewing and actual distribution of earnings, improving transparency and predictability of the protocol for users.