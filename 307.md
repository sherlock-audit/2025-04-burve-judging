Damp Pewter Perch

Medium

# Island shares can be minted with lesser tokens than expected

### Summary

When users mint Burve liquidity, the liquidity for ranges is calculated according to their weights. If the Burve has an island in use, then the amount to be deposited is queried directly using the island’s current tick positions, given the calculated weighted liquidity for the island range. However, due to how the island calculates how much token0 and token1 it requires, there can be significantly more or fewer tokens than needed to mint a defined share in the island. This creates unfair minting of Burve liquidity due to uneven liquidity mining in the underlying island.

### Root Cause

When shares are minted in Burve, the user inputs a nominal liquidity amount. The liquidity needed for each range is then calculated as follows:

```solidity
for (uint256 i = 0; i < distX96.length; ++i) {
    uint128 liqInRange = uint128(
        shift96(uint256(mintNominalLiq) * distX96[i], true)
    );

    if (liqInRange == 0) {
        continue;
    }

    TickRange memory range = ranges[i];
    if (range.isIsland()) {
        islandShares = mintIsland(recipient, liqInRange);
    } else {
        // mint the V3 ranges
        pool.mint(
            address(this),
            range.lower,
            range.upper,
            liqInRange,
            abi.encode(msg.sender)
        );
    }
}
```

As we can see, if the range is an island, we call a special function `mintIsland` with the input being the calculated liquidity:

```solidity
function mintIsland(
    address recipient,
    uint128 liq
) internal returns (uint256 mintIslandShares) {
    (uint160 sqrtRatioX96, , , , , , ) = pool.slot0();
    (uint256 amount0, uint256 amount1) = getAmountsForLiquidity(
        sqrtRatioX96,
        liq,
        island.lowerTick(),
        island.upperTick(),
        true
    );
    (uint256 mint0, uint256 mint1, uint256 mintShares) = island.getMintAmounts(amount0, amount1);
    //..
```

We can see that the amounts are first calculated using Uniswap’s liquidity math via `getAmountsForLiquidity`, and the ticks are fetched directly from the island. So basically, we simulate what the amounts would be if we were to mint liquidity in the island’s tick range. Then, with those `amount0` and `amount1`, we call `island.getMintAmounts`.

Now here's where the problem arises: the island’s implementation calculates the shares to mint based on the ratio of the provided `amount0/1` to the *total* `amount0/1` the contract currently holds. But this total includes not only the active Uniswap position, but also any idle tokens and unclaimed fees.

This means that if the island contract has a lot of unclaimed fees or idle tokens, the `mintShares` calculated will be either much higher or much lower than expected. However, Burve only simulates a Uniswap liquidity provision when calling `getMintAmounts`, without taking into account the actual idle/fee balances in the island.

So if those other balances are high, the shares minted from the island can be low or high—but the Burve shares minted to the user are not adjusted for that, leading to potentially unfair results.

Here’s the relevant part of the island implementation:

```solidity
function getMintAmounts(uint256 amount0Max, uint256 amount1Max) external view returns (uint256 amount0, uint256 amount1, uint256 mintAmount) {
    uint256 totalSupply = totalSupply();
    if (totalSupply > 0) {
        (amount0, amount1, mintAmount) = _computeMintAmounts(totalSupply, amount0Max, amount1Max);
    } else {
        (uint160 sqrtRatioX96,,,,,,) = pool.slot0();
        uint128 newLiquidity = LiquidityAmounts.getLiquidityForAmounts(sqrtRatioX96, lowerTick.getSqrtRatioAtTick(), upperTick.getSqrtRatioAtTick(), amount0Max, amount1Max);
        mintAmount = uint256(newLiquidity);
        (amount0, amount1) = LiquidityAmounts.getAmountsForLiquidity(sqrtRatioX96, lowerTick.getSqrtRatioAtTick(), upperTick.getSqrtRatioAtTick(), newLiquidity);
    }
}
```

And the `getUnderlyingBalances` function used during mint calculation:

```solidity
function getUnderlyingBalances() public view returns (uint256 amount0Current, uint256 amount1Current) {
    (uint160 sqrtRatioX96, int24 tick,,,,,) = pool.slot0();
    return _getUnderlyingBalances(sqrtRatioX96, tick);
}

function _getUnderlyingBalances(uint160 sqrtRatioX96, int24 tick) internal view returns (uint256 amount0Current, uint256 amount1Current) {
    (uint128 liquidity, uint256 feeGrowthInside0Last, uint256 feeGrowthInside1Last, uint128 tokensOwed0, uint128 tokensOwed1) = pool.positions(_getPositionID());

    (amount0Current, amount1Current) = LiquidityAmounts.getAmountsForLiquidity(sqrtRatioX96, lowerTick.getSqrtRatioAtTick(), upperTick.getSqrtRatioAtTick(), liquidity);

    uint256 fee0 = _computeFeesEarned(true, feeGrowthInside0Last, tick, liquidity) + uint256(tokensOwed0);
    uint256 fee1 = _computeFeesEarned(false, feeGrowthInside1Last, tick, liquidity) + uint256(tokensOwed1);

    (fee0, fee1) = _subtractManagerFee(fee0, fee1, managerFeeBPS);

    amount0Current += fee0 + token0.balanceOf(address(this)) - managerBalance0;
    amount1Current += fee1 + token1.balanceOf(address(this)) - managerBalance1;
}
```

And finally, how `mintAmount` is computed:

```solidity
function _computeMintAmounts(uint256 totalSupply, uint256 amount0Max, uint256 amount1Max) private view returns (uint256 amount0, uint256 amount1, uint256 mintAmount) {
    (uint256 amount0Current, uint256 amount1Current) = getUnderlyingBalances();

    if (amount0Current == 0 && amount1Current > 0) {
        mintAmount = FullMath.mulDiv(amount1Max, totalSupply, amount1Current);
    } else if (amount1Current == 0 && amount0Current > 0) {
        mintAmount = FullMath.mulDiv(amount0Max, totalSupply, amount0Current);
    } else if (amount0Current == 0 && amount1Current == 0) {
        revert("");
    } else {
        uint256 amount0Mint = FullMath.mulDiv(amount0Max, totalSupply, amount0Current);
        uint256 amount1Mint = FullMath.mulDiv(amount1Max, totalSupply, amount1Current);
        require(amount0Mint > 0 && amount1Mint > 0, "mint 0");

        mintAmount = amount0Mint < amount1Mint ? amount0Mint : amount1Mint;
    }

    amount0 = FullMath.mulDivRoundingUp(mintAmount, amount0Current, totalSupply);
    amount1 = FullMath.mulDivRoundingUp(mintAmount, amount1Current, totalSupply);
}
```

* [Burve.sol L226–L290](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/single/Burve.sol#L226-L290)
* [Burve.sol L296–L344](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/single/Burve.sol#L296-L344)


### Internal Pre-conditions

None needed

### External Pre-conditions

1. Underlying island has idle tokens or unclaimed fees that are significant enough (more than 10$ should be enough to create an unfair situation)

### Attack Path

1. Spot a Burve pool with an island where the island has lots of unclaimed fees or idle tokens (alternatively airdrop if the final outcome is profitable)
2. Mint liquidity in Burve, since the island shares requires lesser amount0 amount1 user will not 

### Impact

TBD

### PoC

none needed

### Mitigation

tbd