Sneaky Burlap Pigeon

High

# Owner's Initial Funds Not Registered in Asset Book


### Summary
When the owner adds a closure to the diamond, they are required to send funds to the diamond. However, these funds are not registered in the asset book but are included in the `totalStaked`. As a result, the yield for the owner's share cannot be accessed, and the initial funds cannot be withdrawn.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/SimplexFacet.sol#195
```solidity
    function addClosure(
        uint16 _cid,
        uint128 startingTarget,
        uint128 baseFeeX128,
        uint128 protocolTakeX128
    ) external {
        AdminLib.validateOwner();

        ClosureId cid = ClosureId.wrap(_cid);
        // We fetch the raw storage because Store.closure would check the closure for initialization.
        Closure storage c = Store.load().closures[cid];

        uint256 initTarget = Store.simplex().initTarget;
        if (startingTarget < initTarget) {
            revert InsufficientStartingTarget(startingTarget, initTarget);
        }

        uint256[MAX_TOKENS] storage neededBalances = c.init(
            cid,
            startingTarget,
            baseFeeX128,
            protocolTakeX128
        );

        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue;

            address token = tokenReg.tokens[i];
            uint256 realNeeded = AdjustorLib.toReal(
                token,
                neededBalances[i],
                true
            );

            TransferHelper.safeTransferFrom(
                token,
                msg.sender,
                address(this),
                realNeeded
            );

            Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded);
        }
    }
```
As evident, the `SimplexFacet::addClosure` function does not register the funds in `Store.assets()`. The correct registration can be found in the `ValueFacet::addValue::L95`.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/ValueFacet.sol#L95
```solidity
    function addValue(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue
    )
        external
        nonReentrant
        returns (uint256[MAX_TOKENS] memory requiredBalances)
    {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid);
        uint256[MAX_TOKENS] memory requiredNominal = c.addValue(
            value,
            bgtValue
        );
        // Fetch balances
        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue; // Irrelevant token.
            address token = tokenReg.tokens[i];
            uint256 realNeeded = AdjustorLib.toReal(
                token,
                requiredNominal[i],
                true
            );
            requiredBalances[i] = realNeeded;
            TransferHelper.safeTransferFrom(
                token,
                msg.sender,
                address(this),
                realNeeded
            );
            Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded);
        }
95:     Store.assets().add(recipient, cid, value, bgtValue);
    }
```

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_ownerShare() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        simplexFacet.setClosureFees(0x3, uint128(oneX128 / 1000), uint128(oneX128 / 10));

        vm.prank(alice);
        swapFacet.swap(alice, tokens[0], tokens[1], 1e18, 0, 0x3);

        (uint256 value, uint256 bgtValue, uint256[MAX_TOKENS] memory earnings, uint256 bgtEarnings) = valueFacet.queryValue(owner, 0x3);
        console.log("value      :", value);
        console.log("bgtValue   :", bgtValue);
        console.log("earnings[0]:", earnings[0]);
        console.log("earnings[1]:", earnings[1]);
        console.log("bgtEarnings:", bgtEarnings);
    }
```
forge test --match-test "test_ownerShare" -vv
Result:
```bash
Ran 1 test for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_ownerShare() (gas: 640308)
Logs:
  value      : 0
  bgtValue   : 0
  earnings[0]: 0
  earnings[1]: 0
  bgtEarnings: 0
```

### Impact
1. The yield for the owner's share is inaccessible and remains locked in the vault.
2. The owner's initial funds cannot be withdrawn.

### Mitigation
Consider either registering the owner's share in the asset book or skipping the distribution of rewards for the owner's share.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/facets/SimplexFacet.sol#195
```solidity
    function addClosure(
        uint16 _cid,
        uint128 startingTarget,
        uint128 baseFeeX128,
        uint128 protocolTakeX128
    ) external {
        AdminLib.validateOwner();

        ClosureId cid = ClosureId.wrap(_cid);
        // We fetch the raw storage because Store.closure would check the closure for initialization.
        Closure storage c = Store.load().closures[cid];

        uint256 initTarget = Store.simplex().initTarget;
        if (startingTarget < initTarget) {
            revert InsufficientStartingTarget(startingTarget, initTarget);
        }

        uint256[MAX_TOKENS] storage neededBalances = c.init(
            cid,
            startingTarget,
            baseFeeX128,
            protocolTakeX128
        );

        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue;

            address token = tokenReg.tokens[i];
            uint256 realNeeded = AdjustorLib.toReal(
                token,
                neededBalances[i],
                true
            );

            TransferHelper.safeTransferFrom(
                token,
                msg.sender,
                address(this),
                realNeeded
            );

            Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded);
+           Store.assets().add(msg.sender, cid, startingTarget, 0);
        }
    }
```
