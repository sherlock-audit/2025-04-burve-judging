Urban Mango Walrus

High

# Rounding Loss in Underlying Vault Leads to Emission of `InsufficientBalance` Event, Confusing Users

### Summary

The `Closure` contract tracks the balance of each token (`self.balances[idx]`) and, every time it processes a value addition or removal, it calls the `trimBalance` function to check for any loss in its balance by interacting with the `Vertex`, `E4626`, and the underlying ERC4626 vault. If a loss is detected, it emits the `InsufficientBalance` event, which is intended to signal users that the underlying vault has been compromised and they should withdraw their liquidity.

However, due to the nature of the ERC4626 vault and the way it handles rounding during deposit and withdrawal operations, the swap ratio is not always `1:1`. Even for a newly created vault by the Burve team, a malicious hacker could potentially inflate the share value by transferring assets directly to the vault. As a result, rounding loss can occur during deposit and withdrawal operations, which may trigger the emission of the `InsufficientBalance` event.

This can lead users to believe that the underlying vault has been hacked, prompting them to prematurely withdraw their liquidity, which may not be necessary or appropriate.


### Root Cause
[Vertex.sol#L60-L74](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/Vertex.sol#L60-L74)

The `Closure` contract calls the `trimBalance` function in several key operations such as `addValue`, `addValueSingle`, `removeValue`, `removeValueSingle`, and `addTokenForValue`. The function `trimBalance` is defined as follows:

```solidity
   function trimBalance(Closure storage self, VertexId vid) internal {
      uint256 nonBgtValueStaked = self.valueStaked - self.bgtValueStaked;
@     _trimBalance(self, vid, nonBgtValueStaked);
   }
```

Then, in `_trimBalance`, the function proceeds to calculate the real balance and call the `trimBalance` function of the `Vertex` contract:

```solidity
   function _trimBalance(
      Closure storage self,
      VertexId vid,
      uint256 nonBgtValueStaked
   ) private {
      uint8 idx = vid.idx();
      // Roundup the balance we need.
      uint256 realBalance = AdjustorLib.toReal(idx, self.balances[idx], true);
@     (uint256 earnings, uint256 bgtReal) = Store.vertex(vid).trimBalance(
         self.cid,
         realBalance,
         self.valueStaked,
         self.bgtValueStaked
      );

      ...
   }
```

In the `trimBalance` function of the `Vertex` contract:

```solidity
   function trimBalance(
      Vertex storage self,
      ClosureId cid,
      uint256 targetReal,
      uint256 value,
      uint256 bgtValue
   ) internal returns (uint256 reserveSharesEarned, uint256 bgtResidual) {
      VaultProxy memory vProxy = VaultLib.getProxy(self.vid);
      uint256 realBalance = vProxy.balance(cid, false);
      // We don't error and instead emit in this scenario because clearly the vault is not working properly but if
      // we error users can't withdraw funds. Instead the right response is to lock and move vaults immediately.
@    if (targetReal > realBalance) {
         emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
         return (0, 0);
      }

      ...
   }
```

The Burve team interprets the emission of the `InsufficientBalance` event as an indicator that the vault is compromised and users should withdraw funds immediately. However, due to rounding losses during deposit and withdrawal operations, this event may be triggered even when no actual security breach has occurred.


### Internal Pre-conditions

1. A `Closure` is created, and the `Vertex` is initialized with `100e18` assets, with `self.balances[]` set to `100e18`.


### External Pre-conditions

1. Assume the vault is a newly initiated ERC4626 vault by the Burve team, with `totalSupply = 100e18` and `totalAssets = 100e18`, and a swap ratio of `1:1` (in this case, there would be no rounding loss).



### Attack Path

1. A hacker transfers `100 wei` to that ERC4626 vault via a direct `ERC20.transfer()` call, inflating the share value.
2. Through a sequence of `withdraw` and `deposit` operations, rounding loss occurs, leading to the emission of the `InsufficientBalance` event.

### Impact

`InsufficientBalance` event would be emitted.

This can lead users to believe that the underlying vault has been hacked, prompting them to prematurely withdraw their liquidity, which may not be necessary or appropriate.

### PoC


```solidity
contract SwapFacetTest is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(3);
        _initializeClosure(0x7, 100e18);
        _initializeClosure(0x3, 100e18);
        _fundAccount(alice);
        _fundAccount(bob);
        vm.stopPrank();
    }

    function testSwapCustom() public {
        //someone may intended transfer assets to underlying vault to inflate the swap ratio
        vm.startPrank(bob);
        MockERC20(tokens[0]).mint(bob, 100);
        MockERC20(tokens[0]).approve(address(vaults[0]), 100);
        MockERC20(tokens[0]).transfer(address(vaults[0]), 100);
        vm.stopPrank();

        vm.startPrank(alice);
        //after some liquidation and withdrawing.
        for (uint128 i = 1; i < 100; ++i) {
            valueFacet.addValueSingle(
                alice,
                0x7,
                i * 1e16 + i ** 2,
                0,
                tokens[0],
                0
            );
            valueFacet.removeValueSingle(
                alice,
                0x7,
                i * 1e14 + i ** 2,
                0,
                tokens[0],
                0
            );
        }

        // `InsufficientBalance` event would be emitted
        vm.expectEmit();
        emit VertexImpl.InsufficientBalance(
            VertexId.wrap(256),
            ClosureId.wrap(0x7),
            150305901412227386941,
            150305901412227386913
        );
        valueFacet.viewBalances(0x7, tokens[0]);
    }
}

```

### Mitigation

_No response_