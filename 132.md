Basic Fleece Wolf

High

# VaultE4626Impl::commit causes over-withdrawal of tokens during rehypothecation

### Summary

Over-withdrawal of tokens when `commit()` is called will cause funds to sit idle in the diamond contract. As this function is used frequently, as it is used in `trimBalance()`, idle tokens will accrue rapidly.

### Root Cause


In [VaultE4626Impl::commit](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L71-L73), `assetsToDeposit` is set to `0` before being used to subtract from `assetsToWithdraw`.

This causes an over-withdrawal, leading the excess funds sitting idle in the `diamond`, and not transferred back into the vault to accrue interest.

### Internal Pre-conditions

1. User to call `ValueFacet::addSingleForValue`



### External Pre-conditions

1. Vault must have accrued more underlying balance of tokens, causing the `vaultBalance` to go over `targetBalance`.

### Attack Path

1. Setup Diamond and initialize Closure using intended parameters given by protocol founders.
2. call `ValueFacet::addValue` to setup realistic conditions to send both tokens.
3. Call `ValueFacet::addSingleForValue` to deposit only 1 token.
4. Simulate external vault accruing underlying tokens.
5. Call `ValueFacet::collectEarnings` to trigger erroneous commit `else if` block.

### Impact

1. LPers will continue to lose potential interest due to idle funds when `trimBalance` is called.
2. Only Admin Roles can recover the idle funds, but redistribution is non-trivial.

### PoC

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import "forge-std/Test.sol";
import {IERC4626} from "openzeppelin-contracts/interfaces/IERC4626.sol";
contract ValueFacetBugTest is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(4);
        _fundAccount(alice);
        _fundAccount(bob);
        // Its annoying we have to fund first.
        _fundAccount(address(this));
        _fundAccount(owner);
        // So we have to redo the prank.
        vm.startPrank(owner);
        _initializeClosure(0xF, 100e18); // 1,2,3,4
        _initializeClosure(0xE, 100e18); // 2,3,4
        _initializeClosure(0xD, 100e18); // 1,3,4
        _initializeClosure(0xc, 1e12); // 3,4
        _initializeClosure(0xB, 100e18); // 1,2,4
        _initializeClosure(0xa, 1e12); // 2,4
        _initializeClosure(0x9, 1e18); // 1,4
        _initializeClosure(0x8, 1e12); // 4
        _initializeClosure(0x7, 100e18); // 1,2,3
        _initializeClosure(0x6, 1e12); // 2,3
        _initializeClosure(0x5, 1e12); // 1,3
        _initializeClosure(0x4, 1e12); // 3
        _initializeClosure(0x3, 100e18); // 1,2
        _initializeClosure(0x2, 1e12); // 2
        _initializeClosure(0x1, 1e12); // 1
        
        vm.stopPrank();
    }
        function getBalances(
        address who
    ) public view returns (uint256[4] memory balances) {
        for (uint8 i = 0; i < 4; ++i) {
            balances[i] = ERC20(tokens[i]).balanceOf(who);
        }
    }

    function diffBalances(
        uint256[4] memory a,
        uint256[4] memory b
    ) public pure returns (int256[4] memory diffs) {
        for (uint8 i = 0; i < 4; ++i) {
            diffs[i] = int256(a[i]) - int256(b[i]);
        }
    }


    function testBugInCommitFunction() public {
        uint256 oneBPSX128 = 1_000_000 << 128;
        
        // Step 1: Set Closure Fees to 0.1%
        console2.log("Step 1: Set Closure Fees: 0.1%%");
        vm.prank(owner);
        simplexFacet.setClosureFees(0xA, uint128(oneBPSX128 / 10000), 0); // 1000 basis points

        // Step 2: Add initial value (deposit tokens[1] and tokens[3])
        console2.log("Step 2: Add Value");
        valueFacet.addValue(address(this), 0xA, 1e12, 0); // Stakes 1e12, affects tokens[1] and tokens[3]

        // Step 3: Add more value to trigger deposit
        console2.log("Step 3: Add Single for Value");
        valueFacet.addSingleForValue(alice, 0xA, tokens[1], 6e12, 1 << 255, 0); // Deposits 6e12 of tokens[1]

        // Step 4: Simulate yield by minting tokens to the vault
        console2.log("Step 4: Mint tokens to vault (simulating yield)");
        MockERC20(tokens[1]).mint(address(vaults[1]), 3e12);

        // Step 5: Check vault balance before collectEarnings
        console2.log("Step 5: Record vault balance before collectEarnings");
        uint256 vaultBalanceBefore = ERC20(tokens[1]).balanceOf(address(vaults[1]));
        console2.log("Vault balance before: %e", vaultBalanceBefore);

        // Step 6: Collect earnings, triggering the bug
        console2.log("Step 6: Collect earnings");
        (uint256[MAX_TOKENS] memory collectedBalances,) = 
            valueFacet.collectEarnings(address(this), 0xA);
        console2.log("collectedBalances[1]: %e", collectedBalances[1]); // with error:  6.9599673e7
        // Step 7: Check vault balance after collectEarnings
        console2.log("Step 7: Record vault balance after collectEarnings");
        uint256 vaultBalanceAfter = ERC20(tokens[1]).balanceOf(address(vaults[1]));
        console2.log("Vault balance after: %e", vaultBalanceAfter);
        uint256 actualWithdrawn = vaultBalanceBefore - vaultBalanceAfter;
        console2.log("Actual withdrawn: %e", actualWithdrawn);

        // Step 8: Calculate expected withdrawal (based on logs from your test)
        uint256 assetsToDeposit = 3.0329e4; // From logs: 30329
       
       
        // Step 9: Assert the bug - actual withdrawal should exceed expected
        console2.log("Step 9: Verify the bug");
     
         uint256 currBalToken1InDiamond = ERC20(tokens[1]).balanceOf(address(diamond));
        // assertEq(currBalToken1InDiamond, assetsToDeposit);
        // Assert 1: tokens in the diamond is not due to protocol fees
        uint256[MAX_TOKENS] memory earningsArr =  simplexFacet.protocolEarnings();
        assertEq(0, earningsArr[1]);
        for (uint i=0; i< MAX_TOKENS;i++){
            assertEq(earningsArr[i] ,0);
        }
           // Assert 2: Over withdrawal causes `assetsToDeposit` amount of to be in the diamond,
        // and not earnings fees in the vault
        assertTrue(currBalToken1InDiamond >0);
        console2.log("currBalToken1InDiamond:%e",currBalToken1InDiamond);
        //Assert 3, current balance of tokens in diamond is the same
        assertTrue(currBalToken1InDiamond==assetsToDeposit);
    }
}
```

### Mitigation

Change the following function:

```diff
  function commit(VaultE4626 storage self, VaultTemp memory temp) internal {
        uint256 assetsToDeposit = temp.vars[1];
        uint256 assetsToWithdraw = temp.vars[2];

        if (assetsToDeposit > 0 && assetsToWithdraw > 0) {
            // We can net out and save ourselves some fees.
            if (assetsToDeposit > assetsToWithdraw) {
                assetsToDeposit -= assetsToWithdraw;
                assetsToWithdraw = 0;
            } else if (assetsToWithdraw > assetsToDeposit) {
-               assetsToDeposit = 0;
                assetsToWithdraw -= assetsToDeposit;
+.             assetsToDeposit = 0;
            } else {
                // Perfect net!
                return;
            }
        }

        if (assetsToDeposit > 0) {
            // Temporary approve the deposit.
            SafeERC20.forceApprove(
                self.token,
                address(self.vault),
                assetsToDeposit
            );
            self.totalVaultShares += self.vault.deposit(
                assetsToDeposit,
                address(this)
            );
            SafeERC20.forceApprove(self.token, address(self.vault), 0);
        } else if (assetsToWithdraw > 0) {
            // We don't need to hyper-optimize the receiver.
            self.totalVaultShares -= self.vault.withdraw(
                assetsToWithdraw,
                address(this),
                address(this)
            );
        }
    }
}
```