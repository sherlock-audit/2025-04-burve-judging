Shiny Macaroon Raccoon

Medium

# Mismatch in Closure Balance Accounting for Vaults with Deposit/Withdrawal Fees

# Mismatch in Closure Balance Accounting for Vaults with Deposit/Withdrawal Fees

### Summary

In the Burve protocol, some ERC4626 vaults impose deposit or withdrawal fees that are not directly exposed by the ERC4626 standard. To detect these, the system relies on preview functions. However, the internal accounting in `Closure` contracts does not properly align the recorded balances (`balances[i]`) with the actual redeemable balances in the underlying vault when fees are present. This mismatch can break downstream logic, particularly when the system tries to redistribute surplus tokens across closures. If the recorded balances reflect the pre-fee amounts but the vault can only deliver the net post-fee amount, surplus distribution calculations can silently fail, leaving funds stuck.


### Root Cause

Many key functions modify `Closure.balances`:

```solidity
SimplexFacet::addClosure(),
SwapFacet::swap(),
ValueFacet::addValue(),
ValueFacet::removeValue(),
ValueFacet::addValueSingle(),
ValueFacet::removeValueSingle(),
ValueFacet::addSingleForValue(),
ValueFacet::removeSingleForValue()
```

For example, in `SimplexFacet::addClosure()`, the system calls `Closure::init`:

```solidity
    function init(
        Closure storage self,
        ClosureId cid,
        uint256 target,
        uint256 baseFeeX128,
        uint256 protocolTakeX128
    ) internal returns (uint256[MAX_TOKENS] storage balancesNeeded) {
...
        for (
            VertexId vIter = VertexLib.minId();
            !vIter.isStop();  
            vIter = vIter.inc()
        ) {
            if (cid.contains(vIter)) {
                self.n += 1;
                // We don't need to check this assignment.
@>              self.balances[vIter.idx()] += target;  
            }
        }
        require(self.n != 0, "InitEmptyClosure");
        // Tiny burned value.
        self.valueStaked += target * self.n;  
        return self.balances;
    }
```

Later, it converts to real vault values:

```solidity
    function addClosure(
        uint16 _cid,
        uint128 startingTarget,
        uint128 baseFeeX128,
        uint128 protocolTakeX128
    ) external {


        TokenRegistry storage tokenReg = Store.tokenRegistry();
        for (uint8 i = 0; i < MAX_TOKENS; ++i) {
            if (!cid.contains(i)) continue;

            address token = tokenReg.tokens[i];
@>          uint256 realNeeded = AdjustorLib.toReal(
                token,
                neededBalances[i],   
                true
            );

            TransferHelper.safeTransferFrom(  
                token,
                msg.sender,
                address(this),
                realNeeded
            );  

@>          Store.vertex(VertexLib.newId(i)).deposit(cid, realNeeded); 
        }
    }
```

Here lies the issue: if the vault charges a fee on deposits, the actual vault balance will be reduced (some tokens are taken as fees). But when the system later calculates the closure’s token share, it calls:

```solidity
    function balance(  
        VaultE4626 storage self,
        VaultTemp memory temp,
        ClosureId cid,
        bool roundUp
    ) internal view returns (uint128 amount) {
        if (self.totalShares == 0) return 0;
        uint256 newlyAdding = FullMath.mulX128(
            temp.vars[1],
            temp.vars[3],
            roundUp
        );
        uint256 totalAssets = temp.vars[0] + newlyAdding - temp.vars[2];

        uint256 fullAmount = roundUp
            ? FullMath.mulDivRoundingUp(
                self.shares[cid],
                totalAssets,
                self.totalShares
            )
            : FullMath.mulDiv(self.shares[cid], totalAssets, self.totalShares);
        amount = min128(fullAmount);
    }
```

Where:

`temp.vars[0] = vault.previewRedeem(totalVaultShares)` → actual redeemable assets.

`temp.vars[3] = vault.previewRedeem(vault.previewDeposit(1 << 128))` → fee-adjusted discount factor.

Thus, the system compares:

`Closure.balances[i]` (pre-fee recorded value)    vs.  `vProxy.balance(cid, false)` (post-fee real value)

For example, if:

* `Closure.balances[A] = 10000`
* Deposit  =  withdraw fees = 5%
* `vProxy.balance(cid, false)` returns 9025

Then surplus redistribution using `Closure.trimAllBalances` will fail.


### Attack Path

`Closure.trimAllBalances` will call:

```solidity
function _trimBalance(...) private {
    uint256 realBalance = AdjustorLib.toReal(idx, self.balances[idx], true);
    (uint256 earnings, uint256 bgtReal) = Store.vertex(vid).trimBalance(
        self.cid,
        realBalance,
        self.valueStaked,
        self.bgtValueStaked
    );
}
```

This calls:

```solidity
function trimBalance(...) internal returns (...) {
    uint256 realBalance = vProxy.balance(cid, false);
    if (targetReal > realBalance) {
        emit InsufficientBalance(self.vid, cid, targetReal, realBalance);
        return (0, 0);
    }
}
```

This is exactly where the vulnerability occurs, as seen in the previous example. At this point, realBalance = 9025, while the passed-in targetReal = 10000, which directly triggers the condition targetReal > realBalance and leads to a return(0, 0). The critical point is that even if realBalance increases by, say, 500 (to 9525), as long as the condition targetReal > realBalance still holds, this additional 500 in profits cannot be distributed, effectively leaving it stuck.


### Affected Code

https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L51-L59


### Impact

Closures interacting with ERC4626 vaults that charge fees will gradually drift out of sync between internal balances and actual vault balances. This blocks surplus redistributions, leads to silent revenue loss, and can trap funds permanently if left unaddressed.


### Mitigation

Ensure that `Closure.balances[i]` reflects vault-adjusted, fee-discounted values by multiplying them with the discount factor:

```solidity
temp.vars[3] = vault.previewRedeem(vault.previewDeposit(1 << 128));
```

This aligns internal accounting with the real, net-of-fee vault balances, allowing redistribution functions to operate correctly even when deposit or withdrawal fees apply.