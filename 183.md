Damaged Admiral Grasshopper

High

# User may not be able to remove value from a closure through a single token or they receive less token than expected

### Summary

After user add value to a closure, if they want to remove value through a single token, the transaction will revert or they receive less token than expected.

### Root Cause

When user calls to remove value from a closure through a single token, [removeValueSingle()]() is invoked to calculate the values and balances.

[ClosureImpl::removeValueSingle()](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/closure/Closure.sol#L269-L284):
```solidity
        uint256 fairVBalance = iterSingleValueDiff(self, valIter, false);
        removedAmount = self.balances[valIter.vIdx] - fairVBalance;
        // Now we have the addedValue which we can remove, and the fair balance for our vertex.
        uint256 veX128 = SimplexLib.getEX128(valIter.vIdx);
        uint256 currentValueX128 = ValueLib.v(
            self.targetX128,
            veX128,
            fairVBalance,
            false
        );
        uint256 finalAmount = ValueLib.x(
            self.targetX128,
            veX128,
@>          currentValueX128 - valIter.valueSumX128,
            true
        );
```
`currentValueX128` is calculated based to the value function V(x):

$$V(x) = {(e+2)t-{(e+1)^2t^2 \over (x+et)}}$$

â€‹And `valIter.valueSumX128` is calculated in `iterSingleValueDiff()` and it is the sum of the value difference before and after removing the each tokens.

$$Value Sum = {\sum_{i=0}^n (V(self.balances[i])-V(fairVBalance_i))}$$

The problem is that based on the value of $$t$$ (i.e. `self.targetX128`) at the time of calculation, `currentValueX128` might be less than `valIter.valueSumX128`, and the transaction will be reverted due to underflow error, or the transaction goes through but the token received by the user can be less than expected.

Taking the example of the POC, when `self.targetX128` is `340282366920938463463374607431768211456000000000000000000`(1e18), user will only receive 0.96 token by remove value from a 2-token closure through a single token, whereas they initially deposited 0.5 in each token, and the loss is 0.04 token. When the target balance is a less value, for example, 1e17, then `currentValueX128` would be less than `valIter.valueSumX128` and the transaction will revert.

The time is limited for a thorough mathematical analysis, but it's obviously the value function is not implemented properly and likely there is precision loss in the calculation.

### Internal Pre-conditions

User removes value from a closure through a single value.

### External Pre-conditions

None

### Attack Path

None

### Impact

1. User cannot remove value through a single value;
2. User receives less token and suffers a loss.

### PoC

Please run `forge test --mt testAudit_RemoveValueSingleError`.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "./mocks/MockERC20.sol";
import "openzeppelin-contracts/mocks/docs/ERC4626Fees.sol";
import {VaultType} from "../src/multi/vertex/VaultPointer.sol";
import "../src/multi/Diamond.sol";
import "../src/integrations/adjustor/MixedAdjustor.sol";
import "../src/integrations/adjustor/DecimalAdjustor.sol";

contract AuditTest is Test {
    uint8 constant MAX_TOKENS = 16;

    address owner = makeAddr("OWNER");
    address diamond;

    MockERC20 tokenA = new MockERC20("Token A", "A", 18);
    MockERC20 tokenB = new MockERC20("Token B", "B", 6);

    MockERC4626WithFees vaultA = new MockERC4626WithFees(tokenA, "Vault A", "VA");
    MockERC4626WithFees vaultB = new MockERC4626WithFees(tokenB, "Vault B", "VB");

    function setUp() public {
        vm.startPrank(owner);

        BurveFacets memory facets;

        {
            address vauleFacet = address(new ValueFacet());
            address valueTokenFacet = address(new ValueTokenFacet());
            address simplexFacet = address(new SimplexFacet());
            address swapFacet = address(new SwapFacet());
            address vaultFacet = address(new VaultFacet());
            MixedAdjustor mixedAdjustor = new MixedAdjustor();
            // Configure adjustor
            {
                DecimalAdjustor decimalAdjustor = new DecimalAdjustor();
                mixedAdjustor.setAdjustor(address(tokenB), address(decimalAdjustor));
            }
            address adjustor = address(mixedAdjustor);

            facets = BurveFacets({
                valueFacet: vauleFacet,
                valueTokenFacet: valueTokenFacet,
                simplexFacet: simplexFacet,
                swapFacet: swapFacet,
                vaultFacet: vaultFacet,
                adjustor: adjustor
            });
        }

        diamond = address(new SimplexDiamond(facets, "ValueToken", "BVT"));

        vm.label(diamond, "Diamond");
        vm.label(address(tokenA), "Token A");
        vm.label(address(tokenB), "Token B");
        vm.label(address(vaultA), "Vault A");
        vm.label(address(vaultB), "Vault B");

        // Add Vertex
        {
            // TokenA
            SimplexFacet(diamond).addVertex(address(tokenA), address(vaultA), VaultType.E4626);

            // TokenB
            SimplexFacet(diamond).addVertex(address(tokenB), address(vaultB), VaultType.E4626);
        }

        // Add Closure
        {
            bool success;

            uint16 cid = 3; // 0b11
            uint128 startingTarget = 1e18;
            // uint128 baseFeeX128 = 34028236692093846346337460743176821145; // 10%
            // uint128 protocolTakeX128 = 34028236692093846346337460743176821145;  //10%
            uint128 baseFeeX128 = 0;
            uint128 protocolTakeX128 = 0;

            uint256 amountA = startingTarget / 10 ** (18 - tokenA.decimals());
            uint256 amountB = startingTarget / 10 ** (18 - tokenB.decimals());

            tokenA.mint(owner, amountA);
            tokenA.approve(diamond, amountA);
            tokenB.mint(owner, amountB);
            tokenB.approve(diamond, amountB);
            SimplexFacet(diamond).addClosure(cid, startingTarget, baseFeeX128, protocolTakeX128);
        }

        vm.stopPrank();
    }

    function testAudit_RemoveValueSingleError() public {
        uint16 cid = 3;

        address alice = makeAddr("Alice");
        tokenA.mint(alice, 0.5e18 + 1);
        tokenB.mint(alice, 0.5e6 + 1);

        vm.startPrank(alice);
        tokenA.approve(diamond, 0.5e18 + 1);
        tokenB.approve(diamond, 0.5e6 + 1);
        ValueFacet(diamond).addValue(alice, cid, 1e18, 0);
        vm.stopPrank();

        vm.prank(alice);
        ValueFacet(diamond).removeValueSingle(alice, cid, 1e18, 0, address(tokenA), 0);

        // Alice only receives 0.96 token back, the loss is 0.4
        assertApproxEqRel(tokenA.balanceOf(alice), 0.96e18, 0.002e18);
    }
}

contract MockERC4626WithFees is ERC4626Fees {
    uint256 entryFeeBasisPoints;
    uint256 exitFeeBasisPoints;
    address feeRecipient;

    constructor(
        IERC20 asset_, 
        string memory name_, 
        string memory symbol_
    ) ERC4626(asset_) ERC20(name_, symbol_) {
        feeRecipient = msg.sender;
    }

    function setEntryFee(uint256 entryFeeBasisPoints_) public {
        entryFeeBasisPoints = entryFeeBasisPoints_;
    }

    function setExitFee(uint256 exitFeeBasisPoints_) public {
        exitFeeBasisPoints = exitFeeBasisPoints_;
    }

    function _entryFeeBasisPoints() internal view override returns (uint256) {
        return entryFeeBasisPoints;
    }

    function _exitFeeBasisPoints() internal view override returns (uint256) {
        return exitFeeBasisPoints;
    }

    function _entryFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }

    function _exitFeeRecipient() internal view override returns (address) {
        return feeRecipient;
    }
}
```

### Mitigation

Implement the value function properly.