Sneaky Burlap Pigeon

Medium

# The `removeSingleForValue` Function is Not Safe

### Summary
When a user removes value by specifying a token amount, the target value is verified by adjusting it from the current value. However, the amount of one token can never be less than `target value / (e + 2)`, and for the template first target value, this condition may not be satisfied. Consequently, this could lead to the transaction being reverted.

### Root Cause
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L365
```diff
    function removeTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(!isAnyLocked(self), CannotRemoveWithLockedVertex(self.cid));
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // We tax first so the amount which moves up the value they're paying.
        uint256 taxedRemove = UnsafeMath.divRoundingUp(
            amount << 128,
            ONEX128 - self.baseFeeX128
        );
        tax = taxedRemove - amount;
        // Use the ValueLib's newton's method to solve for the value removed and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky and strange, but see addTokenForValue for reason.
        self.balances[idx] -= taxedRemove;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);     
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
365:            self.targetX128 // TODO: Add better starting estimate logic
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        uint256 valueX128 = ((self.targetX128 - newTargetX128) * self.n);
        value = valueX128 >> 128;
        if ((value << 128) > 0) value += 1; // We need to round up.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down both ways.
        self.targetX128 = newTargetX128;
        self.setBalance(idx, self.balances[idx]);
        self.valueStaked -= value;
        self.bgtValueStaked -= bgtValue;
    }
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L221
```solidity
    /// Evaluate the total value of all tokens minus N * target where N is the number of tokens.
    /// This can go negative during the process of searching for a valid t.
    function f(
        uint256 tX128,
        uint256[] memory esX128,
        uint256[] memory xs
    ) internal pure returns (int256 ftX128) {
        uint256 n = xs.length;
        for (uint256 i = 0; i < n; ++i) {
221:        ftX128 += int256(v(tX128, esX128[i], xs[i], false));
        }
        ftX128 -= int256(n * tX128);
    }
```
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/Value.sol#L79-L102
```solidity
    function v(
        uint256 tX128,
        uint256 eX128,
        uint256 _x,
        bool roundUp
    ) internal pure returns (uint256 valueX128) {
        uint256 etX128 = FullMath.mulX128(eX128, tX128, roundUp);
        valueX128 = etX128 + 2 * tX128;
        uint256 denomX128 = (_x << 128) + etX128;
        uint256 sqrtNumX128 = etX128 + tX128;
        uint256 subtract;
        if (roundUp) {
            subtract = FullMath.mulDivRoundingUp(
                sqrtNumX128,
                sqrtNumX128,
                denomX128
            );
        } else {
            subtract = FullMath.mulDiv(sqrtNumX128, sqrtNumX128, denomX128);
        }
        if (subtract > valueX128)
            revert XTooSmall(_x, (tX128 / (eX128 + TWOX128)) + 1);
        valueX128 -= subtract;
    }
```
As can be seen in `Value.sol::L221`, the `v` function, which does not allow negative values, is being used.

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### PoC
The PoC below demonstrates that a user can withdraw `1.88861066235864297251e20` token0 using the `removeValueSingle` function, while they cannot withdraw `1.84e20` token0 using the `removeSingleForValue` function.

https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/test/facets/ValueFacet.t.sol
```solidity
    function test_removeSingleForValue1() public {
        valueFacet.addValue(alice, 0x7, 3e20, 0);

        uint256[4] memory initBalances = getBalances(alice);
        vm.startPrank(alice);
        valueFacet.removeValueSingle(alice, 0x7, 2.06e20, 0, tokens[0], 0);
        uint256[4] memory currentBalances = getBalances(alice);
        int256[4] memory diffs = diffBalances(currentBalances, initBalances);
        console.log("alice's withdraw[0] : %e", diffs[0]);
    }
    function test_removeSingleForValue2() public {
        valueFacet.addValue(alice, 0x7, 3e20, 0);

        vm.startPrank(alice);
        valueFacet.removeSingleForValue(alice, 0x7, tokens[0], 1.84e20, 0, 0);

        console.log("1.84e20 withdraw Success!");
    }
```
forge test --match-test "test_removeSingleForValue" -vv
Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_removeSingleForValue1() (gas: 949075)
Logs:
  alice's withdraw[0] : 1.88861066235864297251e20

[FAIL: XTooSmall(16000000000000000000 [1.6e19], 16666666666666666667 [1.666e19])] test_removeSingleForValue2() (gas: 952620)
```

After mitigation, Result:
```bash
Ran 2 tests for test/facets/ValueFacet.t.sol:ValueFacetTest
[PASS] test_removeSingleForValue1() (gas: 949075)
Logs:
  alice's withdraw[0] : 1.88861066235864297251e20

[PASS] test_removeSingleForValue2() (gas: 1021210)
Logs:
  1.84e20 withdraw Success!
```
### Impact
The time sensitivity function is DoSed.

### Mitigation
https://github.com/sherlock-audit/2025-04-burve/tree/main/Burve/src/multi/closure/Closure.sol#L365
```diff
    function removeTokenForValue(
        Closure storage self,
        VertexId vid,
        uint256 amount,
        uint256 bgtPercentX256,
        SearchParams memory searchParams
    ) internal returns (uint256 value, uint256 bgtValue, uint256 tax) {
        require(!isAnyLocked(self), CannotRemoveWithLockedVertex(self.cid));
        require(self.cid.contains(vid), IrrelevantVertex(self.cid, vid));
        trimAllBalances(self);
        uint8 idx = vid.idx();
        // We tax first so the amount which moves up the value they're paying.
        uint256 taxedRemove = UnsafeMath.divRoundingUp(
            amount << 128,
            ONEX128 - self.baseFeeX128
        );
        tax = taxedRemove - amount;
        // Use the ValueLib's newton's method to solve for the value removed and update target.
        uint256[MAX_TOKENS] storage esX128 = SimplexLib.getEsX128();
        // This is tricky and strange, but see addTokenForValue for reason.
        self.balances[idx] -= taxedRemove;
        uint256 newTargetX128;
        {
            (uint256[] memory mesX128, uint256[] memory mxs) = ValueLib
                .stripArrays(self.n, esX128, self.balances);
+            if (self.targetX128 > self.balances[idx] * (esX128[idx] + (2 << 128))) 
+                self.targetX128 = self.balances[idx] * (esX128[idx] + (2 << 128));       
            newTargetX128 = ValueLib.t(
                searchParams,
                mesX128,
                mxs,
365:            self.targetX128 // TODO: Add better starting estimate logic
            );
        }
        // The pool is now entirely correct by just updating the target and value balances.
        uint256 valueX128 = ((self.targetX128 - newTargetX128) * self.n);
        value = valueX128 >> 128;
        if ((value << 128) > 0) value += 1; // We need to round up.
        bgtValue = FullMath.mulX256(value, bgtPercentX256, false); // Convention to round BGT down both ways.
        self.targetX128 = newTargetX128;
        self.setBalance(idx, self.balances[idx]);
        self.valueStaked -= value;
        self.bgtValueStaked -= bgtValue;
    }
```