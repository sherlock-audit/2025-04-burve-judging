Dandy Denim Kitten

Medium

# Protocol and LPs lose all fees due to uninitialized variable in removal function

### Summary

A variable reference error in [removeValueSingle](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L236) will cause a complete loss of fees for Burve Protocol and LPs as users will avoid paying tax when performing single-token liquidity removals.

### Root Cause

In [removeValueSingle](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L236) the tax calculation uses `removedBalance` instead of `realRemoved`:
```solidity
uint256 realTax = FullMath.mulDiv(
            removedBalance,   // audit
            nominalTax,
            removedNominal
        );
```

### Internal Pre-conditions

- Protocol needs to be deployed with non-zero fees (baseFeeX128 > 0)
- Users need to have deposited liquidity into the protocol
- Users need to withdraw liquidity using the removeValueSingle function

### External Pre-conditions

- None required

### Attack Path

User deposits liquidity normally into a closure with addValue or addValueSingle
User calls removeValueSingle to withdraw a portion of their liquidity as a single token
The undefined variable removedBalance defaults to 0, causing realTax to be 0
No tax is collected, but the user receives the full token amount
The protocol and LPs receive no fees from this operation

### Impact

The protocol and liquidity providers suffer revenue loss of up to 100% of expected fees from single-token removals. For a protocol with a 1% fee, this represents a 1% direct loss on all single-token withdrawals. The user gains this fee that should have been paid to the protocol and other LPs.

### PoC

In the `MultiSetup.u.sol` kindly add
```solidity
    function _initializeClosureWithDefaultFee(uint16 cid, uint128 initValue) internal {
        // 1% fee in X128 format: 1 * 2^128
        uint128 feeTaxX128 = 1 << 127;
        uint128 feeProtocolX128 = 1 << 127;
        
        // Mint ourselves enough to fund the initial target of the pool
        for (uint256 i = 0; i < tokens.length; ++i) {
            if ((1 << i) & cid > 0) {
                MockERC20(tokens[i]).mint(owner, initValue);
                MockERC20(tokens[i]).approve(
                    address(diamond),
                    type(uint256).max
                );
            }
        }
        simplexFacet.addClosure(cid, initValue, feeTaxX128, feeProtocolX128);
    }
```

Update
```solidity
function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(4);
        _fundAccount(alice);
        _fundAccount(bob);
        // Its annoying we have to fund first.
        _fundAccount(address(this));
        _fundAccount(owner);
        // So we have to redo the prank.
        vm.startPrank(owner);
        // ... other init calls ...
        _initializeClosureWithDefaultFee(0x9, 1e18); // 1,4

        // ... other init calls ...
        vm.stopPrank();
    }
```

```solidity
function testZeroTaxInRemoveValueSingle() public {
        // 1. Setup initial state with a non-trivial amount
        // Create a position with initial value
        valueFacet.addValue(alice, 0x9, 10e18, 0);
        
        // 2. Setup withdrawal parameters
        address token = tokens[0];
        uint128 valueToRemove = 1e18;
        uint128 minReceive = 0; // No slippage protection for testing
        
        // 3. Track token balances before withdrawal
        uint256 aliceBalanceBefore = ERC20(token).balanceOf(alice);
        
        // 4. Track protocol earnings before withdrawal
        uint256 earningsBefore = getEarnings(0x9, token);
        uint256 protocolEarningsBefore = getProtocolEarnings(token);
        
        // 5. Execute the function with the bug
        vm.startPrank(alice);
        uint256 removedBalance = valueFacet.removeValueSingle(
            alice,
            0x9,
            valueToRemove,
            0, // No BGT
            token,
            minReceive
        );
        vm.stopPrank();

        
        // 6. Verify tokens were received
        uint256 aliceBalanceAfter = ERC20(token).balanceOf(alice);
        uint256 receivedAmount = aliceBalanceAfter - aliceBalanceBefore;
        assertEq(receivedAmount, removedBalance, "User received the expected amount");
        
        // 7. Check earnings (tax) after withdrawal
        uint256 earningsAfter = getEarnings(0x9, token);
        uint256 protocolEarningsAfter = getProtocolEarnings(token);
        uint256 taxCollected = earningsAfter - earningsBefore;
        
        // 8. Bug demonstration: With the bug, tax will be 0
        // This is because the line:
        // uint256 realTax = FullMath.mulDiv(removedBalance, nominalTax, removedNominal);
        // uses removedBalance which is uninitialized (0) at that point in the code
        
        // This assertion should fail with the bug and pass after fixing
        // assertTrue(taxCollected > 0, "Tax should be collected on single-token withdrawal");
        
        // Verify the exact issue by checking that no earnings were added
        assertEq(taxCollected, 0, "With the bug, tax is exactly zero");
        assertEq(protocolEarningsAfter, protocolEarningsBefore, "Protocol earnings should not change");
    }
    
    // Helper function to get earnings for a token in a closure
    function getEarnings(uint16 _closureId, address token) internal view returns (uint256) {
        uint8 tokenIdx = getTokenIndex(token);
        (,, uint256[MAX_TOKENS] memory earnings,) = valueFacet.queryValue(alice, _closureId);
        return earnings[tokenIdx];
    }

    function getProtocolEarnings(address token) internal view returns (uint256) {
        uint8 tokenIdx = getTokenIndex(token);
        uint256[MAX_TOKENS] memory protocolEarnings = simplexFacet.protocolEarnings();
        return protocolEarnings[tokenIdx];
    }
    
    // Helper function to get token index
    function getTokenIndex(address token) internal view returns (uint8) {
        for (uint8 i = 0; i < tokens.length; i++) {
            if (tokens[i] == token) {
                return i;
            }
        }
        revert("Token not found");
    }
```

Test with `forge test --match-test testZeroTaxInRemoveValueSingle`

### Mitigation


```solidity
uint256 realTax = FullMath.mulDiv(
            realRemoved, // update removeBalance with realRemoved
            nominalTax,
            removedNominal
        );
```