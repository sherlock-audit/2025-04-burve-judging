Interesting Taupe Ostrich

High

# Incorrect `realTax` calculation in `removeValueSingle()` leads to zero tax collection

### Summary

In the `removeValueSingle()` function of `ValueFacet`, the real tax calculation incorrectly uses `removedBalance` (which is uninitialized) instead of `realRemoved` when calculating the tax amount. This results in zero tax being deducted from the user's withdrawal, allowing users to withdraw their full balance without paying the required fees.


### Root Cause

The root cause is in the `removeValueSingle()` function where the [tax calculation](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/ValueFacet.sol#L235-L241) is performed before `removedBalance` is initialized.
```solidity
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);
        uint256 realTax = FullMath.mulDiv(
            // @audit-issue This uses removedBalance which is effectively  0
>>          removedBalance,
            nominalTax,
            removedNominal
        );
        // @audit-issue Protoocol misses out on funds here
>>      c.addEarnings(vid, realTax);
        // @audit-issue No deduction from removedBalance
>>      removedBalance = realRemoved - realTax;
        require(removedBalance >= minReceive, PastSlippageBounds());
        // @audit-issue Users bypass tax
>>      TransferHelper.safeTransfer(token, recipient, removedBalance);
```
The function calculates `realTax` using `removedBalance` which is still zero at that point, instead of using `realRemoved` which contains the actual amount being withdrawn.

### Internal Pre-conditions

1. The user must have value deposited in a closure
2. The closure must have fees configured

### External Pre-conditions

1. The user must have sufficient balance to withdraw


### Attack Path

1. User deposits value into a closure with configured fees
2. User calls `removeValueSingle()` to withdraw their value
3. The function calculates `realRemoved` from `removedNominal`
4. The function incorrectly calculates `realTax` using uninitialized `removedBalance`
5. Since `removedBalance` is zero, `realTax` becomes zero
6. Zero fees are collected for a given token i.e `c.addEarnings(vid, 0)`
7. The function sets `removedBalance = realRemoved - realTax`
8. User receives full withdrawal amount without any fee deduction


### Impact

This vulnerability allows users to bypass fee payments when withdrawing value from closures. This leads to:
1. Omission of fee collection for a given token as `realTax` applied in the process is zero

### PoC

// (1) Create a new function `removeValueSingleModified()` in `ValueFacet.sol` which is just
// the existing `removeValueSingle()` but returning both `removedBalance` and `realTax`:
```solidity
    function removeValueSingleModified(
        address recipient,
        uint16 _closureId,
        uint128 value,
        uint128 bgtValue,
        address token,
        uint128 minReceive
    ) external nonReentrant returns (uint256 removedBalance, uint256 realTax) {
        if (value == 0) revert DeMinimisDeposit();
        require(bgtValue <= value, InsufficientValueForBgt(value, bgtValue));
        ClosureId cid = ClosureId.wrap(_closureId);
        Closure storage c = Store.closure(cid); // Validates cid.
        VertexId vid = VertexLib.newId(token); // Validates token.
        Store.assets().remove(msg.sender, cid, value, bgtValue);
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);
        realTax = FullMath.mulDiv(
            removedBalance,
            nominalTax,
            removedNominal
        );
        c.addEarnings(vid, realTax);
        removedBalance = realRemoved - realTax; // How much the user actually gets.
        require(removedBalance >= minReceive, PastSlippageBounds());
        TransferHelper.safeTransfer(token, recipient, removedBalance);
    }
```
// (2) Include this new function's selector in the diamond contract.
```diff
        {
-           bytes4[] memory valueSelectors = new bytes4[](8);
+           bytes4[] memory valueSelectors = new bytes4[](9);
            valueSelectors[0] = ValueFacet.addValue.selector;
            valueSelectors[1] = ValueFacet.addValueSingle.selector;
            valueSelectors[2] = ValueFacet.addSingleForValue.selector;
            valueSelectors[3] = ValueFacet.removeValue.selector;
            valueSelectors[4] = ValueFacet.removeValueSingle.selector;
            valueSelectors[5] = ValueFacet.removeSingleForValue.selector;
            valueSelectors[6] = ValueFacet.queryValue.selector;
            valueSelectors[7] = ValueFacet.collectEarnings.selector;
            // Add this
+           valueSelectors[8] = ValueFacet.removeValueSingleModified.selector;
            cuts[3] = FacetCut({
                facetAddress: facets.valueFacet,
                action: FacetCutAction.Add,
                functionSelectors: valueSelectors
            });
        }
```
// (3) In `test/facets/ValueFacet.t.sol`, add this test:
```solidity
    function testConstantZeroRealTaxInRemoveValueSingle() public {
        uint256 oneX128 = 1 << 128;
        vm.prank(owner);
        // owner sets closure fee. This is to be deducted upon value addition and removal
        simplexFacet.setClosureFees(0xD, uint128(oneX128 / 10000), 0);
        
        // performs addValueSingle()
        uint256 tokensSent = valueFacet.addValueSingle(
            address(this),
            0xD,
            13e13,
            12e12,
            tokens[2],
            0
        );
        // he then removes this value.
        // When fees exist, we expect a positive deduction(realTax) 
        (uint256 tokensReceived, uint256 realTax) = valueFacet.removeValueSingleModified(
            address(this),
            0xD,
            13e13,
            12e12,
            tokens[2],
            0
        );
        
        // assert that fee is actually not deducted during value removal
        assertEq(realTax, 0);

        // However, we still expect tokensReceived to be less than tokensSent as tax 
        // was included during value addition
        assertGt(tokensSent, tokensReceived);
    }
```
// (4) Run: `forge test --mt testConstantZeroRealTaxInRemoveValueSingle -vvvv`

### Mitigation

Modify the function to ensure tax deduction from `realRemoved`:
```diff
        (uint256 removedNominal, uint256 nominalTax) = c.removeValueSingle(
            value,
            bgtValue,
            vid
        );
        uint256 realRemoved = AdjustorLib.toReal(token, removedNominal, false);
        Store.vertex(vid).withdraw(cid, realRemoved, false);
        uint256 realTax = FullMath.mulDiv(
-           removedBalance,
+           realRemoved,
            nominalTax,
            removedNominal
        );
```