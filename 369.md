Sunny Cloth Cod

High

# Unaccounted BGT Tokens in Migration Process

### Summary

The untracked token transfer in BGTExchanger migration will cause users to earn 0 BGT rewards when they should be earning rewards as the protocol will silently use an incorrect bgt amount due to unaccounted tokens in the bgtBalance; the rewards are effectively lost forever.

### Root Cause

In [BGTExchanger.sol::L110-112](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/integrations/BGTExchange/BGTExchanger.sol#L110) when a migration is performed, tokens are transferred using `sendBalance()` but the internal `bgtBalance` accounting state in the new exchanger is not updated. This creates a discrepancy between the actual token balance in the contract and the internally tracked balance, which is used to distribute user rewards.

```solidity
    function exchange(
        address inToken,
        uint128 amount
    ) external returns (uint256 bgtAmount, uint256 spendAmount) {
        if (!isExchanger[msg.sender]) revert NoExchangePermissions();
        
        // bgtAmount calculated
-->    (bgtAmount, spendAmount) = viewExchange(inToken, amount);
        if (bgtAmount > 0) {
        // decrements bgtBalance by calculated bgtAmount
-->         bgtBalance -= bgtAmount;
            TransferHelper.safeTransferFrom( // We take what we need.
                    inToken,
                    msg.sender,
                    address(this),
                    spendAmount
                );
         owed[msg.sender] += bgtAmount;
        }
```

### Internal Pre-conditions

As per the Migration procedure from the [SimplexFacet::setBGTExchanger() docstring](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/SimplexFacet.sol#L317)

1. A migration from the old BGTExchanger to a new one needs to be performed
2. The old exchanger needs to have BGT tokens (bgtBalance > 0)
3. Tokens are transferred from the old exchanger to the new one using sendBalance()

### External Pre-conditions

NA

### Attack Path

1. Protocol admin follows the migration process as documented in `SimplexFacet::setBGTExchanger()`
2. Admin sets the new exchanger as backup for the old one using `setBackup()`
3. Admin transfers all BGT tokens from the old exchanger to the new one using `sendBalance()`
4. Any call using `exchange` is silently failing right now; there is BGT within the contract but users are not earning them.
5. Admin funds the new exchanger with additional BGT tokens using `fund()`
6. The new exchanger now has more tokens than are accounted for in `bgtBalance`
7. Users will only be able to start earning BGT up to the new amount of `bgtBalance`
### Impact

The users suffer the loss of the right to earn BGT tokens that were transferred during migration but not properly accounted for. The `bgtBalance` will be/reach 0 when there is still BGT available to be distributed, therefore, when users perform operations that call `exchange`, they will be unrightfully rewarded 0 BGT. These unaccounted tokens are effectively lost forever as they will never be distributed to users.

### PoC

- This PoC is a simplified version of the interactions between the diamond and the BGTExchanger; it is important to understand how these interact so that the simplified version can be justified

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/d97ba814-3e7e-4271-8c54-d9d6b78d4135)

- The BGTExchanger only keeps track of the accounting of the diamond as whole, not the individual users; therefore, a placeholder address has been used to mimic the interactions of whenBGTExchanger.sol::exchange() or BGTExchanger.sol::withdraw() are called.
- The PoC also follows the [migration pattern](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/SimplexFacet.sol#L317) described in SimplexFacet.
- BGT token is token controlled by the team; it is basically just a proxy to the actual BGT. therefore, it is funded to the exchanger by the team

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.27;

import {Test, console} from "forge-std/Test.sol";

import {MockERC20} from "../mocks/MockERC20.sol";

import {IBGTExchanger} from "src/integrations/BGTExchange/IBGTExchanger.sol";
import {BGTExchanger} from "src/integrations/BGTExchange/BGTExchanger.sol";

contract BGTExchangerMigrationTest is Test{
    IBGTExchanger public exchangerOriginal;
    IBGTExchanger public exchangerMigrated;
    Tokens tokensDeployer;
    
    address bgtToken;
    address inTokenAddress;

    address[16] tokensAddresses;

    address owner = makeAddr("owner");

    address diamondPlaceHolder = makeAddr("diamondPlaceHolder");

    uint256 MINT_AMOUNT_OWNER = 1000e18;
    uint256 MINT_AMOUNT_DIAMOND_PLACEHOLDER = 1000e18;
    uint256 FUND_AMOUNT = 100e18;
    uint256 EXCHANGE_AMOUNT = 10e18;

    function setUp() public {
        vm.startPrank(owner);
        tokensDeployer = new Tokens();

        // Deploy bgt token and mint tokens to owner
        bgtToken = tokensDeployer.createToken("BGT", "BGT", 18);
        vm.stopPrank();
        mintTokens(owner, bgtToken, MINT_AMOUNT_OWNER);
        tokensAddresses[0] = bgtToken;

        // Deploy mock inToken and mint to diamondPlaceHolder an amount
        vm.startPrank(owner);
        inTokenAddress = tokensDeployer.createToken("inToken", "INT", 18);
        vm.stopPrank();
        mintTokens(diamondPlaceHolder, inTokenAddress, MINT_AMOUNT_DIAMOND_PLACEHOLDER);
        tokensAddresses[1] = inTokenAddress;

        vm.startPrank(owner);
        exchangerOriginal = new BGTExchanger(bgtToken);
        exchangerMigrated = new BGTExchanger(bgtToken);

        // Add diamondPlaceHolder as an exchanger
        // NOTE: We can use any address to be a placeholder for the protocol
        exchangerOriginal.addExchanger(diamondPlaceHolder);
        
        // Set rate for inToken
        exchangerOriginal.setRate(inTokenAddress, 1 << 128);

        // repeat for backup
        exchangerMigrated.addExchanger(diamondPlaceHolder);
        exchangerMigrated.setRate(inTokenAddress, 1 << 128);
        vm.stopPrank();
    }

   function test_bgtMigration_UnaccountedBgtToken() public {
        // fund exchangerOriginal with bgtToken
        approveTokens(owner, address(exchangerOriginal), bgtToken);
        vm.startPrank(owner);
        exchangerOriginal.fund(FUND_AMOUNT);
        vm.stopPrank();
        printDivider("exchangerOriginal funded with bgtToken");
        printBalance(address(exchangerOriginal), "exchangerOriginal", bgtToken);

        ///// Migration /////
        printDivider("Migration");
        // NOTE: pretending DiamondPlaceHolder is the SimplexDiamond, BGTExhange would now be pointing to exchangeBackup
        // Migrated exchanger points to the original exchanger as per migration docustring in SimplexFacet::setBGTExchanger()
        vm.startPrank(owner);
        exchangerMigrated.setBackup(address(exchangerOriginal));
        vm.stopPrank();

        // fund exchangerMigrated with bgtToken by transferring from the original exchanger
        vm.startPrank(owner);
        exchangerOriginal.sendBalance(bgtToken, address(exchangerMigrated), MockERC20(bgtToken).balanceOf(address(exchangerOriginal)));
        vm.stopPrank();
        assert(MockERC20(bgtToken).balanceOf(address(exchangerOriginal)) == 0);
        console.log("original exchanger sends bgt to migrated exchanger");

        // fund exchangerMigrated with new bgtToken
        approveTokens(owner, address(exchangerMigrated), bgtToken);
        vm.startPrank(owner);
        exchangerMigrated.fund(FUND_AMOUNT);
        vm.stopPrank();
        printDivider("exchangerMigrated funded with bgtToken");
        console.log("Balance accounted for internally: ", exchangerMigrated.bgtBalance());
        console.log("Actual balance : ", MockERC20(bgtToken).balanceOf(address(exchangerMigrated)));
    }
    /*//////////////////////////////////////////////////////////////
                                  MISC
    //////////////////////////////////////////////////////////////*/
    function mintTokens(address recipient, address token, uint256 amount) public {
        _mintTokens(recipient, token, amount);
    }

    function mintTokensMultipleUsers(address[] memory recipients, address token, uint256 amount) public {
        for(uint256 i = 0; i < recipients.length; i++){
            _mintTokens(recipients[i], token, amount);
        }
    }

    function approveTokens(address approver, address approved, address token) public {
        _approveToken(approver, approved, token);
    }

    function printBalance(address user, string memory userString, address token) public view {
        for(uint256 i = 0; i < tokensAddresses.length; i++) {
            if(token == tokensAddresses[i]) {
                console.log("%s %s balance: ", userString, MockERC20(token).name(), MockERC20(token).balanceOf(user));
            }
        }
    }

    function printDivider(string memory dividerTitle) public pure{
        console.log("\n ----- %s -----", dividerTitle);
    }

    /*//////////////////////////////////////////////////////////////
                             MISC-INTERNAL
    //////////////////////////////////////////////////////////////*/

    function _mintTokens(address recipient, address token, uint256 amount) internal { 
        vm.startPrank(owner);
        MockERC20(token).mint(recipient, amount);
        vm.stopPrank();
    }

    function _approveToken(address approver, address approved, address token) internal {
        vm.startPrank(approver);
        MockERC20(token).approve(
            address(approved),
            type(uint256).max
        );
        vm.stopPrank();
    }

}

contract Tokens is Test {

    // Helper function to create a single token with specified decimals
    function createToken(string memory name, string memory symbol, uint8 decimals) public returns (address token) {
        token = address(new MockERC20(name, symbol, decimals));
    }
}

```
Output:
```markdown
[PASS] test_bgtMigration_UnaccountedBgtToken() (gas: 249512)
Logs:
  
 ----- exchangerOriginal funded with bgtToken -----
  exchangerOriginal BGT balance:  100000000000000000000
  
 ----- Migration -----
  original exchanger sends bgt to migrated exchanger
  
 ----- exchangerMigrated funded with bgtToken -----
  Balance accounted for internally:  100000000000000000000
  Actual balance :  200000000000000000000
```

### Mitigation

Ensure `bgtBalance` is updated once `sendBalance` has been called. Depending on solution used, if a new codependant tracking between the orginal and the migrated is implemented, ensure `sendBalance` only allows BGT Token to be sent aswell.