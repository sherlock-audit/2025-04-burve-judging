Curved Lemonade Dove

Medium

# The amount when calculating the expenses does not take into account the cost of the vault, which will lead to a deviation in the fee allocation.

### Summary

In Reserve.sol, when calculating shares, the amount used by deposit function is the amount that does not consider the vault handling fee. The balance uses the balance that calculates the vault handling fee. If the vault handling fee does not change, there is no problem. However, if the vault fee changes(or the vault is modified and the two vaults have different handling fees), it will lead to uneven distribution. If the vault fee increases, users who add liquidity before the fee changes will lose a portion of their fee. Because after the vault handling fee changes, the calculated shares are too large.

### Root Cause

In [Reserve.sol](https://github.com/sherlock-audit/2025-04-burve/blob/44cba36e2a0c3cd7b6999459bf7746db92f8cc0a/Burve/src/multi/vertex/Reserve.sol#L44-L46), the vault fee is not considered when calculating shares using amount.

### Internal Pre-conditions

none

### External Pre-conditions

1. Vault handling fee changes.

### Attack Path

1. There is a group of normal users who "alice" to add liquidity.
2. There is a normal swap operation as liquidity accumulation processing fee, and the processing fee is stored in the Reserve.
3. The handling fee of the vault has been modified.
4. Another group of normal users "bob" adds liquidity.
5. There is also normal swap to continue to accumulate handling fees for liquidity.

In this process, since the swap calculated fee shares in step 5 is larger, it will infringe on the handling fee of the liquidity user before it is added. So the alices will lose their handling fees.

### Impact

Liquidity providers may lose part of their fees.

### PoC

It's not easy to prove the impact of this problem, so here is a comparison of the number of fees paid by the Alice before and after the repair to get their losses.
The idea is to calculate the amount1 of fees that Alice will eventually get according to the above attack path. Then modify Reserve.sol to recalculate the amount to account for the impact of the vault fee. Re-executing the POC yields an amount2. Thus deriving the percentage of fee loss.

The changes to the POC section are as follows:
Put the following poc into the test/facets/ folder:
```solidity
pragma solidity ^0.8.27;

import {MultiSetupTest} from "./MultiSetup.u.sol";
import {console2 as console} from "forge-std/console2.sol";
import {ValueFacet} from "../../src/multi/facets/ValueFacet.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {AssetBookImpl} from "../../src/multi/Asset.sol";
import {MAX_TOKENS} from "../../src/multi/Token.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import {MockERC4626} from "../mocks/MockERC4626.sol";

contract FeeDiffTest is MultiSetupTest {
    function setUp() public {
        vm.startPrank(owner);
        _newDiamond();
        _newTokens(4);
        _fundAccount(alice);
        _fundAccount(bob);
        // Its annoying we have to fund first.
        _fundAccount(address(this));
        _fundAccount(owner);
        // So we have to redo the prank.
        vm.startPrank(owner);
        _initializeClosure(0xF, 100e18); // 1,2,3,4
        _initializeClosure(0xE, 100e18); // 2,3,4
        _initializeClosure(0xD, 100e18); // 1,3,4
        _initializeClosure(0xc, 1e12); // 3,4
        _initializeClosure(0xB, 100e18); // 1,2,4
        _initializeClosure(0xa, 1e12); // 2,4
        _initializeClosure(0x9, 1e18); // 1,4
        _initializeClosure(0x8, 1e12); // 4
        _initializeClosure(0x7, 100e18); // 1,2,3
        _initializeClosure(0x6, 1e12); // 2,3
        _initializeClosure(0x5, 1e12); // 1,3
        _initializeClosure(0x4, 1e12); // 3
        _initializeClosure(0x3, 100e18); // 1,2
        _initializeClosure(0x2, 1e12); // 2
        _initializeClosure(0x1, 1e12); // 1
        vm.stopPrank();
    }

    function test_fee_diff() public {
        uint256 oneX128 = 1 << 128;
        // The default fee of ERC4626. 0.01%
        MockERC4626(address(vaults[0])).setFee(1);
        vm.prank(owner);
        simplexFacet.setClosureFees(0x9, uint128(oneX128 / 10000), 0);

        // Use alice and bob to refer to the two groups of liquidity providers before and after the change in fee.
        valueFacet.addValue(alice, 0x9, 1e28, 0);

        swapFacet.swap(
            alice, // recipient
            tokens[0], // tokenIn
            tokens[3], // tokenOut
            int256(100e18), // positive for exact input
            0, // no price limit{}
            0x9
        );

        // The fee for ERC4626 is modified. The modification of the fee will lead to the unfairness of the calculation of share.
        // The 0.3% fee is reasonable.
        MockERC4626(address(vaults[0])).setFee(30);
        valueFacet.addValue(bob, 0x9, 1e28, 0);

        swapFacet.swap(
            alice, // recipient
            tokens[0], // tokenIn
            tokens[3], // tokenOut
            int256(100e18), // positive for exact input
            0, // no price limit{}
            0x9
        );

        vm.prank(alice);
        (uint256[16] memory collectedBalances, uint256 collectedBgt) = valueFacet.collectEarnings(alice, 0x9);
        console.log("alice fee receipt:", collectedBalances[0]);

        vm.prank(bob);
        (uint256[16] memory collectedBalances_bob, uint256 collectedBgt_bob) = valueFacet.collectEarnings(bob, 0x9);
        console.log("bob fee receipt:", collectedBalances_bob[0]);
    }
}
```

Replace mocks/MockERC4626.sol with the following file to make it easier to set the vault fee:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "openzeppelin-contracts/token/ERC20/extensions/ERC4626.sol";
import "openzeppelin-contracts/token/ERC20/ERC20.sol";

contract MockERC4626 is ERC4626 {
    uint _fee;

    constructor(
        ERC20 asset,
        string memory name,
        string memory symbol
    ) ERC20(name, symbol) ERC4626(asset) {}

    /** @dev See {IERC4626-previewWithdraw}. */
    function previewWithdraw(uint256 assets) public view virtual override returns (uint256) {
        assets = assets * (10000 - _fee) / 10000;
        return _convertToShares(assets, Math.Rounding.Ceil);
    }

    /** @dev See {IERC4626-previewRedeem}. */
    function previewRedeem(uint256 shares) public view virtual override returns (uint256) {
        shares = shares * (10000 - _fee) / 10000;
        return _convertToAssets(shares, Math.Rounding.Floor);
    }

    function setFee(uint fee) public {
        _fee = fee;
    }
}
```

run `forge test --mt test_fee_diff -vvv`
The results of the previous tests without modifying Reserve.sol are as follows
```diff
Ran 1 test for test/facets/2.fee_diff.t.sol:FeeDiffTest
[PASS] test_fee_diff() (gas: 1714021)
Logs:
  alice fee receipt: 14947511264410094
  bob fee receipt: 4992488732600423

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 15.25ms (4.69ms CPU time)
```

Then modify the Reserve.sol as follows to simulate the correct calculation of the handling fee:
```diff
+ import {FullMath} from "../../FullMath.sol";
···
        vProxy.deposit(RESERVEID, amount);

+        amount = FullMath.mulX128(
+           amount,
+            vProxy.active.temp.vars[3],
+            true // Round up to round shares down.
+        );

        shares = (balance == 0)
            ? amount * SHARE_RESOLUTION
            : (amount * reserve.shares[idx]) / balance; // No need for mulDiv.
        reserve.shares[idx] += shares;
```

The test results obtained are as follows:
```diff
Ran 1 test for test/facets/2.fee_diff.t.sol:FeeDiffTest
[PASS] test_fee_diff() (gas: 1716056)
Logs:
  alice fee receipt: 14954999997507497
  bob fee receipt: 4984999999501522

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 14.54ms (4.72ms CPU time)
```

The loss ratio of the alice is 0.05%.
```diff
> (14954999997507497 - 14947511264410094) / 14954999997507497 * 100
0.050075112662321127
```

### Mitigation

_No response_