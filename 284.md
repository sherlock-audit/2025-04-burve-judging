Sunny Cloth Cod

Medium

# VaultFacet Allows Adding Incompatible Backup Vaults, Leading to Denial of Service to protocol operations

### Summary

The `VaultFacet.sol` contract allows admins to add a backup vault for a vertex without validating that the vault's underlying asset matches the token. When such an incompatible vault becomes active through hotswapping, or used as a source of extra liquidity when required for operations, it causes permanent Denial of Service (DoS) for critical protocol functions. 

### Root Cause

The [VaultFacet.addVault()::L32-38](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/multi/facets/VaultFacet.sol#L32) function and the subsequent `acceptVault()` function do not verify that the vault's underlying asset matches the token it's being associated with. This means an admin can unknowingly add a backup vault with a different underlying token. This leads to Denial of Service to multiple protocol operations.

As per the [Sherlock Issue Validity Criteria:](https://docs.sherlock.xyz/audits/judging/guidelines#:~:text=Note%3A%20if%20the,of%20his%20action.)

> Note: if the (external) admin will unknowingly cause issues, it can be considered a valid issue.

> Example: Admin sets fee to 20%. This will cause liquidations to fail in case the utilization ratio is below 10%, this can be Medium as the admin is not aware of the consequences of his action.



### Internal Pre-conditions

1. Admin sets the backup E4626 vault for a vertex to be a vault with a different underlying token.
2. The backup vault becomes the active vault through `VaultFacet.sol::hotswap()`or the liquidity is required from the backup vault due to the active vault's liquidity being insufficient when called upon.

### External Pre-conditions

N/A

### Attack Path

1. Vaults and closures are operating normally, users have value deposited within it, users are swapping etc.
2. Admin sets the backup vault for a vertex to be a vault with a different underlying token through `VaultFacet::addVault()`.
- This can be set during vertex initialisation or later on at any point in the protocol uptime

From here, it depends how the sequence of events pan out:
1. `VaultFacet::hotSwap()` is called so the backup vault (with the wrong underlying) becomes the active vault
2. The active vault's liquidity is not enough for a user operation so the backup vault is called upon

### Impact

Denial of Service to users trying to provide liquidity to a vertex and also swaps. This leads to a loss in fees generated for exisiting LPers and the protocol for the duration of the DoS. If this were to manifest at a time of high volume, the impact is critical.

However, the vault can be removed, therefore it is medium severity rather than high.

### PoC

- The following test uses the MultiSetUp test from the project's test folder; paste the following contract into the project's test directory.

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.17;

import {Test, console} from "forge-std/Test.sol";

import {MockERC20} from "../mocks/MockERC20.sol";
import {MockERC4626} from "../mocks/MockERC4626.sol";

import {SimplexDiamond} from "../../src/multi/Diamond.sol";
import {IDiamond} from "Commons/Diamond/interfaces/IDiamond.sol";
import {DiamondCutFacet} from "Commons/Diamond/facets/DiamondCutFacet.sol";
import {LibDiamond} from "Commons/Diamond/libraries/LibDiamond.sol";

import {MultiSetupTest} from "../facets/MultiSetup.u.sol";

import {VaultFacet} from "../../src/multi/facets/VaultFacet.sol";
import {SimplexFacet} from "../../src/multi/facets/SimplexFacet.sol";
import {VaultType} from "../../src/multi/vertex/VaultPointer.sol";
import {ClosureId} from "../../src/multi/closure/Id.sol";
import {TokenRegistry} from "../../src/multi/Token.sol";
import {Store} from "../../src/multi/Store.sol";

contract Finding2POC is MultiSetupTest {
    TokenandVaultDeployer tokenandVaultDeployer;
    CustomFacet public customFacet;

    uint256 constant MAX_TOKENS = 16;
    uint256 public INIT_VALUE = 1000e18;
    uint256 SWAP_AMOUNT = 100e18;

    address[MAX_TOKENS] tokensAddresses;
    TokenandVaultDeployer.TokenVaults[MAX_TOKENS] vaultAddresses;

    address[] public actors = [owner, alice, bob];

    function setUp() public virtual{
        vm.startPrank(owner);
        _newDiamond();

        tokenandVaultDeployer = new TokenandVaultDeployer();
        // tokensAddresses = tokenandVaultDeployer.init_Standard_mixed_ERC20_tokens();
        // vaultAddresses = tokenandVaultDeployer.init_Vaults();

        // deploy token 0 - regular token
        tokensAddresses[0] = tokenandVaultDeployer.createToken("TOKEN0", "TKN0", 18);
        // deploy vault
        vaultAddresses[0] = tokenandVaultDeployer.createActiveandBackupVaultPairOneToken(tokensAddresses[0]);

        // deploy token 1 - regular token
        tokensAddresses[1] = tokenandVaultDeployer.createToken("TOKEN1", "TKN1", 18);
        // deploy vault 
        vaultAddresses[1] = tokenandVaultDeployer.createActiveandBackupVaultPairOneToken(tokensAddresses[1]);

        // Mint users tokens
        mintTokensMultipleUsers(actors, tokensAddresses[0], INIT_VALUE);
        mintTokensMultipleUsers(actors, tokensAddresses[1], INIT_VALUE);

        require(tokensAddresses.length > 0);
        require(vaultAddresses.length > 0);

        // Deploy custom facet
        _customFacet();
        customFacet = CustomFacet(diamond);

        vm.stopPrank();
    }
    /*//////////////////////////////////////////////////////////////
                           PROOF OF CONCEPT
    //////////////////////////////////////////////////////////////*/
    function test_Finding2_BackUpVaultIncorrectAssetMultipleVertexClosure() public {
        printDivider("Create vertex 0");
        // Create vertex for token 0 // id 0 | vId 256
        _addVertexThroughSimplexFacet(tokensAddresses[0], vaultAddresses[0].active, VaultType.E4626);
        
        // Add backup for vertex id 1 but with vault of token 1
        _addVertexThroughVaultFacet(tokensAddresses[0], vaultAddresses[1].backup, VaultType.E4626);

        (address active, address backup) = vaultFacet.viewVaults(tokensAddresses[0]);
        console.log("vault active: ", active, "| Asset: ", MockERC4626(active).asset());
        console.log("vault backup: ", backup, "| Asset: ", MockERC4626(backup).asset());

        printDivider("Create vertex 1");
        // Create vertex for token 1 // id 1 | vId 513
        _addVertexThroughSimplexFacet(tokensAddresses[1], vaultAddresses[1].active, VaultType.E4626);
        (active,) = vaultFacet.viewVaults(tokensAddresses[1]);
        console.log("vault active: ", active, "| Asset: ", MockERC4626(active).asset());

        // Create closure for token 0 & token 1
        // cId = 0000000000000011 = 3
        address[] memory vertices1 = new address[](2);
        vertices1[0] = tokensAddresses[0];
        vertices1[1] = tokensAddresses[1];

        createClosure(vertices1, 1000e18, 0, 0);
        printDivider("Closure Created");
        
        // Alice adds value to closure for token 0
        approveTokens(alice, diamond, tokensAddresses[0]);
        uint256 aliceValueReceived = userAddSingleForValue(alice, 3, tokensAddresses[0], uint128(INIT_VALUE), 0, 0);
        assert(aliceValueReceived > 0);
        printDivider("Alice adds value to closure - works fine when active vault has correct underlying");

        // Hot swap vaults to make backup vault active
        vm.startPrank(owner);
        vaultFacet.hotSwap(tokensAddresses[0]);
        vm.stopPrank();
        (active, backup) = vaultFacet.viewVaults(tokensAddresses[0]);
        printDivider("Vaults Hot Swapped");
        console.log("Vertex 0: ");
        console.log("vault active: ", active, "| Asset: ", MockERC4626(active).asset());
        console.log("vault backup: ", backup, "| Asset: ", MockERC4626(backup).asset());   

        printDivider("Bob inital token amounts reference");
        printBalance(bob, "bob", tokensAddresses[0]);
        printBalance(bob, "bob", tokensAddresses[1]);

        // Bob tries to add Value to closure with swapped vault
        approveTokens(bob, diamond, tokensAddresses[0]);
        vm.startPrank(bob);
        vm.expectRevert();
        uint256 bobValueReceived = valueFacet.addSingleForValue(bob, 3, tokensAddresses[0], uint128(INIT_VALUE), 0, 0);
        printDivider("Bob tries to added value for token 0 but call reverted :: DOS");
        assert(bobValueReceived == 0);


        // Bob tries to swap token 0 for token 1
        vm.expectRevert();
        (uint256 inAmount1, uint256 outAmount1) = swapFacet.swap(bob, tokensAddresses[0], tokensAddresses[1], int256(SWAP_AMOUNT), 0, 3);
        assert(MockERC20(tokensAddresses[0]).balanceOf(bob) - inAmount1 == MockERC20(tokensAddresses[0]).balanceOf(bob));
        assert(MockERC20(tokensAddresses[1]).balanceOf(bob) + outAmount1 == MockERC20(tokensAddresses[1]).balanceOf(bob));
        vm.stopPrank();

        printDivider("Bob tries to swap token 0 for token 1 but swap fails :: DOS");
        printBalance(bob, "bob", tokensAddresses[0]);
        printBalance(bob, "bob", tokensAddresses[1]);
        vm.stopPrank();
        }
    /*//////////////////////////////////////////////////////////////
                                HELPERS
    //////////////////////////////////////////////////////////////*/
    /**
     *                ----- ADD CLOSURE -----
     */

    function createClosure(address[] memory tokenAddrs, uint128 startingTarget, uint128 baseFeeX128, uint128 protocolTakeX128) public {
        if(tokenAddrs.length == 0) revert("No tokens provided");

        ClosureId cid = customFacet.getClosureId(tokenAddrs);

        _initialiseClosure( cid,  startingTarget, baseFeeX128, protocolTakeX128);
    }

    /**
     *               ----- USER ADDS VALUE -----
     */
    function userAddSingleForValue(address user, uint16 cId, address token, uint256 amount, uint256 bgtPercentX256, uint128 minValue) public returns (uint256 requiredBalance){
        _approveToken(user, diamond, token);
        vm.startPrank(user);
        (requiredBalance) = valueFacet.addSingleForValue(user, cId, token, uint128(amount), bgtPercentX256, minValue);
        vm.stopPrank();
    }

    /**
     *                ----- USER SWAPS -----
     */

    function userSwaps(address user, address tokenIn, address tokenOut, int256 amount, uint256 amountLimit, uint16 cId) public returns (uint256 inAmount, uint256 outAmount){
        _approveToken(user, diamond, tokenIn);
        vm.startPrank(user);
        (inAmount, outAmount) = swapFacet.swap(user, tokenIn, tokenOut, amount, amountLimit, cId);
        vm.stopPrank();
    }

    /*//////////////////////////////////////////////////////////////
                                  MISC
    //////////////////////////////////////////////////////////////*/
    function mintTokens(address recipient, address token, uint256 amount) public {
        _mintTokens(recipient, token, amount);
    }

    function mintTokensMultipleUsers(address[] memory recipients, address token, uint256 amount) public {
        for(uint256 i = 0; i < recipients.length; i++){
            _mintTokens(recipients[i], token, amount);
        }
    }

    function approveTokens(address approver, address approved, address token) public {
        _approveToken(approver, approved, token);
    }

    function printBalance(address user, string memory userString, address token) public view {
        for(uint256 i = 0; i < tokensAddresses.length; i++) {
            if(token == tokensAddresses[i]) {
                console.log("%s token %s balance: ", userString, i, MockERC20(token).balanceOf(user));
            }
        }
    }

    function printDivider(string memory dividerTitle) public pure{
        console.log("\n ----- %s -----", dividerTitle);
    }

   /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////    /*//////////////////////////////////////////////////////////////
                                                                                                                                                
    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/    //////////////////////////////////////////////////////////////*/

    /*//////////////////////////////////////////////////////////////
                            HELPERS-INTERNAL
    //////////////////////////////////////////////////////////////*/
    /**
     *               ----- CREATE CUSTOM FACET -----
     */
    
    function _customFacet() internal {
        IDiamond.FacetCut[] memory cuts = new IDiamond.FacetCut[](1);

        bytes4[] memory selectors = new bytes4[](1);
        selectors[0] = CustomFacet.getClosureId.selector;

        cuts[0] = (
            IDiamond.FacetCut({
                facetAddress: address(new CustomFacet()),
                action: IDiamond.FacetCutAction.Add,
                functionSelectors: selectors
            })
        );

        vm.startPrank(owner);
        DiamondCutFacet cutFacet = DiamondCutFacet(diamond);
        cutFacet.diamondCut(cuts, address(0), "");
        vm.stopPrank();
    }
    
    /**
     *                  ----- ADD VAULTS -----
     */

     // Used For first vault of a token
    function _addVertexThroughSimplexFacet(address _token, address _vaultAddress, VaultType _vaultType) internal {
        vm.startPrank(owner);
        simplexFacet.addVertex( _token, _vaultAddress, _vaultType);

        // mint admin tokens
        _mintTokens(owner, _token, INIT_VALUE);

        // Approve diamond to spend tokens
        _approveToken(owner, diamond, _token);

        vm.stopPrank();
    }

    // Used For second vault of a token
    function _addVertexThroughVaultFacet(address _token, address _vaultAddress, VaultType _vaultType) internal {
        vm.startPrank(owner);
        vaultFacet.addVault( _token, _vaultAddress, _vaultType);

        // Warp to when vault can be accepted
        vm.warp(block.timestamp + 5 days);

        vaultFacet.acceptVault(_token);

        // mint admin tokens
        _mintTokens(owner, _token, INIT_VALUE);

        // Approve diamond to spend tokens
        _approveToken(owner, diamond, _token);

        vm.stopPrank();
    }

    /**
     *                  ----- ADD CLOSURES -----
     */

    function _initialiseClosure(ClosureId cid, uint128 startingTarget, uint128 baseFeeX128, uint128 protocolTakeX128) internal {
        vm.startPrank(owner);
        simplexFacet.addClosure(ClosureId.unwrap(cid), startingTarget, baseFeeX128, protocolTakeX128);
        vm.stopPrank();
    } 
    /*//////////////////////////////////////////////////////////////
                             MISC-INTERNAL
    //////////////////////////////////////////////////////////////*/

    function _mintTokens(address recipient, address token, uint256 amount) internal { 
        vm.startPrank(owner);
        MockERC20(token).mint(recipient, amount);
        vm.stopPrank();
    }

    function _approveToken(address approver, address approved, address token) internal {
        vm.startPrank(approver);
        MockERC20(token).approve(
            address(approved),
            type(uint256).max
        );
        vm.stopPrank();
    }
}

contract TokenandVaultDeployer is Test {

    struct TokenVaults {
        address active;
        address backup;
    }

    uint256 constant TOKENS = 16;

    // Helper function to create a single token with specified decimals
    function createToken(string memory name, string memory symbol, uint8 decimals) public returns (address token) {
        token = address(new MockERC20(name, symbol, decimals));
    }

    // Helper function to create a vault for a given token
    function createVault(address token, string memory name, string memory symbol) public returns (address vault) {
        vault = address(new MockERC4626(MockERC20(token), name, symbol));
    }

    // Helper function to create active and backup vault for a given token
    function createActiveandBackupVaultPairOneToken(address token) public returns(TokenVaults memory vaults){
        vaults.active = createVault(token, "Active Vault", "AV");
        vaults.backup = createVault(token, "Backup Vault", "BV");
    }
}

contract CustomFacet {

    function getClosureId(address[] memory tokens) external view returns (ClosureId) {
    uint16 cid = 0;
    TokenRegistry storage tokenReg = Store.tokenRegistry();
    for (uint256 i = 0; i < tokens.length; ++i) {
        uint16 idx = uint16(1 << tokenReg.tokenIdx[tokens[i]]);
        cid |= idx;
    }
    return ClosureId.wrap(cid);
    }   

}
```

Output:
```markdown
[PASS] test_Finding2_BackUpVaultIncorrectAssetMultipleVertexClosure() (gas: 2361311)
Logs:
  
 ----- Create vertex 0 -----
  vault active:  0x9EdF8B110633B5a359f9371884200ffC69003c60 | Asset:  0x26f0273d10E2d5858Db48E56b8b1B4C75063ceB0
  vault backup:  0xf648b85bBe2984c0Cb32212B866AC83475Eb603C | Asset:  0x706BB86133A1863b5fa2996E1fa38269d6875400
  
 ----- Create vertex 1 -----
  vault active:  0x79aD7179CbDF6b5767D013e63e780BB6C0b2DF46 | Asset:  0x706BB86133A1863b5fa2996E1fa38269d6875400
  
 ----- Closure Created -----
  
 ----- Alice adds value to closure - works fine when active vault has correct underlying -----
  
 ----- Vaults Hot Swapped -----
  Vertex 0: 
  vault active:  0xf648b85bBe2984c0Cb32212B866AC83475Eb603C | Asset:  0x706BB86133A1863b5fa2996E1fa38269d6875400
  vault backup:  0x9EdF8B110633B5a359f9371884200ffC69003c60 | Asset:  0x26f0273d10E2d5858Db48E56b8b1B4C75063ceB0
  
 ----- Bob inital token amounts reference -----
  bob token 0 balance:  1000000000000000000000
  bob token 1 balance:  1000000000000000000000
  
 ----- Bob tries to added value for token 0 but call reverted :: DOS -----
  
 ----- Bob tries to swap token 0 for token 1 but swap fails :: DOS -----
  bob token 0 balance:  1000000000000000000000
  bob token 1 balance:  1000000000000000000000
```

### Mitigation

Add validation for the backup vault to have the same underlying as the active vault.